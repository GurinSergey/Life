/****************************************************************************/
/*                     R-Style SoftLab, RS-Bank V6                          */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*          Работа с пользовательской таблицей dbsgvbranch_dbt              */
/*                                                                          */
/*  Имя файла: bsgvbranch.mac                                               */
/*  Создан:   19.12.08                                      Лавренов А.А.   */
/****************************************************************************/
// KS 03.10.2012 C-14283 Нечеткий поис теперь зависит от скроллинга

/*Для запсука из модуля использовать fm_terr_imp.mac */

//13-01-2012 Жаворонкова Н. (joy) C-7958-6 Добавлено order by t.t_text в MACRO СписокТеррористов
//RR 26.2013 Ввиду отключения RSAPP2 убрана константа
//LAO 20.11.2013 Признак должен ставиться на все просканированные документы
import rsbdataset, bankinter, compare, globals, календарь, rsexts, timestat;
import lib_compare, lib_agents;

record dlg1 (chk_ter, "fm_ter.lbr") dialog;

var outfile, errCode;// = "..\\txtfile\\terr"+strsubst(string(date()),".","")+{oper}+".txt";

array kinddoc;
kinddoc(0)= "Начальные";
kinddoc(1)= "Ответные";
kinddoc(2)= "Внутренние";
var start_date, end_date, doc_kind, prizn;
var count = 0;

private var flag = True, flag_24_7 = True;

macro STR_LPad (st, len, c, td)
   private var n;
   if (Valtype(td) == V_Undef)
      td = FALSE;
   end;
   n = StrLen(st);
   while (n < len)
      st = c + st;
      n = StrLen(st);
   end;
   if(td and (n > len))
      st = SubStr(st, n - len + 1)
   end;
   return st;
end;

// KS 03.10.2012 C-14283 Нечеткий поис теперь зависит от скроллинга
MACRO ШАПКА(startdate, enddate, dockind, ScrolStates);
   var str1, str2;
   str1 = substr(strlwr(dockind),1,strlen(dockind)-1)+"х";
   if(startdate==enddate)
      str2 = "за "+ startdate +" г.";
   elif(startdate==date("00.00.0000"))
      str2 =  "за период 00.00.0000 - "+ enddate +" г.";
   else
      str2 =  "за период "+ startdate +" - "+ enddate +" г.";
   end;


   //KS 03.10.2012 C-14283 Добавил поле Режим выпуска
   [                                   Список ########### документов, совпадающих с перечнем экстремистов и лиц причастных к терроризму    #
   Режим выпуска: #
   --------------T-------------T---------T--------------T------------------T--------------------------------------------------------------------------------T-------T-------------------------T---------┐
   │ Код клиента │ Код клиента │  № Док. │     Дата     │      Сумма       │                             Стоп фраза                                         │  Коэф.│   Имя поля документа    │   Вид   │
   │ плательщика │ получателя  │         │              │                  │                                                                                │       │                         │документа│
   ](str1:c,
   str2,
   ternary(((ValType(ScrolStates) != 26) and (ScrolStates == 10)),"по закрытым документам","по открытым документам")
   );

END;

MACRO СписокТеррористов(stopfrase)
   var rs, str, itogstring;
   str = "SELECT * " +
         "  FROM dstop_dbt t " +
         " WHERE t.t_str = '"+stopfrase+"' order by t.t_text "; // 13-01-2012 Жаворонкова Н. (joy) C-7958-6 Добавлено order by t.t_text
   [+-------------+-------------+---------+--------------+------------------+--------------------------------------------------------------------------------+-------+-------------------------+---------+];
   
   rs = trsbdataset(str);
   while(rs and rs.movenext)
      if(valtype(rs.text) == 0)
         itogstring = " ";
      else
         itogstring = rs.text;
      end;


      [│             │             │         │              │                  │################################################################################│       │                         │         │]
      (itogstring:w);
   end;
END;

// KS 03.10.2012 C-14283 Нечеткий поис теперь зависит от скроллинга
MACRO ТЕЛО(startdate, enddate, codekind, Prizn, ScrolStates, id)
   var rs, str, terr, rs1,sovp;
   var query, inside = ""; 
   var cnt = 0;

   str = "SELECT pmpaym.t_valuedate, pmrmprop.t_date, pmpaym.t_amount, "+
         "       pmrmprop.t_number, "+
         "       DECODE (pmpaym.t_payer, -1, ' ', pmpaym.t_payer) t_payer, "+
         "       pmrmprop.t_payername, "+
         "       DECODE (pmpaym.t_receiver, -1, ' ', pmpaym.t_receiver) t_receiver, "+
         "       pmrmprop.t_receivername, pmrmprop.t_ground, pmpaym.t_paymentid, "+
         "       pmpaym.t_dockind "+
         "  FROM dpmprop_dbt t, dpmpaym_dbt pmpaym, dpmrmprop_dbt pmrmprop "+
         " WHERE t.t_issender = CHR (0) AND t.t_debetcredit = 1 AND t.t_group = 1 ";
         if ((ValType(ScrolStates) != 26) and (ScrolStates == 10))
            str = str +
            "   AND t.t_propstatus = 32000 "+
            "   AND t.t_corschem not in (select t_number from dcorschem_dbt c where instr(' '||c.t_userfield2||' ',' STOP ')>0) ";
         else
            str = str +
            "   AND t.t_propstatus = 3000 ";
         end;
         str = str +
         "   AND t.t_paymentid = pmpaym.t_paymentid "+
         "   AND t.t_paymentid = pmrmprop.t_paymentid "+
         "   AND t.t_group = 1 "+
         "   AND pmpaym.t_dockind IN (16, 17, 27, 201, 202, 450, 320, 322, 311) "+
         "   AND t.t_transferdate BETWEEN TO_DATE ('"+startdate+"', 'DD.MM.YYYY') AND TO_DATE ('"+enddate+"', 'DD.MM.YYYY') ";
         if (flag)
            str = str + 
            " AND pmpaym.t_paymentid = " + id;            
         elif (flag_24_7)
            str = str +
            " AND t.t_bankcode NOT IN ('044525986','046311808','043601706','046577781','044525732','042908701','041806835','042406718','042007755') "+
            " AND EXISTS (SELECT 1 "+
            "               FROM dcorschem_dbt "+
            "              WHERE t_number = t.t_corschem AND t_fiid = pmpaym.t_fiid AND t_isnostro = CHR(88) AND t_number != 1) ";
         end;

   if(prizn == "Да")

      str = str + 
      "   AND not EXISTS ( SELECT * "+
      "                      FROM dnotetext_dbt t "+
      "                     WHERE t.t_notekind = 999 AND t.t_objecttype = 501 AND t.t_documentid = lpad(pmpaym.t_paymentid,10,0) ) ";
   end;


   terr = "SELECT DISTINCT t_str t_stopfrase, t_factor  " +
        "    FROM dstop_dbt " +
        "   WHERE length (t_str) > 0 and t_terror != 0 and t_number != 0 " ;


   rs = trsbdataset(str);
   if (not flag)
      initprogress(-1, "Обработка документов","Обработка документов");
   end;
   while (rs and rs.movenext)
      rs1 = trsbdataset(terr);
      while (rs1 and rs1.movenext)
         if(strlen(rs.payername) > 0)
            sovp = similarity( strlen(rs1.stopfrase), StrUpr(rs.payername), StrUpr(rs1.stopfrase));
            if(sovp > rs1.factor)
               [+-------------+-------------+---------+--------------+------------------+--------------------------------------------------------------------------------+-------+-------------------------+---------+
                │#############│#############│#########│##############│##################│################################################################################│#######│                         │  #####  │]
               ( rs.t_payer, rs.t_receiver, rs.t_number, substr(string(rs.t_date),1,10), rs.t_amount, rs1.stopfrase, sovp, rs.dockind:c );
               if (flag)
                  if (not Checks_LogProc(rs.t_paymentid,"Плательщик",4,rs1.stopfrase,string(sovp))) end;
                  if (not setCtgVal(rs.t_paymentid, PT_USR_CAT_CHECK_STATE, 4/*Нечеткий поиск*/, {curdate})) end;
                  if (not setCtgVal(rs.t_paymentid, PT_USR_CAT_CHECK_BEFORE_UNLOAD, 7/*Ручная обработка*/, {curdate})) end;
               elif (flag_24_7)
                  if (not Checks_LogProc_24_7(rs.t_paymentid,"Плательщик",rs1.stopfrase,string(sovp))) end;
                  if (not setCtgVal(rs.t_paymentid, PT_USR_CAT_CHECK_BEFORE_UNLOAD, 8/*115-ФЗ*/, {curdate})) end;
               end;
               СписокТеррористов(rs1.stopfrase);

               [+-------------+-------------+---------+--------------+------------------+--------------------------------------------------------------------------------+-------+-------------------------+---------+
                │             │             │         │              │                  │################################################################################│       │ ####################### │         │]
               (rs.t_payername:w, "Плательщик");

               count = count + 1;
            end;
         end;

         if(strlen(rs.receivername) > 0)
            sovp = similarity( strlen(rs1.stopfrase), StrUpr(rs.receivername), StrUpr(rs1.stopfrase));
            if(sovp > rs1.factor)
               [+-------------+-------------+---------+--------------+------------------+--------------------------------------------------------------------------------+-------+-------------------------+---------+
                │#############│#############│#########│##############│##################│################################################################################│#######│                         │  #####  │]
               ( rs.t_payer, rs.t_receiver, rs.t_number, substr(string(rs.t_date),1,10), rs.t_amount, rs1.stopfrase, sovp, rs.dockind:c );
               if (flag)
                  if (not Checks_LogProc(rs.t_paymentid,"Получатель",4,rs1.stopfrase,string(sovp))) end;
                  if (not setCtgVal(rs.t_paymentid, PT_USR_CAT_CHECK_STATE, 4/*Нечеткий поиск*/, {curdate})) end;
                  if (not setCtgVal(rs.t_paymentid, PT_USR_CAT_CHECK_BEFORE_UNLOAD, 7/*Ручная обработка*/, {curdate})) end;
               elif (flag_24_7)
                  if (not Checks_LogProc_24_7(rs.t_paymentid,"Получатель",rs1.stopfrase,string(sovp))) end;
                  if (not setCtgVal(rs.t_paymentid, PT_USR_CAT_CHECK_BEFORE_UNLOAD, 8/*115-ФЗ*/, {curdate})) end;
               end;
               СписокТеррористов(rs1.stopfrase);

               [+-------------+-------------+---------+--------------+------------------+--------------------------------------------------------------------------------+-------+-------------------------+---------+
                │             │             │         │              │                  │################################################################################│       │ ####################### │         │]
               (rs.t_receivername:w, "Получатель");

               count = count + 1;
            end;
         end;
 
         if(strlen(rs.ground) > 0)
            sovp = similarity( strlen(rs1.stopfrase), StrUpr(rs.ground), StrUpr(rs1.stopfrase));
            if(sovp > rs1.factor)
               [+-------------+-------------+---------+--------------+------------------+--------------------------------------------------------------------------------+-------+-------------------------+---------+
                │#############│#############│#########│##############│##################│################################################################################│#######│                         │  #####  │]
               ( rs.t_payer, rs.t_receiver, rs.t_number, substr(string(rs.t_date),1,10), rs.t_amount, rs1.stopfrase, sovp, rs.dockind:c );
               if (flag)
                  if (not Checks_LogProc(rs.t_paymentid,"Основание",4,rs1.stopfrase,string(sovp))) end;
                  if (not setCtgVal(rs.t_paymentid, PT_USR_CAT_CHECK_STATE, 4/*Нечеткий поиск*/, {curdate})) end;
                  if (not setCtgVal(rs.t_paymentid, PT_USR_CAT_CHECK_BEFORE_UNLOAD, 7/*Ручная обработка*/, {curdate})) end;
               elif (flag_24_7)
                  if (not Checks_LogProc_24_7(rs.t_paymentid,"Основание",rs1.stopfrase,string(sovp))) end;
                  if (not setCtgVal(rs.t_paymentid, PT_USR_CAT_CHECK_BEFORE_UNLOAD, 8/*115-ФЗ*/, {curdate})) end;
               end;
               СписокТеррористов(rs1.stopfrase);

               [+-------------+-------------+---------+--------------+------------------+--------------------------------------------------------------------------------+-------+-------------------------+---------+
                │             │             │         │              │                  │################################################################################│       │ ####################### │         │]
               (rs.t_ground:w, "Основание");

               count = count + 1;
            end;
         end;
      end;

     //20.11.2013 Если признак Да, тогда отметим отсканированные документы.
     if(prizn== "Да")
        addNoteForObject (501, str_lpad(rs.paymentid, 10, "0"), 999, "X");
     end;
     if (not flag)
        useprogress(cnt);
     end;
     cnt = cnt+1;
   end;
   if (flag)
      if (not setCtgVal(id, PT_USR_CAT_CHECK_STATE, 4/*Нечеткий поиск*/, {curdate})) end;
   end;
   if (not flag)
      remprogress;
   end;
END;


MACRO ТАПКИ()
   [+-------------+-------------+---------+--------------+------------------+--------------------------------------------------------------------------------+-------+-------------------------+---------+  
    │ ИТОГО       │  ####################   записей отобрано                                                                                                                                             │ 
    L-------------+--------------------------------------------------------------------------------------------------------------------------------------------------------------------------------------+]
   (count);
END;

//Обработчик для панели
macro fm_ter_dlg (dlg, cmd, id, key)
   var choice;
   if ( cmd == DLG_INIT )
      dlg.startdate = {curdate};
      dlg.enddate   = {curdate};
      dlg.dockind   = "Начальные";
      dlg.prizn     = "Да";
      DisableFields(dlg,2);
      message("Esc Выход F3 Список Пробел Переключение F2(F9) Запуск ");
      UpdateFields(dlg);
   elif ( cmd == DLG_REMFOCUS ) 
      if (id==0)
         UpdateFields(dlg);
      elif (id==1)
         UpdateFields(dlg);
      end;
      return CM_IGNORE;

   elif( cmd == DLG_KEY ) //Проверки на нажатие клавиш
      if( key == 27 ) //Esc
         exit(1);

         return CM_CANCEL;
      elif ( key == 32 ) //Пробел
         if (id==3)
            if(dlg.prizn=="Да")
               dlg.prizn="Нет"
            else
               dlg.prizn="Да"
            end;
         end;
         UpdateFields(dlg);
      elif ( key == 317 ) //F3
         if (id==0)
            dlg.startdate = getdatebycalendar(dlg.startdate);
            UpdateFields(dlg);
         elif (id==1)
            dlg.enddate = getdatebycalendar(dlg.enddate);
            UpdateFields(dlg);
         elif (id==2)
            choice = menu(kinddoc,"","Вид документов");
            if(choice >=0)
               dlg.dockind = kinddoc(choice);
            end;
            UpdateFields(dlg);
         end;

         return CM_IGNORE;
      elif ( (key == 316) or (key == 323) ) //F2 F9
         if(dlg.startdate == "")
            start_date = "01.01.0001";
         else
            start_date = dlg.startdate;
         end; 
         if(dlg.enddate == "")
            end_date = "01.01.0001";
         else
            end_date   = dlg.enddate;
         end; 
         doc_kind   = dlg.dockind;
         prizn      = dlg.prizn;

         return CM_SAVE;
      end;
   end;

   return CM_DEFAULT;
END;

// KS 03.10.2012 C-14283 Нечеткий поис теперь зависит от скроллинга
// Gurin S. 14.01.2015 C-36593-7 добавил plan_24_7
macro fm_run(ScrolStates, id, plan_24_7)
   if (isNull(id))        flag      = False; end;
   if (isNull(plan_24_7)) flag_24_7 = False; end;

   outfile = GetTxtFileName ("terr"+strsubst(strsubst(string(date),".","")," ","0")+strsubst(string(time),":",""));

   //пересоздаем файл
   if ((not flag) and (not flag_24_7))
      setoutput(outfile,false);
      setoutput(null,true);
   end;
   if (flag or flag_24_7)
      start_date = end_date = {Curdate};
      doc_kind = "Начальные";
      prizn = "Да";
   else
      RunDialog (dlg1, @fm_ter_dlg);
   end;
   if ((not flag) and (not flag_24_7))
      SaveBeginTime();//Старт
      setoutput(outfile,true);
   end;

   ШАПКА(start_date, end_date, doc_kind, ScrolStates);
   ТЕЛО(start_date, end_date, doc_kind, prizn, ScrolStates, id);
   ТАПКИ();

   if ((not flag) and (not flag_24_7))
      setoutput(null,true);
      viewfile(outfile);
   end;

   SaveEndTime (4); //Конец
end;

//fm_run(1,40505606);
//fm_run(1, null, True);




  
