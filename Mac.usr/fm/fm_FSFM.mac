/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   RR Макрос выписки по счету для предоставления в ФСФМ
   
   Автор не известен
   
   UPD:
       //RR 19.12.2012 I-00301792-3 Исправил ошибки запроса в макросе GetData,
            добавил параметризацию, для ускорения работы.
       //VDN 15.10.2015 R-624018 Отбираем только закрытые проводки
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/
import rsbdataset, globals, ptinter, ActX, календарь;

 record dlg1 (fm_fsfm, "fm_ter.lbr") dialog;

 file party (party);
 file accnt (account);

 var ЧислоЛистов=1,      
      File_Name="";

 var Код_Ячейки="";
 var Номер_Строки  = 4,  /*с которой начинается заполнение данных*/
     лист = 1;
 
  var startdate, enddate, acnt;

MACRO GetChapter(acc)
  var balacc, chapter;

  if(strbrk(acc, "ОВП") > 0)
     return 1;
  else
     balacc = int(SUBSTR (acc, 1, 5));
  end;

  IF ((balacc >= 98000) AND (balacc <= 98090))
      chapter = 5;
  ELIF ((balacc >= 93001) AND (balacc <= 97001))
      chapter = 4;
  ELIF (   ((balacc >= 90601) AND (balacc <= 91904))
        OR (balacc == 99999) OR (balacc == 99998))
      chapter = 3;
  ELIF ((balacc >= 80101) AND (balacc <= 85501))
      chapter = 2;
  ELSE
     chapter = 1;
  END;

 return chapter;
END;

MACRO GetPartyNameByAccount(acc)
  var rs, str;
  str = "SELECT p.t_name " +
        "  FROM daccount_dbt t, dparty_dbt p" +
        " WHERE t.t_account = '"+acc+"' " + 
        " and t_chapter = "+ getchapter(acc)+ 
        " and  p.t_partyid = t.t_client ";

  rs = trsbdataset(str);
  if(rs and rs.movenext)
     return rs.name;
  else
     return "";
  end;
END;

MACRO GetClient(code, name, closed)
var rs, str;
  str = "SELECT pt.t_partyid, pt.t_name, decode(PT.T_LOCKED,chr(0), 'Нет', 'Да') T_LOCKED " +
        "FROM dparty_dbt pt, dobjcode_dbt oc " +
        "WHERE     oc.t_code = '"+code+"' " +
        "      AND oc.t_codekind = 1 " +
        "      AND oc.t_objecttype = 3 " +
        "      AND pt.t_partyid = oc.t_objectid ";

  rs = trsbdataset(str);
  if(rs and rs.movenext) 
     setparm(1,rs.name);
     setparm(2,rs.locked);
     return true;
  else
     setparm(1,"Не найден.");
     return false;
  end;

END;

MACRO GetClientByAccount(acc, code, name, closed)
var rs, str;
  str = "SELECT pt.t_partyid, oc.t_code, pt.t_name, " +
        "       DECODE (pt.t_locked, CHR (0), 'Нет', 'Да') t_locked " +
        "FROM daccount_dbt t, dparty_dbt pt, dobjcode_dbt oc " +
        "WHERE     t.t_account = '"+acc+"' " +
        "      AND t.t_chapter = " + getchapter(acc)+
        "      AND pt.t_partyid = t.t_client " +
        "      AND oc.t_codekind = 1 " +
        "      AND oc.t_objecttype = 3 " +
        "      AND pt.t_partyid = oc.t_objectid ";

  rs = trsbdataset(str);
  if(rs and rs.movenext) 
     setparm(1,rs.code);
     setparm(2,rs.name);
     setparm(3,rs.locked);
     return true;
  else
     return false;
  end;
END;

MACRO GetAccount(acc, name)
var rs, str;
  str = "SELECT t_account, t_nameaccount " +
        "FROM daccount_dbt t " +
        "WHERE t_account = '"+acc+"'  " +
        "  AND t_chapter =  "+ getchapter(acc);

  rs = trsbdataset(str);
  if(rs and rs.movenext) 
     setparm(1,rs.nameaccount);
     return true;
  else
     setparm(1,"Не найден.");
     return false;
  end;

END;




MACRO GetData(acc, start, endd)

var rs, str_deb, str_kred, str2, header, inn_receiver, inn_payer, cmd;
var summ=$0, cnt=0;
   debugbreak;
   // EVG 31/1/2014 Переход на 2031
   
   str_deb = " SELECT   rec.t_issender, " +
             "          pm.t_fiid, " +
             "          pr.t_number, " +
             "          trn.t_date_carry AS t_valuedate, " +
             "          pm.t_payerbankid, " +
             "          pr.t_payerbankname, " +
             "          send.t_bankcode t_payerbankcode, " +
             "          pm.t_payer, " +
             "          pr.t_payername, " +
             "          SUBSTR (pr.t_payerinn, 1, 10) t_payerinn, " +
             "          CASE " +
             "             WHEN SUBSTR (trn.T_ACCOUNT_PAYER, 1, 2) = '30' " +
             "             THEN " +
             "                PM.T_PAYERACCOUNT " +
             "             ELSE " +
             "                trn.T_ACCOUNT_PAYER " +
             "          END " +
             "             t_PAYERACCOUNT, " +
             "          pm.t_receiverbankid, " +
             "          pr.t_receiverbankname, " +
             "          rec.t_bankcode t_receiverbankcode, " +
             "          pm.t_receiver, " +
             "          pr.t_receivername, " +
             "          SUBSTR (pr.t_receiverinn, 1, 10) t_receiverinn, " +
             "          CASE " +
             "             WHEN SUBSTR (trn.T_ACCOUNT_RECEIVER, 1, 2) = '30' " +
             "             THEN " +
             "                PM.T_RECEIVERACCOUNT " +
             "             ELSE " +
             "                trn.T_ACCOUNT_RECEIVER " +
             "          END " +
             "             t_receiveraccount, " +
             "          trn.t_sum_payer AS t_sum, " +
             "          pr.t_ground " +
             "   FROM   dpmpaym_dbt pm, " +
             "          dpmrmprop_dbt pr, " +
             "          dpmprop_dbt send, " +
             "          dpmprop_dbt rec, " +
             "          dpmdocs_dbt d, " +
             "          dacctrn_dbt trn " +
             "  WHERE       pm.T_PAYMENTID = D.T_PAYMENTID " +
             "          AND pr.t_paymentid = pm.t_paymentid " +
             "          AND send.t_paymentid = pm.t_paymentid " +
             "          AND send.t_issender = 'X' " +
             "          AND rec.t_paymentid = pm.t_paymentid " +
             "          AND rec.t_issender = CHR (0) " +
//           "          AND pm.t_paymstatus = 32000 " +
             "          AND d.t_acctrnid = trn.t_acctrnid " +
             "          AND trn.t_date_carry BETWEEN :startdate1 " +
             "                                   AND :enddate1 " +
             "          AND trn.t_account_payer = :account2 " +
             "          AND trn.T_CHAPTER = 1 " +
             "          AND trn.t_state = 1 "
             " UNION " +
             " SELECT   send.t_issender, " +
             "          pm.t_fiid, " +
             "          pr.t_number, " +
             "          trn.t_date_carry AS t_valuedate, " +
             "          pm.t_payerbankid, " +
             "          pr.t_payerbankname, " +
             "          send.t_bankcode t_payerbankcode, " +
             "          pm.t_payer, " +
             "          pr.t_payername, " +
             "          SUBSTR (pr.t_payerinn, 1, 10) t_payerinn, " +
             "          CASE " +
             "             WHEN SUBSTR (trn.T_ACCOUNT_PAYER, 1, 2) = '30' " +
             "             THEN " +
             "                PM.T_PAYERACCOUNT " +
             "             ELSE " +
             "                trn.T_ACCOUNT_PAYER " +
             "          END " +
             "             t_PAYERACCOUNT, " +
             "          pm.t_receiverbankid, " +
             "          pr.t_receiverbankname, " +
             "          rec.t_bankcode t_receiverbankcode, " +
             "          pm.t_receiver, " +
             "          pr.t_receivername, " +
             "          SUBSTR (pr.t_receiverinn, 1, 10) t_receiverinn, " +
             "          CASE " +
             "             WHEN SUBSTR (trn.T_ACCOUNT_RECEIVER, 1, 2) = '30' " +
             "             THEN " +
             "                PM.T_RECEIVERACCOUNT " +
             "             ELSE " +
             "                trn.T_ACCOUNT_RECEIVER " +
             "          END " +
             "             t_receiveraccount, " +
             "          trn.t_sum_receiver AS t_sum, " +
             "          pr.t_ground " +
             "   FROM   dpmpaym_dbt pm, " +
             "          dpmrmprop_dbt pr, " +
             "          dpmprop_dbt send, " +
             "          dpmprop_dbt rec, " +
             "          dpmdocs_dbt d, " +
             "          dacctrn_dbt trn " +
             "  WHERE       pm.T_PAYMENTID = D.T_PAYMENTID " +
             "          AND pr.t_paymentid = pm.t_paymentid " +
             "          AND send.t_paymentid = pm.t_paymentid " +
             "          AND send.t_issender = 'X' " +
             "          AND rec.t_paymentid = pm.t_paymentid " +
             "          AND rec.t_issender = CHR (0) " +
//           "          AND pm.t_paymstatus = 32000 " +
             "          AND d.t_acctrnid = trn.t_acctrnid " +
             "          AND trn.t_date_carry BETWEEN :startdate2 " +
             "                                   AND :enddate2 " +
             "          AND trn.t_account_receiver = :account2 " +
             "          AND trn.T_CHAPTER = 1 " +
             "          AND trn.t_state = 1 "
             " ORDER BY   t_valuedate ";

  cmd = RSDCommand(str_deb);
  cmd.AddParam("startdate1", RSDBP_IN, start);
  cmd.AddParam("enddate1",   RSDBP_IN, endd);
  cmd.AddParam("account1",   RSDBP_IN, acc);
  cmd.AddParam("startdate2", RSDBP_IN, start);
  cmd.AddParam("enddate2",   RSDBP_IN, endd);
  cmd.AddParam("account2",   RSDBP_IN, acc);
  
  if(start==endd)
     str2 = "за дату "+ start ;
  elif(start==date("00.00.0000"))
     str2 =  "за период с 00.00.0000 по "+ endd;
  else
     str2 =  "за период с "+ start +" по "+ endd;
  end;


  header = string("Выписка по счету №" +acc+ " " + str2 + ", принадлежащего " + GetPartyNameByAccount(acc) + ", для предоставления в Федеральную службу по финансовому мониторингу.");

  Запись_ячейка(1,"B1",header);

/**/
println(str_deb);

  //rs = trsbdataset(cmd);
  rs = RSDRecordSet(cmd);
  debugbreak;
  InitProgress(-1,"Ждите...","Идет обработка записей");
  while (rs and rs.movenext())
    cnt = cnt+1;
    summ = summ + money(rs.value("t_sum"));

    Код_Ячейки ="B"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_number"));
    Код_Ячейки ="C"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, date(rs.value("t_valuedate")));
    Код_Ячейки ="D"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_payerbankname"));
    if(rs.value("t_fiid") == 0)
       Код_Ячейки ="E"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, получитькодсубъекта(rs.value("t_payerbankid"),3));           
    else
       Код_Ячейки ="E"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, получитькодсубъекта(rs.value("t_payerbankid"),6));
    end;
    Код_Ячейки ="F"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_payername"));
    splitfullinn(rs.value("t_payerinn"), inn_payer,null);
    Код_Ячейки ="G"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_payerinn"));
    Код_Ячейки ="H"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_payeraccount"));
    Код_Ячейки ="I"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_receiverbankname"));
    if(rs.value("t_fiid") == 0)
       Код_Ячейки ="J"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, получитькодсубъекта(rs.value("t_receiverbankid"),3));
    else
       Код_Ячейки ="J"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, получитькодсубъекта(rs.value("t_receiverbankid"),6));
    end;
    Код_Ячейки ="K"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_receivername"));
    splitfullinn(rs.value("t_receiverinn"), inn_receiver,null);
    Код_Ячейки ="L"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_receiverinn"));
    Код_Ячейки ="M"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_receiveraccount"));
    if(rs.value("t_issender") != "X")
       Код_Ячейки ="N"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_sum"));
    else
       Код_Ячейки ="O"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_sum"));
    end;
    Код_Ячейки ="P"+Номер_Строки;  Запись_ячейка(1,Код_Ячейки, rs.value("t_ground"));
  
    Номер_Строки=Номер_Строки+1;
  UseProgress(cnt);
  end;
  RemProgress();
end;

MACRO GetAccountScroll (clid, acc, nameacc)
  var RecordSet;
  var selacc, name;

  var CmdText,Command;
  var col = TArray;

  macro EvProc (RecordSet, Command, id, key )
    if ((Command == DLG_KEY) and (key == 13))
       selacc = RecordSet.value(2);
       name = RecordSet.value(6);
       return CM_cancel;
    end;

  end;


  macro AddCol (ar,ind, fld, head, width, rdonly)
    ar.value (ind * 6)     = fld;
    ar.value (ind * 6 + 1) = head;
    ar.value (ind * 6 + 2) = width;
    ar.value (ind * 6 + 3 ) = 2;   // fldType
    ar.value (ind * 6 + 4 ) = -1;  // decPoint
    ar.value (ind * 6 + 5 ) = 0;   // reserv
  end;


  AddCol (col, 0, "t_open_close", null, 3, true);
  AddCol (col, 1, "t_close_date", null, 10, true);
  AddCol (col, 2, "t_account", null, 20, true);
  AddCol (col, 3, "t_code_currency", null, 5, true);
  AddCol (col, 4, "t_chapter", null, 3, true);
  AddCol (col, 5, "t_r0", null, 12, true);
  AddCol (col, 6, "t_nameaccount", null, 45, true);

  CmdText = "SELECT t.t_open_close,  " +
            "       DECODE (t_close_date, TO_DATE ('01010001', 'ddmmyyyy'), '',TO_CHAR (t_close_date, 'dd.mm.yyyy'))t_close_date,  " +
            "       t.t_account, SUBSTR (t.t_account, 6, 3) t_code_currency, " +
            /* EVG 31/1/2014 Переход на 2031
            "       t.t_chapter, t.t_r0, t.t_nameaccount " +*/
            "       t.t_chapter, RSB_ACCOUNT.RestAc( t.t_account, t.t_code_currency, '"+{curdate}+"', t.t_chapter, 0 ) as t_r0, t.t_nameaccount " +
            "FROM daccount_dbt t "+
            " where 1 = 1 ";

  debugbreak;
  if((strlen(trim(string(clid)))) and (valtype(clid) != 0))
  CmdText = CmdText + " and t_client = "+ clid; 
  end;

  if((strlen(trim(acc)) != 0) and (valtype(acc)!= 0))
  CmdText = CmdText + " and t_account like replace(replace('"+acc+"', '*', '%'), '?','_')"; 
  end;


  CmdText = CmdText + " order by t_account";

  Command = RSDCommand(CmdText);
  Command.Execute();
  RecordSet = RSDRecordSet(Command, RSDVAL_CLIENT, RSDVAL_STATIC);
  runScroll(RecordSet, 7, col, null, @EvProc, "Выбор счета", "~Enter~   Выбор", false);


  if(valtype(selacc)== 0)
     return false;
  else
     setparm(1,selacc);
     setparm(2,name);
     return true;
  end;
END;




//Обработчик для панели
 macro fm_ter_dlg (dlg, cmd, id, key)
   var code="", selected;
   var cur_acc, cur_acc_name;

   if ( cmd == DLG_INIT )
      dlg.begdate = {curdate};
      dlg.enddate   = {curdate};
      dlg.closed = "Нет";
      message("Esc Выход F3 Список Пробел Переключение F2(F9) Запуск ");
      UpdateFields(dlg);
   elif ( cmd == DLG_REMFOCUS ) 
      if (id==0)

         Getclient(dlg.clcode, dlg.clname, dlg.closed);
         UpdateFields(dlg);
      elif(id == 3)
         GetAccount(dlg.account, dlg.accname);
         //debugbreak;
         if((dlg.accname != "Не найден.") and (dlg.clname  == "Не найден."))
          GetClientByAccount(dlg.account, dlg.clcode, dlg.clname, dlg.closed);
         end;
         UpdateFields(dlg);
      end;
      return CM_IGNORE;

   elif( cmd == DLG_KEY ) //Проверки на нажатие клавиш
      if( key == 27 ) //Esc
         exit(1);
         return CM_CANCEL;
        UpdateFields(dlg);
      elif ( key == 32 ) //Пробел
       if (id==1)
         if(dlg.closed=="Да")
             dlg.closed="Нет"
         else
             dlg.closed="Да"
         end;
       end;
       UpdateFields(dlg);
      elif ( key == 317 ) //F3
       if (id==0)
         if(dlg.closed == "Да")
            selected = listpt(party,1,code,PTLIST_ALLLOCKEDPARTY, party.partyid,  PTCK_ALL);
         else
            selected = listpt(party,1,code,PTLIST_ALLPARTY, party.partyid,  PTCK_ALL);
         end;
         if(selected)
            dlg.clcode = code;
            dlg.clname = party.name;
            dlg.account = "";
            dlg.accname = "";
            UpdateFields(dlg);
         end;
      elif (id==3)
         selected = GetAccountScroll (получитькодсубъекта(dlg.clcode,1), dlg.account, dlg.accname);

         if(selected)
           // dlg.account = cur_acc;
            //dlg.accname = cur_acc_name;
            UpdateFields(dlg);
         end;
      elif (id==5)
        dlg.begdate = getdatebycalendar(dlg.begdate);
        UpdateFields(dlg);
      elif (id==6)
        dlg.enddate = getdatebycalendar(dlg.enddate);
        UpdateFields(dlg);
      end;

         return CM_IGNORE;
      elif ( (key == 316) or (key == 323) or ((key == 13) and (id==6)) ) //F2 F9 Enter

         acnt = dlg.account;
         
         if(dlg.begdate == "")
            startdate = "01.01.0001";
         else
            startdate = dlg.begdate;
         end; 
         if(dlg.enddate == "")
            enddate = "01.01.0001";
         else
            enddate   = dlg.enddate;
         end; 

         if((acnt == "") or (dlg.accname == "Не найден."))
            msgbox("Счет задан неверно!");
            return CM_IGNORE;
         elif (startDate > {curdate})
            msgbox("Начальная дата больше текущего дня!");
            return CM_IGNORE;
         elif (startDate > endDate)
            msgbox("Конечная дата меньше начальной!");
            return CM_IGNORE;
         else
            return CM_SAVE;
         end;
      end;
   end;
   return CM_DEFAULT;
 END;


  RunDialog (dlg1, @fm_ter_dlg);

  debugbreak;
  ОткрытиеТаблицы_Книги("fm_FSFM.xls");
  ИнициализацияЛистов(ЧислоЛистов);

  ws(1).Activate;/*можно писать в этот лист*/
  ob.Visible=false;

  GetData(acnt,startdate, enddate);
  
  msgbox("Все! Теперь можете просмотреть полученный отчет в EXCEL","| Нажмите ESC");
  ob.Visible=true;
  exit(1);
