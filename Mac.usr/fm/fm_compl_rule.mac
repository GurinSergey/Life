/*********************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6.0                          */
/*********************************************************************************/
/*                         Подсистема "Финансовый мониторинг"                    */
/*  Скроллинг редактирования условий отбора платежей на шаг "Комплаенс-контроль" */
/*                                                                               */
/*  Имя файла:  Compl_contr_rule.mac                                             */
/*  Создан   :  01.11.2014                                        Чесноков Д.С.  */
/*  Заявка   :  C-33929                                                          */
/*  Описание :  Реализация пользовательского механизма изменений условий отбора  */
/*               для попадания на шаг "Комплаенс-контроля"                       */
/*  Изменения:  Gurin S. 25.06.2015 C-40459 Добавлено поле                       */
/*                                     USR_COMPILAENS_RULE_DBT.T_RECEIVERINN     */
/*********************************************************************************/
import RsbFormsInter, RSD, "KeyCodes.mac", "zubrunscroll.mac";

  private const FT_STRING = 7, FT_NUMERIC = 25;
  private var scrollIsRunning = true;
  var Edit_id, Edit_mode = false;

class(TRsbPanel) MainPanel();

  macro TParamChoise(_Object:TRsbComboBox)
    _Object.Clear();
    _Object.AddChoice(0, "=");
    _Object.AddChoice(1, "<>");
    _Object.AddChoice(2, ">=");
    _Object.AddChoice(3, "<=");
    _Object.AddChoice(4, ">");
    _Object.AddChoice(5, "<");
    return _Object;	
  end;

  var rs, str;

  InitTRsbPanel();
  setCaption("Панель редактирования условий комплаенс контроля");
  if (Edit_Mode)
    SetStatus("~ESC~ Выход ~F3~ Выбор ~F9~ Сохранить изменения");
  else
    SetStatus("~ESC~ Выход ~F3~ Выбор ~F9~ Сохранить");
  end;
  
  setPosition(14,9);
  setSize(44,12);

  var val_Cond_level:INTEGER = 0;
  var val_Level:INTEGER = 0;
  var val_Cond_sum:INTEGER = 0;
  var val_Sum:MONEY = $0.00;
  var val_RecINN:STRING = "*";
  var val_Ground:STRING = "*";

  var Cond_level:TRsbComboBox = TRsbComboBox();
  var Level:TRsbComboBox = TRsbComboBox();
  var Cond_sum:TRsbComboBox = TRsbComboBox();
  var Sum:TRsbEditField = TRsbEditField(FT_NUMERIC);
  var RecINN:TRsbEditField = TRsbEditField(FT_STRING);
  var Ground:TRsbEditField = TRsbEditField(FT_STRING);

  addLabel(TRsbLabel(3, 2, "УРОВЕНЬ РИСКА"));
  addLabel(TRsbLabel(28, 2, "СУММА"));
  addLabel(TRsbLabel(15, 4, "ИНН ПОЛУЧАТЕЛЯ"));
  addLabel(TRsbLabel(15, 6, "ОСНОВАНИЕ ДОКУМЕНТА"));

  Cond_level.bindValue(this, "val_Cond_level");
  Cond_level.SetPosition(4, 3);
  Cond_level.setSize(2, 1);
  Cond_level.Name = "Cond_level";
  Cond_level = TParamChoise(Cond_level);
  AddControl(Cond_level);

  Level.bindValue(this, "val_Level");
  Level.SetPosition(8, 3);
  Level.setSize(2, 1);
  Level.Name = "Level";
  Level.Clear;

  str = "select ROWNUM-1, t_nameobject from dobjattr_dbt where t_objecttype = 3 and t_groupid = 300";
  rs = RsdRecordSet(str);
  while(rs.movenext())
    Level.AddChoice(rs.value(0), rs.value(1))
  end;
  AddControl(Level);
  
  Cond_sum.bindValue(this, "val_Cond_sum");
  Cond_sum.SetPosition(20, 3);
  Cond_sum.SetSize(2, 1);
  Cond_sum.Name = "Cond_sum";
  Cond_sum = TParamChoise(Cond_sum);
  AddControl(Cond_sum);

  Sum.bindValue(this, "val_Sum");
  Sum.SetPosition(24, 3);
  Sum.setSize(15,1);
  Sum.valuePrecision = 2;
  Sum.Name = "Sum";
  AddControl(Sum);

  RecINN.bindValue(this, "val_RecINN", 400);
  RecINN.SetPosition(3,5);
  RecINN.setSize(39,1);
  RecINN.Editable = true;
  RecINN.Name = "RecINN";
  AddControl(RecINN);

  Ground.bindValue(this, "val_Ground", 400);
  Ground.SetPosition(3,7);
  Ground.setSize(39,4);
  Ground.Editable = true;
  Ground.Name = "Ground";
  AddControl(Ground);

  if (Edit_Mode)
    str = "select * from USR_COMPILAENS_RULE_DBT where t_id = " + Edit_ID;
    rs = RsdRecordset(str);
    rs.NullConversion;
    if (rs and rs.MoveNext())
      if (valtype(rs.value("t_cond_level")) == 26)
        Cond_level.Value = "";
      else
        Cond_level.Value = rs.value("t_cond_level");
      end;
      Level.Value        = rs.value("t_level");
      if (valtype(rs.value("t_cond_sum")) == 26)
        Cond_sum.Value   = "";
      else
        Cond_sum.Value   = rs.value("t_cond_sum");
      end;
      Sum.Value          = rs.value("t_sum");
      RecINN.Value       = rs.value("t_receiverinn");
      Ground.Value       = rs.value("t_ground");
    end;
  end;

  addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "PressKey_MainPanel"));

  MessageTableName = "bank_msg";

  macro CheckData()
    var str, rs;
    str = " select 1 from USR_COMPILAENS_RULE_DBT where t_cond_level  = '" + this.Cond_level.Value + "'" +
          "                                         and t_level       =  " + this.Level.Value +
          "                                         and t_cond_sum    = '" + this.Cond_sum.Value +   "'" +
          "                                         and t_sum         =  " + this.Sum.Value +        
          "                                         and t_receiverinn = '" + this.RecINN.Value +     "'" +        
          "                                         and t_ground      = '" + this.Ground.Value +     "'" ;
    rs = RsdRecordSet(str);
    if(rs and rs.MoveNext)
      return false;
    end;
    return true;
  end;

  macro SaveData()
    debugbreak;
    str = " INSERT INTO USR_COMPILAENS_RULE_DBT (t_cond_level,  " + 
          "                                      t_level,       " +
          "                                      t_cond_sum,    " +
          "                                      t_sum,         " +
          "                                      t_receiverinn, " +
          "                                      t_ground)      " +
	   "	                         VALUES   (?, ?, ?, ?, ?, ?)";
    rs = RsdCommand(str);

    if(Cond_level.Value == "")
      SetFocus("Cond_level");
      prompt(14500, "Cond_level");
      return false;
    else
      rs.AddParam("t_cond_level", RSDBP_IN, Cond_level.Value);
    end;

    if(Level.Value == "")
      SetFocus("Level");
      prompt(14502, "Level");
      return false;
    else
      rs.AddParam("t_level", RSDBP_IN, Level.Value);
    end;

    if(Cond_sum.Value == "")
      SetFocus("Cond_sum");
      prompt(14501, "Cond_sum");
      return false;
    else
      rs.AddParam("t_cond_sum", RSDBP_IN, Cond_sum.Value);
    end;

    if(Sum.Value == $0.0)
      SetFocus("Sum");
      prompt(14503, "Sum");
      return false;
    else
      rs.AddParam("t_sum", RSDBP_IN, Sum.Value);
    end;

    if(RecINN.Value == "")
      rs.AddParam("t_receiverinn", RSDBP_IN, "*");
    else
      rs.AddParam("t_receiverinn", RSDBP_IN, RecINN.Value);
    end;

    if(Ground.Value == "")
      rs.AddParam("t_ground", RSDBP_IN, "*");
    else
      rs.AddParam("t_ground", RSDBP_IN, Ground.Value);
    end;

    rs.Execute();

    return true;

    OnError(x);
      if((index(rs.connection.environment.error(0).descr, "ORA-00001") != 0) or (index(rs.connection.environment.error(0).descr, "Ivalid") != 0)) 
        msgbox("Данное условие уже введено!| Измените условия!");
        return false;
      end;
  end;

  macro UpdateData
    var rs, str;
    if(Edit_Mode)
      str = " UPDATE USR_COMPILAENS_RULE_DBT SET t_cond_level  = '" + this.Cond_level.Value + "'," +
            "                                    t_level       =  " + this.Level.Value +      ","  +
            "                                    t_cond_sum    = '" + this.Cond_sum.Value +   "'," +
            "                                    t_sum         =  " + this.Sum.Value +        ","  +
            "                                    t_receiverinn = '" + this.RecINN.Value +     "',"  +
            "                                    t_ground      = '" + this.Ground.Value +     "'"  +
            "                              WHERE t_id          = " + Edit_ID;

      rs = RsdCommand(str);
      rs.Execute();
    end;
  end;

  macro PressKey_MainPanel(RsbEvent:object)
    if ((RsbEvent.KeyCode == KEY_F9) and (Edit_mode))
      if(not CheckData)
        msgbox("Правило с такими условиями уже есть!!! | Измените параметры правила!");
      else
        UpdateData();//обновляем данные
        this.hide();
      end;
    elif (RsbEvent.KeyCode == KEY_F9)
      if(not CheckData)
        msgbox("Правило с такими условиями уже есть!!! | Измените параметры правила!");
      else

        if (SaveData)
          this.hide();
        else
        end;

      end;
    end;
  end;

end;

macro exitScroll( objkakoyto, rs, id, key )
  scrollIsRunning = false;
  return CM_CANCEL;
end;

macro DeleteData (objkakoyto, rs, id, key)
  var str, cmd;
  str = "delete USR_COMPILAENS_RULE_DBT where t_id = " + rs.value("t_id");
  cmd = RsdCommand(str);
  cmd.Execute;
  return CM_CANCEL;
end;

macro EditData(objkakoyto, rs, id, key)
  Debugbreak;

  Edit_ID = rs.value("t_id");
  Edit_Mode = true;

  var panel:TRsbPanel = MainPanel;
  panel.run;

  Edit_Mode = false;

  return CM_CANCEL;
end;

macro InsertData
  var panel:TRsbPanel = MainPanel;
  panel.run;
  return CM_CANCEL;
end;

private macro Main()
   var scroll = ZubScroll ();

   scroll.Columns.Add ("t_cond_level",  "Усл. уровня риска",    3, ZUB_SCR_COL_NONEDITABLE);
   scroll.Columns.Add ("t_level",       "Уровень риска",       20, ZUB_SCR_COL_NONEDITABLE);
   scroll.Columns.Add ("t_cond_sum",    "Усл. суммы",           3, ZUB_SCR_COL_NONEDITABLE);
   scroll.Columns.Add ("t_sum",         "Сумма",               15, ZUB_SCR_COL_NONEDITABLE);
   scroll.Columns.Add ("t_receiverinn", "ИНН получателя",      15, ZUB_SCR_COL_NONEDITABLE);
   scroll.Columns.Add ("t_ground",      "Основание документа", 80, ZUB_SCR_COL_NONEDITABLE);

   scroll.ScrollReadOnly = true;
   scroll.SqlText = " SELECT t_cond_level, t_level, t_cond_sum,   " +
                    "        t_sum, t_receiverinn, t_ground, t_id " +
                    "   FROM USR_COMPILAENS_RULE_DBT              " +
                    "  ORDER BY t_id                              ";

   scroll.ScrollHead     = "Список условий комплаенс контроля";
   scroll.scrollPrompt   = "~ESC~ Выход, ~F8~ Удалить, ~F9~ Ввод новой записи, ~Enter~ Просмотр/Редактирование";
   scroll.SetMacroOnKeys(KEY_F8,    "DeleteData");
   scroll.SetMacroOnKeys(KEY_F9,    "InsertData");
   scroll.SetMacroOnKeys(KEY_ESC,   "exitScroll");
   scroll.SetMacroOnKeys(KEY_ENTER, "EditData");

   while(scrollIsRunning)
     scroll.was_getrecordset = false;
     scroll.Scroll ();
   end;
end;

main();
exit(1);