/*----------------------------------------------------------------------
Макрос с функциями проверок для изменения категория вида хранения
05-02-2012 Жаворонкова Н. (joy) C-17206

-----------------------------------------------------------------------*/
import  CashInter, Globals, Payminter, CTInter, lib_access, lib_const, oralib;

/*Функция проверки выгрузки документа*/
Macro CheckEAUnloading (PaymentID, closedate)
    var sql  = "select  1 from   user_ealog  \n"
               " where  (t_abs_id = concat ('PAYMENT_', :paymentID) or t_basedoc = concat ('PAYMENT_', :basePaymentID)) \n"
               "   and t_bankdate = :bankdate"; 
    sql = execSQLSelect (sql, makeArray (SQLParam ("paymentID",  PaymentID), 
                                         SQLParam ("basePaymentID",  PaymentID),
                                         SQLParam ("bankdate",   closedate)));
    return sql.moveNext ();
end;

/*Функция проставляет тот или иной вид хранения на платеже*/
Macro SetEAStoreKind (Payment)
    // Возможные значения категории Вид хранения в ЭА
    private const PM_USR_CAT_EA_STOREKIND_PAPER = 1; // бумажный
    private const PM_USR_CAT_EA_STOREKIND_DIGIT = 2; // электронный
    private const CLEARATTR = 0; 

    var StoreKind; 
    var stat;
    
    var mn = makeArray ( "Бумажный", "Электронный", "Удалить значение категории" );
    var m = menu(mn, null, "Выберите вид хранения", null, null, 0);
   
    if (m == -2) // Обработка ESC 
        return; 
    elif ( mn(m) == "Удалить значение категории")
        StoreKind = CLEARATTR;
    elif   ( mn(m) == "Бумажный")
        StoreKind = PM_USR_CAT_EA_STOREKIND_PAPER;
    elif ( mn(m) == "Электронный")
        StoreKind = PM_USR_CAT_EA_STOREKIND_DIGIT;
    end;
    
    var ctg = RsbObjCategories(OBJTYPE_PAYMENT, UniID (payment, OBJTYPE_PAYMENT));
    stat = ctg.DisconnectAttr(PM_USR_CAT_EA_STORAGE, 0);
    if (StoreKind != CLEARATTR)
        stat = ctg.ConnectAttr(PM_USR_CAT_EA_STORAGE, StoreKind, null, null, {curdate});
    end;

    stat = ctg.Save(UniID (payment, OBJTYPE_PAYMENT)); 
    return stat;
END;


/*Функция проверяет приндлженость пользователя к той или иной группе для ЭА и в зависимости от этого позволяет\не позволяет менять вид хранения*/
Macro CheckEAGroup (payment)
    /* Группы для ЭА сейчас во всех банках следующие:*/
    private const ACS_GRP_EA_UPLOAD  = 102;
    private const ACS_GRP_CONTROLLER = 200;

    if ( ACS_CheckGroupOper (ACS_GRP_EA_UPLOAD, {OPER}, false))
        if (CheckEAUnloading (Payment.paymentId, Payment.closeDate))
            if (GetTrue(true, ("Платеж уже выгружен в ЭА. Изменить вид хранения?")))
                SetEAStoreKind (Payment); 
            end;
        else
            SetEAStoreKind (Payment); 
        end;
        
    elif ( ACS_CheckGroupOper (ACS_GRP_CONTROLLER, {OPER}, false))
        if (CheckEAUnloading (Payment.paymentId, Payment.closeDate))
            msgbox ("Платеж уже выгружен в ЭА. Изменения вида хранения запрещены! | Обратитесь к пользователям, входящим в группу 102.");
        else
            SetEAStoreKind (Payment); 
        end;
        
    else // никуда не входит
        if ( Payment.closeDate == {curdate})
            if (CheckEAUnloading (Payment.paymentId, Payment.closeDate)) 
                msgbox ("Платеж уже выгружен в ЭА. Изменения вида хранения запрещены! | Обратитесь к пользователям, входящим в группу 102");
            else 
                SetEAStoreKind (Payment); 
            end;
        else
            msgbox ("Дата закрытия документа меньше даты операционного дня пользователя! | Обратитесь к контролёрам (группа 200)!");
        end;
    end;
END;