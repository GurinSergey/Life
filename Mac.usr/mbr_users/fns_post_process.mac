// -------------------------------------------------------------------------------------------------
// @filename : fns_post_process.mac
// @author   : Lavrenov A.A. 
// @desc     : Постобработка загрузки сообщений ФНС
// @request  : C-12273-6 
// @revision : 1.0 27.06.2012
// Lavrenov: 02.07.2012 C-12402-6 Логгирование
// -------------------------------------------------------------------------------------------------
import rsexts, bankinter, globals, rsbdataset;

//Создаем папку для архива
macro get_archive_path(path, filedate)
  var strdate, dd,mm,yyyy;

  datesplit(filedate,dd,mm,yyyy);

  if(dd<=9)
     dd = "0"+dd;
  end;

  if(mm<=9)
     mm = "0"+mm;
  end;

  strdate = string(yyyy) + string(mm) + string(dd);
  //Создаем папку, если такая уже есть, то вернем ее имя
  MakeDir (path+"\\"+strdate);
 
  return path+"\\"+strdate+"\\";
end;

//Lavrenov: 02.07.2012 C-12402-6 Получаем либо создаем логфайл 
macro get_logfile_name(numsess, oper, path)
  var file_name, i = 1;
  var rs, str, cmd;
  
  str = " select t_filename from Dwllogfile_usr "+
        " where t_num = "+numsess+ 
        " and t_directory = '"+path+"'";
   
   rs = trsbdataset(str);
   //Если уже есть, то возвращаем имя файла
   if(rs and rs.movenext)
      return rs.t_filename; 
   end;
  
  //Если нет формируем новое
  file_name = path+"Batch_"+OPER+"_"+i+".txt"; 
   while( existfile( file_name ))
     i = i + 1;
     file_name = path+"Batch_"+OPER+"_"+i+".txt"; 
   end;
   
   //Сразу в файл суем шапку   
   setoutput(file_name,true);
   [       Сообщения, загруженные в RS-Bank
     Операционистом: #####  за дату: ########## ##########
     
    =====                                             ]
   ({oper}, date, time);
   setoutput(null,true);
   
   //В темповскую таблицу суем данные по файлу
   cmd = rsdcommand("insert into Dwllogfile_usr values ("+numsess + ", '"+path +"', '" +file_name+ "');");
   cmd.execute;
   
   return file_name;
end;


macro post_import(Sess, NumberRace)
  var ARCHIVE_PATH="", errcode, copy_to;
  var v_filename, v_filedir, v_fileext;
  var v_logfile_name = "", cnum;
  var i=0, rs;
  debugbreak;
  //Читаем настройку
  GetRegistryValue("PRBB\\FNS\\ARCHIVE_PATH", V_STRING, ARCHIVE_PATH, errCode);
  if ( errCode > 0 )
     ARCHIVE_PATH = "";
  else
     // Lavrenov: 02.07.2012 C-12402-6 к настройке добавляем Import
     ARCHIVE_PATH = ARCHIVE_PATH + "\\IMPORT";
     //Lavrenov: 10.07.2012 Если директории нет - создаем. 
     if (not existfile(ARCHIVE_PATH))
        MakeDir (ARCHIVE_PATH);
     end;
  end;
  
  //Если прочитать настройку не удалось или в ней пусто то ни чего не делаем.
  if(ARCHIVE_PATH != "")
     //Получаем имя файла 
     v_filedir = splitfile(sess.rec.filename, v_filename, v_fileext); 
     v_filename = v_filename + v_fileext;
     
     //Формируем новое имя файла
     ARCHIVE_PATH = get_archive_path(ARCHIVE_PATH, sess.rec.filedate);
     
    // Определяем номер сессии
    rs = trsbdataset("select RSBSESSIONDATA.CNUM  from dual ");
    if (rs and rs.movenext)
       cnum = int(rs.cnum);
    else
       cnum = {oper};
    end;

     //Lavrenov: 02.07.2012 C-12402-6 Получаем имя файла
     v_logfile_name =  get_logfile_name(cnum, {oper}, ARCHIVE_PATH);  
     
     copy_to = ARCHIVE_PATH + v_filename;
     
     //Если файл уже есть добавляем префикс к нему
     while( existfile( copy_to ))
       i = i + 1;
       copy_to =  ARCHIVE_PATH + v_filename + "#" + String( i );
     end;

     //Если скопировать не удалось возвращаем ошибку
     if (not CopyFile(sess.rec.filename, copy_to))//, true, v_filename))
        println("!!!ОШИБКА: Ошибка копирования файла " + sess.rec.filename + " в " + copy_to);
        return 1;
     else
        //Lavrenov: 02.07.2012 C-12402-6 пишем в лог
        setoutput(v_logfile_name,true);
        println(sess.rec.filename);
        setoutput(null,true);
        //удаляем нафиг
        RemoveFile(sess.rec.filename);
     end;
  end;

  return 0;
end;