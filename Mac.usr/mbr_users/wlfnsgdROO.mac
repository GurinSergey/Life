/***********************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6.0                            */
/***********************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                             */
/*  Генерация учетного объекта ROO из сообщения налогового органа                  */
/*                                                                                 */
/*  Имя файла:  wlfnsgdROO.mac                                                     */
/*  Создан   :  01.12.2011                                          Чесноков Д.С.  */
/*  Изменен  :  20.07.2012 Chesnokov D.S. добавил заполнение параметров            */
/*              BankDate, SysDate, SysTime, UserId в функцию ВставитьСвязанныйСчет */
/***********************************************************************************/
Import MesInter, WldInter, globals, RSD, likepy;
import "mnsROOusr.mac", "fns_const.mac", "diver.mac"; 

record wlreqOtv(wlreq);

macro GenDoc (AddrMes)

  if (not ВходитВГруппу({oper},197))
    msgBox("Вы не включены в группу \"197 - Обработка приостановлений по счетам\"");
    return false;
  end;

  debugbreak;
  SetBuff( wlmes, addrMes );
  var error = 0, ReqID = -1, i = 0;
  var Message:MnsMessageFormROO;
  var stat = 0, ClientId, rs;
  var cmd, queries;

  printlog(2, "Генерация формы ROO");
  ClearRecord(wlreq);
  
  if (Message.ВидРеш == 1)
    queries = "Решение об отмене приостановления операций по счетам налогоплательщика или налогового агента в банке";
  elif (Message.ВидРеш == 2)
    queries = "Решение об отмене приостановления операций по счетам налогоплательщика или налогового агента в банке в части превышения суммы, указанной в решении";
  else
    queries = " ";
  end;

  /* Заполнение учетных буферов */
  wlreq.Kind                     = MESKIND_REQUEST;
  wlreq.SubKind                  = 0;
  wlreq.FIID                     = -1;
  wlreq.Corschem                 = -1;
  wlreq.PmDateValue              = date();
  wlreq.Trn                      = Message.НомРешОт;
  wlreq.RelatedRef               = wlmes.Trn;
  wlreq.Queries                  = queries;
  wlreq.OriginatorID             = ПолучитьКодСубъекта( Message.КодНО, PTCK_MNS, error );
  wlreq.OriginatorCodeKind       = PTCK_MNS;
  wlreq.OriginatorCode           = Message.КодНО;
  wlreq.OriginatorName           = Message.НаимНО;
  wlreq.RecipientID              = {OurBank};
  wlreq.RecipientCodeKind        = PTCK_BIC;
  wlreq.RecipientCode            = {MFO_Bank};
  wlreq.RecipientName            = {Name_Bank};

  if( not ВставитьЗапрос( wlreq, string("Отменяет решение №"+Message.НомРешПр+" от "+Message.ДатаРешПр), Message.ВидРеш, NULL, ReqID ) )
    std.msg("Ошибка при вводе запроса");
    return FALSE;
  end;

  /* Заполнение связанных счетов */
  if( ReqID > 0 )
    while( i < Message.НомСч.Size )
      ClearRecord( wlacclnk );
      wlacclnk.ObjectID      = ReqID;
      wlacclnk.ObjectType    = OBJTYPE_REQ;
      wlacclnk.Account       = Message.НомСч[i];
      wlacclnk.FIID          = Get_FIID( Message.НомСч[i] );
      wlacclnk.Chapter       = 1;
      wlacclnk.BankDate      = {curdate};
      wlacclnk.SysDate       = Date();
      wlacclnk.SysTime       = DtTm(Date, Time);
      wlacclnk.UserId        = {oper};
      if( not ВставитьСвязанныйСчет( wlacclnk ) )
        std.msg("Ошибка при вставке связанного счета");
      end;
      i = i + 1;
    end;
  end;
  
  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    return FALSE;

end;