//------------------------------------------------------------------------------
//                                                                _,,--,,_     
// @filename: fssp_loader.mac                                   /`       .`\  
// @author  : Golovkin                                         /  '  _.-'   \ 
// @desc    : Загрузчик сообщений ФССП                         |  `'_{}_    | 
// @changes : Golovkin 21.08.2014 перенос функций и констант   |  /`    `\  | 
//                                в fssp_lib                    \/ ==  == \/  
//                                                              /| (.)(.) |\  
//                                                              \|  __)_  |/  
//                                                               |\/____\/|   
//                                                               | ` ~~ ` |   
//                                                               \        /   
//                                                                `.____.`    
//------------------------------------------------------------------------------

import rsd, RsExts;
import "globals.mac", "lib_registry.mac","fssp_lib.mac";

macro print_report()

    private var rs, cur_mes_id = -1;
    rs = RsdRecordset( " SELECT   ERR.T_MES_ID, ERR.T_ERR_TEXT, "
                       "          (SELECT EXTRACTVALUE(XMLTYPE(MES.T_MES_DATA),'/*/*/IP/IPNum/text()' ) FROM USR_FSSP_MES MES WHERE MES.T_ID = ERR.T_MES_ID) IPNum, "
                       "          (SELECT EXTRACTVALUE(XMLTYPE(MES.T_MES_DATA),'/*/*/IP/IPDate/text()') FROM USR_FSSP_MES MES WHERE MES.T_ID = ERR.T_MES_ID) IPDate "
                       "   FROM   USR_FSSP_ERR_TMP ERR ORDER BY ERR.T_MES_ID " );
    while( rs.movenext )
        if( cur_mes_id != rs.value(0) )
            [  ];
            if( int( rs.value(0) ) == 0 )
                [Необработанные сообщения:];
            else
                [Сообщение № ############### от ##########  mes_id = #]( rs.value(2):l, rs.value(3), string( int( rs.value(0) ) ) );
            end;
            cur_mes_id = rs.value(0);
        end;
        [Ошибка! #]( rs.value(1) );

    end;

end;

macro load_one_file( fname )

    var ha = TArray, hd, strm, doc_count, sess_id, mes_id, i;
    var stat, mes_type, error_mes;

    rsldefcon.BeginTrans;

    strm = TStream( fname, "R" );
    AddField( ha, "xml", V_STRING,  strm.getsize, 0 );
    hd = TRecHandler( "xmlread", ha );
    strm.read2( hd );    
    
    sess_id = int( gen_sess_id );
    load_xml( hd, sess_id, fName );
    doc_count = get_docs_count_in_session( sess_id );

    if( doc_count == 0 ) runError( "Нет сообщений" ); end;

    i = 1;

    InitProgress( doc_count, "Ждите...", "Обработка входящего сообщения" );

    while( i <= doc_count )
        stat = get_document( sess_id, i );

        if( stat != 2 ) 
            mes_id   = get_mes_id_by_session_part( sess_id, i );
            mes_type = set_mes_type( mes_id );
        end;

        UseProgress(i); 

        i = i + 1;    
        //testevent(100); zmp 15.08.2014 R-431374-2
    end;

    RemProgress;

    rsldefcon.CommitTrans;
onerror( x )
    println( fName + "  :: " + x.message );
    rsldefcon.RollbackTrans;
end;

macro importFSSPFiles ( fsspPath, fsspPathOld, fsspMask )

    var dir, fullPath = "", fullPathOld = "", i;
        
    dir = TDirList ( string ( fsspPath, "/", fsspMask ), "f" );
    dir.sort (0);        

    clear_tmp( "USR_FSSP_ERR_TMP" );

    if( dir.count != 0 )

        InitProgress ( dir.count, "Ждите...", "Загрузка файлов" );

        for ( i, 0, dir.count - 1 )
            fullPath    = string ( fsspPath   , "/", dir.name (i) );
            fullPathOld = string ( fsspPathOld, "/", dir.name (i) );
            load_one_file        ( fullPath              );
            move_file            ( fullPath, fullPathOld );
            UseProgress (i + 1); 
        end;

        RemProgress;
    else
        [Отсутствуют файлы в каталоге или не достаточно прав доступа к папке #]( fsspPath );
    end;

    print_report ();

end;

importFSSPFiles( fssp_dir_import, fssp_dir_oldmsg, "*.xml" );
