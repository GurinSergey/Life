/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6.0                     */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Отчет по квитовке сообщений 365-П                                       */
/*                                                                          */
/*  Имя файла:  365p_log.mac                                                */
/*  Создан   :  29.02.2012                                   Лавренов А.А.  */
/*  Изменен  :  09.04.2012                                   Чесноков Д.С.  */
/*  Заявка   :  С-10077                                                     */
/****************************************************************************/

Import ldmklbr, globals, BankInter, rsbdataset, ptinter;

Class (TRecHandler) TParm(LbrName)

    InitTRecHandler ("date1", (LbrName), true);
    this.("StartDate") = {curdate};
    this.("EndDate") = {curdate};
    this.("Oper") = {oper};
    this.("OperName") = {name_oper};
    this.("ReceiverName") = "По всем";
    this.("Sort") = " дате исходящих сообщений";

   // Проверка на числовой номер
   MACRO IsDigit( Number )
    var stat = true, i = 1, ch, DigitString = "0123456789";
    while( stat and (i <= strlen(Number)) )
       ch = SubStr( Number, i, 1 );
       if( not Index( DigitString, ch ))
          stat = false;
       end;
       i = i + 1;
    end;
    return stat;
   END;


   MACRO GetPartyNameByCode(code, codekind)
   var rs, str;
   str = "SELECT T.T_SHORTNAME FROM dparty_dbt t, dobjcode_dbt oc " +
      " WHERE  t.t_partyid = oc.t_objectid " +
      "    AND oc.t_objecttype = 3 " +
      "    AND oc.t_state = 0 " +
      "    AND oc.t_codekind = "+codekind +
      "    AND oc.t_code = '"+code+"' ";
   
   rs = trsbdataset(str);
   if(rs and rs.movenext)
         return rs.t_shortname;
   else
         return "По всем";
   end;
   END;

   MACRO GetOperNameByCode(code)
   var rs, str;
//   debugbreak;
   if(not isdigit(code))
      return "По всем";
   end;

   if(strlen(code)==0 )
      return "По всем";
   else
      str = "SELECT T.T_NAME FROM dperson_dbt t " +
      " WHERE  t.t_USERCLOSED = chr(0) and t.t_oper = "+code;
	   
      rs = trsbdataset(str);
      if(rs and rs.movenext)
         return rs.t_name;
      else
         return "По всем";
      end;
   end;
   END;

   
    Macro Handler ( dlg, cmd, id, key )
       file party (party);
       file person (person);
       var code="", selected, v_oper;

      if ((cmd == DLG_REMFOCUS)and (id == FldIndex ("StartDate")))
       if (dlg.("StartDate")> dlg.("EndDate"))
        MemoryError(5827/*, "сообщение об ошибке"*/);
        DisplayError();
        return CM_CANCEL;
       end;
      elif ((cmd == DLG_REMFOCUS)and (id == FldIndex ("EndDate")))
       if (dlg.("StartDate")> dlg.("EndDate"))
        MemoryError(5827/*, "сообщение об ошибке"*/);
        DisplayError();
        return CM_CANCEL;
       end;
      elif ((cmd == DLG_REMFOCUS)and (id == FldIndex ("Receiver")))
        dlg.("ReceiverName") = GetPartyNameByCode(dlg.("Receiver"),28);
        UpdateFields(dlg); 
        return cm_default;
      elif ((cmd == DLG_REMFOCUS)and (id == FldIndex ("Oper")))
        dlg.("OperName") = GetOperNameByCode(dlg.("Oper"));
        UpdateFields(dlg); 
        return cm_default;
      elif ((cmd == DLG_KEY) and ((key == 316) or (key == 323)))
        return CM_SAVE;
      elif ((cmd == DLG_KEY) and (key == 317) and (id == FldIndex ("Receiver")) )
         selected = listpt(party,1,code,PTlist_TAXINSTITUTE, party.partyid, ptck_MNS );
         if(selected)
            dlg.("Receiver") = code;
            dlg.("ReceiverName") = party.shortname;
            UpdateFields(dlg); 
         end;
      elif ((cmd == DLG_KEY) and (key == 317) and (id == FldIndex ("Oper")) )
         selected = listoper(person,true,dlg.("Oper") );
         if(selected)
            dlg.("Oper") = person.oper;
            dlg.("OperName") = person.name;
            UpdateFields(dlg); 
         end;
      elif ((cmd == DLG_KEY) and (key == 32) and (id == FldIndex ("Sort")) )
         if(dlg.("Sort")== " дате входящих сообщений")
            dlg.("Sort")= " дате исходящих сообщений";  
         else
            dlg.("Sort")= " дате входящих сообщений";  
         end;
        return cm_default;
      end;
     // msgbox(key);
    End;

    Macro Run
        return RunDialog (this, R2M (this, "Handler"));
    End;
End;


Macro MkPanel( lbr )

Var rc = lbr.AddPanel( "Date1", 40, 12, 47, 14, lbr.EColour.SHLP, lbr.EFRame.UNARY ),
   elem;

   rc.StatusLine = "ESC Выход F2/F9 Сохранить, Пробел Переключить, F3 Список";
   rc.Title      = "Формирование отчета";

   elem = rc.AddLabel( 3, 2, "Начало периода:");
   elem = rc.AddField( 34, 2, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_DATE, "StartDate" );	

   elem = rc.AddLabel( 3, 4, "Конец периода:");
   elem = rc.AddField( 34, 4, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_DATE, "EndDate" );	

   elem = rc.AddLabel( 3, 6, "Получатель:");
   elem = rc.AddField( 33, 6, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "Receiver", 11, 1 , 33);	
   elem = rc.AddLabel( 3, 7, "Наименование получателя:");
   elem = rc.AddField( 3, 8, lbr.EFieldType.listbox, lbr.EFocus.no, lbr.EDataType.V_String, "ReceiverName", 41, 2 );	

   elem = rc.AddLabel( 3,  11, "Операционист:");
   elem = rc.AddField( 13, 11, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "Oper", 5, 1 , 6);	
   elem = rc.AddField( 19, 11, lbr.EFieldType.listbox, lbr.EFocus.no, lbr.EDataType.V_String, "OperName", 25, 1 );	

   elem = rc.AddLabel( 3, 13, "Формировать отчет по:");
   elem = rc.AddField( 19,13, lbr.EFieldType.noEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "Sort", 25, 1 );	
   
end;

Macro MkLbrFile

  Var  LbrName, rs, cnum, TxtPath, errCode;
  var   lbr;
  rs = trsbdataset("select RSBSESSIONDATA.CNUM  from dual ");
  if (rs and rs.movenext)
     cnum = int(rs.cnum);
  else
     cnum = {oper};
  end;

  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, TxtPath, errCode);
  if ( errCode > 0 )
     msgbox("Ошибка при определении директории TxtFile");
     return 1;
  end;
   lbrname = txtpath+"\\rep_"+{oper}+"_"+cnum+".lbr";

   lbr = TLbrFile( LbrName, "C" );

   MkPanel( lbr );
     
   lbr.Save();	//сохраняем ресурсы в файле
   lbr = NULL;

   return LbrName;
end;

macro RunReport()
  /*Точка входа*/
  Var LbrName = MkLbrFile();
  Var Parm = TParm(LbrName);
  var rs, str, old_name = "", i = 1;
  var KWT_MES, KWT_DATE, KWT_CHECK, KWT_OPER, FIRST = TRUE;;

  if ( not Parm.Run () )
    Exit (1);
  end;

  str = "SELECT t.*, " +
        "     (SELECT t_code FROM dobjcode_dbt oc " +
        "       WHERE     oc.t_objecttype = 3 AND oc.t_codekind = 28 AND t_objectid = t.t_recipientid) t_code, " +
        "        (SELECT t_shortname FROM dparty_dbt p " +
        "          WHERE        p.t_partyid = t.t_recipientid) t_name " +
        "   FROM dmes365_log_dbt t " +
        "  WHERE 1=1 ";
  if((Parm.rec.receiver != "") and (Parm.rec.receivername != "По всем")) 
      str = str + "      AND t_recipientid IN " +
            "               (SELECT t_objectid FROM dobjcode_dbt oc " +
            "                 WHERE oc.t_objecttype = 3 AND oc.t_codekind = 28 AND t_code = '"+Parm.rec.receiver+"') " ;
  end;

  if((Parm.rec.Oper != "") and (Parm.rec.Opername != "По всем")) 
      str = str + "      AND t_oper = " +Parm.rec.oper;
  end;

  if(Parm.rec.Sort == " дате входящих сообщений")    
      str = str + "  and t.t_date_in between to_date('"+Parm.rec.StartDate+"','dd.mm.yyyy') and to_date('"+Parm.rec.enddate+"','dd.mm.yyyy') ";
  else
      str = str + "  and t.t_date_out between to_date('"+Parm.rec.StartDate+"','dd.mm.yyyy') and to_date('"+Parm.rec.enddate+"','dd.mm.yyyy') ";
  end;  
      str = str + " ORDER BY t_date_in, T_INSIDEMES, T_OUTSIDEMES "; 

  rs = trsbdataset(str);
  [ Журнал обработки сообщений ФНС 
    за период с ########## по ########## 
   ┌─────┬────────────────────────────────────┬─────────────────────────────────────────┬──────────┬──────┬───────────────────────────────────────────────┬──────────┬───┬────────┐
   │  №  │   Наименование полученного файла   │     Наименование отправленного файла    │   Дата   │ Опер │          Наименование файла квитовки          │   Дата   │Ошб│  Опер  │
   │ п/п │                                    │                                         │ отправки │ отпр.│                                               │ квитовки │   │квитовки│
   ├─────┼────────────────────────────────────┼─────────────────────────────────────────┼──────────┼──────┼───────────────────────────────────────────────┼──────────┼───┼────────┤]
   (Parm.rec.StartDate, Parm.rec.enddate);

  while(rs and rs.movenext)
   
   if (ValType(rs.KWT_MES) == 0)
     KWT_MES = "";
   else
     KWT_MES = rs.KWT_MES;
   end;
   
   if (ValType(rs.KWT_DATE) == 0 )
     KWT_DATE = "";
   else
   DtTmSplit(rs.KWT_DATE, KWT_DATE, Null);
   end;
     
   if ((rs.T_CHECK == StrFor(1)) or (rs.T_CHECK == StrFor(0)))
     KWT_CHECK = "";
   else
     KWT_CHECK = "X";
   end;
   
   if (ValType(rs.T_KWT_OPER) == 0)
     KWT_OPER = "";
   else
     KWT_OPER = rs.T_KWT_OPER; 
   end;
   
   if (old_name == "")
     [│#####│####################################│#########################################│##########│######│###############################################│##########│###│########│]
     (i:r, rs.T_INSIDEMES:c, rs.T_OUTSIDEMES:c, date(rs.T_DATE_OUT), rs.T_OPER, KWT_MES, KWT_DATE, KWT_CHECK:c, KWT_OPER:r);
     old_name = rs.T_INSIDEMES;
     i = i + 1;
   else
     if (old_name == rs.T_OUTSIDEMES)
       [│#####│####################################│#########################################│##########│######│###############################################│##########│###│########│]
       ("", "", rs.T_OUTSIDEMES:c, date(rs.T_DATE_OUT), rs.T_OPER, KWT_MES, KWT_DATE, KWT_CHECK:c, KWT_OPER:r);
       old_name = rs.T_OUTSIDEMES;
     else
       [├─────┼────────────────────────────────────┼─────────────────────────────────────────┼──────────┼──────┼───────────────────────────────────────────────┼──────────┼───┼────────┤];
       [│#####│####################################│#########################################│##########│######│###############################################│##########│###│########│]
       (i:r, rs.T_INSIDEMES:c, rs.T_OUTSIDEMES:c, date(rs.T_DATE_OUT), rs.T_OPER, KWT_MES, KWT_DATE, KWT_CHECK:c, KWT_OPER:r);
       old_name = rs.T_OUTSIDEMES;
       i = i + 1;
     end;
   end;
  
  end;
  [└─────┴────────────────────────────────────┴─────────────────────────────────────────┴──────────┴──────┴───────────────────────────────────────────────┴──────────┴───┴────────┘];
  [ Итого: ###### ] (i-1:c);
  

  
  str = " SELECT t.* " +
        "   FROM dmes365_log_dbt t " +
        "  WHERE t.t_reqid IS NULL " +
        "    AND t.t_kwt_date BETWEEN to_date('"+Parm.rec.StartDate+"','dd.mm.yyyy') " +
        "    AND to_date('"+Parm.rec.enddate+"','dd.mm.yyyy')" +
        "  ORDER BY t.t_kwt_date, t_kwt_mes ";
  rs = trsbdataset(str);
  
  [  
    
   Журнал сообщений о квитовке
   для которых не найдены исходящие сообщения 
    
   ┌─────┬───────────────────────────────────────────────────┬──────────┬───────┬────────┐
   │  №  │            Наименование файла квитовки            │   Дата   │ Ошибка│  Опер  │
   │ п/п │                                                   │ квитовки │       │квитовки│
   ├─────┼───────────────────────────────────────────────────┼──────────┼───────┼────────┤
  ];
  
  i = 1;
  while (rs and rs.movenext)
    DtTmSplit(rs.KWT_DATE, KWT_DATE, Null);
    if ((rs.T_CHECK == StrFor(1)) or (rs.T_CHECK == StrFor(0)))
      KWT_CHECK = "";
    else
      KWT_CHECK = "X";
    end;
   
    if (ValType(rs.T_KWT_OPER) == 0)
      KWT_OPER = "";
    else
      KWT_OPER = rs.T_KWT_OPER; 
    end;
    
    if (FIRST)
      [│#####│###################################################│##########│   #   │########│]
      (i:r, rs.T_KWT_MES:c, KWT_DATE, KWT_CHECK, KWT_OPER:r);
      FIRST = False;
    else
      
      [├─────┼───────────────────────────────────────────────────┼──────────┼───────┼────────┤
       │#####│###################################################│##########│   #   │########│]
      (i:r, rs.T_KWT_MES:c, KWT_DATE, KWT_CHECK, KWT_OPER:r);
    end;
    i = i + 1;
  end;
  
  [└─────┴───────────────────────────────────────────────────┴──────────┴───────┴────────┘];
  
end;


RunReport();