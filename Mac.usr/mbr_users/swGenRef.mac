/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Макрос формирования референсов исходящих сообщений SWIFT                */
/*                                                                          */
/*  Имя файла: swGenRef.mac                                                 */
/*  Создан:  25.03.09                                        Гуцу Е.В.      */
/****************************************************************************/
import cb_sql, globals, payminter;

record pmpaymRec(pmpaym);

macro GenReference( SeqValue, ObjKind, ObjAddr )
    var refer, query, rs, 
        FormName = "",
        DepCode  = "",
        dd = "",
        mm = "", 
        yy = "";
    
    SetBuff(pmpaymRec,  ObjAddr);
    var PmOBJ = RSBPayment(pmpaymRec.paymentID);

    DateSplit (PmOBJ.ClientDate, dd, mm, yy);

    dd = StrSubst( string(dd:2), " ", "0" );
    mm = StrSubst( string(mm:2), " ", "0" );
    yy = StrSubst( string(subStr(string(yy), 3, 2):2), " ", "0" );

    refer = yy + mm + dd;


    /* Определим форму сообщения */

    query = " Select frm.t_Name " +
            "   From DPMPROP_DBT prop, DWLMESRLS_DBT rls, DWLMESFRM_DBT frm " +
            "  Where prop.t_PaymentID   = " + pmpaymRec.PaymentID +
            "    and prop.t_DebetCredit = 1 " +
            "    and rls.t_RlsFormID    = prop.t_RlsFormID " +
            "    and frm.t_FormID       = rls.t_FormID ";
    rs = RsdRecordSet(query);
    if (rs and rs.MoveNext())
       FormName = rs.Value(0, null, V_STRING);
    end; 

    refer = refer + FormName;


    /* Определим подразделение ТС счёта-плательщика */
    query = " Select dep.t_Name " +
            "   From DDP_DEP_DBT dep " +
            "  Where dep.t_Code = " + pmpaymRec.StartDepartment;
    rs = RsdRecordSet(query);
    if (rs and rs.MoveNext())
       DepCode = rs.Value(0, null, V_STRING);
    end;

    refer = refer + DepCode + string(SeqValue:3);
    refer = StrSubst( refer, " ", "0" );

    return refer;
end;
