/*******************************************************************************/
/*  RS-Bank 6.0                                          R-Style Software Lab  */
/*                                                                             */
/*  File name   : fns_lib.mac                                                  */
/*  Created     : 15.01.2012                                                   */
/*  Programmer  : Lavrenov A.                                                  */
/*  Description : Макрос вспомогательных процедур для отчетов и протоколов     */
/*                по 365-П                                                     */
/*  Modify      : 09.04.2012 Chesnokov D.S. по заявкам С-10076, 10077          */
/*  Modify      : 04.07.2012 Chesnokov D.S. по заявке C-11468 добавлен новый   */
/*                статус состояния счета для отложенной обработки ROO          */
/*              : 06.08.2012 Golovkin C-12887 вид отменяемого ареста в отчете  */
/*  Modify      : 17.09.13 zmp C-23347-6                                       */
/*******************************************************************************/
import rsbdataset, "fns_lib.mac", "mnsRPOusr.mac","mnsROOusr.mac", "mnsZNOusr.mac", "mnsPBusr.mac";

macro GetReqDataset(startdate, enddate, reqids, exclude_reqids, formname) 
  var rs, str;
  debugbreak;
  str = "select * from ( " +
      "SELECT t.t_reqid, T.T_BANKDATE, " +
      "      (SELECT t_name " +
      "      FROM dwlstatus_dbt " +
      "     WHERE t_typestate = 4 AND t_state = t.t_state) t_status, " +
      "      (SELECT mesfrm.t_name " +
      "      FROM dwlmeslnk_dbt lnk, " +
      "           dwlmes_dbt mes, " +
      "              dwlmesrls_dbt mesrls, " +
      "              dwlmesfrm_dbt mesfrm, " +
      "              dwltpshem_dbt shem " +
      "        WHERE lnk.t_objid = t.t_reqid " +
      "              AND lnk.t_objkind = 505 " +
      "              AND lnk.t_mesid = mes.t_mesid " +
      "              AND mes.t_rlsformid = mesrls.t_rlsformid " +
      "              AND mesrls.t_formid = mesfrm.t_formid " +
      "              AND mes.t_tpschemid = shem.t_tpshemid " +
      "              AND mesfrm.t_tpid = shem.t_tpid) t_frm " +
      "  FROM dwlreq_dbt t " +
      " WHERE t.t_kind = 5 AND t_direct = 'X' ) req" +
      " where 1 = 1 ";  
  if(valtype(reqids) != 0)    
    str = str + " AND req.t_reqid in ("+reqids+")";
  end; 
  if((strlen(exclude_reqids) > 0) and (valtype(exclude_reqids) != 0))   
    str = str + " AND req.t_reqid not in ("+exclude_reqids+")";
  end; 

  if(valtype(formname) != 0)    
    str = str + " and req.t_frm ='"+formname+"' ";
  end; 
  if((valtype(startdate) != 0) and (valtype(enddate) != 0) ) 
    str = str + " AND req.t_BankDate BETWEEN to_date('"+startdate+"','dd.mm.yyyy') AND to_date('"+enddate+"','dd.mm.yyyy')";
  end;
    str = str + "  order by req.t_ReqID " ;

 rs = trsbdataset(str);
 return rs;
end;

macro GetReqChildDataset(reqid)
  var str, rs;

  str =  " select * from DMES365_LOG_DBT t where t_reqid =" + reqid;

  rs = trsbdataset(str);
  return rs;
end;

macro print_body_rpo (startdate, enddate, reqids, exclude_reqids)
  var mes, rs, rs_otv, statetxt, nameNO, nameCL;
  var acc, isfirst, cnt = 0, name, ext;

  if((valtype(startdate) != 0) and (valtype(enddate) != 0) ) 
[
  Журнал загруженных решений о приостановлении операций по счетам 
  за период ########## - ##########
  ](startdate, enddate);
  else
[
  Протокол обработки решений о приостановлении операций по счетам ];
  end;

[┌───────────┬──────────┬────────────────────────────────────────────────┬────────────────────────────────────────────────┬─────────────────────┬────────────────┬────────────────┐
 │  Номер    │   Дата   │           Наименование входящего файла         │    Наименование клиента(Юр.лица)  Ф.И.О. ИП    │   ИНН/КПП клиента   │    Сумма       │ Статус запроса │
 │ реш/запр  │ реш/запр │                                                │                                                │                     │                │                │];

 rs = GetReqDataset(startdate, enddate, reqids, exclude_reqids, "RPO");
 
 while (rs and rs.movenext)
[├───────────┼──────────┼────────────────────────────────────────────────┼────────────────────────────────────────────────┬─────────────────────┬────────────────┬────────────────┤];

    mes = MnsMessageFormRPO(rs.t_reqid);

    SplitFile(ПолучитьИмяВходФайла( rs.t_reqid ), name, ext);
    nameNO = name + ext;

    if(mes.НаимНП == "")
      nameCL = mes.ФИОИП;
    else
      nameCL = mes.НаимНП;
    end;

[│###########│##########│################################################│################################################│#####################│################│################│]
 (mes.НомРешПр, mes.ДатаРешПр, nameNO:w, nameCL:w, string(mes.ИНННП+"/"+mes.КППНП), money(mes.СумВзыск):a, strupr(rs.t_status):c  );

   if(mes.НомСч.Size > 0)
     isfirst = true;
     acc = getRsWlacclnk(rs.t_reqid, 505);
     while(acc and acc.MoveNext() )
        if(acc.value("T_STATE") == 100)
           statetxt = "По счету создана претензия"; 
        elif(acc.value("T_STATE") == 32)
           statetxt = "В банке (филиале банка) отсутствует номер счета "; 
        elif(acc.value("T_STATE") == 33)
           statetxt = "Счет не принадлежит клиенту"; 
        elif(acc.value("T_STATE") == 35)
           statetxt = "Счет закрыт "; 
        elif(acc.value("T_STATE") == 40) // 17.09.13 zmp C-23347-6
           statetxt = "Транзитный валютный счет";
        elif(acc.value("T_STATE") == 41) // 25.11.14 zmp R-496387
           statetxt = "Депозитный счет";    
        elif(acc.value("T_STATE") == 50)
           statetxt = "При создании претензии возникли ошибки"; 
        elif(acc.value("T_STATE") == 0)
           statetxt = "Счет не обрабатывался"; 
        else
           statetxt = "Неопознанная ошибка "+ acc.value("T_STATE"); 
        end;

        if(isfirst)
           isfirst = false;
[│           │          ├────────────────────────────────────────────────┼────────────────────────────────────────────────┼─────────────────────┴────────────────┴────────────────┤
 │           │          │                             Обработанные счета │                  Номер счета                   │                        Примечание                     │ 
 │           │          │                                                ├────────────────────────────────────────────────┼───────────────────────────────────────────────────────┤];
        end;


[│           │          │                                                │################################################│#######################################################│]
 ( acc.value("T_ACCOUNT"):c, statetxt:w  );

     end;
   end;
 
   rs_otv = GetReqChildDataset(rs.t_reqid);
   isfirst = true;
   while(rs_otv and rs_otv.movenext)
     if(isfirst)
        isfirst = false;
[│           │          ├────────────────────────────────────────────────┼────────────────────────────────────────────────┴───────────────────────────────────────────────────────┤
 │           │          │                    Созданные по запросу ответы │                                                Имя файла                                               │ 
 │           │          │                                                ├────────────────────────────────────────────────────────────────────────────────────────────────────────┤];
     end;

[│           │          │                                                │########################################################################################################│]
 (rs_otv.t_outsidemes:w );
   
   end;
   cnt = cnt+1;
 end;

[└───────────┴──────────┴────────────────────────────────────────────────┴────────────────────────────────────────────────────────────────────────────────────────────────────────┘
  Итого запросов RPO: #

](cnt:l);
end;

macro print_body_roo (startdate, enddate, reqids, exclude_reqids)
  var rs, rs_otv, statetxt, nameNO, nameCL;
  var mes, acc, isfirst, cnt = 0, name, ext;
  var cmd, rs_claim, ClaimKind, ClaimSum = $0;

  if((valtype(startdate) != 0) and (valtype(enddate) != 0) ) 
[
  Журнал загруженных решений об отмене приостановления операций по счетам 
  за период ########## - ##########
  ](startdate, enddate);
  else
[
  Протокол обработки решений об отмене приостановления операций по счетам ];
  end;

[┌───────────┬──────────┬──────────────────────────────────────────────┬──────────────────────────────────────────┬─────────────────────┬─────────────┬──────────┬────────────────┐
 │   Номер   │   Дата   │           Наименование входящего файла       │  Наименование клиента(Юр.лица) Ф.И.О. ИП │   ИНН/КПП клиента   │ № отменяем. │ Дата отм.│ Статус запроса │
 │ реш/запр  │ реш/запр │                                              │                                          │                     │   решения   │ решения  │                │];

  rs = GetReqDataset(startdate, enddate, reqids, exclude_reqids, "ROO");
  while (rs and rs.movenext)
  
[├───────────┼──────────┼──────────────────────────────────────────────┼──────────────────────────────────────────┼─────────────────────┼─────────────┼──────────┼────────────────┤];
    mes = MnsMessageFormROO(rs.t_reqid);

    SplitFile(ПолучитьИмяВходФайла( rs.t_reqid ), name, ext);
    nameNO = name + ext;
    if(mes.НаимНП == "")
       nameCL = mes.ФИОИП;
    else
       nameCL = mes.НаимНП;
    end;

[│###########│##########│##############################################│##########################################│#####################│#############│##########│################│]
 (mes.НомРешОт, mes.ДатаРешОт, nameNO:w, nameCL:w, string(mes.ИНННП+"/"+mes.КППНП), mes.НомРешПр, mes.ДатаРешПр, strupr(rs.t_status):c  );

    isfirst = true;
    if(mes.НомСч.Size > 0)
      acc = getRsWlacclnk(rs.t_reqid, 505);
      while(acc and acc.MoveNext() )
        if(acc.value("T_STATE") == 100)
           statetxt = "Претензия по счету отменена"; 
        elif(acc.value("T_STATE") == 32)
           statetxt = "В банке (филиале банка) отсутствует номер счета "; 
        elif(acc.value("T_STATE") == 33)
           statetxt = "Счет не принадлежит клиенту"; 
        elif(acc.value("T_STATE") == 35)
           statetxt = "Счет закрыт"; 
        elif(acc.value("T_STATE") == 50)
           statetxt = "При отмене претензии возникли ошибки"; 
        elif(acc.value("T_STATE") == 55)
           statetxt = "Претензия не найдена"; 
        elif(acc.value("T_STATE") == 56)
           statetxt = "Претензия уже отменена"; 
        elif(acc.value("T_STATE") == 0)
           statetxt = "Счет не обрабатывался"; 
        else
           statetxt = "Неопознанная ошибка "+ acc.value("T_STATE"); 
        end;

        /*06.08.12 Golovkin C-12887 вид отменяемого ареста в отчете*/
        cmd = RsdCommand( " select t_startamount, t_restkind from dacclaim_dbt  "
                          "    where t_docnumber = ? "
                          "      and t_docdate = ? "
                          "      and t_account = ? "
                          "      and t_initiator = 1 " );

        cmd.addparam( "DocNum",  RSDBP_IN, mes.НомРешПр );
        cmd.addparam( "DocDate", RSDBP_IN, mes.ДатаРешПр );    
        cmd.addparam( "Account", RSDBP_IN, acc.value("t_account") );    

        rs_claim = RsdRecordSet(cmd);
        if(rs_claim.movenext())
           if( rs_claim.value("t_restkind") == 2 )
              ClaimKind = "Частичный арест";   
           elif( rs_claim.value("t_restkind") == 3 )
              ClaimSum = rs_claim.value("t_startamount");
              ClaimKind = "На сумму " + ClaimSum;
           end;
           statetxt = statetxt + " (" + ClaimKind + ")";
        else
           ClaimSum  = $0;
           ClaimKind = "";
        end;

        if(isfirst)
           isfirst = false;
[│           │          ├──────────────────────────────────────────────┼──────────────────────────────────────────┼─────────────────────┴─────────────┴──────────┴────────────────┤
 │           │          │                           Обработанные счета │             Номер счета                  │                            Примечание                         │ 
 │           │          │                                              ├──────────────────────────────────────────┼───────────────────────────────────────────────────────────────┤];
        end;


[│           │          │                                              │##########################################│###############################################################│]
 ( acc.value("T_ACCOUNT"):c,  statetxt:w  );

      end;
    end;

    rs_otv = GetReqChildDataset(rs.t_reqid);
    isfirst = true;
    while(rs_otv and rs_otv.movenext)
       if(isfirst)
          isfirst = false;
[│           │          ├──────────────────────────────────────────────┼──────────────────────────────────────────┴───────────────────────────────────────────────────────────────┤
 │           │          │                  Созданные по запросу ответы │                                           Имя файла                                                      │ 
 │           │          │                                              ├──────────────────────────────────────────────────────────────────────────────────────────────────────────┤];
       end;

[│           │          │                                              │##########################################################################################################│]
 (rs_otv.t_outsidemes:w );

    end;

    cnt = cnt+1;
 end;

[└───────────┴──────────┴──────────────────────────────────────────────┴──────────────────────────────────────────────────────────────────────────────────────────────────────────┘
  Итого запросов ROO: #
](cnt:l);

end;

macro print_body_ZNO (startdate, enddate, reqids, exclude_reqids)
  var rs, rs_otv, str, statetxt, nameNO, nameCL;
  var usr_mes, acc, isfirst = true;
  var cnt = 0;
  var name, ext;


  if((valtype(startdate) != 0) and (valtype(enddate) != 0) ) 
[
  Журнал загруженных запросов по счетам клиента 
  за период ########## - ##########
  ](startdate, enddate);
  else
[
  Протокол обработки запросов по счетам клиента ];
  end;

[┌───────────┬──────────┬───────────────────────────────────────────┬─────────────────────────────────────────────┬─────────────────────┬────────────────┬────────────────────────┐
 │  Номер    │   Дата   │       Наименование входящего файла        │        Наименование клиента(Юр.лица)        │   ИНН/КПП клиента   │ Статус запроса │       Примечание       │
 │ реш/запр  │ реш/запр │                                           │                  Ф.И.О. ИП                  │                     │                │                        │];

   rs = GetReqDataset(startdate, enddate, reqids, exclude_reqids, "ZNO");

   while (rs and rs.movenext)
[├───────────┼──────────┼───────────────────────────────────────────┼─────────────────────────────────────────────┬─────────────────────┬────────────────┬────────────────────────┤];

     usr_mes = MnsMessageFormZNO(rs.t_reqid);

     SplitFile(ПолучитьИмяВходФайла( rs.t_reqid ), name, ext);
     nameNO = name + ext;
     if(usr_mes.НаимНП == "")
        nameCL = usr_mes.ФИОИП;
     else
        nameCL = usr_mes.НаимНП;
     end;

     if(usr_mes.НомСч.Size > 0)
        acc = "";
     else
        acc = "По всем счетам ";
     end;

[│###########│##########│###########################################│#############################################│#####################│################│########################│]
 (usr_mes.НомЗапр, usr_mes.ДатаЗапр, nameNO:w, nameCL:w, string(usr_mes.ИНННП+"/"+usr_mes.КППНП), strupr(rs.t_status):c, acc );
     isfirst = true;
     if(usr_mes.НомСч.Size > 0)
        acc = getRsWlacclnk(rs.t_reqid, 505);
        while(acc and acc.MoveNext() )
           if(acc.value("T_STATE") == 100)
              statetxt = "Счет обработан"; 
           elif(acc.value("T_STATE") == 32)
              statetxt = "В банке (филиале банка) отсутствует номер счета "; 
           elif(acc.value("T_STATE") == 33)
              statetxt = "Счет не принадлежит клиенту"; 
           elif(acc.value("T_STATE") == 35)
              statetxt = "Счет закрыт ";            
           elif(acc.value("T_STATE") == 36)
              statetxt = "Счет открыт позднее запрашиваемого периода";
           elif(acc.value("T_STATE") == 40) // 16.05.14 zmp 
              statetxt = "Транзитный валютный счет";    
           elif(acc.value("T_STATE") == 41) // 25.11.14 zmp R-496387
              statetxt = "Депозитный счет";    
           elif(acc.value("T_STATE") == 50)
              statetxt = "При обработке счета возникли ошибки"; 
           elif(acc.value("T_STATE") == 0)
              statetxt = "Счет не обработан"; 
           else
              statetxt = "Неопознанная ошибка "+ acc.value("T_STATE"); 
           end;

           if(isfirst)
              isfirst = false;
[│           │          ├───────────────────────────────────────────┼─────────────────────────────────────────────┼─────────────────────┴────────────────┴────────────────────────┤
 │           │          │                        Обработанные счета │               Номер счета                   │                          Примечание                           │ 
 │           │          │                                           ├─────────────────────────────────────────────┼───────────────────────────────────────────────────────────────┤];
           end;

[│           │          │                                           │#############################################│###############################################################│]
 ( acc.value("T_ACCOUNT"):c,  statetxt:w  );

        end;
    end;

     rs_otv = GetReqChildDataset(rs.t_reqid);
     isfirst = true;
     while(rs_otv and rs_otv.movenext)
        if(isfirst)
           isfirst = false;
[│           │          ├───────────────────────────────────────────┼─────────────────────────────────────────────┴───────────────────────────────────────────────────────────────┤
 │           │          │               Созданные по запросу ответы │                                                    Имя файла                                                │ 
 │           │          │                                           ├─────────────────────────────────────────────────────────────────────────────────────────────────────────────┤];
        end;

[│           │          │                                           │#############################################################################################################│]
 (rs_otv.t_outsidemes:w );
    
     end;
     cnt = cnt+1;
   end;


[└───────────┴──────────┴───────────────────────────────────────────┴─────────────────────────────────────────────────────────────────────────────────────────────────────────────┘
  Итого запросов ZNO: #

](cnt:l);

end;

macro print_body_ERR (startdate, enddate, reqids, exclude_reqids)
  var rs, rs_otv, str, i;
  var usr_mes, isfirst = true;
  var cnt = 0;

  if((valtype(startdate) != 0) and (valtype(enddate) != 0) ) 
[
  Журнал загруженных запросов с ошибками 
  за период ########## - ##########
  ](startdate, enddate);
  else
[
  Протокол обработки запросов с ошибками ];
  end;

[┌────────────────────────────────────────────────────┬──────────┬──────────┬────────────────┬────────────────────────────────────────────────────────────────────────────────────┐
 │              Имя входящего файла                   │   Тип    │   Дата   │ Статус запроса │                                     Текст ошибки                                   │
 │                                                    │  ответа  │ проверки │                │                                                                                    │                                                   │];

  rs = GetReqDataset(startdate, enddate, reqids, exclude_reqids, "ERR");

  while (rs and rs.movenext)
[├────────────────────────────────────────────────────┼──────────┬──────────┬────────────────┼────────────────────────────────────────────────────────────────────────────────────┤];

     usr_mes = MnsMessageFormPB(rs.t_reqid);
     i = 0;
     isfirst = true;
     while (i<usr_mes.ОшСооб.Size)
       if(isfirst)
          isfirst = false;
[│####################################################│##########│##########│################│####################################################################################│]
 (usr_mes.ИмяВхФайла, usr_mes.ТипОтв, usr_mes.ДатаПроверки, strupr(rs.t_status):c, usr_mes.ОшСооб[i]:w );
    
       else
[│                                                    │          │          │                │####################################################################################│]
 ( usr_mes.ОшСооб[i]:w );
       end;
       i = i+1;
     end;

     rs_otv = GetReqChildDataset(rs.t_reqid);
     isfirst = true;
     while(rs_otv and rs_otv.movenext)
        if(isfirst)
           isfirst = false;
           
[│                                                    ├──────────┴──────────┴────────────────┼────────────────────────────────────────────────────────────────────────────────────┤
 │                                                    │          Созданные по запросу ответы │                                      Имя файла                                     │ 
 │                                                    │                                      ├────────────────────────────────────────────────────────────────────────────────────┤];
        end;

[│                                                    │                                      │####################################################################################│]
 (rs_otv.t_outsidemes:w );
    
     end;
     cnt = cnt+1;
  end;

[└────────────────────────────────────────────────────┴──────────────────────────────────────┴────────────────────────────────────────────────────────────────────────────────────┘
  Итого запросов ERR: #
](cnt:l);

end;

macro GetOutputFileName()
  var filename,TxtPath, errCode, rs,cnum;
  
    rs = trsbdataset("select RSBSESSIONDATA.CNUM  from dual ");
    if (rs and rs.movenext)
       cnum = int(rs.cnum);
    else
       cnum = {oper};
    end;
    
    filename = strsubst(string(date),".","")+strsubst(string(time),":","")+{oper}+"."+cnum;

    GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, TxtPath, errCode);
    if ( errCode > 0 )
       msgbox("Ошибка при определении директории TxtFile");
       return 1;
    else
       TxtPath = TxtPath+"\\"+filename;
    end;
    
    removefile(TxtPath);
    return TxtPath;
end;