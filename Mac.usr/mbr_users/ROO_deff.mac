/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6.0                     */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Отложенная обработка отмен приостановлений налоговых орагнов            */
/*                                                                          */
/*  Имя файла:  ROO_deff.mac                                                */
/*  Создан   :  06.07.2012                                   Чесноков Д.С.  */
/*  Заявка   :  C-11468                                                     */
/*  Изменен  :  26.07.2012 Chesnokov D.S. добавлена массовая обработка      */
/*           :  04.09.2012 Chesnokov D.S. I-00230656 показ протокола после  */
/*                         выполнения действий.                             */
/*              29.01.2012 Chesnokov D.S. заявка I-00318581 обработка       */
/*              нажатия ESC в панели выбора Должностного лица               */
/*              18.04.2013 Chesnokov D.S. Убрал ограничение на 1000 записей */
/****************************************************************************/
import BankInter, RSD, "KeyCodes.mac", "fns_lib.mac", "wlreq.mac", "fns_pb.mac";
var CmdText, Command, reqids;
var RecordSet, col = TArray;
var f_exit = true, flag = false;
var state = 0, stat, pd = PersData(), mes_usr, i, cnt;
var Ошибки:errors;

private var rset, str, exclude_reqs, repfilename;

private file wlr_ROO(wlreq);

macro EvProcScroll (RecordSet, Cmd, id, key )

  var rs;
  var CM_FLAG = CM_DEFAULT;  /*Возвращаемое значение по умолчанию*/

  if (Cmd == DLG_INIT)
    if (not AddMultiAction (RecordSet, 316)) 
      msgbox ("Неопознанная ошибка инициализации массовой обработки");
    end;
    reqids = "";
  end;

  if ((Cmd == DLG_MSEL) and (key == KEY_F2))
     reqids = reqids + RecordSet.value("t_reqid")+", ";
     return CM_MSEL_CONT_CLEAR;
  end;

  if (Cmd == DLG_MSELEND)
    flag = true;//Взводим режим массовой обработки
    return CM_CANCEL;
  end;

  if (cmd == DLG_KEY)
    if (key == KEY_ESC)
      CM_FLAG = CM_DEFAULT;
    elif(Flag)//Обработать массово
      reqids = substr(reqids, 1, strlen(reqids)-2);
      state = 1;
      CM_FLAG = CM_SELECT;
    elif (key == KEY_F2)//Обработать
      state = 2;
      f_exit = true;
      reqids = RecordSet.value("t_reqid");
      CM_FLAG = CM_SELECT;
    elif (key == KEY_F8)//Сформировать PB2 и закрыть
      if (Gen_PB(RecordSet.value("t_reqid")))//Формируем PB2
        rs = RsdCommand("update dwlreq_dbt t set t.t_state = 70 where t.t_reqid = " + RecordSet.value("t_reqid"));
        rs.Execute;
        f_exit = false; // Ставим флаг выхода
      end;
      CM_FLAG = CM_SELECT;
      
    elif (key == KEY_F9)//Закрыть без обработки
      rs = RsdCommand("update dwlreq_dbt t set t.t_state = 70 where t.t_reqid = " + RecordSet.value("t_reqid"));
      rs.Execute;
      f_exit = false; // Ставим флаг выхода
      CM_FLAG = CM_SELECT;
    end;
  end;

  return CM_FLAG;

end;

macro AddCol (ar,ind, fld, head, width, rdonly)
  ar.value (ind * 6)     = fld;
  ar.value (ind * 6 + 1) = head;
  ar.value (ind * 6 + 2) = width;
  ar.value (ind * 6 + 3 ) = 2;   // fldType
  ar.value (ind * 6 + 4 ) = -1;  // decPoint
  ar.value (ind * 6 + 5 ) = 0;   // reserv
end;

/*Start*/

  AddCol (col, 0, "accounts",                "Счет клиента", 40, true);
  AddCol (col, 1, "num_decision",         "Номер сообщения", 20,  true);
  AddCol (col, 2, "date_decision",         "Дата сообщения", 10,  true);
  AddCol (col, 3, "pmdatevalue",            "Дата загрузки", 10,  true);
  AddCol (col, 4, "userid",                         "Опер.",  5,  true);
  AddCol (col, 5, "num_claim",  "Номер отменяемого решения", 10,  true);
  AddCol (col, 6, "date_claim",  "Дата отменяемого решения", 10,  true);
  AddCol (col, 7, "t_reqid",                  " ID объекта", 10,  true);

  CmdText = "    SELECT usr_fld_value_merge (wlmes.t_mesid, " +
            "                                wlmesfrm.t_tpid, " +
            "                                'НомСч', " +
            "                                ',') " +
            "           AS accounts, " +
//            "           usr_fld_value_merge (wlmes.t_mesid, " +
//            "                                wlmesfrm.t_tpid, " +
//            "                                'НомРешОт', " +
//            "                                ',') " +
//            "           AS num_decision, " +
            "           t.t_relatedref AS num_decision, " +
            "           TO_DATE (usr_fld_value_merge (wlmes.t_mesid, " +
            "                                         wlmesfrm.t_tpid, " +
            "                                         'ДатаРешОт', " +
            "                                         ','), 'dd.mm.yyyy') " +
            "           AS date_decision, " +
            "           t.t_pmdatevalue AS pmdatevalue, " +
            "           t.t_userid AS userid, " +
            "           usr_fld_value_merge (wlmes.t_mesid, " +
            "                                wlmesfrm.t_tpid, " +
            "                                'НомРешВзыск', " +
            "                                ',') " +
            "           AS num_claim, " +
            "           usr_fld_value_merge (wlmes.t_mesid, " +
            "                                wlmesfrm.t_tpid, " +
            "                                'ДатаРешВзыск', " +
            "                                ',') " +
            "           AS date_claim, " +
            "           t.t_reqid, " +
            "           t.t_state " +
            "      FROM dwlreq_dbt t, " +
            "           dwlmes_dbt wlmes, " +
            "           dwlmesfrm_dbt wlmesfrm, " +
            "           dwlmeslnk_dbt wlmeslnk, " +
            "           dwlmesrls_dbt wlmesrls " +
            "     WHERE wlmeslnk.t_objid(+) = t.t_reqid " +
            "       AND wlmeslnk.t_objkind(+) = 505 " +
            "       AND wlmeslnk.t_direct(+) = 'X' " +
            "       AND wlmes.t_mesid(+) = wlmeslnk.t_mesid " +
            "       AND wlmeslnk.t_mesid = " +
            "           (SELECT NVL (MIN (sublnk.t_mesid), 0) " +
            "              FROM dwlmeslnk_dbt sublnk " +
            "             WHERE sublnk.t_objid = t.t_reqid " +
            "               AND sublnk.t_objkind = 505) " +
            "       AND wlmes.t_department = 1 " +
            "       AND wlmesrls.t_rlsformid(+) = wlmes.t_rlsformid " +
            "       AND wlmesfrm.t_formid(+) = wlmesrls.t_formid " +
            "       AND wlmesfrm.t_formid > 100000 " +
            "       AND t.t_state = 66 " +
            "       AND t.t_kind = 5 " +
            "       AND t.t_direct = 'X' " +
//            "       AND ROWNUM < 1000 " +
            "  ORDER BY   t.t_BankDate, t.t_reqid ";
    
  RecordSet = RSDRecordSet(CmdText, RSDVAL_CLIENT, RSDVAL_STATIC);

  While (runScroll(RecordSet, 8, col, null, @EvProcScroll, string("Необработанные запросы ROO"), "~F2~ Выполнить ~F8~ Оформить PB2 и закрыть ~F9~ Закрыть", true))
    debugbreak;
    if (f_exit)
      stat = PutPersData(pd.SurName, null, null, pd.Officer, pd.Phone );
      //29.01.2012 Chesnokov D.S. заявка I-00318581
      if((pd.SurName=="") and (pd.Officer=="") and (pd.Phone==""))
        msgbox("Вы не выбрали Должностное лицо банка!!! || Перезайдите в RS-Bank и больше ни когда не нажимайте ESC в панели выбора Должностного лица.|| Файл сформирован не будет. ");
        exit(1);
      end;
      if (state == 1)
        debugbreak;
        Ошибки.init();
        
        exclude_reqs = "";
        str = "select count(*) cnt from dwlreq_dbt where t_reqid in ("+reqids+") ";
        rset = trsbdataset(str);
        if(rset and rset.movenext)
          cnt = int(rset.cnt);  
        else
          cnt = 0; 
        end;
        
        str = "select * from dwlreq_dbt where t_reqid in ("+reqids+") ";
        rset = trsbdataset(str);
        i = 0;
        initprogress(cnt,"","Обработка отложенных отмен приостановлений");
        
        while( rset and rset.movenext) 
          i = i + 1;
          useprogress(i);
          wlr_ROO.reqid = rset.t_reqid;
          if(not geteq(wlr_ROO))
             msgbox("Ошибка");
          end;
          
          ОбработатьЗапрос_mas(wlr_ROO, @mes_usr, pd, 2);
          if(mes_usr.DoNotProcess == 1)
            Ошибки.Add(rset.t_reqid, ОтрезатьПутьОтИмениФайла(mes_usr.filename), mes_usr.errormes);
            exclude_reqs = exclude_reqs + rset.t_reqid +", "; //Gurin I-00208193-1
          end;
          
        end;
        
        remprogress();
        if(Ошибки.errs.size > 0)
          Ошибки.print;
          exclude_reqs = substr(exclude_reqs, 1, strlen(exclude_reqs)-2);
        end;
        
        repfilename = GetOutputFileName();
        setoutput(repfilename,true);
        println("Массовый режим обработки");
        
        if(cnt_ROO > 0)
          print_body_ROO(null,null, reqids, exclude_reqs);
        end;
        
        setoutput(null,true);
        viewfile(repfilename);
        
        flag = false;//Сбросим флаг для обработчика
      elif (state == 2)
        wlr_ROO.reqid = reqids;
        if(not geteq(wlr_ROO))
           msgbox("Ошибка");
        end;
        ОбработатьЗапрос(wlr_ROO, mes_usr, pd, 2); //флаг режима, откуда вызывается
        
        repfilename = GetOutputFileName();
        setoutput(repfilename,true);
        println("Еденичный режим обработки");
        
        if (cnt_ROO > 0)
          print_body_ROO(null,null, wlr_ROO.reqid);
        end;
        
        setoutput(null,true);
        viewfile(repfilename);
        
      end;
    end;
    
    RecordSet = RSDRecordSet(CmdText, RSDVAL_CLIENT, RSDVAL_STATIC);
  end;
  RecordSet.Close();