/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*         функции для контроля соответсвия платежа и сообщения             */
/*                               SWIFT                                      */
/*  Имя файла: wlCheckMes.mac                                               */
/*  Создан:  03.05.06                                        Желнов Ю.Н.    */
/****************************************************************************/
import "swgenmes.mac", "wlchmes.mac", "likepy.mac";

const  SW102     = "102",
       SW103     = "103",
       SW103P    = "103+",
       SW103RUR6 = "103RUR6",
       SW202     = "202",
       SW202RUR6 = "202RUR6",
ТранспортСообщения = 2;  /* Важно!! Указан ID транспорта, если по каким то причинам он будет отличаться 
                          от Swift то сменить! */ 


macro CheckFirstSymbol( str:@string )
   var aBadSymbs = TArray();
       aBadSymbs(0) = ":";
       aBadSymbs(1) = "-";

   var i = 0,
       isBad = false,
       FirstSymbol = subStr(str, 1, 1);

   while ((i < aBadSymbs.Size) and not isBad)
      if (FirstSymbol == aBadSymbs(i))
         isBad = true;
      end;
      i = i + 1;
   end;

   if (isBad)
      str = " " + str;
   end;
end;


// Выдача сообщения о неуспешном контроле поля
PRIVATE MACRO BadField( context:string, name:string, value:string, good_value:string ):bool
  var fullName:string = join( filter( split( context + "\\" + name, "\\") ), "\\");
  var mestext:string = string( "Значение поля ", fullName, " в сообщении не соответствует данным платежа" );
  if( good_value != NULL )
    mestext = mestext + "|В собщении: " + value
                      + "|По платежу: " + good_value;
  end;
  MsgBox( mestext );
  return false;
END;

PRIVATE MACRO CutEndl(value:string)
  if( SubStr(value, Strlen(value)) == "\n")
     return CutEndl(SubStr(value, 1, Strlen(value)-1));
  else 
     return value;
  end;
END;

/*-----------------------------------------------------------
  Контроль сообщений для формы SWIFT103 релиз 103/103+/103RUR
                               SWIFT202 релиз 202/202RUR   
  -----------------------------------------------------------*/
macro CheckMesSWIFTEx( addrMes,  FormRelise:string )

  var field_name, field_value,loop=1,
      tempVal, BaseAmount, ОстатокПоля70 = "";
  var context:string = "";
  var RsPaym;

  SetBuff( wlmes, addrMes );
  if( not FillBuffPayment( wlmes ) )
    RunError( "|Не найден платеж" );
  end;

  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  while( СчитатьПоле( field_name, field_value, context ) )

    tempVal = "";
    
    /* Определим считанное поле нужно ли вообще контролировать */
    if( not GetBlockEdit(field_name, wlmes.RlsFormID ) )
      continue;  /* Контроль не нужен. Читаем следующее */
    end;

    /*   23E    */
    if( (FormRelise != SW202) or (FormRelise != SW202RUR6) )
      if( field_name == InstructionCodeField )
        StrChangeRusXSet( RsPaym.InstructionCode, tempVal );
        CheckFirstSymbol(@tempVal);
        if( field_value != tempVal )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*   32A    */
    if( field_name == ValueDateCurrencyCodeAmountField_A )
      tempVal =  GetSWIFTDate( RsPaym.OutTransferDate ) +
                 FillCurrencyOriginalOrderedAmountField( RsPaym );
        CheckFirstSymbol(@tempVal);
      if( tempVal != field_value )
        return BadField( context, field_name, field_value, tempVal );
      end;
    end;

    /*    33B   */
    if( (FormRelise == SW103) or (FormRelise == SW103P) )
      if( field_name == CurrencyOriginalOrderedAmountField )
        tempVal = "";
        BaseAmount = GetOrigenSumPayment(RsPaym);
        /* EVG Нет, поле 33B заполняется не только в платежах с конверсией.
        if(( RsPaym.ReceiverFIID != RsPaym.PayerFIID) OR (BaseAmount!=RsPaym.ReceiverAmount))*/
          tempVal = ПолучитьКодВалютыЛог( RsPaym.PayerFIID ) + GetSWIFTAmount( BaseAmount );
        CheckFirstSymbol(@tempVal);
        //end;
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*   36  */
    if( (FormRelise == SW103) or (FormRelise == SW103P) )
      if( field_name == ExchangeRateField)
        tempVal = DefineSWIFTRateByPmPaym(RsPaym);
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;
   
    /*   50A     */
    if( (FormRelise == SW102) or(FormRelise == SW103) or (FormRelise == SW103P) )
      if( field_name == OrderingCustomerField_A)
        tempVal = FillOrderingCustomerField103( RsPaym );        
        CheckFirstSymbol(@tempVal);
        if( CutEndl(tempVal) != CutEndl(field_value) )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /* 50F */
    if( (FormRelise == SW102) or (FormRelise == SW103) or (FormRelise == SW103P))
      if( field_name == OrderingCustomerField_F )
        tempVal = FillOrderingCustomerField103( RsPaym );        
        CheckFirstSymbol(@tempVal);
        if( CutEndl(tempVal) != CutEndl(field_value) )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*     50K    */
    if( field_name == OrderingCustomerField_K)
      if(FormRelise == SW103RUR6)
         tempVal = FillOrderingCustomerField103_K( RsPaym, ST_RUR6 );
      elif((FormRelise == SW102) or (FormRelise == SW103) or (FormRelise == SW103p) )
         tempVal = FillOrderingCustomerField103( RsPaym );
      else
         tempVal = FillOrderingCustomerField103_K( RsPaym );
      end;
        CheckFirstSymbol(@tempVal);
      if( CutEndl(tempVal) != CutEndl(field_value) )
        return BadField( context, field_name, field_value, tempVal );
      end;
    end;

    /*     52A    */
    if( field_name == OrderingInstitutionField_A)
      tempVal = FillOrderingInstitutionField(RsPaym, SWIFT_Tag_A );
        CheckFirstSymbol(@tempVal);
      if( tempVal != field_value )
        return BadField( context, field_name, field_value, tempVal );
      end;
    end;

    /*     52D    */
      if( field_name == OrderingInstitutionField_D)
      if( (FormRelise == SW103) or (FormRelise == SW202) )
        tempVal = FillOrderingInstitutionField(RsPaym, SWIFT_Tag_D );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      elif( (FormRelise == SW103RUR6) or (FormRelise == SW202RUR6))
        tempVal = FillOrderingInstitutionField(RsPaym, SWIFT_Tag_D, ST_RUR6 );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*    53A    */
    if( (FormRelise == SW103) or (FormRelise == SW103P) or (FormRelise == SW202))
      if( field_name == Sender_sCorrespondentField_A)
        tempVal = FillSenderCorrespondentField(RsPaym, SWIFT_Tag_A );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*    53B    */
    if( field_name == Sender_sCorrespondentField_B)
    if( (FormRelise == "200") and (RsPaym.dockind == 27))
      tempVal = FillSenderCorrespondentField(RsPaym, SWIFT_Tag_B, null , "200" );          
    elif ((FormRelise == SW103) and (RsPaym.dockind == 27))
    else
      tempVal = FillSenderCorrespondentField(RsPaym, SWIFT_Tag_B );          
    end;
        CheckFirstSymbol(@tempVal);
      if( tempVal != field_value )
        return BadField( context, field_name, field_value, tempVal );
      end;
    end;

    /*    53D    */
    if( (FormRelise == SW103) or (FormRelise == SW202))
      if( field_name == Sender_sCorrespondentField_D)
        tempVal = FillSenderCorrespondentField(RsPaym, SWIFT_Tag_D );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*    56A    */  
    if( field_name == IntermediaryField_A)
      tempVal = FillIntermediaryField(RsPaym, "" );
        CheckFirstSymbol(@tempVal);
      if( tempVal != field_value )
        return BadField( context, field_name, field_value, tempVal );
      end;
    end;

    /*    56C    */  
    if( FormRelise == SW103)   
      if( field_name == IntermediaryField_C)
        tempVal = FillIntermediaryField(RsPaym, SWIFT_Tag_C );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*    56D    */  
      if( field_name == IntermediaryField_D)
      if(   (FormRelise == SW103) or (FormRelise == SW202))
        tempVal = FillIntermediaryField(RsPaym, SWIFT_Tag_D );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      elif( (FormRelise == SW103RUR6) or (FormRelise == SW202RUR6))
        tempVal = FillIntermediaryField(RsPaym, SWIFT_Tag_D, ST_RUR6 );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
           return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*    57A    */  
    if( field_name == AccountWithInstitutionField_A)
      tempVal = FillAccountWithInstitutionField(RsPaym, "" );
        CheckFirstSymbol(@tempVal);
      if( tempVal != field_value )
        return BadField( context, field_name, field_value, tempVal );
      end;
    end;

    /*    57B    */  
    if( (FormRelise == SW103) or (FormRelise == SW202) ) 
      if( field_name == AccountWithInstitutionField_B)
        tempVal = FillAccountWithInstitutionField(RsPaym, SWIFT_Tag_B );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*    57C    */  
    if( FormRelise == SW103)
      if( field_name == AccountWithInstitutionField_C)
        tempVal = FillAccountWithInstitutionField(RsPaym, SWIFT_Tag_C );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*    57D    */  
    if( field_name == AccountWithInstitutionField_D)
      if( (FormRelise == SW103) or (FormRelise == SW202))
        tempVal = FillAccountWithInstitutionField(RsPaym, SWIFT_Tag_D );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
    elif( (FormRelise == SW103RUR6) or (FormRelise == SW202RUR6))
        tempVal = FillAccountWithInstitutionField(RsPaym, SWIFT_Tag_D, ST_RUR6 );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*       58А     */
    if( (FormRelise == SW202RUR6) or (FormRelise == SW202))
      if( field_name == BeneficiaryInstitutionField_A)
        tempVal = FillBeneficiaryInstitutionField(RsPaym, "" );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*       58D     */
    if( field_name == BeneficiaryInstitutionField_D)
      if(   FormRelise == SW202)
        tempVal = FillBeneficiaryInstitutionField(RsPaym, SWIFT_Tag_D );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      elif( FormRelise == SW202RUR6)
        tempVal = FillBeneficiaryInstitutionField(RsPaym, SWIFT_Tag_D, ST_RUR5 );
        CheckFirstSymbol(@tempVal);
        if( tempVal != field_value )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*    59    */  
    if( field_name == BeneficiaryCustomerField)
      if(FormRelise == SW103RUR6)
        tempVal = FillBeneficiaryCustomerField( RsPaym, ST_RUR6 );
      else
      tempVal = FillBeneficiaryCustomerField( RsPaym );
      end;
        CheckFirstSymbol(@tempVal);
      if( CutEndl(tempVal) != CutEndl(field_value) )
        return BadField( context, field_name, field_value, tempVal );
      end;
    end;

    /*    59A    */  
    if( (FormRelise == SW103) or (FormRelise == SW103P) )
      if( field_name == BeneficiaryCustomerField_A)
        tempVal = FillBeneficiaryCustomerField_A( RsPaym );
        CheckFirstSymbol(@tempVal);
        if( CutEndl(tempVal) != CutEndl(field_value) )
          return BadField( context, field_name, field_value, tempVal );
        end;
      end;
    end;

    /*    70    */  
    if( field_name == DetailsOfPaymentField)
      if(FormRelise == SW103RUR6)
        tempVal = FillDetailsOfPaymentField( RsPaym, ALLOWCODES70_MT103_RUR6, ОстатокПоля70, ST_RUR6 );
      else
      debugbreak;
      if((FormRelise == "103") and (RsPaym.dockind == 27))
      tempVal = FillDetailsOfPaymentField( RsPaym, ALLOWCODES70_MT103_STANDART, ОстатокПоля70, null, "103" );
      else
      tempVal = FillDetailsOfPaymentField( RsPaym, ALLOWCODES70_MT103_STANDART, ОстатокПоля70 );
      end;  end;
        CheckFirstSymbol(@tempVal);
      if( tempVal != field_value )
        return BadField( context, field_name, field_value, tempVal );
      end;
    end;

    /*    71A    */  
    if( field_name == DetailsOfChargesField_A )
      tempVal = GetCommisCode( wlpmrmprop.ComissCharges );
        CheckFirstSymbol(@tempVal);
      if( tempVal != field_value )
        return BadField( context, field_name, field_value, tempVal );
      end;
    end;

    /*    71F    */  
    if( (FormRelise == SW103) or (FormRelise == SW103P) )
      if( field_name == SendersChargesField )
        /* Пока не известна реализация проверки этого поля :( */
      end;
    end;

    /*    72    */  
    if( field_name == SenderToReceiverInformationField)

      if(   FormRelise == SW103RUR6 )
        tempVal = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT103_RUR6, ОстатокПоля70 );
      elif( FormRelise == SW202 )
        ОстатокПоля70 = "";
        StrChangeRusXSet(wlpmrmprop.Ground, ОстатокПоля70);
        tempVal = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT202_STANDART, ОстатокПоля70);      
      elif( FormRelise == SW202RUR6 )
        ОстатокПоля70 = "";
        StrChangeRusXSet(wlpmrmprop.Ground, ОстатокПоля70, ST_RUR5);
        tempVal = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT202_RUR5, ОстатокПоля70, ST_RUR5);      
      else
        if( not IsAllowCode(ALLOWCODES_MT103_STANDART, Field72CodesNZP))
           ОстатокПоля70 = "";
        end;
        tempVal = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT103_STANDART, ОстатокПоля70 );
      end;

        CheckFirstSymbol(@tempVal);
      if( tempVal != field_value )
         return BadField( context, field_name, field_value, tempVal );
      end;
    end;

  end;  /* loop */

  /* Проверка прошла успешно*/
  return true;
end;

/*----------------------------------------------
  Функции контроля сообщения, зовутся из настроек 
  привязанных к релизам форм
------------------------------------------------*/
macro CheckMes103 (addrMes)
  return CheckMesSWIFTEx(addrMes, SW103);
end;

macro CheckMes103P(addrMes)
  return CheckMesSWIFTEx(addrMes, SW103P);
end;

macro CheckMes103RUR(addrMes)
  return CheckMesSWIFTEx(addrMes, SW103RUR6);
end;

macro CheckMes202 (addrMes)
  return CheckMesSWIFTEx(addrMes, SW202);
end;

macro CheckMes202RUR(addrMes)
  return CheckMesSWIFTEx(addrMes, SW202RUR6);
end;

