// -------------------------------------------------------------------------------------------------
// @filename: uf_common.mac v.1
// @author  : 2012-08-01 zip_z. C-12896
// @desc    : Общие функции для обработки ED243/244
// @changes : 2013-01-10 zip_z. I-00310148  исправлено формирование даты - UF_DateYYYYMMDD ()
//          : 2013-02-11 zip_z. I-00323064 добавлена плюшка ручного заполнения адреса Lotus 
//          : 2013-03-03 zip_z. С-17661 функции отправки сообщений перенесены из uf_common в uf_letter_ic
//          : 2013-06-22 Gurin S. C-20508
//          : 2014-03-29 Gurin S. УФЭБС альбом v2.6.0
//          : 2014-07-08 Gurin S. R-419016-2 UF_StrSubSt (экранируем спец. символа)
//          : 2015-01-22 Gurin S. C-31132-7 
// -------------------------------------------------------------------------------------------------

import BankInter, PtInter, likepy, lib_oper, lib_const, lib_registry, Globals, treport;
import uf_letter_ic;
    
/* EDDefineRequestCode */
const REQ_ACCOUNT               = "01", 
      REQ_ACCOUNT_NAME          = "02", 
      REQ_NODOUBLE              = "03",
      REQ_PAYER_NAME            = "04", 
      REQ_RECV_NAME             = "05", 
      REQ_INN_ADDRESS           = "06",
      REQ_GROUND                = "07", 
      REQ_NEED_INFO_BY_MASK     = "08",
      //new UFEBS
      REQ_NEED_UPD_DETAILS             = "09",
      REQ_RETURN_TRANSF_AMOUNT         = "10",
      REQ_INFORM_PAYEE_ABOUT_CASH      = "11",
      REQ_CONFIRM_DETAILS_IN_REGISTER  = "12",
      REQ_NEED_UPD_DETAILS_IN_REGISTER = "13",
      REQ_NEED_INFO_USE_PAYM_CARDS     = "14",
      REQ_NEED_DETAILS_OF_PAYM_CARDS   = "15",
      REQ_TO_USE_OTHER_CODES           = "99";

/* EDDefineAnswerCode */
const ANS_PROOF_OF_ACCEPT_CASH       = "09",
      ANS_NEW_BANK_RECV_ACCOUNT      = "10",
      ANS_UPD_VALUE_DETAILS          = "11",
      ANS_UPD_VALUE_OF_REGISTRY      = "12",
      ANS_AMOUNT_NOT_REFUNDED        = "13",
      ANS_AMOUNT_WILL_BE_REFUNDED    = "14",
      ANS_REFUNDED_AMOUNT_BY_AGREE   = "15",
      ANS_INFO_BY_USE_PAYM_CARDS     = "16",
      ANS_INFO_DETAILS_OF_PAYM_CARDS = "17",
      ANS_TO_USE_OTHER_CODES         = "99";

var MsgNames = TArray();
          
// @desc   : поиск библиотеки ресурсов. 
// @return : Возвращает полный путь к найденному файлу *.lbr
// @changes: 2013-03-03 zip_z. deprecated.
macro getResourceLibPath (fname) 
    return RSL_GetLbrPath (fname, true);
end;

class ForEDProtocol(_MsgName, _Path)
    var m_MsgName    = _MsgName;
    var m_Path       = _Path;
end;

class ForProtocol208(_Format, _MsgName)
    var m_Format     = _Format;
    var m_MsgName    = _MsgName;
end;

// @desc   : Формируем протокол массовой выгрузки сообщений 
// @return : Возвращает файл протокола
macro ShowEDProtocol (arr)
    var reportName = getTxtFileName ("outgoing_ED");
    var table = CTableReport ();
    var i = 0;

    setoutput(reportName,false);
   [##########################################################################################] ("Протокол выгрузки сообщений":c:40);
   [                                                                       Пользователь: #####] ({oper});
   [                                                       Дата: ########### Время: ##########] (date (), time ());
   [                                                                                          ];

    if (arr.size ())
        table.addColumn ("Сообщение:",                       25,  AL_LEFT); 
        table.addColumn ("Файл выгрузки находится по пути:", 80,  AL_LEFT); 
        table.printHead (string ("Выгружены сообщения ":c:0));

        while (i < arr.size ())
            table.printStringTransferByWord (arr[i].m_MsgName, 
                                             arr[i].m_Path); 
            i = i + 1;
        end;

        table.printBottom ();
        println("ИТОГО: ", i);
    else
        println("Не было выгружено ни одного сообщения");
    end;
    SetOutput (NULL, true);
    viewfile(reportName);

end;

// @desc   : Формируем протокол массовой выгрузки сообщений 
// @return : Возвращает файл протокола
macro ShowED208Protocol (arr)
    var reportName = getTxtFileName ("ed208_finished");
    var table = CTableReport ();
    var i = 0;

    setoutput(reportName,false);
   [##########################################################################################] ("Протокол массовой обработки сообщений":c:40);
   [                                                                       Пользователь: #####] ({oper});
   [                                                       Дата: ########### Время: ##########] (date (), time ());
   [                                                                                          ];

    if (arr.size ())
        table.addColumn ("Формат:",      10,  AL_LEFT); 
        table.addColumn ("Имя файла:",   40,  AL_LEFT); 
        table.printHead (string ("Обработанные сообщения ":c:0));

        while (i < arr.size ())
            table.printStringTransferByWord (arr[i].m_Format,
                                             arr[i].m_MsgName); 
            i = i + 1;
        end;

        table.printBottom ();
        println("ИТОГО: ", i);
    else
        println("Не было выгружено ни одного сообщения");
    end;
    SetOutput (NULL, true);
    viewfile(reportName);

end;

// @desc: Параметры обработки ED
class EDParams ()
    var m_txtdir_import:string = ""; // директория импорта
    var m_txtdir_oldmsg:string = ""; // директория архивации
    var m_txtdir_export_243:string = ""; // директория экспорта исходящих ED243
    var m_txtdir_export:string = ""; // директория экспорта
    //var m_txtdir_msword:string = ""; // директория для отчётов MS Word
    
    // @changes: 2013-03-03 zip_z. переписал на RSL_GetRegistryValue
    macro readEDParams ()
        m_txtdir_import     = RSL_GetRegistryValue ("PRBB/ED243PATH/IN"    );
        m_txtdir_oldmsg     = RSL_GetRegistryValue ("PRBB/ED243PATH/OLDMES");
        m_txtdir_export_243 = RSL_GetRegistryValue ("PRBB/ED243PATH/OUT");
        m_txtdir_export     = RSL_GetRegistryValue ("PRBB/ED244PATH/OUT"   );
    end;
    
end;


// @desc  : параметры идентификации исходящего ED244
class EDIdentity ()
    var EdDate   = "";
    var EdAuthor = "";
    var EdNo     = "";

    macro init ()
        // generateReference (83, this.EdNo);
        generateReference (3, this.EdNo);
        this.EdNo = string ((600000 + int (this.EdNo)));
        
        var sql = "select   (select   concat (substr (t_code, 3), '000')" +
                  "            from   dobjcode_dbt" +
                  "           where   t_objecttype = 3 and t_codekind = 3 and t_objectid = rsbSessionData.ourbank) edauthor, " +
                  "       to_char (rsbSessionData.curdate, 'yyyy-mm-dd') eddate" +
                  "  from   dual";
        sql = execSQLSelect (sql);
        if (sql.moveNext ())
            this.EdDate   = sql.value ("EdDate");
            this.EdAuthor = sql.value ("EdAuthor");
        end;
    end;
    init ();
end;

macro UF_DateYYYYMMDD (d)
    var yyyy, mm, dd;
    DateSplit (d, dd, mm, yyyy);
    return (string (yyyy, "-", strsubst (string (mm:2), " ", "0"), "-", strsubst (string (dd:2), " ", "0"))); 
end;

macro UF_StrSubSt (xmldata:string):string
    const val1 = "&amp;";
    xmldata = strsubst(xmldata,"&",val1);
    return xmldata; 
end;


