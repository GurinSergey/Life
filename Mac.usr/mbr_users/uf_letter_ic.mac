// -------------------------------------------------------------------------------------------------
// @filename: uf_letter_ic.mac v.1
// @author  : 2012-10-25 zip_z. C-14660
// @desc    : Панель обработки сообщения в Интернет-Клиент о принятом ED243
// @changes : 2012-10-30 zip_z. C-15056 (третья очередь доработок)
//          : 2013-03-03 zip_z. С-17661 функции отправки сообщений перенесены из uf_common
// -------------------------------------------------------------------------------------------------

import lib_iclient, send_lotus;

/* типы оповещений в ходе обработки ED24x */
const UF_MESSAGE_TYPE_ICLIENT = 1; // сообщение в ИК
const UF_MESSAGE_TYPE_LOTUS   = 2; // сообщение в Lotus
const UF_MESSAGE_TYPE_WINWORD = 3; // сообщение в письме Word

/* виды действий с отправкой сообщений */
const UF_MESSAGE_SEND = 1; // отправлять сообщение
const UF_MESSAGE_DENY = 0; // забить на отправку сообщения

// @desc  : отправка оповещения в Lotus. Понятная заглушка специально под себя :)
// @return: void
macro sendLotusNotify (subject:string, letter:string, mail:string)
    const MAGIC_FRONT_CONST = 73;
    send_l( subject, letter, MAGIC_FRONT_CONST, mail);
end;

// @desc  : поиск адреса Lotus (примечание 200 для сотрудника банка)
// @return: адрес Lotus (строка) или "", если ничего не найдено
macro findLotusMailByOper (m_oper:integer):string
    /* коды пользовательских ответов */
    const RET_DEFINE_MANUALLY = 0;
    const RET_DONT_SEND       = 1;

    var oper      = RSL_Person (m_oper);
    var lotusMail = "";
    var ret       = "";
    var answer    = 0;

    var ptObject = RsbParty (oper.partyID);
    var ptNote   = ptObject.partyNote;

    if ( not (ret = ptNote.readNote (PT_USR_NOTEKIND_LOTUS_MAIL, {CurDate})) )
        answer  = confWin (makeArray (string ("Для пользователя ", oper.oper, " (", oper.name, 
                                              ")| не задан адрес Lotus в примечании ", /*200*/ PT_USR_NOTEKIND_LOTUS_MAIL )), 
                           makeArray ("Задать адрес самостоятельно", "Не отправлять уведомление"));
        if (answer == RET_DEFINE_MANUALLY) 
            if (true == GetString (LotusMail, "Введите адрес Lotus"))
                LotusMail = trim (LotusMail);
                LotusMail = strSubst (LotusMail, " ", "_");
                var stat = ptNote.addNote (PT_USR_NOTEKIND_LOTUS_MAIL, lotusMail, {CurDate});
                if (stat == 0)
                    ptObject.update ();
                    ret = lotusMail;
                else
                    memoryError (stat);
                    displayError ();
                end;
            end;
        elif (answer == RET_DONT_SEND)
            msgbox ("Уведомление в Lotus не отослано");
        end;
    end;
    
    return ret;
end;

/* отправлятор сообщений */
class (TRecHandler) UF_MessageSender (_partyid, _text)
    var m_partyid      = _partyid; // кому посылаем сообщение
    var m_text         = _text;    // текст сообщения
    var m_isSentNotify = false;    // признак отосланности сообщения
    
    // обработчик панели
    macro PanelHandler (dlg, cmd, id, key) 
        if(cmd == DLG_INIT)
            dlg.rec.TxtBox  = m_text;
            message ("~F9~ Сохранить и отправить в ИК ~ESC~ Отказаться от отправки ");
        elif (cmd == DLG_KEY ) 
            if (key == 27) // Esc
                return CM_CANCEL;
            elif (key == 323 ) // F9
                IC_SendMessage (m_partyid, "Запрос на уточнение реквизитов платежа", dlg.rec.TxtBox);
                m_isSentNotify = true;
                return CM_SAVE;
            end;
        end;
    end;
   
    initTRecHandler ("Prt_Soob", RSL_GetLbrPath ("diver.lbr", true), true);
    
    // собственно, отправка сообщения (то, ради чего всё и затевалось)
    macro SendNotify ()
        runDialog (this, R2M (this, "PanelHandler"));
        return this.m_isSentNotify;
    end;
   
end;