// ------------------------------------------------------------------------------------------------------------------
// @filename: SetIncompleteFlag.mac
// @author  : Gurin S. N.
// @desc    : Зачисление входящего сеанса через незавершенные расчеты:
//              (Для указаного сеанса записывается информация (дата, рейс, сеанс) в настрйоке PRBB\MCI\INCOMPLETE.
//               Обрабатываются данные с настройки на шагах операции)
// @request : C-36456-7
// ------------------------------------------------------------------------------------------------------------------
import RsbFormsInter, Globals, KeyCodes, OraLib, LikePy, Календарь;

private class infoObject()
    var oDate = {curdate};
    var oRace = 0;
    var oSession = 0;
end;

private macro SetInComplFlag(obj) 
   var sql = execSqlSelect("SELECT 1 FROM dwlsess_dbt WHERE T_BANKDATE = :d AND T_NUMBERRACE = :r AND T_NUMBER = :s ",
                            makeArray (SQLParam ("d", obj.oDate), SQLParam ("r", obj.oRace), SQLParam ("s", obj.oSession)));  
   
   if (sql and sql.movenext())
       var stat = SetDefaultRegistryValue ("PRBB/MCI/INCOMPLETE",  string(obj.oDate, ";", obj.oRace, ";", obj.oSession));
       if (stat) msgbox("Процедура выполнена"); msgbox("ВНИМАНИЕ!!! | Для того, чтобы изменения вступили в силу, необходимо перезайти в РС-Банк");
       else      msgbox("Ошибка выполнения процедуры"); 
       end;
   else
       msgbox("Такого сеанса не существует!!! | (Дата: ", obj.oDate, ", Рейс: ", obj.oRace, ", Сеанс: ", obj.oSession, ")")
   end;
end;

private macro RemoveInComplFlag() 

  var stat = SetDefaultRegistryValue ("PRBB/MCI/INCOMPLETE",  string(""));
  if (stat) msgbox("Процедура выполнена"); msgbox("ВНИМАНИЕ!!! | Для того, чтобы изменения вступили в силу, необходимо перезайти в РС-Банк");
  else      msgbox("Ошибка выполнения процедуры"); 
  end;

end;

private class(TRsbPanel) MainPanel()
    InitTRsbPanel();
    setCaption("Сеанс на незавершенные");
    setStatus("Esc - Выход, F2 - Выполнить, F3 - Вызов календаря, F8 - Отмена действия процедуры");
    
    private const FT_INTEGER = 0;
    private const FT_DATE = 9;

    var DateRace = {curdate}, Race = 0, Session = 0;

    var startVal = "";
    var info: infoObject;

    var fieldDate:    TRsbEditField = TRsbEditField(FT_DATE);
    var fieldRace:    TRsbEditField = TRsbEditField(FT_INTEGER);
    var fieldSession: TRsbEditField = TRsbEditField(FT_INTEGER);

    setSize(19,6);
    setPosition(43,18);

    var labelDate: TRsbLabel = TRsbLabel(3, 2, "Дата:");   
    fieldDate.bindValue(this, "DateRace", 8);
    fieldDate.setPosition(8,2);   
    fieldDate.setSize(9,1);
    fieldDate.onKeyPressed(R2M(this, "fldDate")); 

    var labelRace: TRsbLabel = TRsbLabel(3, 3, "Рейс:");   
    fieldRace.bindValue(this, "Race", 8);
    fieldRace.setPosition(8,3);   
    fieldRace.setSize(9,1);
    fieldRace.onKeyPressed(R2M(this, "fldInt")); 

    var labelSession: TRsbLabel = TRsbLabel(3, 4, "Сеанс:");   
    fieldSession.bindValue(this, "Session", 8);
    fieldSession.setPosition(8,4);   
    fieldSession.setSize(9,1);
    fieldSession.onKeyPressed(R2M(this, "fldInt")); 

    addLabel(labelDate);
    addLabel(labelRace);
    addLabel(labelSession);

    addControl(fieldDate);
    addControl(fieldRace);
    addControl(fieldSession);
   
    macro fldDate(key:TRsbKeyPressedEvent)
        if(key.keyCode == KEY_F3)
            var d:Date = Date(GetDateByCalendar({curdate}));
            fieldDate.value = d; 
            info.oDate = fieldDate.value;            
        end;        
        if(key.KeyCode == KEY_F2)
            info.oDate    = fieldDate.value; 
            info.oRace    = fieldRace.value; 
            info.oSession = fieldSession.value;
            SetInComplFlag(info);
            close(1);
            exit(0);
        end;
        if(key.KeyCode == KEY_F8)
            RemoveInComplFlag();
            close(1);
            exit(0);
        end;
    end;

    macro fldInt(key:TRsbKeyPressedEvent)
        if(key.KeyCode == KEY_F2)
            info.oDate    = fieldDate.value; 
            info.oRace    = fieldRace.value; 
            info.oSession = fieldSession.value;
            SetInComplFlag(info);
            close(1);
            exit(0);
        end;
        if(key.KeyCode == KEY_F8)
            RemoveInComplFlag();
            close(1);
            exit(0);
        end;
    end;
end;

var panel:TRsbPanel = MainPanel;
panel.run;    