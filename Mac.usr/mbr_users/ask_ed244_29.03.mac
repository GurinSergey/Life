// -------------------------------------------------------------------------------------------------
// @filename: ask_ed244.mac
// @author  : Гурин С.
// @desc    : Квитовка сообщений ED244
// @request : C-17145 
// @modify  : 12.02.2013 Gurin S. (исключил загрузку ранее загружаемого файла ) 
//          : 18.10.2013 Gurin S. C-24420-4 (добавил проверку на ResultCode + существование ED244)
// -------------------------------------------------------------------------------------------------
import  globals, likepy, RsExts, treport, BankInter, lib_registry;

private const ERR_EXIT_SUCCESS = 0;
private const ERR_EXIT_FAILURE = 1;

var ask_array: TArray = TArray();

class AskEDParams ()
    var path_ask_ed244_in :string = ""; // директория импорта
    var path_ask_ed244_old:string = ""; // директория архивации
    var ask_ed244_mask            = "*.xml";
  
    private macro normalizePath(path)
        if (SubStr(path, StrLen(path), 1)!="\\")
            path = path + "\\";
        end;
        return path;
    end;

    macro readParams ()
        path_ask_ed244_in  = normalizePath(RSL_GetRegistryValue ("PRBB/ED244_ASK/IN"));
        path_ask_ed244_old = normalizePath(RSL_GetRegistryValue ("PRBB/ED244_ASK/OLDMES"));
    end;

end;

class AskRecord(_EDAuthor, _EDDate, _EDNo)
    var m_EDAuthor = _EDAuthor;
    var m_EDDate   = _EDDate;
    var m_EDNo     = _EDNo;
end;

private class ASKED244 ()
    var params :AskEDParams = AskEDParams (); // параметры обработки ED
    var reportName = getTxtFileName ("ask_ed244");

    macro moveFile (src:string, dst:string)
        var m_copy   = copyFile (src, dst);
        var m_remove = removeFile (src);
        return (m_copy and m_remove);
    end;

    private macro readEDFile (fileName)
         file f () txt;
         var ret = "";
         var sql, parm;
     
         if (not open (f, string (params.path_ask_ed244_in, fileName))) 
             ret = "Невозможно открыть файл для чтения "; 
         end;
        
         var xmlData = "";
         while (next (f))
             xmlData = xmlData + f.str;
         end;

         sql  = "insert into USR_ASK_CONTAINER_TMP (fname, xmldata) values (trim (:fname), xmltype (convert (:xmldata ,'RU8PC866' ,'CL8MSWIN1251')))";
         parm = makeArray (SQLParam ("fname", fileName), SQLParam ("xmldata", xmlData));
         if (0 != execSQL (sql, parm, false))
             ret = ERR_EXIT_SUCCESS;
         else
             ret = ERR_EXIT_FAILURE;
         end;
                          
         return ret;
    end;

    macro printImportRep ()
        var obj;
        SetOutput (reportName);
        
        println (string ("                  Отчет о выполнении процедуры импорта отбивок ED244"):c:80);
        println (string ("                           Дата - ", date, ", время - ", time):c:70);

        var sql = "with vw " + "\n" +
                  "       as (select fname, " + "\n" +
                  "                  to_char (extractvalue (xmldata, '/ED208/EDRefID/@EDDate', 'xmlns=urn:cbr-ru:ed:v2.0')) eddate, " + "\n" +
                  "                  to_char (extractvalue (xmldata, '/ED208/EDRefID/@EDAuthor', 'xmlns=urn:cbr-ru:ed:v2.0')) edauthor, " + "\n" +
                  "                  to_char (extractvalue (xmldata, '/ED208/EDRefID/@EDNo', 'xmlns=urn:cbr-ru:ed:v2.0')) edno, " + "\n" +
                  "                  regexp_substr (t.xmldata.extract ('/', 'xmlns=urn:cbr-ru:ed:v2.0').getstringval (), 'ED[0-9]{3}') format, " + "\n" +
                  "                  case " + "\n" +
                  "                     when not exists " + "\n" +
                  "                                 (select 1 " + "\n" +
                  "                                    from usr_uf_container c " + "\n" +
                  "                                   where extractvalue (c.xml_answer, '/ED244/@EDAuthor', 'xmlns=urn:cbr-ru:ed:v2.0') = extractvalue (t.xmldata, '/ED208/EDRefID/@EDAuthor', 'xmlns=urn:cbr-ru:ed:v2.0') " + "\n" +
                  "                                         and to_date (extractvalue (c.xml_answer, '/ED244/@EDDate', 'xmlns=urn:cbr-ru:ed:v2.0'), 'yyyy-mm-dd') = " + "\n" +
                  "                                             to_date (extractvalue (t.xmldata, '/ED208/EDRefID/@EDDate', 'xmlns=urn:cbr-ru:ed:v2.0'), 'yyyy-mm-dd') " + "\n" +
                  "                                         and extractvalue (c.xml_answer, '/ED244/@EDNo', 'xmlns=urn:cbr-ru:ed:v2.0') = extractvalue (t.xmldata, '/ED208/EDRefID/@EDNo', 'xmlns=urn:cbr-ru:ed:v2.0')) " + "\n" +
                  "                     then" + "\n" +
                  "                        2" + "\n" +
                  "                     else " + "\n" +
                  "                        case " + "\n" +
                  "                           when exists " + "\n" +
                  "                                   (select 1 " + "\n" +
                  "                                      from usr_uf_container tmp " + "\n" +
                  "                                     where (extractvalue (tmp.xml_answer, '/ED244/@EDAuthor', 'xmlns=urn:cbr-ru:ed:v2.0') = extractvalue (t.xmldata, '/ED208/EDRefID/@EDAuthor', 'xmlns=urn:cbr-ru:ed:v2.0') " + "\n" +
                  "                                            and to_date (extractvalue (tmp.xml_answer, '/ED244/@EDDate', 'xmlns=urn:cbr-ru:ed:v2.0'), 'yyyy-mm-dd') = " + "\n" +
                  "                                                to_date (extractvalue (t.xmldata, '/ED208/EDRefID/@EDDate', 'xmlns=urn:cbr-ru:ed:v2.0'), 'yyyy-mm-dd') " + "\n" +
                  "                                            and extractvalue (tmp.xml_answer, '/ED244/@EDNo', 'xmlns=urn:cbr-ru:ed:v2.0') = extractvalue (t.xmldata, '/ED208/EDRefID/@EDNo', 'xmlns=urn:cbr-ru:ed:v2.0')) " + "\n" +
                  "                                            and (tmp.xml_ask is null " + "\n" +
                  "                                                 or extractvalue (tmp.xml_ask, '/ED208/EDRefID/@EDAuthor', 'xmlns=urn:cbr-ru:ed:v2.0') != extractvalue (t.xmldata, '/ED208/EDRefID/@EDAuthor', 'xmlns=urn:cbr-ru:ed:v2.0') " + "\n" +
                  "                                                 or to_date (extractvalue (tmp.xml_ask, '/ED208/EDRefID/@EDDate', 'xmlns=urn:cbr-ru:ed:v2.0'), 'yyyy-mm-dd') != " + "\n" +
                  "                                                     to_date (extractvalue (t.xmldata, '/ED208/EDRefID/@EDDate', 'xmlns=urn:cbr-ru:ed:v2.0'), 'yyyy-mm-dd') " + "\n" +
                  "                                                 or extractvalue (tmp.xml_ask, '/ED208/EDRefID/@EDNo', 'xmlns=urn:cbr-ru:ed:v2.0') != extractvalue (t.xmldata, '/ED208/EDRefID/@EDNo', 'xmlns=urn:cbr-ru:ed:v2.0'))) " + "\n" +
                  "                           then" + "\n" +
                  "                              1" + "\n" +
                  "                           else" + "\n" +
                  "                              0" + "\n" +
                  "                        end" + "\n" + 
                  "                  end" + "\n" +
                  "                     res, " + "\n" +
                  "                  TO_CHAR(EXTRACTVALUE (t.xmldata, " + "\n" +
                  "                                        '/ED208/@ResultCode', " + "\n" +
                  "                                        'xmlns=urn:cbr-ru:ed:v2.0')) " + "\n" +
                  "                     code " + "\n" +
                  "           from usr_ask_container_tmp t)" + "\n" +
                  "select fname, eddate, edauthor, edno, format," + "\n" +
                  "       case" + "\n" +
                  "          when format = 'ED201' then '!!! Получатель не является участником обмена информационными ЭС'" + "\n" +
                  "          when format = 'ED208' and code = '1' THEN 'Передано в МЦИ'" + "\n" +
                  "          when format = 'ED208' and code = '3' THEN 'ЭД не доставлен получателю'" + "\n" +
                  "          when res = 0 and format = 'ED208' and code = '2' then 'Пропущено. Ранее уже загружалось это ED208'" + "\n" +
                  "          when res = 1 and format = 'ED208' and code = '2' then 'Загружено. Сообщение сквитовано'" + "\n" +
                  "          when res = 2 and format = 'ED208' and code = '2' then 'Пропущено. Не найдено ED244'" + "\n" +
                  "          else '!!! Не соответствует формату'" + "\n" +
                  "       end" + "\n" +
                  "          res " + "\n" +
                  "from vw" + "\n" +
                  "order by res";
        
        sql = execSQLSelect (sql);
        var table = CTableReport();

        table.addColumn ("Имя файла",              25, AL_LEFT);
        table.addColumn ("EDDate",                 10, AL_LEFT);
        table.addColumn ("EDAuthor",               10, AL_LEFT);
        table.addColumn ("EDNo",                   10, AL_LEFT);
        table.addColumn ("Формат",                  3, AL_LEFT);
        table.addColumn ("Результат обработки",    55, AL_LEFT);

        table.printHead ();
        var i = 0;
            
        while (sql.moveNext ())
            table.printStringTransferByWord ( sql.value ("fname")   , sql.value ("EdDate"), 
                                              sql.value ("EdAuthor"), sql.value ("EdNo"), 
                                              sql.value ("format")  , sql.value ("res") );
            if (substr(sql.value ("res"),1,9) == "Загружено")
                obj = AskRecord(sql.value ("EdAuthor"), sql.value ("EdDate"), sql.value ("EdNo"));
                ask_array(ask_array.size()) = obj;
            end;
            i = i + 1;
        end;
            
        table.printSeparator ();
        table.printStringTransferByWord ("Всего файлов:", i);
        table.printBottom ();
        
        SetOutput (NULL, true);
    end;

    macro MatchMessage ()
        var sql, parm, i = 0;

        while (i < ask_array.size())
            sql = "update usr_uf_container set is_ask = chr (88), ask_count = (decode(ask_count, null, 0, ask_count) + 1), " + "\n" +
                                  "   xml_ask = (select xmldata from USR_ASK_CONTAINER_TMP t where " + "\n" +
                                  "   extractvalue (t.xmldata, '/ED208/EDRefID/@EDAuthor', 'xmlns=urn:cbr-ru:ed:v2.0') = :EDAuthor1 " + "\n" +
                                  "   and to_date (extractvalue (t.xmldata, '/ED208/EDRefID/@EDDate', 'xmlns=urn:cbr-ru:ed:v2.0'), 'yyyy-mm-dd') = to_date(:EDDate1, 'yyyy-mm-dd') " + "\n" +
                                  "   and extractvalue (t.xmldata, '/ED208/EDRefID/@EDNo', 'xmlns=urn:cbr-ru:ed:v2.0') = :EDNo1 " + "\n" +
                                  "   and EXTRACTVALUE (t.xmldata,'/ED208/@ResultCode','xmlns=urn:cbr-ru:ed:v2.0') = '2') " + "\n" +
                                  "   where " + "\n" +
                                  "   extractvalue (xml_answer, '/ED244/@EDAuthor', 'xmlns=urn:cbr-ru:ed:v2.0') = :EDAuthor2 "
                                  "   and extractvalue (xml_answer, '/ED244/@EDDate', 'xmlns=urn:cbr-ru:ed:v2.0')   = :EDDate2 "
                                  "   and extractvalue (xml_answer, '/ED244/@EDNo', 'xmlns=urn:cbr-ru:ed:v2.0')     = :EDNo2 ";
            parm = makeArray (SQLParam ("EDAuthor1"    , ask_array(i).m_EDAuthor), SQLParam ("EDDate1"      , ask_array(i).m_EDDate),
                              SQLParam ("EDNo1"        , ask_array(i).m_EDNo),     SQLParam ("EDAuthor2"    , ask_array(i).m_EDAuthor), 
                              SQLParam ("EDDate2"      , ask_array(i).m_EDDate),   SQLParam ("EDNo2"        , ask_array(i).m_EDNo));
            execSQL (sql, parm);
            i = i + 1;
        end;
    end;
    
    macro update_xml_ask ()
       var sql, parm, i = 0;

       while (i < ask_array.size())
           sql = "update usr_uf_container set xml_ask = (select xmldata from USR_ASK_CONTAINER_TMP t where " + "\n" +
                 "                   extractvalue (t.xmldata, '/ED208/EDRefID/@EDAuthor', 'xmlns=urn:cbr-ru:ed:v2.0') = :EDAuthor1 " + "\n" +
                 "                   and to_date (extractvalue (t.xmldata, '/ED208/EDRefID/@EDDate', 'xmlns=urn:cbr-ru:ed:v2.0'), 'yyyy-mm-dd') = to_date(:EDDate1, 'yyyy-mm-dd') " + "\n" +
                 "                   and extractvalue (t.xmldata, '/ED208/EDRefID/@EDNo', 'xmlns=urn:cbr-ru:ed:v2.0') = :EDNo1) " + "\n" +
                 " where extractvalue (xml_answer, '/ED244/@EDAuthor', 'xmlns=urn:cbr-ru:ed:v2.0') = :EDAuthor2 " + "\n" +
                 "       and to_date (extractvalue (xml_answer, '/ED244/@EDDate', 'xmlns=urn:cbr-ru:ed:v2.0'), 'yyyy-mm-dd') = to_date(:EDDate2, 'yyyy-mm-dd') " + "\n" +
                 "       and extractvalue (xml_answer, '/ED244/@EDNo', 'xmlns=urn:cbr-ru:ed:v2.0') = :EDNo2 ";  
           parm = makeArray (SQLParam ("EDAuthor1"  , ask_array(i).m_EDAuthor), SQLParam ("EDDate1"  , ask_array(i).m_EDDate), SQLParam ("EDNo1"  , ask_array(i).m_EDNo),
                             SQLParam ("EDAuthor2"  , ask_array(i).m_EDAuthor), SQLParam ("EDDate2"  , ask_array(i).m_EDDate), SQLParam ("EDNo2"  , ask_array(i).m_EDNo));
           execSQL (sql, parm, false);
           i = i + 1;
       end;
    end;

    macro importEDAsk ()
        var dir, fullPath = "", fullPathOldMes = "";
        var i; 
        
        var sql = "delete USR_ASK_CONTAINER_TMP";
        execSQL (sql);
        
        //debugbreak;                                                 
        dir = TDirList (string (params.path_ask_ed244_in, params.ask_ed244_mask), "f");
        dir.sort (0);
        
        // читаем файлы и заполняем временную таблицу (USR_ASK_CONTAINER_TMP)
        for (i, 0, dir.count - 1)
            fullPath       = string (params.path_ask_ed244_in , dir.name (i));
            fullPathOldMes = string (params.path_ask_ed244_old, dir.name (i));
        
            readEDFile (dir.name (i));
            moveFile   (fullPath, fullPathOldMes);
        end;
    end;

    macro ViewReport ()
        file fp () txt;
        if (open (fp, this.reportName)) viewFile (fp); else MsgBox ("Не удалось открыть файл протокола"); end;
    end;


    macro init()
        //1. Установка выходных параметров
        params.readParams ();

        //2. Импорт отбивок
        this.importEDAsk ();

        //3. Печать отчета
        this.printImportRep ();

        //4. Квитуем ED244 
        this.MatchMessage (); 

        //5. Отражаем в usr_uf_container.xml_ask последнее ED208
        //this.update_xml_ask ();

        //6. Протокол
        this.ViewReport ();

    onError (e)
        if (e.code == 47) // Error: Файл данных или таблица не открыты
            msgbox ("В директории загрузки |" + params.path_ask_ed244_in + "|отсутствуют файлы ED208 для импорта");
        end;
        exit (1);
    end;
end;

AskED244.init ();




