/*******************************************************************************/
/*  RS-Bank 6.0                                          R-Style Software Lab  */
/*                                                                             */
/*  File Name   : fns_pb.mac                                                   */
/*  Created     : 05.07.2012                                                   */
/*  Programmer  : Chesnokov D.                                                 */
/*  Description : Макрос создания подверждений PB2 при отложенной обработке    */
/*                отмене приостановлений Налоговых огранов                     */
/*  Заявка      : C-11468                                                      */
/*******************************************************************************/

import BankInter, "FNS_lib.mac", "wlreq.mac";

private var path, TxtFileDir, resname = "FNS_panel.lbr", pathfile, ExpPath;
private var errCode, CodeClient, CodeNO, Name, dir, ext;
private var dlg_pb;
private var persn;
private file wlr_ROO(wlreq);
private var col = TArray;

private macro ПрочитатьНастройки

  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",V_STRING, path, errCode);
  pathfile = FindPath(resname, path);
  if (not pathfile)
    msgbox("Не найдена LBR с именем " + resname);
    return false;
  end;
  if ( errCode > 0 )
    msgbox("Проверьте наличие настройки: BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR");
    return false;
  end;
    
  dlg_pb = TRecHandler("p_fnspb", pathfile, True);
  persn = Tbfile("persn.dbt", "r");
  
  return true;

end;

macro GetNameAndAdrSoun(CodeSoun, NameSoun, AdrSoun)

var rs, cmd, str;
  
  
  str = " SELECT party.t_name AS name, " + 
        "        DECODE (adr.t_adress, CHR (1), CHR (0), adr.t_adress) AS adress " + 
        "   FROM dobjcode_dbt t, dparty_dbt party, dadress_dbt adr " + 
        "  WHERE t.t_objecttype = 3 " + 
        "    AND t.t_codekind = 28 " + 
        "    AND t_code = :code " + 
        "    AND party.t_partyid = t.t_objectid " + 
        "    AND adr.t_partyid(+) = t.t_objectid ";
        
  cmd = RsdCommand(str);
  cmd.AddParam("code", RSDBP_IN, CodeSoun);
  rs = RsdRecordSet(cmd);
  
  if (rs.Movenext)
    SetParm(1, rs.value(0));
    SetParm(2, rs.value(1))
  else
    SetParm(1, "");
    SetParm(2, "");
  end;

end;

macro HandleEvent (dlg, cmd, id, key)

var str, rs, CodeSoun, NameSoun, AdrSoun;
var const_message = "~F2~ Выполнить ";

/*Обработчик RunScroll*/
private macro EvProc (rs, cmd, id, key )
  debugbreak;
  if ((cmd == DLG_KEY) and (key == 13) )/*ENTER*/
    return CM_SELECT;
  end;
end;

private macro AddCol (ar,ind, fld, head, width, rdonly)
  ar.value (ind * 6)     = fld;
  ar.value (ind * 6 + 1) = head;
  ar.value (ind * 6 + 2) = width;
  ar.value (ind * 6 + 3 ) = 2;   // fldType
  ar.value (ind * 6 + 4 ) = -1;  // decPoint
  ar.value (ind * 6 + 5 ) = 0;   // reserv
end;

  if (cmd == DLG_INIT)
    
    Name = ПолучитьИмяВходФайла( wlr_ROO.reqid );
    dir = SplitFile(Name, name, ext);
    CodeSoun = substr(name, 13, 4);
    GetNameAndAdrSoun(CodeSoun, NameSoun, AdrSoun);
    
    dlg.rec.BeginDate = date(int(substr(name, 23, 2)), int(substr(name, 21, 2)), int(substr(name, 17, 4)));
    dlg.rec.NumRevisionInp = substr(name, 4, 1);
    dlg.rec.NumInFile = substr(name, 26, 6);
    dlg.rec.NumRevisionOut = "2";
    dlg.rec.NameFNS = NameSoun;
    dlg.rec.AdrFNS = AdrSoun;
    dlg.rec.shablonFNS = "PB2_"+name;

    UpdateFields(dlg);
    
  elif (cmd == DLG_SETFOCUS)
    
    if(FldName(dlg, id) == "KodFNS")
      message(const_message + "~F3~ Выбор кода ответа")
    else
      message(const_message);
    end;
    
    UpdateFields(dlg);
    
  elif (cmd == DLG_REMFOCUS)
    
    if (FldName(dlg, id) == "KodFNS")
      if(dlg.rec.kodFNS == "")
         msgbox("Не выбран код ошибки");
         return CM_CANCEL;
      elif (IsDigitalNumber(dlg.rec.kodFNS) == 1)
          msgbox("Поле код результата проверки может содержать только числовые значения");
          return CM_CANCEL;
      end;
    end;
    
  elif (cmd == DLG_KEY)
    
    if (key == KEY_ESC)
      if (GetTrue(TRUE,"Вы действительно хотите выйти?"))
        exit(1);
        return CM_CANCEL;
      else
        return CM_IGNORE;
      end;
      
    elif (key == KEY_F2)
      
      if (dlg.rec.kodFNS == "")
        msgbox("Не указан код результата проверки");
        return CM_IGNORE;
      elif (dlg.rec.textFNS == "")
        msgbox("Не указано текстовое пояснение");
        return CM_IGNORE;   
      end;
      return CM_SAVE;
      
    elif (key == KEY_F3)
      
      if (FldName(dlg, id) == "KodFNS")
        
        str = RsdCommand ("select to_char(31) t_num, 'Электроный документ ошибочно направлен налоговым органом не в тот банк (филиал банка)' t_text from dual " 
                          "union all "
                          "select to_char(32) t_num, 'В банке (филиале банка) отсутствует номер счета клиента указанного в электронном документе' t_text from dual " 
                          "union all "
                          "select to_char(33) t_num, 'Наименование клиента не соответствует номеру счета клиента указанного в электронном документе' t_text from dual " 
                          "union all "
                          "select to_char(34) t_num, 'Невозможно исполнение поручения налогового органа в установленый срок в связи отсутсвия денежных средств на кореспондентском счете банка (филиала банка)' t_text from dual " 
                          "union all "
                          "select to_char(35) t_num, 'Другие случаи' t_text from dual ");

        rs = RSDRecordset(str, RSDVAL_CLIENT, RSDVAL_STATIC );
            
        AddCol (col, 0, "t_num",  "Номер",        10 , true);
        AddCol (col, 1, "t_text", "Наименование", 100, true);
            
        if (rs.moveNext () )
          if (RunScroll (rs, 2, col, "test", "EvProc", "Выбор", "~Esc~ Выйти", true))
            dlg.rec.KodFNS = int(rs.Value(0));
            if (dlg.rec.KodFNS!=35)
              dlg.rec.textFNS = rs.Value (1);
            else
              dlg.rec.textFNS = "";
            end;
          end;
        end;
      
      end;
      
    elif (key == KEY_F9)
      return CM_IGNORE;
    end;
    
    UpdateFields(dlg);
  end;

end;

macro Create_PB(dlg_pb, wlr_ROO)

var stat, out_filename;

  stat = CreatePB(wlr_ROO, dlg_pb.rec.KodFNS +";"+ dlg_pb.rec.TextFNS, "2", out_filename);
  if( stat != 0 )
    msgbox("Ошибка при обработке запроса "+wlr_ROO.ReqID);
    return FALSE;
  end;
  
  Name = ПолучитьИмяВходФайла( wlr_ROO.reqid );
  dir = SplitFile(Name, name, ext);
  name = name + ext;
  
  insert_MES365_LOG(wlr_ROO.reqid, Name, wlr_ROO.bankDATE, ОтрезатьПутьОтИмениФайла(out_filename), {curdate}, wlr_ROO.OriginatorID);
  
  return true;
end;

macro Gen_PB(reqid)
  
  wlr_ROO.reqid = reqid;
  if(not geteq(wlr_ROO))
    msgbox("Не могу спозиционироваться на запись.");
    return false;
  else
    if(ПрочитатьНастройки)
      if (RunDialog(dlg_pb, "HandleEvent"))
        if (Create_PB(dlg_pb, wlr_ROO))
          return true;
        else
          return false;
        end;
      end;
    end;
    return false;
  end;
end;
