/*******************************************************************************/
/*  RS-Bank 6.0                                          R-Style Software Lab  */
/*                                                                             */
/*  File Name   : fns_bos.mac                                                  */
/*  Created     : 01.12.2011                                                   */
/*  Programmer  : Chesnokov D.                                                 */
/*  Description : Макрос генерации справки об остатках на счетах, через        */
/*                пользовательскую функциональность Ctrl+Z                     */
/*  Rev.        : 1.01 Chesnokov D. 27.12.2011 Изменена обработка директорий   */
/*                для формирования файлов выгрузки                             */
/*  Изменен     : Зленко М.П. по заявке I-00137971-1  I-00138309-1 I-00138442-1*/
/*                I-00140109-1  I-00139468  I-00141186-1  I-00143434-1         */
/*                I-00145483-1                                                 */
/*                Чесноков Д.С. по заявке I-00142992 от 17.01.2012             */
/*              : 18.01.2012 Чесноков Д.С.по заявке С-8297                     */
/*              : 25.01.2012 Gurin S. N. C-8300-6, C-8473-6                    */
/*******************************************************************************/
import BankInter, globals,  RSD, PsInter, currinter, rsexts;
import "wlmnstls.mac", "FNS_lib.mac", "FNS_const.mac";

/*Создание файла информационного сообщения*/
macro CreateBNS(wlreq, mes, fl_name, PersData)

  var party = Tbfile("party.dbt","r");
  var persn = Tbfile("persn.dbt", "r");
  var INN, KPP, INNNP, KPPNP;
  var stat, Error = 0, Code = "", ind = 0, tmpstr = "";
  var TxtFileDir, refnumber, docnumber, ExpPath, ДолжнПрБ, ФИОПрБ;
  var rs, fullfilename, name, dir, ext, cnum;
  var mas_filename = tarray();

  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, TxtFileDir, error);
  if ( error > 0 )
    msgbox("Проверьте наличие настройки: BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR");
    return 1;
  end;

  GetRegistryValue("PS\\REQOPENACC\\OPERATION\\РЕФЕРЕНС_СПРАВКАОСТ", V_INTEGER, refnumber, error);
  if ( error > 0 )
    msgbox("Проверьте наличие настройки: PS\\REQOPENACC\\OPERATION\\РЕФЕРЕНС_СПРАВКАОСТ");
    return 1;
  end;
  
  GetRegistryValue("PS\\REQOPENACC\\OPERATION\\СОЗДАНИЕ ИНФ СООБЩЕНИЙ\\ДИРЕКТОРИЯ ВЫГРУЗКИ", V_STRING, ExpPath, error);
  if ( error > 0 )
    msgbox("Проверьте наличие настройки: PS\\REQOPENACC\\OPERATION\\СОЗДАНИЕ ИНФ СООБЩЕНИЙ\\ДИРЕКТОРИЯ ВЫГРУЗКИ");
    return 1;
  end;

  GetRegistryValue("PS\\REQOPENACC\\OPERATION\\СПРАВКИ_ИФНС_ДОЛЖ", V_STRING, ДолжнПрБ, error);
  if ( error > 0 )
    msgbox("Проверьте наличие настройки: PS\\REQOPENACC\\OPERATION\\СПРАВКИ_ИФНС_ДОЛЖ");
    return 1;
  end;
  
  GetRegistryValue("PS\\REQOPENACC\\OPERATION\\СПРАВКИ_ИФНС_ФИО", V_STRING, ФИОПрБ, error);
  if ( error > 0 )
    msgbox("Проверьте наличие настройки: PS\\REQOPENACC\\OPERATION\\СПРАВКИ_ИФНС_ФИО");
    return 1;
  end;

  rs = trsbdataset("select RSBSESSIONDATA.CNUM  from dual ");
  if (rs and rs.movenext)
     cnum = int(rs.cnum);
  else
     cnum = {oper};
  end;
  
  dir = SplitFile(mes.FileName, name, ext);
  fullfilename = TxtFileDir+"\\BOS1_" + name + "."+cnum;

  /*Переопределим вывод в нужный нам файл*/
  SetOutPut(fullfilename, false);
  
  generatereference(refnumber,docnumber);

  //zmp 15.07.2014 R-412157 
  if(valtype(docnumber) == v_undef)
     msgBox("Не удалось получить значение референса " + refnumber + ". Попробуйте перезайти в RS-Bank и обработать сообщение повторно.");
     return 1;
  end;
 
  SplitFullInn(ПолучитьКодСубъекта(wlreq.RecipientID, PTCK_INN, Error), INN, KPP);
  println("ИдФайл:" + INN + "**" + KPP + strYYYYMMDD({curdate}) + docnumber);
  println("ТипИнф:НАЛСЧЕТОВ");
  println("ВерсПрог:RS-Bank V.6");
  
  println("ТелОтпр:"+PersData.Phone);
  println("ДолжнОтпр:"+PersData.Officer);
  println("ФамОтпр:"+PersData.SurName);
  
  println("КолДок:1");
  println("ВерсФорм:2.01");
  
  println("###");
  println("@@@");
  
  println("ИдДок:" + execStoredFunc( "sys_guid", V_STRING ));
  println("НомСправ:"+ docnumber);
  println("ИННКО:"+INN);
  println("КППКО:"+KPP);
  
  
  println("БИК:"+ПолучитьКодСубъекта(wlreq.RecipientID, PTCK_BIC, error, 1));
  println("НаимКО:"+wlreq.RecipientName);
  Code = ПолучитьКодСубъекта( wlreq.RecipientID, PTCK_BANKREGNUM, Error);
  ind = index(Code, "/");
  if ( (ind == 0) or (ind == strlen(Code) ) )
    println("НомФ:0");
  else
    println("НомФ:" + SubStr(Code, ind + 1) );
  end;
  
  if (ПолучитьИмяФормы(wlreq.ReqID) == ФормаZNO)
    println( "НомЗапр:", Mes.НомЗапр );
    println( "ДатаЗапр:", ДатаДДpММpГГГГ(date(Mes.ДатаЗапр)) );
  elif (ПолучитьИмяФормы(wlreq.ReqID) == ФормаRPO)
    println( "НомРеш:", Mes.НомРешПр );
    println( "ДатаРеш:", ДатаДДpММpГГГГ(date(Mes.ДатаРешПр)) );    
  end;
  
  if (wlreq.OriginatorID > 0)
    Code = ПолучитьКодСубъекта( wlreq.OriginatorID, PTCK_MNS, Error);
  else
    Code = wlreq.OriginatorCode;
  end;
  println("КодНО:" + Code);
  
  splitfullinn(ПолучитьКодСубъекта(mes.partyid, PTCK_INN, Error),INNNP,KPPNP);
  
  println("ИНННП:" + INNNP);
  /*КППНП*/
  if (strlen(KPPNP)==9)
    println("КППНП:" + KPPNP );
  elif ( strlen(INNNP) != 12 )
    println("КППНП: ");
  end;
  /*НаимНП / ФИОИП*/
  
  if ( strlen(INNNP) != 12 )
    if (mes.partyid > 0)
      party.rec.PartyID = mes.partyid;
      party.GetEQ();
      println("НаимНП:" + party.rec.Name);
    else
      println("НаимНП:Не найден субъект");
    end;
  else
    if (mes.partyid > 0)
      persn.rec.PersonID = mes.partyid;
      persn.GetEQ();
      println("ФИОИП:" + persn.rec.Name1 + "," + persn.rec.Name2 + "," + persn.rec.Name3);
    else
      println( "ФИОИП:" + РазделитьЗапятымиФИО(Trim("Не найден субъект")) );
    end;
  end;
  
  println("ДолжнПрБ:"+ДолжнПрБ);
  println("ФИОПрБ:"+РазделитьЗапятымиФИО(ФИОПрБ));
  
  /*ДатаСправ*/
  println( "ДатаСправ:", ДатаДДpММpГГГГ({curdate}) );
  /*ДатаСооб*/
  println( "ДатаСооб:", ДатаДДpММpГГГГ(date) );
  println("###");
 
   /*Блок "Счет" */
  var rs_acclnk;
  record Acc(account);
  var block = "Счет";
  
  //Lavrenov: I-00146116 теперь для всех запросов счета берутся из одного места
  rs_acclnk = getRsWlacclnk(wlreq.reqid, 505);
  
  while(rs_acclnk and rs_acclnk.MoveNext() )
    if ((rs_acclnk.value("t_State") == 0) or (rs_acclnk.value("t_State") == 100))          
      ПолучитьСчет( rs_acclnk.value("t_FIID"), rs_acclnk.value("t_Account"), Acc );
      
      /*НомСч*/
      println("НомСч:" + Acc.Account );
      /*ВидСч*/
      println("ВидСч:" + ПолучитьВидСчДляСправки(Acc.Type_Account) );
      /*ВалСч*/
      println("ВалСч:" + ПолучитьКодФинИн_usr(Acc.Code_Currency) );
      /*ДатаОткрСч*/
      println( "ДатаОткрСч:" + ДатаДДpММpГГГГ(Acc.Open_Date) );
      /*ДатаЗакрСч*/
      if(Acc.Close_Date != date(0,0,0))
        println( "ДатаЗакрСч:" + ДатаДДpММpГГГГ(Acc.Close_Date) );
      end;
      
    end;
    println("###");
  end;

  println("@@@");
  println("===");
  
  SetOutput(null, true);
  
  Name = "BNS1_" + Name + ".vrb";

  removefile(ExpPath + Name);
  if (CopyFile(fullfilename, ExpPath + "\\" + Name))
    mas_filename(mas_filename.size) = Name;
    setparm(2, mas_filename);
    return 0;
  else
    msgbox("Не удалось скопировать файл "+ Name + "| в каталог "+ ExpPath + "\\");
    return 1;
  end;

end;