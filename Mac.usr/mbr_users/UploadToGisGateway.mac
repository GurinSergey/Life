// --------------------------------------------------------------------
// @filename: UploadToGisGateway.mac
// @author  : Teleshova A. 
// @desc    : Отправка сообщения ГИС ГМП в очередь в Шлюз
// @changes : TAM 29.10.2013 С-23435
// ---------------------------------------------------------------------

import rcw;
import "wluftool.mac", "fg_Life_parm.mac";

//Константы
const REFID_GISNUMBER = 1000045;
const Login = "rsbank";
const Password = "k1nda!pass";
const RESULT_SUCCESS = 9500;

//Константы кодов банков
const PRBB_bankid      = 252;
const VUZ_bankid       = 2519;
const Saratov_bankid   = 133;
const Volgograd_bankid = 127;
const Ulyanovsk_bankid = 7743;
const Voronezh_bankid  = 9000;
const Stavropol_bankid = 8484;
const GEB_bankid       = 1963;
const GO_bankid        = 8437;
const NBS_bankid       = 536;
const SLD_bankid       = 346;

private const fgBank = fg_life_subject({OurBank});

macro GetPaymentIdByTrn(id_mes)
   var sqlstring, cmd, data, source = "";
   sqlstring = " SELECT  UTL_RAW.CAST_TO_VARCHAR2 (DBMS_LOB.SUBSTR (t_fmtblobdata_xxxx, 200)) source " +
               " FROM    dwlinfo_dbt inf, dwlmeslnk_dbt lnk                                          " +
               " WHERE   LNK.T_MESID = ? AND INF.T_INFOID = LNK.T_OBJID                              ";
   cmd = RsdCommand(sqlstring);
   cmd.AddParam("mesid", RSDBP_IN, id_mes);
   cmd.execute();
   data = RsdRecordset(cmd);
   if(data.movenext())
      source = data.value("source");
   end;
   if(source != "")
      return int(Substr(source,index(source,"ID платежа")+11));
   else
      return 0;
   end;
end;

//Сохраняем информация о результатах обработки сервисом сообщений ГИС ГМП
macro SaveInfoForGisMonitor(request, response, mes, mes_trn, pmdate,error, err_desc, request_time, response_time, status)
   var command, rsdcmd;
   command = " INSERT INTO USR_GIS_GMP_MONITOR_DBT                                                                                                        " +
             " (T_REQUEST_ID, T_RESPONSE_ID, T_PAYMENT_ID, T_MES_TRN, T_DATE, T_ERROR_CODE, T_ERROR_DESC, SEND_REQUEST_TIME, GET_RESPONSE_TIME, T_STATUS) " +
             " VALUES   (?, ?, ?, ?, ?, ?, ?, ?, ?, ?)                                                                                                    ";
   rsdcmd = RSDCommand(command);
   rsdcmd.addparam("request_id"  ,  RSDBP_IN,  request);
   rsdcmd.addparam("response_id" ,  RSDBP_IN,  response);
   rsdcmd.addparam("payment_id"  ,  RSDBP_IN,  GetPaymentIdByTrn(mes));
   rsdcmd.addparam("mes_trn"     ,  RSDBP_IN,  mes_trn);
   rsdcmd.addparam("date"        ,  RSDBP_IN,  pmdate);
   rsdcmd.addparam("err_code"    ,  RSDBP_IN,  error);
   rsdcmd.addparam("err_desc"    ,  RSDBP_IN,  err_desc);
   rsdcmd.addparam("request_tm"  ,  RSDBP_IN,  request_time);
   rsdcmd.addparam("response_tm" ,  RSDBP_IN,  response_time);
   rsdcmd.addparam("status"      ,  RSDBP_IN,  status);
   rsdcmd.execute();
end;

macro Send_Request(mes_xml, mes_id, payment_date, mes_trn):bool
   debugbreak;
   var ret, GenNumber;
   private var jvm = CreateObject ("rsjvm", "TJavaHost", "GlobalJavaHost");
   private var jrequest = jvm.createJavaObject ("ru.softlab.integration.ws.SmevRequest");
   private var jloginData = jvm.createJavaObject ("ru.softlab.integration.ws.LoginData");
   jloginData.login = Login;
   jloginData.password = Password;
   jrequest.loginData = jloginData;
   ret = GenerateReference(REFID_GISNUMBER, GenNumber);
   
   if (ret > 0)
      msgbox ("При формировании номера запроса ГИС ГМП по референсу возникла ошибка " + ret + "!|" + "Формирование номера невозможно!");
   else 
      jrequest.orgRequestId = GenNumber;
   end;

   if   (fgBank.is_PRBB)
      jrequest.bankId = PRBB_bankid;
   elif (fgBank.is_VUZ)
      jrequest.bankId = VUZ_bankid;
   elif (fgBank.is_EXV_Volgograd)
      jrequest.bankId = Volgograd_bankid;
   elif (fgBank.is_EXV_Ulyanovsk)
      jrequest.bankId = Ulyanovsk_bankid;
   elif (fgBank.is_EXV_Voronezh)
      jrequest.bankId = Voronezh_bankid;
   elif (fgBank.is_EXV_Stavropol)
      jrequest.bankId = Stavropol_bankid;
   elif (fgBank.is_EXV)
      jrequest.bankId = Saratov_bankid;
   elif (fgBank.is_GEB)
      jrequest.bankId = GEB_bankid;
   elif (fgBank.is_GO)
      jrequest.bankId = GO_bankid;
   elif (fgBank.is_NBS)
      jrequest.bankId = NBS_bankid;
   //TAM 19.06.2014 C-29126
   elif (fgBank.is_SLD)
      jrequest.bankId = SLD_bankid;
   end;
   jrequest.reqDt  = date();
   jrequest.env    = 1;
   jrequest.sidId  = "SID0003572";
   jrequest.requestName = "UnifoTransferMsg";
   jrequest.desc = "";
   jrequest.message = mes_xml;
   
   private var jservice = jvm.createJavaObject ("ru.softlab.integration.ws.SmevService_Service");
   //private var jservice = jvm.createJavaObject ("ru.softlab.integration.ws.SmevService_Service",
   //                                             "<init>@(Ljava/lang/String;)V","http://172.16.0.209:7001/SmevService/SmevService?WSDL");
   private var jcaller = jservice.getSmevServicePort();
   var send_request_time = string(date()," ",time());
   var result = jcaller.wsuSendRequestQueueSmev(jrequest);
   var get_response_time = string(date()," ",time());
   //TAM 29.10.2013 C-23435
   if (result.status == 99)
      SaveInfoForGisMonitor(jrequest.orgRequestId, result.smevRequestId, mes_id, mes_trn, payment_date, result.errCode, result.errMess,send_request_time, get_response_time, result.status);
      return false;
   else
      SaveInfoForGisMonitor(jrequest.orgRequestId, result.smevRequestId, mes_id, mes_trn, payment_date, 0, "Передан в шлюз", send_request_time, get_response_time, result.status);
      return true;
   end;
end;