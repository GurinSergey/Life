// @filename: abby_report.mac
// @author  : Гурин С.
// @desc    : Отчет открытых платежных поручений ABBYY FC
// @request : C-18485 
// @modify  : Gurin S. 29.07.2013 I-00405020-2 (в отчет добавил платежи с отвергнутым шагом)
// -------------------------------------------------------------------------------------------------
import SbCrdInter, RSD, treport;

var dateBegin, dateEnd; 
var reportName = getTxtFileName ("abby_report");

private macro Report(BegDate, EndDate)
    var sql, cmd, rs, title;
    var table = CTableReport();
    var i = 0;

    SetOutput (reportName, false);

    if (BegDate == EndDate) 
        title = string(BegDate); 
    else 
        title = string("период с ", BegDate," по ", EndDate); 
    end;
        
    println (string ("                Отчет открытых платежных поручений ABBYY FC за ", title):c:100);
    println (string ("                                   Дата - ", date, ", время - ", time):c:70);

    sql = "   SELECT   DPMRM.T_NUMBER numdoc, DPM.T_PAYERACCOUNT payeracc, DPM.T_RECEIVERACCOUNT recacc, DPM.T_AMOUNT amount, " + 
          "            DPM.T_NUMBERPACK numpack, DPM.T_OPER oper, TO_CHAR(DPM.T_CREATIONDATE,'DD.MM.YYYY') createdate, "        +
          "            DOPST.T_NAME stepname "                                                                                  +
          "     FROM   dpmpaym_dbt dpm, dpspayord_dbt dps, dpmrmprop_dbt dpmrm, doprstep_dbt dstep, "                           +
          "            doproper_dbt dopr, doprostep_dbt dopst "                                                                 +
          "    WHERE       DPM.T_PAYMENTID = DPS.T_ORDERID "                                                                    +
          "            AND dpm.T_PAYMENTID = DPMRM.T_PAYMENTID "                                                                +
          "            AND DPMRM.T_SHIFROPER = '01' "                                                                           +
          "            AND DOPR.T_DOCKIND = 201 "                                                                               +
          "            AND DOPR.T_DOCUMENTID = LPAD (dpm.T_PAYMENTID, 34, '0') "                                                +
          "            AND DOPR.T_ID_OPERATION = DSTEP.T_ID_OPERATION "                                                         +
          "            AND ( (DSTEP.T_ISEXECUTE = 'R' "                                                                         +
          "                  AND DSTEP.T_BLOCKID NOT IN (10000145, 1029058)) "                                                  +
          "                OR (DSTEP.T_ISEXECUTE = 'W' "                                                                        +
          "                    AND DSTEP.T_BLOCKID NOT IN (1029058))) "                                                         +
          "            AND dpm.t_valuedate BETWEEN TO_CHAR (:BegDate, 'dd.mm.yyyy') "                                           +
          "                                    AND  To_CHAR (:EndDate, 'dd.mm.yyyy') "                                          +
          "            AND dps.t_origin = 2100 "                                                                                +
          "            AND DOPST.T_BLOCKID = DSTEP.T_BLOCKID "                                                                  +
          "            AND DOPST.T_NUMBER_STEP = DSTEP.T_NUMBER_STEP "                                                          +
          " ORDER BY   DPM.T_PAYMENTID";                                                                                        

    cmd = RsdCommand(sql);                 

    cmd.AddParam("BegDate", RSDBP_IN, BegDate);
    cmd.AddParam("EndDate", RSDBP_IN, EndDate);
    cmd.execute();
    rs = RsdREcordset(cmd);
    
    table.addColumn ("№ п/п",                   5, AL_RIGHT);
    table.addColumn ("Счет плательщика",        20, AL_LEFT );
    table.addColumn ("Счет получателя",         20, AL_LEFT );
    table.addColumn ("Сумма п/п",               10, AL_RIGHT);
    table.addColumn ("№ пачки",                 7,  AL_RIGHT);
    table.addColumn ("№ операциониста",         10, AL_RIGHT);
    table.addColumn ("Дата загрузки документа", 20, AL_LEFT );
    table.addColumn ("Шаг документа",           30, AL_LEFT );

    table.printHead ();

    while (rs and rs.movenext())
        table.printStringTransferByWord ( rs.value ("numdoc"),  rs.value ("payeracc"), rs.value ("recacc"),    rs.value ("amount"), 
                                          rs.value ("numpack"), rs.value ("oper"),     rs.value ("createdate"),  rs.value ("stepname"));
        i = i + 1;
    end;
            
    table.printBottom ();
    println ("Всего файлов: ", i);

    SetOutput (NULL, true);
    ViewFile(reportName);
end; 
  
if (ВвестиПериодДат (dateBegin, dateEnd))
    Report(dateBegin, dateEnd);    
end;