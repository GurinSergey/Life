	/*
Отчет о правах доступа пользователя 
Гурин С. Н. 30.11.2011 Сортировка групп по t_groupid  I-00126499-2
*/
import BankInter, "globals.mac", RSD, RsbDataSet;
import oralib, likepy,treport;
import RSD, ldmklbr;
import  KeyCodes;
private array main;
private var varmain,flag = false;;


private macro PrintHeader()
  [
  
                      
                      Отчет о правах доступа пользователя ];
end;

private macro PrintDprt()
  var strSql : string;
  var rs : RsdRecordset;
  var Dprt : string;

  strSql = "SELECT dp.t_Name, pt.t_ShortName " +
           "FROM ddp_dep_dbt dp, dparty_dbt pt " +
           "WHERE dp.t_Code = " + {OperDprtNode} + " AND pt.t_PartyID = dp.t_PartyID";

  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    Dprt = rs.value( 0 ) + " " + rs.value( 1 );
  end;
  [
        Подразделение: ##########################################################] ( Dprt );
end;

private macro PrintUser( userType, userNum )
  var strSql : string;
  var rs : RsdRecordset;
  var User : string;
  array _User;
  var j : integer;

  strSql = "SELECT t1.t_oper, t2.t_name_type, t1.t_name, dp.t_Name, pt.t_ShortName " +
           "FROM dperson_dbt t1, dtypeac_dbt t2, ddp_dep_dbt dp, dparty_dbt pt     " +
           "WHERE                                                                  " +
           "  t1.t_oper = " + userNum + " AND                                      " +
           "  t2.t_inumtype = 13 AND                                               " +
           "  t2.t_type_account = t1.t_ctypeperson AND                             " +
           "  dp.t_Code = t1.t_codedepart AND                                      " +
           "  pt.t_PartyID = dp.t_PartyID                                          ";
           
  rs = RsdRecordset( strSql );

  if( rs.MoveNext())
    User = rs.value( 0 ) + ", " + rs.value( 1 ) + ", " + rs.value( 2 ) + ", " + rs.value( 3 ) + " " + rs.value( 4 );
    StrSplit( User, _User, 58, 58, 2 );
    j = 0;
    while( StrLen(_User( j )) > 0 )
      if( j == 0 )
        [
              ############## ##########################################################] ( userType, _User( j ));
      else
        [                    ##########################################################] ( _User( j ));
      end;
      j = j + 1;
    end;
  end;
end;

private macro PrintDate()
  [
        Дата/время: ########## ########] ( date, time );
end;

private macro PrintTop( curOper, repOper )
  PrintDprt();
  PrintUser( "Пользователь:", repOper );
  PrintUser( "Сформирован: ", curOper );
  PrintDate(); 
end;

private macro PrintRole( repOper )
  var strSql : string;
  var rs : RsdRecordset;
  var Role : string;
  array _Role;
  var j : integer;

  strSql = "SELECT t1.t_name                                    " +
           "FROM dacsroletree_dbt t1, dacsoprole_dbt t2         " +
           "WHERE t1.t_roleid = t2.t_roleid AND t2.t_oper = " + repOper +
           "ORDER BY t1.t_name                                  ";

  rs = RsdRecordset( strSql );

  j = 0;
  Role = "";
  while( rs.MoveNext())
    if( j > 0 )
      Role = Role + ", ";
    end;
    Role = Role + rs.value( 0 );
    j = j + 1;
  end;
  StrSplit( Role, _Role, 72, 72, 10 );
  j = 0;
  while( StrLen(_Role( j )) > 0 )
    if( j == 0 )
      [
      Роли: ########################################################################] ( _Role( j ));
    else
      [      ########################################################################] ( _Role( j ));
    end;
    j = j + 1;
  end;
end;

private macro PrintGroup( repOper )
  var strSql : string;
  var rs : RsdRecordset;
  var Group : string;
  array _Group;
  var j : integer;

  strSql = "SELECT DISTINCT t1.t_name, t1.t_groupid                             " +
           "FROM dacsgroup_dbt t1, dacsgroupoper_dbt t2           " +
           "WHERE t1.t_groupid = t2.t_groupid AND t2.t_oper = " + repOper +
           "ORDER BY t1.t_groupid                                    ";

  rs = RsdRecordset( strSql );

  j = 0;
  Group = "";
  while( rs.MoveNext())
  /*  if( j > 0 )
      Group = Group + ", ";
    end; */
    Group = Group + "(" + rs.value( 1 ) + ")" + rs.value( 0 )+ "\n";
    j = j + 1;
  end;
  StrSplit( Group, _Group, 70, 70, 10 );
  j = 0;
  while( StrLen(_Group( j )) > 0 )
    if( j == 0 )
      [
      Группы: ######################################################################] ( _Group( j ));
    else
      [        ######################################################################] ( _Group( j ));
    end;
    j = j + 1;
  end;
end;

private macro PrintRestrict( PrivID, PrivName, repOper )
  var IsExist : bool;
  var RestValue : string;
  array _RestValue;
  var j : integer;
  var stat;

  stat = RebuildResultRestrict( PrivID, repOper, IsExist, RestValue );
  if(( not stat ) and strlen( RestValue ))
    [
    {####} ########################################################################] ( PrivID, PrivName );
    StrSplit( RestValue, _RestValue, 71, 71, 10 );
    j = 0;
    while( StrLen(_RestValue( j )) > 0 )
      [       #######################################################################] ( _RestValue( j ));
      j = j + 1;
    end;
  end;
end;

private macro PrintPrivilege( repOper )
  var strSql : string;
  var rs : RsdRecordset;

  strSql = "SELECT t.t_nodeid, t.t_name FROM dacsprivtree_dbt t WHERE t.t_nodeid > 0 ORDER BY t.t_nodeid";

  rs = RsdRecordset( strSql );

  [
  Привилегии:];
  while( rs.MoveNext())
    PrintRestrict( rs.value( 0 ), rs.value( 1 ), repOper );
  end;
end;

private macro PrintARM ( repOper )
	//debugbreak;
	var sql = "SELECT menu.t_name FROM dperson_lnk_dbt pers, dmenutpl_dbt menu WHERE menu.t_menuid = pers.t_role AND pers.t_user = "+ repOper +"; ";
	var cmd = TRsbDataSet(sql);
	cmd.MoveNext ();
	[
	 Типовой АРМ: ##################################################################] (IfThenElse(ValType(cmd.t_name) == V_UNDEF," Отсутсвует ",cmd.t_name));
end;

private macro PrintContext( repOper )
  PrintRole( repOper );
  PrintARM( repOper ); //Агеев С. В. типовой АРМ
  PrintGroup( repOper );
  //PrintPrivilege( repOper );

end;

private macro GetPath( a )

   var sql , rs, path = "", prev;
   prev = a;
    //   println("!!!");
   while (prev!=0)
// println("///");

      sql = " select t_parentid, t_name from dregparm_dbt where t_keyid = '"+prev+"' ";
      rs = RsdRecordset(sql);
      rs.movenext();
      path =  rs.value(1) +"/"+ path;
      prev = rs.value(0);
   end;
       return path
end;


private macro PrintReg( repOper )
  [

  
  Личные настройки ресстра для пользователя   


  ];
   
  var cmdtext, rs;
//debugbreak;
//  cmdtext = " select t_keyid, t_lintvalue from dregval_dbt where t_objectid = '"+repOper+"' and t_keyid not in (2985,3722,3724,3733,3734,3735,3736,3789,3800,3801,3802,3806,3807,3860,3881,3884,3886,3888,3891,3896,3730,3897,3758,3759,3803,3887,3909,3779,3814,3876,3882,3892,3908,3794,3883,3894,3895,3898,3917,3809,3893,3916,3768,3919,3767,3776,3921,3920,3845,3905,3824,3906,3820,3511,3718,3728,3731,3738,3752,3760,3763,3764,3770,3771,3772,3774,3782,3783,3784,3785,3786,3791,3795,3796,3797,3805,3815,3817,3825,3830,3834,3836,3837,3838,3840,3841,3843,3846,3856,3859,3872,3873,3875,3878,3899,3901,3902,3516,3725,3727,3761,3762,3773,3775,3812,3855,3874,3880,3911,3720,3723,3726,3732,3737,3743,3744,3745,3748,3751,3753,3765,3780,3781,3787,3788,3790,3792,3804,3813,3821,3829,3835,3847,3848,3851,3852,3853,3854,3857,3858,3890,3900,3903,3904,3907,3740,3741,3742,3747,3749,3750,3769,3818,3839,3913,3918,3922,3923,3755,3756,3777,3879,3910,3912,3914,3799,3877,3885,3889,3850) order by t_keyid  ";
  cmdtext = " select t_keyid, t_lintvalue from dregval_dbt where t_objectid = '"+repOper+"' and t_keyid not in (select t_keyid from dregparm_dbt where t_parentid in (select t_keyid from dregparm_dbt where t_parentid = 3509) "+
" union  "+
" select t_keyid from dregparm_dbt where t_parentid = 3509 "+
" union "+
" select t_keyid from dregparm_dbt where t_parentid in (select t_keyid from dregparm_dbt where t_parentid = 3252) "+
" union  "+
" select t_keyid from dregparm_dbt where t_parentid = 3252) order by t_keyid  ";
  rs = RsdRecordset(cmdtext);
  while(rs.MoveNext)
     // println( rs.value(0));
      [#######################################################################    ########
](GetPath( rs.value(0) )   , rs.value(1) );
  end;

end;

macro getSID()
var sql;
    sql = RSDRecordset("Select RSBSessionData.cnum from dual");
    if (sql.movenext)
       return  sql.value(0, null, V_INTEGER);
    end;
       return 0;
end; 

macro getSP()
  var sql:string = "select v.terminal from v$session v where  v.sid =:SID " ;	
  var params:Tarray = makeArray(SQLParam(":SID",getSID()));
  var rset:RsdRecordset = execSQLselect(sql, params, TRUE );
  
  if( rset and rset.moveNext() )
    return rset.value(0);
  end;
    return 0;  
end;

macro isBlocked(_oper)
  var sql:string = "SELECT T_OPER, T_NAME  FROM   dperson_dbt   where t_userblocked = 'X' and t_oper =:oper";
  var params:Tarray = makeArray(SQLParam(":oper",_oper ));
  var rset:RsdRecordset = execSQLselect(sql, params, TRUE );
  
  if( rset and rset.moveNext() )
    return true;
  end;
    return false;
end;



private macro PrintBottom( curOper,repOper)
  var strSql : string;

  if (isBlocked(repOper)) 
      [
           ВНИМАНИЕ! ПОЛЬЗОВАТЕЛЬ ЗАБЛОКИРОВАН.] ;
  end;


  var rs : RsdRecordset;
  
  strSql = "SELECT t2.t_post, t1.t_name                      " +
           "FROM dperson_dbt t1 FULL JOIN dofficer_dbt t2 ON " +
           "( t2.t_personid = t1.t_partyid ) WHERE t1.t_oper = " + curOper;
           
  rs = RsdRecordset( strSql );
  
  if( rs.MoveNext())
    if( StrLen(rs.value( 0 )) > 0 )
      [
      #####################________________________#################################] ( rs.value( 0 ), rs.value( 1 ));
    else
      [
                           ________________________#################################] ( rs.value( 1 ));
    end;
  end;
end;


macro ActivexInfo()
   var startAX, WordApplication;
   var table = ctablereport;  		
    startAX = CreateObject("rsax", "TRsAxServer", "RsAxServer", isStandalone());
    WordApplication = startAX.CreateComObject("Word.Application");
    WordApplication.Visible = false;

    table.addcolumn("Наименование",30); 
    table.addcolumn("Значение",70);
    table.printhead("                                          Дополнительная информация");
    table.printstringtransferbyword("Имя",WordApplication.name);
    table.printstringtransferbyword("Версия",WordApplication.version);
    table.printstringtransferbyword("Сборка",WordApplication.build);
    table.printstringtransferbyword("Имя пользователя",WordApplication.username);
    table.printstringtransferbyword("Путь",WordApplication.path);
    table.printstringtransferbyword("Путь запуска",WordApplication.startuppath);
    table.printstringtransferbyword("Объект","CreateObject(\"rsax\", \"TRsAxServer\", \"RsAxServer\", isStandalone())"); 
    table.printseparator();
    table.printstringtransferbyword("ОС",WordApplication.System.operatingSystem);
    table.printstringtransferbyword("ОС версия",WordApplication.System.version);
    table.printstringtransferbyword("Место на диске",WordApplication.system.freediskspace);
    table.printseparator();
    table.printstringtransferbyword("SID",getSID());
    table.printstringtransferbyword("СП",getSP());
    table.printbottom();
    run(getenv ("COMSPEC"),"/c ipconfig | find \"IP\"",">");

end;

//Добавляем колонки в скроллинг
macro AddCol (ar,ind, fld, head, width, rdonly)
   ar.value (ind * 6)     = fld;
   ar.value (ind * 6 + 1) = head;
   ar.value (ind * 6 + 2) = width;
   ar.value (ind * 6 + 3 ) = 2;   // fldType
   ar.value (ind * 6 + 4 ) = -1;  // decPoint
   ar.value (ind * 6 + 5 ) = 0;   // reserv
end;

macro EvProc (rs, cmd, id, key ) 
   if (cmd == DLG_KEY) 
      if ((key == KEY_ENTER) or (key == KEY_F2))
        return CM_SELECT;
      end;
   end;
end;


macro printReport( curOper, repOper )
  PrintHeader();
  PrintTop( curOper, repOper );

  PrintContext( repOper );
  //PrintReg(repOper);
  PrintBottom( curOper,repOper);

end;

macro start()
    var rcmd = RsdCommand ("SELECT T_OPER, T_NAME  FROM   dperson_dbt   where t_userclosed <> 'X' and t_oper > 0 ORDER BY T_OPER");
    var rs = RSDRecordset(rcmd , RSDVAL_CLIENT, RSDVAL_STATIC );
     
    var col = TArray;
    AddCol (col, 0, "T_OPER", "Номер", null, true);
    AddCol (col, 1, "T_NAME", "Пользователь",    null, true);

    if((RunScroll (rs, 2, col, "OPER_CASE", "EvProc", "Списокок пользователей", "~Enter~ выбор")) )
       printReport({OPER},rs.value(0));
       
    end;  

end;

start();



 
