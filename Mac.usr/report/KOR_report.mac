// @filename: KOR_report.mac
// @author  : Гурин С.
// @desc    : Отчет о перенесенных документов в КОР
// @request : C-17880-6
// @modify  : none
// -------------------------------------------------------------------------------------------------
import SbCrdInter, RSD, treport;

var dateBegin, dateEnd; 
var reportName = getTxtFileName ("KOR_report");

private macro Report(BegDate, EndDate)
    var sql, cmd, rs, title;
    var table = CTableReport();
    var i = 0;

    SetOutput (reportName, false);

    if (BegDate == EndDate) 
        title = string(BegDate); 
    else 
        title = string("период с ", BegDate," по ", EndDate); 
    end;
        
    println (string ("                Отчет о перенесенных документов в КОР за ", title):c:100);
    println (string ("                                   Дата - ", date, ", время - ", time):c:70);

    sql = "   SELECT   DPMRM.T_NUMBER numdoc, DPM.T_PAYERACCOUNT payeracc, DPM.T_RECEIVERACCOUNT recacc, DPM.T_AMOUNT amount, " + 
          "            DPM.T_NUMBERPACK numpack, DPM.T_OPER oper, TO_CHAR(DPM.T_CREATIONDATE,'DD.MM.YYYY') createdate, "        +
          "            DOPST.T_NAME stepname "                                                                                  +
          "     FROM   dpmpaym_dbt dpm, dpmrmprop_dbt dpmrm, doprstep_dbt dstep, doproper_dbt dopr, doprostep_dbt dopst "       + 
          "    WHERE       dpm.T_PAYMENTID = DPMRM.T_PAYMENTID "                                                                +
          "            AND DOPR.T_DOCUMENTID = LPAD (dpm.T_PAYMENTID, 34, '0') "                                                +
          "            AND DOPR.T_ID_OPERATION = DSTEP.T_ID_OPERATION "                                                         +
          "            AND DSTEP.T_ISEXECUTE = 'X' "                                                                            +
          "            AND ( (DSTEP.T_NUMBER_STEP = 610 "                                                                       +
          "                   AND DSTEP.T_BLOCKID IN (10000165, 10000166)) "                                                    +
          "                 OR (DSTEP.T_NUMBER_STEP = 10 AND DSTEP.T_BLOCKID IN (10000134) "                                    +
          "                     AND EXISTS "                                                                                    +
          "                          (SELECT   1 "                                                                              +
          "                             FROM   dacclaim_dbt claim, dacclaimstate_dbt clst "                                     +
          "                            WHERE       claim.t_account = dpm.t_payeraccount "                                       +
          "                                    AND claim.t_claimid = clst.t_claimid "                                           +
          "                                    AND (claim.t_initiator = 1 "                                                     +
          "                                         OR claim.t_initiator = 100) "                                               +
          "                                    AND claim.t_claimkind = 1 "                                                      +
          "                                    AND claim.t_restkind = 2 "                                                       +
          "                                    AND clst.t_state = 4 "                                                           +
          "                                    AND clst.t_stateid = "                                                           +
          "                                         (SELECT   MAX (t_stateid) "                                                 +
          "                                            FROM   dacclaimstate_dbt "                                               +
          "                                           WHERE   t_claimid = claim.t_claimid)) "                                   +
          "                     AND EXISTS "                                                                                    +
          "                          (SELECT   1 "                                                                              +
          "                             FROM   doprstep_dbt "                                                                   +
          "                            WHERE       t_id_operation = DOPR.T_ID_OPERATION "                                       +
          "                                    AND T_BLOCKID IN (10000166, 10000166) "                                          +
          "                                    AND T_NUMBER_STEP = 610 "                                                        +
          "                                    AND T_ISEXECUTE = 'X'))) "                                                       +
          "            AND DSTEP.T_FACT_DATE BETWEEN TO_CHAR (:BegDate, 'dd.mm.yyyy') "                                         +
          "                                     AND  TO_CHAR (:EndDate, 'dd.mm.yyyy') "                                         +
          "            AND DOPST.T_BLOCKID = DSTEP.T_BLOCKID "                                                                  +
          "            AND DOPST.T_NUMBER_STEP = DSTEP.T_NUMBER_STEP "                                                          +
          " ORDER BY   DPM.T_PAYMENTID";

    cmd = RsdCommand(sql);                 

    cmd.AddParam("BegDate", RSDBP_IN, BegDate);
    cmd.AddParam("EndDate", RSDBP_IN, EndDate);
    cmd.execute();
    rs = RsdREcordset(cmd);
    
    table.addColumn ("№ п/п",                   10, AL_RIGHT);
    table.addColumn ("Счет плательщика",        20, AL_LEFT );
    table.addColumn ("Счет получателя",         20, AL_LEFT );
    table.addColumn ("Сумма п/п",               10, AL_RIGHT);
    table.addColumn ("№ пачки",                 7,  AL_RIGHT);
    table.addColumn ("№ операциониста",         10, AL_RIGHT);
    table.addColumn ("Дата загрузки документа", 20, AL_LEFT );
    table.addColumn ("Шаг документа",           35, AL_LEFT );

    table.printHead ();

    while (rs and rs.movenext())
        table.printStringTransferByWord ( rs.value ("numdoc"),  rs.value ("payeracc"), rs.value ("recacc"),    rs.value ("amount"), 
                                          rs.value ("numpack"), rs.value ("oper"),     rs.value ("createdate"),  rs.value ("stepname"));
        i = i + 1;
    end;
            
    table.printBottom ();
    println ("Всего файлов: ", i);

    SetOutput (NULL, true);
    ViewFile(reportName);
end; 
  
if (ВвестиПериодДат (dateBegin, dateEnd))
    Report(dateBegin, dateEnd);    
end;