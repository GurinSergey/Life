/**********************************************************************/
/* ПРИВЯЗКА КАРТОЧЕК С ОБРАЗЦАМИ ПОДПИСЕЙ К КЛИЕНТАМ РКО              */
/*                                                                    */
/* Дунаев Аркадий                                                     */
/* Начато 12.01.2012                                                  */
/*                                                                    */
/* С-4630, C-7875                                                     */
/**********************************************************************/
//11.07.2012 vihrov C-12122 смена диапазонов кодов

import bankinter, RsbDataSet;
import "fastexcel.mac";

var nameTS;
var TextSQL, rs, col1, col2;
var ob, Fulloutput, output = "Результат привязки.xlt";
var obBook, obSheet;

macro InputDep();
record dpdep(dp_dep);
  if (ListDepartment (dpdep))
    nameTS = "and TS.T_NAME = '"+dpdep.name+"' ";
  else
    if (getTrue(False,"Анализировать все подразделения?"))
      nameTS = " ";
    else
      exit(1);
    end;
  end;
end;

macro OpenExcel(output);
var out;
   ob = CFastExcel(False);
   GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\USERTEMPLSDIR",2,out);
   Fulloutput = FindPath(output,out);
   if (not Fulloutput)
      msgbox("Не найден шаблон");
      exit();
   end;
   obBook = ob.Open(Fulloutput);
   obSheet = obBook.ActiveSheet(); 	
end;

macro ReadSubjRKO(rs);
var TextSQL = "SELECT DISTINCT LPAD (SUB.T_PARTYID, 10, '0') AS IDFORIM, "+
              "       SUB.T_PARTYID, "+
//	       "       SUBSTR (COD.T_CODE, LENGTH (COD.T_CODE) - 4) AS CODCL, "+
	       "       SUBSTR (COD.T_CODE, LENGTH (COD.T_CODE) - 5) AS CODCL, "+ //11.07.2012 vihrov C-12122 смена диапазонов кодов
	       "       SUB.T_NAME, "+
	       "       TS.T_NAME AS TSCOD, "+
	       "       RKO.T_OPER "+
	       "  FROM dclient_dbt rko, "+
	       "       dparty_dbt sub, "+
	       "       ddp_dep_dbt ts, "+
	       "       dobjcode_dbt cod "+
	       " WHERE RKO.T_SERVICEKIND = 3 "+
	       "       AND RKO.T_PARTYID = SUB.T_PARTYID "+
	       "       AND TS.T_CODE IN (SELECT a.t_branch "+
 	       "                             FROM daccount_dbt a "+
	       "                            WHERE a.t_chapter = 1 "+
	       "                              AND a.t_client = rko.t_partyid "+
	       "                              AND a.t_type_account LIKE '%Ч%') "+
//	       "       AND COD.T_CODEKIND = 1 "+
	       "       AND COD.T_CODEKIND = 1 and cod.t_state=0 "+   //11.07.2012 vihrov C-12122 смена диапазонов кодов
	       "       AND COD.T_OBJECTID = RKO.T_PARTYID "+NameTS+
		"       AND 0 in (SELECT COUNT (*) AS COUSUB "+
		"                   FROM dimgdata_dbt "+
		"                  WHERE T_OBJECTTYPE = 3 "+
		"                    AND T_OBJECTID = LPAD (SUB.T_PARTYID, 10, '0')) "+
	       " ORDER BY TS.T_NAME, sub.t_name ";

  rs = TRsbDataSet(TextSQL);
  if (rs == NULL)
    exit(1);
  else
    if (SetParm(0,rs) == FALSE)
      exit(1);
    end;
  end;
end;

macro CountAccount_Subj(partyid):integer;
var TextSQL1 = "SELECT COUNT (*) AS COUACC "+
	        "  FROM dimgdata_dbt im "+
	        " WHERE     IM.T_OBJECTTYPE = 4 "+
	        "       AND IM.T_OBJECTID IN (SELECT '010000000' || ACC.T_ACCOUNT AS CODACC "+
	        "                               FROM daccount_dbt acc "+
	        "                              WHERE ACC.T_CLIENT = "+PARTYID+")";
var rs = TRsbDataSet(TextSQL1);
  if (rs.MoveNext)
    if (rs.COUACC > 0)
      return  round(rs.COUACC,0); 
    else
      return 0; 
    end;
  else
    return 0;
  end;
end;

macro Transfer(partyid, form);
var TextSQL, rs, cmd, id1, id2;
  TextSQL =  "SELECT IM.T_IMAGEID, IM.T_FILENAME, IM.T_IMAGESIZE "+
	        "  FROM dimgdata_dbt im "+
	        " WHERE     IM.T_OBJECTTYPE = 4 "+
	        "       AND IM.T_OBJECTID IN (SELECT '010000000' || ACC.T_ACCOUNT AS CODACC "+
	        "                               FROM daccount_dbt acc "+
	        "                              WHERE ACC.T_CLIENT = "+PARTYID+")";
  rs = TRsbDataSet(TextSQL);
  rs.MoveNext;

  //println(rs.T_FILENAME," ",rs.T_IMAGESIZE," ",form);
  id1 = rs.T_IMAGEID;
  TextSQL = "INSERT INTO dimgdata_dbt (T_IMAGETYPE, T_FILENAME,        T_IMAGESIZE, T_OBJECTTYPE, T_OBJECTID  ) " +
            "                  VALUES (1,'"+     rs.T_FILENAME+"',"+rs.T_IMAGESIZE+",          3,'"+   form+"')";
  cmd = RsdCommand(TextSQL);
  cmd.execute();

  TextSQL = "SELECT t_imageid FROM dimgdata_dbt WHERE t_objecttype = 3 AND t_objectid = '"+form+"'";
  rs = TRsbDataSet(TextSQL);
  rs.MoveNext;
  id2 = rs.t_imageid;

  TextSQL = "SELECT * FROM dimage_dbt WHERE t_imageid = "+id2;
  rs = TRsbDataSet(TextSQL);
  if ( not rs.MoveNext)
    TextSQL = "INSERT INTO dimage_dbt (T_IMAGEID, T_FMTBLOBDATA_XXXX) SELECT "+id2+", T_FMTBLOBDATA_XXXX FROM dimage_dbt WHERE t_imageid = "+id1;
    cmd = RsdCommand(TextSQL);
    cmd.execute();
  else
    println("есть файл с id = ",id2);
  end;

end;

macro AnalizSubjRKO(rs);
var col, ln;
  ln = 4;
  InitProgress(-1,"","Идет анализ клиентов РКО");
  while (rs.MoveNext)
    obSheet.Cells(ln,1).Value=rs.TSCOD;
    obSheet.Cells(ln,2).Value=rs.CODCL;
    obSheet.Cells(ln,3).Value=rs.T_NAME;
    col = CountAccount_Subj(rs.t_partyid);
    if (col == 0)
      obSheet.Cells(ln,5).Value = "ДА"; //println("нет карточек на счетах. переносить нечего");
    elif (col == 1)
      Transfer(rs.t_partyid, rs.IDFORIM);
      obSheet.Cells(ln,4).Value = "ДА"; //println("перенос!");
    else
      obSheet.Cells(ln,6).Value = "ДА"; //println("карточки на нескольких счетах. перенос не однозначен");
    end;
    UseProgress(ln);
    ln = ln + 1;
  end;
  RemProgress();
end;

macro main();
  InputDep();
  OpenExcel(output);
  obSheet.Cells(1,1).Value="ПРОТОКОЛ ПРОЦЕДУРЫ ПРИВЯЗКИ КАРТОЧЕК С ОБРАЗЦАМИ ПОДПИСЕЙ К КЛИЕНТАМ РКО на "+{curdate};
  ReadSubjRKO(rs);
  AnalizSubjRKO(rs);
  ob.Show;
end;

main();
exit(1);
