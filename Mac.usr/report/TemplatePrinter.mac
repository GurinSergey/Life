/**
 * Module:  TTemplatePrinter.h
 * Author:  Ivkina
 * Modified: 11 февраля 2008 г. 10:18:46
 * Purpose: Declaration of the class TTemplatePrinter
 * Comment: Печать таблицы.
 */

import prnfrm;
import RsbDataSet;

/**
 * Печать таблицы.
 */
class TTemplatePrinter(templateFileName : String)
    /**
     * Имя файла шаблона.
     */
    private var m_templateFileName : String;
    /**
     * Печать денежных сумм с апострофами?
     */
    private var m_isApostrophe : Bool;
    /**
     * Количество знаков после запятой.
     */
    private var m_dimention : Integer;

    macro getTemplateFileName()
        return m_templateFileName;
    end;

    macro setPrintingMoneyApostrophe(isApostrophe : Bool, dimention : Integer)
        m_isApostrophe = isApostrophe;
    end;

    macro setPrintingMoneyDimention(dimention : Integer)
        m_dimention = dimention;
    end;


    /**
     * Печать блока таблицы.
     * 
     * @param     blockName  имя блока вывода
     * @param     record запись с данными для вывода
     */
    macro tableBlockPrintNextStr(repForm, str) // KS 16.03.2012 Перенос по строкам
        var field : TPattFieldR;

        var i = 0;

        while (true)
            field = repForm.field(i);

            if (field.name == "accountName")
                  field.value = str;
            else
                  field.value = " ";
            end;

            i = i + 1;
        end;

        onError(e);
        if (e.code != 40)
            runError();
        end;

        repForm.write();
    end;

    macro tableBlockPrint(blockName : String, dataSet)
        var repForm : TRepForm; 
        var field : TPattFieldR;
        private var str = ""; // KS 16.03.2012 Перенос по строкам
		
        repForm = TRepForm(m_templateFileName, false, blockName);

        var i = 0;
		var dimen_ = 2;
		
        while (true)
            field = repForm.field(i);
			if ((GetGlobalParameter ("dimension_asv", false) == 2) or (GetGlobalParameter ("dimension_asv", false) == 0))
				dimen_ = GetGlobalParameter ("dimension_asv", false);
			end;
			setPrintingMoneyDimention(dimen_);
            if (m_isApostrophe)
                field.value = execExp("String(NVL(dataSet." + field.name +", \" \"):0:"+m_dimention+":a)");
            else
                field.value = execExp("String(NVL(dataSet." + field.name +", \" \"):0:"+m_dimention+")");
            end;

            if ((blockName == "Row") and (field.name == "accountName")) // KS 16.03.2012 Перенос по строкам
              str = substr(field.value,41,strlen(field.value)-40);      // KS не понял как определить длину поля,
                                                                        //    поэтому ориентируюсь по наименованию
                                                                        //    и забил константы
            end;

            i = i + 1;
        end;

        onError(e);
        if (e.code != 40)
            runError();
        end;

        repForm.write();

        while (strlen(str)>0) // KS 16.03.2012 Перенос по строкам
          tableBlockPrintNextStr(repForm, str);
          str = substr(str,41,strlen(str)-40);
        end;

    end;
 
    /**
     * @param     templateFileName   имя файла с шаблоном
     */
    private macro constructor(templateFileName : String)
        m_templateFileName = templateFileName;
        m_isApostrophe     = false;
        m_dimention        = 0;
    end;
 
 
    constructor(templateFileName);
end;