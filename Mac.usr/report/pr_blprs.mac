/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank v6                                                 R-Style SoftLab

  File Name   : pr_blprs.mac
  Programmer  : Богданова
  Description : Отчет о блокировании/разблокировании пользователей
  Comment     :
  Modification:
    02.07.2005 BugZ SCR 67395 Фильтрация по ТС/РС.
    04.07.2005 ABP Контроль открытых опердней
└───────────────────────────────────────────────────────────────────────────*/
IMPORT BankInter, CTInter, cb_sql, globals;
IMPORT Reporting; /* RepDepartmentList */
IMPORT oralib;

var  alog = TBfile( "xml_doperlog", "R", 5 );

FILE temp ( "temp_log.tmp" ) write;  /* временный файл для сортировки отчета */
FILE persn ( person );
FILE regparm( regparm ) key 1;

CONST  TableNamePs = "person.dbt",
       TableNameRv = "regval.dbt";

CONST NameRegParm = "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\БЕЗОПАСНОСТЬ\\BLOCKPERIOD";

CONST
    N_DAT     = "1",
    N_KBL     = "2",
    N_OPERIN  = "3",
    N_OPEROUT = "4";

array NameBlock;
      NameBlock(0) = "все";
      NameBlock(1) = "Блокировка без срока";
      NameBlock(2) = "Блокировка на срок  ";
      NameBlock(3) = "Разблокировка";

Var
  FilIn,
  dat1 ,
  dat2 ,
  f_oper =  9999,
  KindOfBlock = 0,
  OperIn = 0,
  FilTek = -1,
  BlockTek = 0,
  PozTek = 0,
  Period = "",
  SortDoc = "",
  RegKeyId = 0;

private var branchFieldFilter;

MACRO GetKeyId( Path )
  var KeyId = 0,
      Name = Path,
      k;

  while( Name != "" )
    regparm.ParentId = KeyId;
    k = strbrk(Name,"\\/");
    if ( k > 0 )
          regparm.Name  = substr(Name, 1, k-1);
    else  regparm.Name  = Name;
    end;
    if ( getEQ(regparm) )  KeyId =  regparm.KeyId;
    end;
    if ( k > 0 ) Name  = substr(Name, k+1);
    else
         return  KeyId;
    end;
  end;
  msgbox( "Не найден параметр настройки ",Path );
  exit(1);
END;

MACRO GetNameOper ( Oper)

   Persn.Oper = Oper;
   if ( GetEQ( Persn) )
      return ( Persn.Name );
   else
    return "Не найден операционист: " + string(Oper);
   end;

END;

MACRO GetNameFil ( Cod )
 var Code = "", Name = "";

 if ( Cod > 0 )
   CB_GetDepartmentCodeAndName(Cod, Code, Name);
 end;

 return string(Code, "  ", Name);
END;

MACRO LZ( num, len )
 var str1, len1;
    str1 = trim( string( num ) );
    len1 = strlen( str1 );
    if ( len1 >= len ) return str1;
    else   return  mkstr("0", len-len1 ) + str1; /* слева */
    end;
end;

MACRO DateLZ( dat )
 var d,m,y;
  datesplit( dat, d, m, y );
  return LZ(y,4) + LZ(m,2) + LZ(d,2);
end;

/*--------------------------------- */
MACRO GetSort( fd )
  var i = 0, st_sort = "";

  macro  GetOneField( p )
    if   ( p == N_DAT )
        return DateLZ(fd.bdDateRec);
    elif ( p == N_KBL )
        return LZ( fd.KindBlock, 8 );
    elif ( p == N_OPERIN ) return LZ( fd.OperIn, 8 );
    elif ( p == N_OPEROUT ) return LZ( fd.OperOut, 8 );
    else return "";
    end;
  end;


   while( i < strlen(SortDoc) )
      i = i + 1;
      st_sort = st_sort + GetOneField( substr(SortDoc,i,1) );
   end;
 /*  println("sts=",st_sort);*/
   return st_sort;
END;

/*--------------------------------- */

Macro PrintBank
[ ############################################################################################################################ ] ({Name_Bank}:w);

END;

MACRO Header
Var Day1, Mes1, Year1,
    Day2, Mes2, Year2;


DateSplit( Dat1, Day1, Mes1, Year1);
DateSplit( Dat2, Day2, Mes2, Year2);

if ( FilTek > 0 )
[

ФИЛИАЛ :   #        ] ( GetNameFil ( FilTek ) );
end;
[
                                             Блокирование / разблокирование пользователей
                                                за период с ##.##.####  по  ##.##.####     ]
(Lz ( Day1,2 ), Lz ( Mes1,2 ), Lz (Year1, 2), Lz ( Day2,2 ), Lz ( Mes2,2 ), Lz (Year2, 2));
[
 ┌───┬────────────────────────────────────────┬───────────────┬─────────────────────┬────────────────────────────────────────┐
 │ № │         Пользователь                   │Дата блокировки│        Вид          │ Кто произвел блокировку / разблокировку│
 │п/п├────┬───────────────────────────────────┤/ разблокировки│     блокировки      ├────┬───────────────────────────────────┤
 │   │  № │       ФИО                         │               │                     │  № │              ФИО                  │
 ├───┼────┼───────────────────────────────────┼───────────────┼─────────────────────┼────┼───────────────────────────────────┤];

END;

MACRO Footer
[└───┴────┴───────────────────────────────────┴───────────────┴─────────────────────┴────┴───────────────────────────────────┘];
END;


MACRO PrintString( counter, temp )
Var Day, Mes, Year;

DateSplit( temp.bdDateRec, Day, Mes, Year);

[│###│####│###################################│   ##########  │#################### │####│###################################│]
( counter, temp.OperIn, getNameOper(temp.OperIn), Lz( Day,2 )+"."+Lz( Mes,2 )+"."+Lz(Year, 2), (NameBlock(temp.kindBlock) + temp.Period) :w,
  temp.OperOut, getNameOper(temp.OperOut) );


END;
/*--------------------------------- */
/* Печать из врем файла  */

MACRO PutReport(count_rec)
 var counter = 0,
     Val;

 InitProgress( count_rec /*NRecords( temp )*/,
                "", "Печать отчета из временного файла" );


 rewind( temp );
 while( next (temp ) )
  /*println("sort=",temp.sort);*/

     if ( FilTek != temp.Fil )
          if (FilTek != -1 )
              Footer;
          else
              PrintBank;
          end;

          FilTek = temp.Fil;
          Header( dat1, dat2);/* Печать заголовка */
          counter = 0;
     end;
    counter = counter + 1;

    printstring(counter, temp);
    UseProgress( counter );
 end;


 RemProgress();

 if( count_rec == 0  )
     println( "Нет данных для отчета." );
 else
     Footer;
 end;

END;


MACRO MaxDays ( Oper )

return True;

END;



MACRO FiltrFil ( Oper )

if ( ( f_oper == 0) or (f_oper == Oper) )

     Persn.Oper = Oper;

     if ( GetEq ( Persn ) )

        FilIn = Persn.CodeDepart;

        if ( branchFieldFilter.isSuitable(Persn.CodeDepart) )
           return True;
        else
           return False;
        end;
     else
       MsgBox ( "Операционист ", Oper, " отсутствует в справочнике");
     end;

    return True;
end;

return False;

END;



MACRO FiltrPers( fd )

VAR pers    = TRecHandler( TableNamePs ),
    pers_do = TRecHandler( TableNamePs );

  pers.SetRecordAddr(     fd, 0, 2);
  pers_do.SetRecordAddr(  fd, 1, 4);

  if ( ( fd.rec.opCode == OLupdate ) and /* обновление записи */
       ( pers.rec.UserBlocked != pers_do.rec.UserBlocked )  and
       branchFieldFilter.isSuitable(pers.rec.codeDepart) and
       ( ( f_oper == 0) or (f_oper == pers.rec.Oper) )
     )

     if ( pers.rec.UserBlocked == "X" )
          BlockTek = 1;
     elif  ( MaxDays ( pers.rec.Oper ) )
          BlockTek = 3;
     else return false;
     end;

     if ( ( KindOfBlock != 0 ) and ( KindOfBlock != BlockTek ) )
         return False;
     end;

     OperIn = pers.rec.Oper;
     FilIn  = pers.rec.CodeDepart;

     return true;
  end;

return false;

END;

MACRO  ZnachPeriod ( Str )
Var i=1, sym, ZN = False;

if ( strlen ( Str ) == 0  )
    return False;
else
   while ( i < strlen ( Str ) + 1 )
     sym = substr (Str,i,1);
     if ( strbrk (sym,"0:.- ") == 0 )
         ZN = True;
     end;
     i = i+1;
   end;

    return ZN;
end;

END;

MACRO FiltrReg(fd)

VAR reg    = TRecHandler( TableNameRv ),
    reg_do = TRecHandler( TableNameRv ),
    str    = TRecHandler( "doc_key.tmp"),
    str_do = TRecHandler( "doc_key.tmp");

  ClearRecord (reg);
  ClearRecord (reg_do);
  ClearRecord (str);
  ClearRecord (str_do);

  reg.SetRecordAddr(    fd,0, 2);
  str.SetRecordAddr(    fd,0, getrecordsize(reg)+ 2);
  reg_do.SetRecordAddr( fd,0, getrecordsize(reg)+ strlen(str.rec.sort)+5);
  str_do.SetRecordAddr( fd,0, 2*getrecordsize(reg)+ strlen(str.rec.sort)+5);

/*   println(  "opcod=",fd.rec.opcode,
               " date=",fd.rec.Date,
               " oper=",reg.rec.Oper,
               " str=",str.rec.sort,
               " do=",str_do.rec.sort,
               " rez=",(str.rec.sort != str_do.rec.sort )
               );  
*/
if ( (reg.rec.Keyid == RegKeyId ) and  ( (reg.rec.RegKind == REGVAL_KIND_OPER) and (reg.rec.ObjectId != 0) ) and
               /* Обновление записи */
     ( (fd.rec.opCode != OLupdate ) or (str.rec.sort != str_do.rec.sort) ) and
      FiltrFil ( reg.rec.ObjectId ) )

   if ( (fd.rec.opCode == OLdelete) and ZnachPeriod ( str.rec.sort ))
        BlockTek = 3;

   elif ( ZnachPeriod ( str.rec.sort ) )
        BlockTek = 2;
        Period = str.rec.sort;

   elif ( (fd.rec.opCode == OLupdate ) and ZnachPeriod ( str_do.rec.sort ) )
        BlockTek = 3;
   else
        return False;
   end;


  if ( ( KindOfBlock != 0 ) and ( KindOfBlock != BlockTek ) )
       return False;
  end;

  OperIn = reg.rec.ObjectId;
  return true;

end;

return false;

END;


MACRO FiltrBlock ( fd )

if (   ( fd.rec.opCode == OLinsert ) or
       ( fd.rec.opCode == OLupdate ) or
       ( fd.rec.opCode == OLdelete ) )

   if ( (trim ( fd.rec.tableName ) == TableNamePs ) and FiltrPers( fd ))
      return true;
   end;

   if ( (trim ( fd.rec.tableName ) == TableNameRv ) and FiltrReg( fd ))
      return true;
   end;
end;

return False;

END;

/* --------Функция вызывается из программы ( C )----------*/

MACRO Report( Dprt, BegDate, EndDate, Oper, Sort, Kind_Block, organizationStructure, issueMode )
 var i,
   str_where = "",
   counter = 0,
   count_rec = 0;

 var departmentList = RepDepartmentList( organizationStructure, issueMode, Dprt );

 macro ScanOpLog( TableName )
   var stat  = 0;

   if ( isSQL )
      alog.addFilter( string(" t_tableName = '",TableName,"' ", str_where ) );
   end;

   ClearRecord ( alog );
   alog.rec.tableName = TableName;
   alog.rec.Date      = dat1;
   stat = getGE( alog );

   while ( stat and
        ( alog.rec.tableName == TableName ) and
        ( alog.rec.Date      <= dat2 ) )

      Period = "";
      counter = counter + 1;
      UseProgress( counter );

     if ( FiltrBlock ( alog )  )

        count_rec = count_rec + 1;
        message(string(" Найдено записей: ", count_rec ));

        ClearRecord ( Temp );
        temp.bdDateRec =  alog.rec.Date;
        temp.OperOut   =  alog.rec.userId;
        temp.KindBlock =  BlockTek;
        temp.OperIn    =  OperIn;
        temp.Fil       =  FilIn;
        temp.SortZ     =  GetSort (temp);

        i = 0;
        while ( i < strlen ( Period ) + 1 )

            if ( substr (Period, i ,1) == " " )
                 strset (Period, i, ",");
            end;
            i = i+1;
        end;

        temp.Period    =  Period;

        if (not insert(temp) )
            msgbox("Ошибка записи во временный файл");
            exit(1);
        end;
      end;
      stat = next( alog );
   end;

   if ( isSQL )
    alog.dropFilter;
   end;
 end;

 /* Контроль открытых опердней */
 var opd = RepOperdaysOpened(departmentList, BegDate, EndDate);
 if (opd.ShouldContinue == false)
   exit(1);
 end;

 branchFieldFilter = RepBranchFieldFilter(departmentList);

 dat1 =  BegDate;
 dat2 =  EndDate;
 f_oper =  Oper;
 KindOfBlock = Kind_Block;
 SortDoc = string (Sort);
 RegKeyId = GetKeyId( NameRegParm );

if (not Open(temp))
     MsgBox ("Невозможно открыть временный файл ",temp);
     exit(1);
else
     execSQL("TRUNCATE TABLE DTEMP_LOG_TMP;");
end;


 if ( isSQL )
      str_where = string(" AND t_date >= TO_DATE('",dat1,"','DD.MM.YYYY') ",
                         " AND t_date <= TO_DATE('",dat2,"','DD.MM.YYYY') ",
                         " AND t_opcode >= ", OLinsert,
                         " AND t_opcode <= ", OLdelete );

 end;
 //23 Mar 07 Fri 17:11:14 Malakhova Irina 103782
 InitProgress(-1, "", "Формирование временного файла");
 counter = 0;
 count_rec = 0;

 ScanOpLog( TableNamePs ) ;
 ScanOpLog( TableNameRv ) ;

 RemProgress();

 PutReport(count_rec);

END;


/*------------------------------*/

/* Report( 0, Date(01,11,2003),Date(09,01,2004), 0, 1, 0); */


/*EOF*/
