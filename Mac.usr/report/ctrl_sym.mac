/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank V6                                                  R-Style Softlab
  Файл подсистемы "Регламентируемая отчетность"

  Общие функции контроля кассовых символов для "Регламентируемой отчетности"
  и "Внутренней отчетности".

  Создан: 11.09.2005 - Sal.
  11.1.2005 BugZ SCR 75746 Убрал вывод отладочной информации.
  04.12.2007 Ivkina SCR 115267 Указание 1881-У
// KS 18.12.2013 Перенос доработок в 31ю сборку
└───────────────────────────────────────────────────────────────────────────*/

import RsbDataSet;
import Reporting;
import ReportInter;
import cb_sql;
import globals;
import treport;
import lib_lang;
import Календарь;
import FIInter;

private var ДАТА1_1660У = date(1, 4, 2006); /* Значение по умолчанию */
private var ДАТА_1881У  = date(1, 1, 2008); /* Значение по умолчанию */

private const KINDDOCUMENT = 3;             /*Bид документа - кассовый*/

private const NUMBERDOCLEN =  9;
private const DATEDOCLEN   = 10;
private const NOTELEN      = 67;

private var protocolFileName =  GetTxtFileName( "controlCashSymbols" );

/* 27.03.2006 ABP Скопировано из ReptReg\chk_regd.mac */
private macro GetRegDateDefault(Dat, Path, msg)
 local macro LastDayInMonth( cDate )
     var cDay, cMonth,  cYear;

     DateSplit( cDate, cDay, cMonth, cYear);
     cMonth = cMonth + 1;
     if ( cMonth == 13 )
       cMonth = 1;
       cYear = cYear + 1;
     end;

     cDate = date( 1, cMonth, cYear);
     DateSplit( cDate-1, cDay, cMonth, cYear);

     return cDay;
 end;

 var  type_val, Val, err = 0, k, delimstr = "-/.:",
      tmpVal, day, mon, year;

   if ( valtype(Dat) == V_UNDEF  ) Dat = date(0,0,0);
   end;
   if ( StrBrk (Path, "\\/") == 0 )
          Path = "REPTREG\\REP_GROUPS\\COMMON\\" + Path;
   end;
   if ( valtype(msg) == V_UNDEF  ) msg = TRUE;
   end;

   type_val = GetRegistryValue( Path, V_STRING, Val, err );
   if ( ( err != 0 ) or (type_val != V_STRING) )
        err = 1;
        if ( msg  )
          MsgBox( "Ошибка чтения ключа |", Path ,",|используется значение по умолчанию ", dat );
        end;
   end;

   if ( ( err == 0 ) and (Val != "") )
      tmpVal = trim( Val );
      k = StrBrk (tmpVal, delimstr);
      if ( k == 0 )
        err = 1;
        if ( msg  )
          MsgBox( "Неверный формат даты в настройке|", Path," :|", Val,",|используется значение по умолчанию ", dat );
        end;
      end;
   end;

   if ( ( err == 0 ) and (Val != "") )
     day = int(SubStr( tmpVal, 1, k-1));
     tmpVal = SubStr( tmpVal, k+1);
     k = StrBrk (tmpVal, delimstr);
     if ( k == 0 )
        err = 1;
        if ( msg  )
          MsgBox( "Неверный формат даты в настройке|", Path," :|", Val,",|используется значение по умолчанию ", dat );
        end;
     end;
   end;

   if ( ( err == 0 ) and (Val != "") )
     mon = int(SubStr( tmpVal, 1, k-1));
     tmpVal = SubStr( tmpVal, k+1);
     year = int(tmpVal);
     if  ( ( year == 0) and (mon == 0) and (day == 0) ) ;  /* Нулевая дата OK*/
     elif( ( year > 0 ) and
           ( mon > 0 )  and ( mon < 13 ) and
           ( day > 0 )  and ( day <= LastDayInMonth(date(day,mon, year) ) )
         )
        Dat = date(day,mon, year);
     else
        err = 1;
        if ( msg  )
          MsgBox( "Неверная дата в настройке|", Path," :|", Val,",|используется значение по умолчанию ", dat );
        end;
     end;
   end;

   SetParm(0, Dat );
   return  err;
end;

class TCommonHeadingPrinter( report : CTableReport, beginDate : Date, endDate : Date )

    private var m_hasHeadingPrinted : Bool;
    private var m_report : CTableReport;
    private var m_beginDate : Date;
    private var m_endDate : Date;

    macro PrintHeading()

        local macro GetOperFio()
            file person("person.dbt") key 0;

            person.oper = {oper};
            if ( GetEQ(person) )
                return person.Name;
            end;

            return "Не найден (#" + {oper} + ")";
        end;

        if ( not m_hasHeadingPrinted )
           m_report.PrintFreeString("Контроль кассовых символов.");
           m_report.PrintFreeString("ПРОТОКОЛ КОНТРОЛЯ" );

           m_report.PrintFreeString("Период отчета с " + m_beginDate + " по " + m_endDate);
           m_report.PrintFreeString("Исполнитель: " + GetOperFio());
           m_report.PrintFreeString("Дата и время выпуска отчета: " + Date() + " " + Time());

           m_hasHeadingPrinted = true;
        end;

    end;

    private macro Constructor( report : CTableReport, beginDate : Date, endDate : Date )
        m_hasHeadingPrinted = false;
        m_report = report;
        m_beginDate = beginDate;
        m_endDate = endDate;
        setOutPut(protocolFileName);
    end;

    Constructor( report, beginDate, endDate );
end;

private const NO_DOCUMENTS = 0; /*нет документов*/
private const NO_ERROR_DOC = 1; /*нет ошибок в документах*/
private const ERROR        = 2; /*есть ошибки*/
private var   statErrDoc   = NO_DOCUMENTS;

private const SYMBOL_KIND_INCOME = 1;
private const SYMBOL_KIND_OUTLAY = 2;
private const SYMBOL_KIND_OFFBAL = 3;

private class TSymbol(name : String, sum : MoneyL, nameRef : String)
    private var m_name    : String;
    private var m_sum     : MoneyL;
    private var m_isExist : Bool;

    private macro Constructor(name : String, sum : MoneyL, nameRef : String)
        m_name    = name;
        m_sum     = sum;
        m_isExist = nameRef != NULL;
    end;

    macro GetName()
        return m_name;
    end;

    macro GetSum()
        return m_sum;
    end;

    macro IsExist()
        return m_isExist;
    end;

    Constructor(name, sum, nameRef);
end;

private class TDocument(dataSet, isLastDocument : Bool)

    private var m_carryDate        : Date;
    private var m_number           : Integer;
    private var m_accTrnId         : Integer;
    private var m_payerAccount     : String;
    private var m_receiverAccount  : String;
    private var m_sum              : MoneyL;
    private var m_isDebetCash      : Bool;
    private var m_isCreditCash     : Bool;
    private var m_incomeSymbolSum  : MoneyL;
    private var m_outlaySymbolSum  : MoneyL;
    private var m_otherSymbolSum   : MoneyL;
    private var m_documentKind     : Integer;
    private var m_incomeSymbolList : TArray;
    private var m_outlaySymbolList : TArray;
    private var m_offbalSymbolList : TArray;
    private var m_otherSymbolList  : TArray;

    private macro Constructor(dataSet, isLastDocument : Bool)

        local macro ProcessSymbols()
            if (dataSet.symbol != NULL)
                if   (dataSet.symbolKind == SYMBOL_KIND_INCOME)
                    m_incomeSymbolList[m_incomeSymbolList.Size()] = TSymbol(dataSet.symbol, dataSet.symbolSum, dataSet.symbolRef);
                    m_incomeSymbolSum = m_incomeSymbolSum + dataSet.symbolSum;
                elif (dataSet.symbolKind == SYMBOL_KIND_OUTLAY)
                    m_outlaySymbolList[m_outlaySymbolList.Size()] = TSymbol(dataSet.symbol, dataSet.symbolSum, dataSet.symbolRef);
                    m_outlaySymbolSum = m_outlaySymbolSum + dataSet.symbolSum;
                elif (dataSet.symbolKind == SYMBOL_KIND_OFFBAL)
                    m_offbalSymbolList[m_offbalSymbolList.Size()] = TSymbol(dataSet.symbol, dataSet.symbolSum, dataSet.symbolRef);
                else
                    m_otherSymbolList[m_otherSymbolList.Size()] = TSymbol(dataSet.symbol, dataSet.symbolSum, dataSet.symbolRef);
                    m_otherSymbolSum = m_otherSymbolSum + dataSet.symbolSum;
                end;
            end;
        end;

        m_carryDate        = dataSet.carryDate;
        m_number           = dataSet.documentNumber;
        m_accTrnId         = dataSet.accTrnId;
        m_payerAccount     = dataSet.payerAccount;
        m_receiverAccount  = dataSet.receiverAccount;
        m_sum              = dataSet.documentSum;
        m_isDebetCash      = dataSet.isDebetCash;
        m_isCreditCash     = dataSet.isCreditCash;
        m_documentKind     = dataSet.documentKind;
        m_incomeSymbolSum  = $0;
        m_outlaySymbolSum  = $0;
        m_otherSymbolSum   = $0;
        m_incomeSymbolList = TArray;
        m_outlaySymbolList = TArray;
        m_offbalSymbolList = TArray;
        m_otherSymbolList  = TArray;

        ProcessSymbols();

        var isSameDocument = true;

        while (isSameDocument)
            if (dataSet.moveNext())
                if (dataSet.accTrnId == m_accTrnId)
                    ProcessSymbols();
                else
                    isSameDocument = false;
                end;
            else
                isSameDocument = false;
                isLastDocument = true;
            end;
        end;

        SetParm(2, isLastDocument);
    end;

    macro GetCarryDate()
        return m_carryDate;
    end;

    macro GetNumber()
        return m_number;
    end;

    macro GetAccTrnId()
        return m_accTrnId;
    end;

    macro GetPayerAccount()
        return m_payerAccount;
    end;

    macro GetReceiverAccount()
        return m_receiverAccount;
    end;

    macro GetSum()
        return m_sum;
    end;

    macro IsDebetCash()
        return m_isDebetCash;
    end;

    macro IsCreditCash()
        return m_isCreditCash;
    end;

    macro GetIncomeSymbolList()
        return m_incomeSymbolList;
    end;

    macro GetOutlaySymbolList()
        return m_outlaySymbolList;
    end;

    macro GetOffBalSymbolList()
        return m_offbalSymbolList;
    end;

    macro GetOtherSymbolList()
        return m_otherSymbolList;
    end;

    macro GetIncomeSymbolSum()
        return m_incomeSymbolSum;
    end;

    macro GetOutlaySymbolSum()
        return m_outlaySymbolSum;
    end;

    macro GetOtherSymbolSum()
        return m_otherSymbolSum;
    end;

    macro GetKindDocument()
        return m_documentKind;
    end;

    Constructor(dataSet, isLastDocument);
    SetParm(2, isLastDocument);
end;

private class TServer(planNumber : Integer, beginDate : Date, endDate : Date, departmentCode : Integer, commonHeadingPrinter : TCommonHeadingPrinter )
    private var m_planNumber : Integer;
    private var m_beginDate : Date;
    private var m_endDate : Date;
    private var m_departmentCode : Integer;
    private var m_report : CTableReport;
    private var m_commonHeadingPrinter : TCommonHeadingPrinter;



    private macro Constructor(planNumber : Integer, beginDate : Date, endDate : Date, departmentCode : Integer, commonHeadingPrinter : TCommonHeadingPrinter)

        macro InitializeReport()

            m_commonHeadingPrinter.PrintHeading();

            m_report.AddColumn("Номер|документа",    NUMBERDOCLEN);
            m_report.AddColumn("Дата|документа",       DATEDOCLEN);
            m_report.AddColumn("Счет по дебету",       ACCOUNTLEN);
            m_report.AddColumn("Счет по кредиту",      ACCOUNTLEN);
            m_report.AddColumn("Сумма",            DOCUMENTSUMLEN);
            m_report.AddColumn("Примечание",              NOTELEN);

            m_report.PrintHead();
        end;

        m_planNumber     = planNumber;
        m_beginDate      = beginDate;
        m_endDate        = endDate;
        m_departmentCode = departmentCode;
        m_report         = CTableReport(0, false, true);
        m_commonHeadingPrinter = commonHeadingPrinter;

        InitializeReport();
    end;

    macro GetPlanNumber()
        return m_planNumber;
    end;

    macro GetBeginDate()
        return m_beginDate;
    end;

    macro GetEndDate()
        return m_endDate;
    end;

    macro GetDepartmentCode()
        return m_departmentCode;
    end;

    macro AddToReport(document, note : String)
        var str_len, str;
        if (ValType(document) == V_GENOBJ)
          m_report.PrintStringTransferByWord(document.GetNumber(),
                               document.GetCarryDate(),
                               document.GetPayerAccount(),
                               document.GetReceiverAccount(),
                               document.GetSum(),
                               note);
        elif(ValType(document) == V_MONEY)
          m_report.PrintStringTransferByWord(NULL,
                                             NULL,
                                             NULL,
                                             NULL,
                                             document,
                                             note);
        else
         str_len = NUMBERDOCLEN + DATEDOCLEN + ACCOUNTLEN + ACCOUNTLEN + DOCUMENTSUMLEN + NOTELEN + 6*2 + 5;
         str = StrAlign( note, str_len, STR_ALIGN_CENTER);
         println("│" + str + "│");
        end;
        statErrDoc = ERROR;
    end;

    private macro Destructor()
        m_report.PrintBottom();
    end;

    Constructor(planNumber, beginDate, endDate, departmentCode, commonHeadingPrinter);
end;

private macro ControlProcedure(server : TServer)

    var organizationStructure : Integer;
    var issueMode : Integer;

    Rep_GetDefaultParmTS(organizationStructure, issueMode);

    var DepartmentList = RepDepartmentList(organizationStructure, issueMode, server.GetDepartmentCode());
    var documentFilter = RepDocumentFilter(DepartmentList);
    var AccountFilter  = RepAccountFilter (DepartmentList, PRIV_GET_ACCOUNT_DATA_FOR_REPORTS);

    local macro GetDocumentQuery()

        var balanceList;

        var departmentCodeFilterString = documentFilter.GetAsSqlString("docum.t_Department", "acpay.t_Branch", "acrec.t_Branch");
        var beginDateString            = GetSqlDate(server.GetBeginDate());
        var endDateString              = GetSqlDate(server.GetEndDate());
        var balanceFieldName           = "t_Balance" + server.GetPlanNumber();

        if (server.GetEndDate() < ДАТА_1881У)
            balanceList = "'20202', '20206', '20207', '20208'";
        else
            balanceList = "'20202', '20206', '20207'";
        end;

        return "SELECT docum.t_Numb_Document      documentNumber,"  + "\n" +
               "       docum.t_Date_Carry         carryDate,"       + "\n" +
               "       docum.t_accTrnId           accTrnId,"        + "\n" +
               "       docum.t_Account_Payer      payerAccount,"    + "\n" +
               "       docum.t_Account_Receiver   receiverAccount," + "\n" +
               "       docum.t_Sum_Natcur         documentSum,"     + "\n" +
               "       symbl.t_Kind               symbolKind,"      + "\n" +
               "       symbl.t_Symbol             symbol,"          + "\n" +
               "       nvl(symbl.t_Sum,0)         symbolSum,"       + "\n" +
               "       docum.t_Kind_Oper          documentKind,"    + "\n" +
               "       CASE"/*заполнение справочника с учетом или без учета 1660У*/        + "\n" +
               "           WHEN(( docum.t_Date_Carry >= " + GetSqlDate(ДАТА1_1660У)        + "\n" +
               "                 AND"                                                      + "\n" +
               "                 (lsmbl.t_Symb_Cash = ' 36' OR lsmbl.t_Symb_Cash = ' 71')" + "\n" +
               "               )"                                                          + "\n" +
               "               OR"                                                         + "\n" +
               "               ( docum.t_Date_Carry < " + GetSqlDate(ДАТА1_1660У)          + "\n" +
               "                 AND"                                                      + "\n" +
               "                 (lsmbl.t_Symb_Cash = ' 89' OR lsmbl.t_Symb_Cash = ' 96')" + "\n" +
               "               ))                                                           " + "\n" +
               "               AND                                                          " + "\n" +
               "               (( docum.t_Date_Carry < " + GetSqlDate(ДАТА_1881У)             + "\n" +
               "                 AND                                                        " + "\n" +
               "                 (   lsmbl.t_Symb_Cash = ' 98' OR lsmbl.t_Symb_Cash = ' 97' " + "\n" +
               "                  OR lsmbl.t_Symb_Cash = ' 75' OR lsmbl.t_Symb_Cash = ' 61' " + "\n" +
               "                  OR lsmbl.t_Symb_Cash = ' 62' OR lsmbl.t_Symb_Cash = ' 47' " + "\n" +
               "                  OR lsmbl.t_Symb_Cash = ' 33' OR lsmbl.t_Symb_Cash = ' 23' " + "\n" +
               "                  OR lsmbl.t_Symb_Cash = ' 22' OR lsmbl.t_Symb_Cash = ' 21' " + "\n" +
               "                  OR lsmbl.t_Symb_Cash = ' 03'                             )" + "\n" +
               "                )                                                           " + "\n" +
               "                  OR ( docum.t_Date_Carry >= " + GetSqlDate(ДАТА_1881У) +") " + "\n" +
               "               )"                                                          + "\n" +
               "               THEN NULL"                                                  + "\n" +
               "           ELSE     lsmbl.t_Symb_Cash"                                     + "\n" +
               "       END symbolRef,"                                                     + "\n" +
               "       CASE"                                        + "\n" +
               "           WHEN SUBSTR(abpay." + balanceFieldName + ", 1, 5) IN (" + balanceList + ")" + "\n" +
               "               THEN 1"                              + "\n" +
               "           ELSE     0"                              + "\n" +
               "       END isDebetCash,"                            + "\n" +
               "       CASE"                                        + "\n" +
               "           WHEN SUBSTR(abrec." + balanceFieldName + ", 1, 5) IN (" + balanceList + ")" + "\n" +
               "               THEN 1"                              + "\n" +
               "           ELSE     0"                              + "\n" +
               "       END isCreditCash"                            + "\n" +
               "FROM   dacctrn_dbt             docum," + "\n" +
               "       dsymbcash_dbt           symbl," + "\n" +
               "       dlistsymb_dbt           lsmbl," + "\n" +
               "       daccount_dbt            acpay," + "\n" +
               "       daccount_dbt            acrec," + "\n" +
               "       daccblnc_dbt            abpay," + "\n" +
               "       daccblnc_dbt            abrec " + "\n" +
               "WHERE  " + departmentCodeFilterString                              + "\n" +
               "       AND ("    + AccountFilter.GetAsSqlString("acpay")           + "\n" +
               "            OR " + AccountFilter.GetAsSqlString("acrec") + ")"     + "\n" +
               "       AND docum.t_state = 1 "                                     + "\n" +
               "       AND docum.t_Date_Carry BETWEEN " + beginDateString          + "\n" +
               "                                  AND " + endDateString            + "\n" +
               "       AND abpay.t_Chapter           = 1                       "   + "\n" +
               "       AND abpay.t_AccountId         = docum.t_AccountId_Payer "   + "\n" +
               "       AND abrec.t_Chapter           = 1                       "   + "\n" +
               "       AND abrec.t_AccountId         = docum.t_AccountId_Receiver" + "\n" +
               "       AND acpay.t_AccountId         = docum.t_AccountId_Payer "   + "\n" +
               "       AND acrec.t_AccountId         = docum.t_AccountId_Receiver" + "\n" +
               "       AND acpay.t_code_currency     = " + NATCUR                  + "\n" +
               "       AND acrec.t_code_currency     = " + NATCUR                  + "\n" +
               "       AND symbl.t_DocKind(+)        = 1"                          + "\n" +
               "       AND symbl.t_accTrnId(+)       = docum.t_accTrnId"           + "\n" +
               "       AND lsmbl.t_DocKind(+)        = 1"                          + "\n" +
               "       AND lsmbl.t_Symb_Cash(+)      = symbl.t_Symbol"             + "\n" +
               "       AND ("                                                      + "\n" +
    "                 (   SUBSTR(abrec." + balanceFieldName + ", 1, 5) IN (" + balanceList + ")" + "\n" +
               "                 AND INSTR(acrec.t_Type_Account, 'А') <> 0)"     + "\n" +
               "              OR (SUBSTR(abpay." + balanceFieldName + ", 1, 5) IN (" + balanceList + ")" + "\n" +
               "                 AND INSTR(acpay.t_Type_Account, 'А') <> 0)"     + "\n" +
               "              OR symbl.t_Symbol IS NOT NULL"                     + "\n" +
               "           )"                                                      + "\n" +
               "ORDER BY carryDate, documentSum, accTrnId";
    end;

    local macro ProcessDocuments(documentQuery : String)

        local macro AnalyzeDocument(document)

            local macro CheckSymbols()

                var i = 0;
                var j = 0;

                var incomeSymbolList = document.GetIncomeSymbolList();
                var outlaySymbolList = document.GetOutlaySymbolList();
                var offbalSymbolList = document.GetOffBalSymbolList();
                var otherSymbolList  = document.GetOtherSymbolList();
                var existSymbol02  = false;
                var existSymbol40  = false;
                var cMonth, cYear, dateOneWorkDay;
                var charSymbolList = "";

                const incomeSymbolListSize = incomeSymbolList.Size();
                const outlaySymbolListSize = outlaySymbolList.Size();
                const offbalSymbolListSize = offbalSymbolList.Size();
                const otherSymbolListSize  = otherSymbolList.Size();

                if (document.IsDebetCash() or document.IsCreditCash())
                    if ((incomeSymbolListSize == 0) and (outlaySymbolListSize == 0) and (otherSymbolListSize == 0))

                        server.AddToReport(document, "Документ не содержит кассовый символ");

                    else

                        if (document.IsDebetCash())
                            i = 0;
                            while (i < incomeSymbolListSize)
                                if (incomeSymbolList[i].IsExist())
                                    if (
                                        (Int(incomeSymbolList[i].GetName()) < 02) or
                                        (Int(incomeSymbolList[i].GetName()) > 39)
                                       )
                                        server.AddToReport(document, "Приходный документ содержит неверный кассовый символ" + incomeSymbolList[i].GetName());
                                    elif( (Int(incomeSymbolList[i].GetName()) == 02))
                                       existSymbol02 = true;
                                    end;
                                else
                                    server.AddToReport(document, "Кассовый символ " + incomeSymbolList[i].GetName() + " не соответствует справочнику символов");
                                end;
                                i = i + 1;
                            end;

                            if (not document.IsCreditCash())
                                i = 0;
                                while (i < outlaySymbolListSize)
                                    server.AddToReport(document, "Приходный документ содержит неверный кассовый символ " + outlaySymbolList[i].GetName());
                                    i = i + 1;
                                end;
                            end;
                        end;

                        if (document.IsCreditCash())
                            if (not document.IsDebetCash())
                                i = 0;
                                while (i < incomeSymbolListSize)
                                    server.AddToReport(document, "Расходный документ содержит неверный кассовый символ " + incomeSymbolList[i].GetName());
                                    i = i + 1;
                                end;
                            end;

                            i = 0;
                            while (i < outlaySymbolListSize)
                                if (outlaySymbolList[i].IsExist())
                                    if (
                                        (Int(outlaySymbolList[i].GetName()) < 40) or
                                        (Int(outlaySymbolList[i].GetName()) > 77)
                                       )
                                        server.AddToReport(document, "Расходный документ содержит неверный кассовый символ" + outlaySymbolList[i].GetName());
                                    elif( (Int(outlaySymbolList[i].GetName()) == 40))
                                       existSymbol40 = true;
                                    end;
                                else
                                    server.AddToReport(document, "Кассовый символ " + outlaySymbolList[i].GetName() + " не соответствует справочнику символов");
                                end;
                                i = i + 1;
                            end;
                        end;

                        /*забалансовые символы*/
                        i = 0;
                        while (i < offbalSymbolListSize)
                            if (offbalSymbolList[i].IsExist())
                              charSymbolList = "";
                              /*89*/
                              if ( (Int(offbalSymbolList[i].GetName()) == 89))
                                 if(not existSymbol02)
                                 j = 0;
                                 while (j < incomeSymbolListSize)
                                      charSymbolList = charSymbolList + incomeSymbolList[j].GetName() + ",";
                                      j = j + 1;
                                 end;
                                 j = 0;
                                 while (j < outlaySymbolListSize)
                                      charSymbolList = charSymbolList + outlaySymbolList[j].GetName() + ",";
                                      j = j + 1;
                                 end;
                                 server.AddToReport(document, "Символ" + offbalSymbolList[i].GetName() + " не соответствует балансовому символу" + substr(charSymbolList, 1, strlen(charSymbolList)-1));
                                 end;
                                 /*Ищем первый рабочий день месяца*/
                                 DateSplit( document.GetCarryDate(), NULL, cMonth, cYear);
                                 dateOneWorkDay = Date(1, cMonth, cYear);
                                 while(not IsWorkday (dateOneWorkDay) )
                                    dateOneWorkDay = dateOneWorkDay + 1;
                                 end;
                                 /**/
                                 if(document.GetCarryDate() != dateOneWorkDay)
                                   server.AddToReport(document, "Дата проведения операции должна быть" + dateOneWorkDay);
                                 end;
                              end;
                              /*96*/
                              if ( (Int(offbalSymbolList[i].GetName()) == 96) and (not existSymbol40))
                                 j = 0;
                                 while (j < outlaySymbolListSize)
                                      charSymbolList = charSymbolList + outlaySymbolList[j].GetName() + ",";
                                      j = j + 1;
                                 end;
                                 j = 0;
                                 while (j < incomeSymbolListSize)
                                      charSymbolList = charSymbolList + incomeSymbolList[j].GetName() + ",";
                                      j = j + 1;
                                 end;
                                 server.AddToReport(document, "Символ" + offbalSymbolList[i].GetName() + " не соответствует балансовому символу" + substr(charSymbolList, 1, strlen(charSymbolList)-1));
                              end;
                              /*97*/
                              if ( (Int(offbalSymbolList[i].GetName()) == 97) and (incomeSymbolListSize == 0))
                                 j = 0;
                                 while (j < outlaySymbolListSize)
                                      charSymbolList = charSymbolList + outlaySymbolList[j].GetName() + ",";
                                      j = j + 1;
                                 end;
                                 server.AddToReport(document, "Символ" + offbalSymbolList[i].GetName() + " не соответствует балансовому символу" + substr(charSymbolList, 1, strlen(charSymbolList)-1));
                              end;
                              /*98*/
                              if ( (Int(offbalSymbolList[i].GetName()) == 98) and (outlaySymbolListSize == 0))
                                 j = 0;
                                 while (j < incomeSymbolListSize)
                                      charSymbolList = charSymbolList + incomeSymbolList[j].GetName() + ",";
                                      j = j + 1;
                                 end;
                                 server.AddToReport(document, "Символ" + offbalSymbolList[i].GetName() + " не соответствует балансовому символу" + substr(charSymbolList, 1, strlen(charSymbolList)-1));
                              end;
                            else
                              server.AddToReport(document,"Кассовый символ" + offbalSymbolList[i].GetName() + " не соответствует справочнику символов");
                            end;
                            i = i + 1;
                        end;

                        i = 0;
                        while (i < otherSymbolListSize)
                            server.AddToReport(document, "Документ содержит неверный кассовый символ" + otherSymbolList[i].GetName());
                            i = i + 1;
                        end;
                    end;
                end;
            end;

            local macro CheckSums()

                var isCorrectSums = true;

                if   (document.IsDebetCash() and document.IsCreditCash())
                    if (     (document.GetIncomeSymbolSum() == document.GetOutlaySymbolSum())
                         and (document.GetIncomeSymbolSum() == document.GetSum()))
                        // Всё в порядке
                    else
                        isCorrectSums = false;
                    end;
                elif (document.IsDebetCash())
                    if ((document.GetIncomeSymbolSum() + document.GetOtherSymbolSum()) == document.GetSum())
                        // Всё в порядке
                    else
                        isCorrectSums = false;
                    end;
                elif (document.IsCreditCash())
                    if ((document.GetOutlaySymbolSum() + document.GetOtherSymbolSum()) == document.GetSum())
                        // Всё в порядке
                    else
                        isCorrectSums = false;
                    end;
                end;

                if (not isCorrectSums)
                    server.AddToReport(document, "Сумма разнесения по символам не равна сумме документа");
                end;
            end;

            local macro CheckNonCash()
                var i = 0;

                var incomeSymbolList = document.GetIncomeSymbolList();
                var outlaySymbolList = document.GetOutlaySymbolList();
                var otherSymbolList  = document.GetOtherSymbolList();

                const incomeSymbolListSize = incomeSymbolList.Size();
                const outlaySymbolListSize = outlaySymbolList.Size();
                const otherSymbolListSize  = otherSymbolList.Size();

                if ((not document.IsDebetCash()) and (not document.IsCreditCash()))
                    if ((incomeSymbolListSize == 0) and (outlaySymbolListSize == 0))
                        // Всё в порядке
                    else
                        i = 0;
                        while (i < incomeSymbolListSize)
                            server.AddToReport(document, "Кассовый символ " + incomeSymbolList[i].GetName() + " стоит на документе, не затрагивающем счет кассы");
                            i = i + 1;
                        end;

                        i = 0;
                        while (i < outlaySymbolListSize)
                            server.AddToReport(document, "Кассовый символ " + outlaySymbolList[i].GetName() + " стоит на документе, не затрагивающем счет кассы");
                            i = i + 1;
                        end;
                    end;
                end;
            //end;

                if(document.GetKindDocument() != KINDDOCUMENT)
                  server.AddToReport(document, "Документ не указан как кассовый, вид документа " + document.GetKindDocument());
                end;
            end;

            //
            // Блок из трех проверок
            //

            // 1. Суммы всех приходных кассовых документов (документы, проведенные по дебету рублевых
            //    счетов, открытых на балансовых 20202, 20206, 20207, 20208) разнесены по кассовым символам
            //    от 02 до 39, а суммы всех расходных кассовых документов (документы, проведенные по
            //    кредиту тех же счетов), должны быть разнесены по символам от 40 до 77.

            CheckSymbols();

            // 2. Если документ разбит на несколько символов, то:
            //    - в случае, если документ затрагивает два счета кассы (и по дебету, и по кредиту),
            //      то сумма документа должна быть в два раза меньше, чем общая сумма всех символов документа,
            //      при этом сумма приходных символов должна быть равна сумме расходных символов, и обе они
            //      равны сумме документа;
            //    - в случае, если документ затрагивает один счет кассы (либо по дебету, либо по кредиту),
            //      то сумма документа должна быть равна общей сумме всех символов документа.

            CheckSums();

            // 3. Балансовые символы (кассовые символы 02 - 77) должны стоять только на документах, проходящих
            //    через счет кассы.

            CheckNonCash();

        end;

        InitProgress(-1, "Выполняется запрос...", "Идет отбор документов для обработки...");
        var dataSet = TRsbDataSet(documentQuery);
        RemProgress();

        var nDocument      = 0;
        var isLastDocument = false;
        var document       = NULL;
        var str_len,str;
        if (not dataSet.moveNext())
            server.AddToReport(NULL, "Нет документов для формирования отчета");
        else
            statErrDoc = NO_ERROR_DOC;
            InitProgress(-1, "", "Обработка документов");
            //println("Начало обработки: " + Time());
            while (not isLastDocument)

                document = TDocument(dataSet, isLastDocument);

                Message("Обработка документов за дату " + Date(document.GetCarryDate()) + " (" + document.GetPayerAccount() + " -> " + document.GetReceiverAccount() + ")");

                AnalyzeDocument(document);

                nDocument = nDocument + 1;
                UseProgress(nDocument);

            end;
            //println("Конец обработки: " + Time());
            //println("Обработано документов: " + nDocument);

            RemProgress();
       // end;

            if(statErrDoc == NO_ERROR_DOC)
              server.AddToReport(NULL, "Все кассовые документы распределены по символам. Ошибок в распределении выявлено не было.");
            end;
        end;
    end;

    macro CheckDelDoc()
        macro makeQuery()
            return          "SELECT   symbcash.t_accTrnId t_accTrnId,"
                   + "\n" + "         SUM(symbcash.t_sum) t_sum"
                   + "\n" + "    FROM dacctrn_dbt document,"
                   + "\n" + "         dsymbcash_dbt symbcash"
                   + "\n" + "   WHERE symbcash.t_docKind = 1"
                   + "\n" + "     AND symbcash.t_kind <> " + SYMBOL_KIND_OFFBAL
                   + "\n" + "     AND symbcash.t_date BETWEEN " + GetSqlDate(server.GetBeginDate())
                   + "\n" + "                             AND " + GetSqlDate(server.GetEndDate())
                   + "\n" + "     AND document.t_accTrnId(+) = symbcash.t_accTrnId"
                   + "\n" + "     AND document.t_accTrnId IS NULL"
                   + "\n" + "GROUP BY symbcash.t_accTrnId";
        end;

        InitProgress(-1, "Обработка документов", "Оработка некорректно удаленных документов...");

        var dataSet = TRsbDataSet(makeQuery());

        dataSet.SetFieldType("sum", V_MONEY);

        var nDocument = 0;

        while (dataSet.moveNext())
            server.AddToReport(dataSet.sum, "Некорректно удален документ "
                                          + "(c accTrnId = " + dataSet.accTrnId
                                          + "), в системе остались данные о разбивке суммы по символам");
            statErrDoc = ERROR;


            nDocument = nDocument + 1;

            UseProgress(nDocument);
        end;

        RemProgress();
    end;

    ProcessDocuments(GetDocumentQuery());

    /* 4. Если в файле разбивки символов (symbcash) есть запись, что документ с ID=nnnnn разбит на символы,
          а в системе документа с ID=nnnnn нет (например его удалили, а разбивка почему-то осталась).*/
    CheckDelDoc();

end;

macro View( name_log, need_mes )
  FILE  f_out () txt;

  SetOutput( NULL );

  if ( ExistFile(name_log,0) and  open( f_out, name_log ) )
      viewFile( f_out );
      close( f_out );
      return TRUE;
  elif ( (need_mes == NULL) or (need_mes) )  /* сообщение по умолчанию */
    msgbox( "Не найден файл протокола: |", name_log );
  end;
  return FALSE;
end;

macro produceReport(departmentCode : Integer, IssueMode : String, OrgStructure : String, beginDate : Date, endDate : Date)
    var commonHeadingPrinter : TCommonHeadingPrinter;
    var planNumber           : Integer;
    DefaultParm( planNumber, 0 );
    DefaultParm( commonHeadingPrinter, TCommonHeadingPrinter( CTableReport( 0, false, true ), beginDate, endDate ) );
    GetRegDateDefault(ДАТА1_1660У, "ДАТА1_1660У");
    GetRegDateDefault(ДАТА_1881У,  "ДАТА_1881У");

    var server = TServer(planNumber, beginDate, endDate, departmentCode, commonHeadingPrinter );

    ControlProcedure(server);
end;

macro Report(planNumber : Integer, beginDate : Date, endDate : Date, departmentCode : Integer, commonHeadingPrinter : TCommonHeadingPrinter )
    DefaultParm( beginDate, {curDate} );
    DefaultParm( endDate, {curDate} );
    DefaultParm( departmentCode, {OperDprt} );
    produceReport(departmentCode, "", "", beginDate, endDate);
end;


macro showReport()

    View( protocolFileName );

end;
