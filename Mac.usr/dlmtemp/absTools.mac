 import iProgBar;
 import absTrait;
 import constant;
 import debug;

  macro count(list):integer
/*    var pbar=CDeferredProgressBar(1," ~Подсчитываю список...~","Подсчет списка");*/
    var i=0,
        stat=list.first();
    while(stat)
      i=i+1;
/*      pbar.next();*/
      stat=list.next();
    end;
    return i;
  end;

  macro iabsPrintList(list)
    dbgPrint(list,true);
    var nrec=count(list);
    dbgPrint(nrec);
    var pbar=CDeferredProgressBar(nrec," ~Печатаю список...~","Распечатка списка");
    var stat=list.first();
    dbgPrint(stat);
    while(stat)
      dbgPrint(list.rec(),true);
      pbar.next();
      stat=list.next();
    end;
  end;

  macro absCheckTypeDesc(o,type)
    if(ValType(type) == V_INTEGER)
      if(ValType(o) == type)
        return true; end;
      if(ValType(o) != V_GENOBJ)
        return string("DLM return ",ValTypeString(o),", expected ",type); end;
      if(IsEqClass("IABSError",o))
        if(IsEqClass("IABSDBMSError",o))
          return string(GenClassName(o),", code: ",o.code(),", type: \"",o.type(),"\", desc: \"",o.what(),"\"");
        else
          return string(GenClassName(o),", type: \"",o.type(),"\", desc: \"",o.what(),"\""); end;
      else
        return string("DLM return object \"",GenClassName(o),"\", expected ",type); end;

    elif(ValType(type) == V_STRING)
      if(ValType(o) != V_GENOBJ)
        return string("DLM return ",ValTypeString(o),", expected object \"",type,"\""); end;
      if(IsEqClass(type,o))
        return true; end;
      if(IsEqClass("IABSError",o))
        if(IsEqClass("IABSDBMSError",o))
          return string(GenClassName(o),", code: ",o.code(),", type: \"",o.type(),"\", desc: \"",o.what(),"\"");
        else
          return string(GenClassName(o),", type: \"",o.type(),"\", desc: \"",o.what(),"\""); end;
      else
        return string("DLM return \"",GenClassName(o),"\", expected object \"",type,"\""); end;

    elif(ValType(type) == V_UNDEF)
      if((ValType(o) != V_GENOBJ) OR (not IsEqClass("IABSError",o)))
        return true; end;
      return string(GenClassName(o),", type: \"",o.type(),"\", desc: \"",o.what(),"\"");

    else
      return "Bad parameters for absCheckTypeDesc";
    end;
  end;

  macro absCheck(v,type):bool
    var result=absCheckTypeDesc(v,type);
    if(ValType(result) != V_BOOL)
      return false; end;
    return result;
  end;

  macro absXCheck(v,type)
    var result=absCheckTypeDesc(v,type);
    if((ValType(result) != V_BOOL) OR (result != true))
      RunError(result); end;
    return v;
  end;

  macro absXAssign(to,from,type)
    if(ValType(type) != V_UNDEF)
      absXCheck(from,type);
    else
      absXCheck(from,ValType(to)); end;
    SetParm(0,from);
  end;


  class CABSList(__list:object,__recordClassName:string,__listClassName)
    var _list=__list;
    var _recordClassName=__recordClassName;
    local var _listClassName="IABSObjectList";
    if(ValType(__listClassName) == V_STRING)
      _listClassName=__listClassName; end;
    absXCheck(_list,_listClassName);

    macro list()
      return _list;
    end;

    macro first():bool
      var result=_list.first();
      absXCheck(result,V_BOOL);
      return result;
    end;

    macro last():bool
      var result=_list.last();
      absXCheck(result,V_BOOL);
      return result;
    end;

    macro next():bool
      var result=_list.next();
      absXCheck(result,V_BOOL);
      return result;
    end;

    macro prev():bool
      var result=_list.prev();
      absXCheck(result,V_BOOL);
      return result;
    end;

    macro rec()
      var r=_list.rec();
      absXCheck(r,_recordClassName);
      return r;
    end;
  end; /* class CABSList */

  macro absdbgPrint(o,indent)
    if((ValType(o) != V_GENOBJ) OR (not IsEqClass("IABSObjectList",o)))
      return dbgPrint(o,indent); end;

    var recursivePrint=false;
    if(((ValType(indent) == V_BOOL) AND indent) OR (ValType(indent) == V_INTEGER))
      recursivePrint=true; end;
    if(ValType(indent) != V_INTEGER)
      indent=0; end;
    var indentStr="";
    var i=0;
    while(i < indent)
      indentStr=indentStr + "│ ";
      i=i + 1;
    end;

    var title=string(o);
    if(indent > 0)
      println(indentStr,title);
    else
      println("\n",title); end;
    var stat=o.first(),v,prevRecurse=false;
    i=0;
    while(stat)
/*      dbgPrint(o.rec(),1);*/
      v=o.rec();
      if(prevRecurse)
        println(indentStr,"│ << continue of ",title," >>"); end;
      println(indentStr,"├record[",i,"]","=",v,", type: ",ValTypeString(v));
      if(recursivePrint AND (ValType(v) == V_GENOBJ))
        absdbgPrint(v,indent + 1);
        println(indentStr,"├─┘ << end of ",v," >>");
        prevRecurse=true;
      else
        prevRecurse=false;
      end;
      stat=o.next();
      i=i+1;
    end;
    if(indent == 0)
      println("╧ << end of ",title," >>"); end;
  end;

  private macro _IsTRecHandler(o,TblName:string):bool
    if((ValType(o) != V_GENOBJ) OR
       ((not IsEqClass("TBfile",o)) AND (not IsEqClass("TRecHandler",o))) OR
       (strupr(o.TblName) != strupr(TblName)))
      return false; end;
    return true;
  end;

  macro absGetImplementationRecHandler(oImpl:object,TblName:string)
    if(not IsEqClass("IABSObjectList",oImpl))
      if(_IsTRecHandler(oImpl,TblName))
        return oImpl; end;
      return null;
    end;
    var stat=absXCheck(oImpl.first(),V_BOOL);
    while(stat AND (not _IsTRecHandler(oImpl.rec(),TblName)))
      stat=absXCheck(oImpl.next(),V_BOOL);
    end;
    if(stat)
      return oImpl.rec(); end;
    return null;
  end;

  macro absCreateRecHandler(oABS,p1,p2,p3,p4,p5,p6)
    if(IsABSInterfaceLoadRemotely(oABS))
      return oABS._callHost("TRecHandler",p1,p2,p3,p4,p5,p6); end;
    return TRecHandler(p1,p2,p3,p4,p5,p6);
  end;

  macro absCreateBFile(oABS,p1,p2,p3,p4,p5)
    if(IsABSInterfaceLoadRemotely(oABS))
      return oABS._callHost("TBFile",p1,p2,p3,p4,p5); end;
    return TBFile(p1,p2,p3,p4,p5);
  end;

  macro GetBORWPDocumentId(rec):string
    var StructName;
    var id;
    if((ValType(rec) == V_GENOBJ) AND
       (IsEqClass("TBfile",rec) OR IsEqClass("TRecHandler",rec)))
      StructName=strlwr(rec.TblName());
      id=rec.rec().id;
    else
      StructName=strlwr(FileName(rec));
      /*SplitFile*/
      id=rec.id;
    end;
    var prefix;
    if(index(StructName,strlwr(clbcStructNamePaymentDoc)))
      prefix=clbcMsgTypePaymentDoc;
    elif(index(StructName,strlwr(clbcStructNameInfoMessage)))
      prefix=clbcMsgTypeInfoMessage;
    elif(index(StructName,strlwr(clbcStructNameExtractHeader)))
      prefix=clbcMsgTypeExtractHeader;
    elif(index(StructName,strlwr(clbcStructNameExtractDoc)))
      prefix=clbcMsgTypeExtractDoc;
    elif(index(StructName,strlwr(clbcStructNameReceipt)))
      prefix=clbcMsgTypeReceipt;
    else
      prefix=StructName;
    end;
    return string(prefix," ",id);
  end;

