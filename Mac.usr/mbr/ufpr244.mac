/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Печатная форма сообщения ED244 транспорта УФЭБС                          */
/*                                                                          */
/*  Имя файла: ufgp244.mac                                                  */
/*  Создан:    25.04.12                                                     */
/****************************************************************************/

import BankInter, "wluftool.mac";
  
//Gurin S. 01.04.2014
private macro ToDateEd244( strYYMMDD )
    var sql  = execSQLSelect ("select to_date(:str,'yy.mm.dd') st from dual", makeArray (SQLParam ("str", strYYMMDD)));
    if (sql.moveNext)
        return sql.value ("st", null, V_DATE);
    end;
OnError
    return Date();
end;

macro PrintDoc( addrMes, MassCopy )
  SetBuff( wlmes, addrMes );
  
  FILE MsgTmpFile() txt;
  var MsgTmpFileName, OldName;
  
  // Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные
  //------------------------------------------ 
  MsgTmpFileName = GetTxtFileName("tmpmsprn");
  OldName = SetOutPut( MsgTmpFileName );

  if( not open(MsgTmpFile, MsgTmpFileName) )
    std.msg( String("Файл не открыт:", "|", MsgTmpFileName) );
    return FALSE;
  end;
  
  // EDNoDateAuthor
  // ED244 Ответ на запрос участника
  //------------------------------------------
  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);
  //Gurin S. 01.04.2014
  ed_nda.EDDate = ToDateEd244(string(substr(wlmes.TRN,1,6)));
  [
    ED244 ОТВЕТ НА ЗАПРОС УЧАСТНИКА

    Номер ЭС:    #########
    Дата ЭС:     ########## ]( ed_nda.EDNo, ed_nda.EDDate );

  var Code = "04"+ SubStr( ed_nda.EDAuthor,1,7 );
  var select = "select party.t_Name "+ 
                   "from dparty_dbt party, dpartcode_dbt partcode "+
                          "where party.t_PartyID = partcode.t_PartyID " +
                          "and partcode.t_Codekind =:CodeKind "+
                          "and partcode.t_Code =:Code ";
  var params = makeArray( SQLParam("CodeKind", 3),
                      SQLParam("Code", Code) );

  var rs = execSQLselect( select, params, FALSE );
  Array aName;
  if( rs.MoveNext() )
    [ Составитель: ########## ################################################ ]( ed_nda.EDAuthor, rs.value(0) );               
  end;
   
  Code = "";
  var PartyID = -1, PartyBIC = "", PartyName = "";
  if(wlmes.Direct == "X")
    select = "select partcode.t_Code, party.t_Name "+
              "from dparty_dbt party, dpartcode_dbt partcode, ddp_dep_dbt dp_dep "+
                        "where dp_dep.t_Code =: Department "+
                        "and dp_dep.t_PartyID = party.t_PartyID "+
                        "and partcode.t_PartyID = party.t_PartyID "+
                        "and partcode.t_CodeKind =: CodeKind ";

    params = makeArray( SQLParam("Department", wlmes.Department),
                        SQLParam("CodeKind", 3) );
    rs = execSQLselect( select, params, FALSE );

    if( rs and rs.MoveNext() )
      PartyBIC = rs.value(0);
      PartyName = rs.value(1);    
    end;
  else
    select = "select party.t_PartyID, party.t_Name " +
             "  from dparty_dbt party, dwlreq_dbt wlreq, dwlmeslnk_dbt lnk " +
             " where party.t_PartyID = wlreq.t_RecipientID " +
             "   and wlreq.t_ReqID = lnk.t_ObjID " +
             "   and lnk.t_ObjKind = :ObjKind " +
             "   and lnk.t_MesID = :MesID " +
             "   and lnk.t_Direct = chr(0) ";
    params = makeArray( SQLParam("ObjKind", OBJTYPE_REQ),
                        SQLParam("MesID",   wlmes.MesID) );
    rs = execSQLselect( select, params );
    if(rs and rs.moveNext)
      PartyID = rs.value("t_PartyID");
      PartyName = rs.value("t_Name");
    end;
    PartyBIC = ПолучитьКодСубъекта(PartyID, PTCK_BIC);
  end;
  Code = SubStr(PartyBIC,3,7) + "000";
  [ Получатель:  ########## ################################################ ]
  ( Code, PartyName );

  var fieldname = "", fieldvalue = "", blockname = "";
  Array aCode, atmpstr;
  var IsRequisiteHeaderPrinted = false;
  while( СчитатьПоле(fieldname, fieldvalue, blockname) )
  
  // Код запроса
  //------------------------------------------
    if (fieldname == "EDDefineRequestCode")
      Code = "/УТ"+ fieldvalue + "/";
      select = "select wlcodes.t_Description "+
                "from dwlcodes_dbt wlcodes "+
                   "where wlcodes.t_AlgNum =: AlgNum "+
                   "and wlcodes.t_Code =: Code ";
  
      params = makeArray( SQLParam("AlgNum", 49),
                         SQLParam("Code", Code) );
  
      rs = execSQLselect( select, params, FALSE );
      
      if( rs and rs.MoveNext() )
        StrSplit( rs.value(0), aCode, 68, 68, 3 );
        [
          Код запроса: ## ####################################################################
                          ####################################################################
                          ####################################################################]( fieldvalue, aCode(0), aCode(1), aCode(2) );
      end;
    end;
  
    // Код ответа
    //------------------------------------------
    if (fieldname == "EDDefineAnswerCode")
      Code = "/ОТ"+ fieldvalue + "/";
      select = "select wlcodes.t_Description "+
                  "from dwlcodes_dbt wlcodes "+
                     "where wlcodes.t_AlgNum =: AlgNum "+
                     "and wlcodes.t_Code =: Code ";
  
      params = makeArray( SQLParam("AlgNum", 52),
                          SQLParam("Code", Code) );
  
      rs = execSQLselect( select, params, FALSE );
 
      if( rs.MoveNext() )
        StrSplit( rs.value(0), aCode, 68, 68, 3 );
        [
          Код ответа: ## ####################################################################
                         ####################################################################
                         ####################################################################]( fieldvalue, aCode(0), aCode(1), aCode(2) );
      end;
    end;

    // Реквизиты ЭПС, поясняющие ответ
    //------------------------------------------
    if ( not IsRequisiteHeaderPrinted and 
         index(blockname, "EDDefineAnswerInfo") and
         (fieldname != beginField) and (fieldname != "") and (fieldvalue != "") 
       )
        [ 
          Реквизиты ЭПС, поясняющие ответ:   ];
      IsRequisiteHeaderPrinted = true;      
    end;

    if (blockname == "EDDefineAnswerInfo")
      if ((fieldname == "AccDocDate") AND (fieldvalue != "") )
        
        [    Дата документа:                     ########## ]( fieldvalue );
      end;
      if ((fieldname == "AccDocNo") AND (fieldvalue != "") )
        [    Номер документа:                    ### ]( fieldvalue );
      end;
    end;

      if ((fieldname == "PayerINN") AND (fieldvalue != "") )
        [    ИНН плательщика (60):               ############ ]( fieldvalue );
      end;
      if ((fieldname == "PayerKPP") AND (fieldvalue != "") )
        [    КПП плательщика (102):              ######### ]( fieldvalue );
      end;

    if ( (blockname == "EDDefineAnswerInfo\\PayerLongName") and
         (fieldname == "PayerLongName") AND (fieldvalue != "") )
        StrSplit( fieldvalue, atmpstr, 34, 34, 2 );
        [    Наименование плательщика:           ##################################
                                                 ################################## ]( atmpstr(0), atmpstr(1));
    end;

    if ( (blockname == "EDDefineAnswerInfo\\Address") and
         (fieldname == "Address") AND (fieldvalue != "") )
        StrSplit( fieldvalue, atmpstr, 34, 34, 2 );
        [    Адрес плательщика:                  ##################################
                                                 ################################## ]( atmpstr(0), atmpstr(1));
    end;

    if (blockname == "EDDefineAnswerInfo")
      if ((fieldname == "PayerAcc") AND (fieldvalue != "") )
        [    Номер счета плательщика:            #################### ]( fieldvalue );
      end;
    end;

      if ((fieldname == "PayeeINN") AND (fieldvalue != "") )       
        [    ИНН получателя (61):                ############]( fieldvalue );
      end;
      if ((fieldname == "PayeeKPP") AND (fieldvalue != "") )       
        [    КПП получателя (103):               #########]( fieldvalue );
      end;

    if ( (blockname == "EDDefineAnswerInfo\\PayeeLongName") and
         (fieldname == "PayeeLongName") AND (fieldvalue != "") )
        //Gurin S. 01.04.2014
        StrSplit( fieldvalue, atmpstr, 34, 34, 3 );
        [    Наименование получателя:            ##################################
                                                 ##################################
                                                 ################################## ]( atmpstr(0), atmpstr(1), atmpstr(2));
    end;

    if (blockname == "EDDefineAnswerInfo")
      if ((fieldname == "PayeeAcc") AND (fieldvalue != "") )
        [    Номер счета получателя:             #################### ]( fieldvalue );
      end;
      if ((fieldname == "Sum") AND (fieldvalue != "") )
        [    Сумма:                              ################## ]( fieldvalue );
      end;
    end;

    //Gurin S. 21.06.2014
    if (blockname == "EDDefineAnswerInfo\\EDDefineAnswerText")
      if ((fieldname == "EDDefineAnswerText") AND (fieldvalue != "") )
        StrSplit( fieldvalue, atmpstr, 66, 66, 4 );
        [    Текст ответа:                 
                  #################################################################
                  #################################################################
                  #################################################################
                  ################################################################# ]( atmpstr(0), atmpstr(1), atmpstr(2), atmpstr(3) );
      end;
    end;

    if ( blockname == "EDDefineAnswerInfo\\AccDocAddInfo" )
      if ((fieldname == "DrawerStatus") AND (fieldvalue != "") )
        [    Статус составителя (101):           ## ]( fieldvalue );
      end;
      if ((fieldname == "CBC") AND (fieldvalue != "") )
        [    КБК (104):                          #################### ]( fieldvalue );
      end;
      if ((fieldname == "OKATO") AND (fieldvalue != "") )
        [    Код ОКАТО (105):                    ########### ]( fieldvalue );
      end;
      if ((fieldname == "PaytReason") AND (fieldvalue != "") )
        [    Основание налогового платежа (106): ## ]( fieldvalue );
      end;
      if ((fieldname == "TaxiPeriod") AND (fieldvalue != "") )
        [    Налоговый период (107):             ########## ]( fieldvalue );
      end;
      if ((fieldname == "DocNo") AND (fieldvalue != "") )
        [    Номер налогового документа (108):   ############### ]( fieldvalue );
      end;
      if ((fieldname == "DocDate") AND (fieldvalue != "") )
        [    Дата налогового документа (109):    ########## ]( fieldvalue );
      end;
      if ((fieldname == "TaxPaytKind") AND (fieldvalue != "") )
        [    Тип налогового платежа (110):       ## ]( fieldvalue );
      end;
    end;

    if ( (blockname == "EDDefineAnswerInfo\\Purpose") and
         (fieldname == "Purpose") AND (fieldvalue != "") )
         StrSplit( fieldvalue, atmpstr, 66, 66, 2 );
        [    Назначение платежа:                 
                  #################################################################
                  ################################################################# ]( atmpstr(0), atmpstr(1) );
    end;

    //Gurin S. 13.06.2014 
    if ( blockname == "EDDefineAnswerInfo\\EDFieldList\\FieldNo" )
        if ((fieldname == "FieldNo") AND (fieldvalue != ""))
            [    #####################:]( fieldvalue );
        end;
    end;
    
    if ( blockname == "EDDefineAnswerInfo\\EDFieldList\\FieldValue" )
        if ((fieldname == "FieldValue") AND (fieldvalue != ""))
            StrSplit( fieldvalue, atmpstr, 67, 67, 3 );
            [        ################################################################
                     ################################################################
                     ################################################################ ]( atmpstr(0), atmpstr(1), atmpstr(2));
        end;
    end;
    
    // Идентификаторы исходного ЭПС
    //------------------------------------------
    if ( blockname == "OriginalEPD" )
      if (fieldname == "EDNo" )
        [
             Номер ЭС:     ######### ]( fieldvalue );
      end; 
      if (fieldname == "EDDate" )
        [    Дата ЭС:      ########## ]( fieldvalue );
      end;
      if (fieldname == "EDAuthor" )
        //Gurin S. 01.04.2014
        [
          Идентификаторы исходного ЭПС: ];      

        Code = "04"+SubStr( fieldvalue, 1, 7 );
        select = "select party.t_Name "+
                    "from dparty_dbt party, dpartcode_dbt partcode "+
                      "where party.t_PartyID = partcode.t_PartyID "+
                      "and partcode.t_CodeKind =: CodeKind "+
                     "and partcode.t_Code =: Code"; 
  
        params = makeArray( SQLParam("CodeKind", 3),
                           SQLParam("Code", Code) );
  
        rs = execSQLselect( select, params, FALSE );

        if( rs.MoveNext() )
          StrSplit( rs.value(0), aName, 46, 46, 2 );
          [    Составитель:  ########## ##############################################
                                        ############################################## ]( fieldvalue, aName(0), aName(1) );
        end;
      end;
    end;
  
    // Идентификаторы исходного ЭСИС
    //------------------------------------------
    if ( blockname == "InitialED" )
      if (fieldname == "EDNo" );
        [
             Номер ЭС:     ######### ]( fieldvalue ); 
      end;
      if (fieldname == "EDDate" );
        [    Дата ЭС:      ########## ]( fieldvalue );
      end;
      if (fieldname == "EDAuthor" );
        //Gurin S. 01.04.2014
        [ 
          Идентификаторы исходного ЭСИС: ];
        Code = "04"+SubStr( fieldvalue, 1, 7 );
        select = "select party.t_Name "+
                    "from dparty_dbt party, dpartcode_dbt partcode "+
                      "where party.t_PartyID = partcode.t_PartyID "+
                      "and partcode.t_CodeKind =: CodeKind "+
                      "and partcode.t_Code =: Code"; 
  
        params = makeArray( SQLParam("CodeKind", 3),
                            SQLParam("Code", Code) );
  
        rs = execSQLselect( select, params, FALSE );

        if( rs.MoveNext() )
          StrSplit( rs.value(0), aName, 46, 46, 2 );
          [    Составитель:  ########## ##############################################
                                        ############################################## ]( fieldvalue, aName(0), aName(1) );
        end;
      end;
    end;
  end;
  // Перенаправляем вывод обратно
  //------------------------------------------
  
  SetOutPut( OldName, true ); 
  while( MassCopy != 0 )
    rewind( MsgTmpFile );
    while( next(MsgTmpFile) )
      println( MsgTmpFile.str );
    end;
    MassCopy = MassCopy - 1;
  end;
  close( MsgTmpFile ); 
    return true;                                                               
end;