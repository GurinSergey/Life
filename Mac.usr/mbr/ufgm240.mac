/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED240 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm240.mac                                                  */
/*  Создан:    04.03.09                                                     */
/****************************************************************************/

import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac";

var ver_st = 1;

const code10 = "/ИП10/", 
      code20 = "/ИП20/",
      code21 = "/ИП21/", 
      code22 = "/ИП22/",
      code23 = "/ИП23/",
      code24 = "/ИП24/",
      code25 = "/ИП25/",
      code26 = "/ИП26/";

const ZeroDate = date(0,0,0);

/* Проверка на содержание нечисловых символов в строке */
/*
macro IsDigitStr( Str )

  var stat = 0, i = 1, ch, DigitString = "0123456789";

      while( (not stat) and (i <= strlen(Str)) )
        ch = SubStr( Str, i, 1 );
        if( not Index( DigitString, ch ))
          stat = 1; 
        end;
        i = i + 1;
      end;

  return stat;

end;
*/

macro GetTypeCodeAndSessionID(Строка, TypeCode, SessionID);
   var Найдено = 0, type, ID = 0;
   if  ( Index(Строка, code10))
      type = "1";
      ID = 0;
   elif( Index(Строка, code20))
      type = "2";
      ID = 0;
   elif( Index(Строка, code21))
      type = "2";
      ID = 1;
   elif( Index(Строка, code22))
      type = "2";
      ID = 2;
   elif( Index(Строка, code23))
      type = "2";
      ID = 3;
   elif( Index(Строка, code24))
      type = "2";
      ID = 4;
   elif( Index(Строка, code25))
      type = "2";
      ID = 5;
   elif( Index(Строка, code26))
      type = "2";
      ID = 6;
   else
     return false;
   end;

   setParm(1, type);
   setParm(2, ID);
   return true;
end;
 
macro GenMes( addrMes, addrReq )
  var field_value, TypeCode, SessionID, MesForm;

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );

  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object;

  mes = xml.createElement("ED240");
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");

  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  mes.setAttribute("EDNo",       ed_nda.EDNo );
  mes.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  mes.setAttribute("EDAuthor",   ed_nda.EDAuthor );  
  
  
  if(not GetTypeCodeAndSessionID(wlReq.Queries, TypeCode, SessionID))
      std.msg("Ошибка: Для формы ED240 в поле 'Сообщение' необходимо |ввести один из фиксированных кодов из справочника");
      return false;
  end;

  mes.setAttribute("ExchangeTypeCode", TypeCode );

  if( wlreq.InitFormIDMes )

    f_wlmesfrm.FormID = wlReq.InitFormIDMes;

    if( GetEQ(f_wlmesfrm) )
      if(wlreq.IsForSess != "X")
        MesForm = "0" + substr(f_wlmesfrm.Name,3);
        mes.setAttribute("EDTypeNo", MesForm );
      else     
        //DAI Hotfix HF2031_19_25_12    
        if( InList( substr(f_wlmesfrm.Name, 1, 5), 
            "ED208", "ED243", "ED244", "ED274", "ED275", "ED113", "ED114", "ED276" )
          )
          mes.setAttribute("EDTypeNo", "1003" );
        elif(f_wlmesfrm.KindMes == MESKIND_PAYMENT)
          mes.setAttribute("EDTypeNo", "1001" );
        else
          mes.setAttribute("EDTypeNo", "1002" );
        end;
      end;
    end;

  end;
  
     
  if( SessionID )
    mes.setAttribute("SessionID", SessionID );
  end;
   
  if( wlreq.InitDateMes != ZeroDate )
    mes.setAttribute("InquiryDate", YYYYMMDD(wlreq.InitDateMes));
  end;

  var IndexCutStart = 0, IndexCutEnd = 0, ARMNo, Temp;
  if  ( Index(wlReq.Queries, "ARMNo:") )
    
    IndexCutStart = Index(wlReq.Queries, "ARMNo:");
    
    Temp = SubStr( wlReq.Queries, IndexCutStart + 6); 
    
    if( IndexCutEnd = Index(Temp, "\n") )
      Temp = SubStr( Temp, 1, IndexCutEnd);
    end;
    ARMNo = Trim( Temp );
    
    if( (StrLen( ARMNo ) == 2) AND (IsDigitStr( ARMNo ) == 0) )
      mes.setAttribute("ARMNo", ARMNo );
    else
      MsgBoxEx( "Неверно задан номер АРМ получателя (отправителя). Поле ARMNo сообщения заполнено не будет", 0);
    end;
  end;

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;
