/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по "Луч" MF170                                       */
/*                                                                          */
/*  Имя файла: raygm170.mac                                                 */
/*  Создан:  30.03.09                                                       */
/****************************************************************************/
import "raygenmes.mac";

macro GenMes( addrMes, addrbuf )
  file llval(llvalues) key 0;
  RECORD DpBuf( dpmsginf );
  var    msginf, msgdoc, msgParty;  
  var    error,PartyCode, ProcessingCode,field_value;

  SetBuff( wlmes,  addrMes );
  SetBuff( DpBuf,  addrbuf );
  
  msginf = RsbDepoInfoMessage(DpBuf.MessageID);

  if ( (valtype(msginf)==V_UNDEF ) or ( valtype(msginf.MessageID)==V_UNDEF ))
     RunError( "|Не определен объект класса" );     
  end;

  PrintLog(2,"Генерация сообщения по МF010");

   /*Блок ORDER_HEADER*/
  Write_ORDER_HEADER_Block( msginf );

  ProcessingCode = msginf.ProcessingCode;

  /* Блок MF170 */
  ЗаписатьПолеЛогВБлок( MF170_Block, BeginOfBlock, MF170_Block );

  if(  (ProcessingCode == "17/0") or (ProcessingCode == "17/2") or
       (ProcessingCode == "17/3") or (ProcessingCode == "17/20")or
       (ProcessingCode == "17/30")or (ProcessingCode == "16/2") or
       (ProcessingCode == "17/1") or (ProcessingCode == "16/3") )
    msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_DEAG);
    if ( valtype(msgParty)!=V_UNDEF )     
      /*dep_acc_c*/
      ЗаписатьПолеЛогСПроверкой( dep_acc_c_Field, msgParty.Account );
      /*sec_c*/
      ЗаписатьПолеЛогСПроверкой( sec_c_Field, msgParty.Partition );
    else
      if((ProcessingCode != "17/1") and (ProcessingCode != "16/3"))
        RunError( "|Отсутствует информация об идентификаторе стороны (DEAG)" );
      end;
    end; 
  end;

  
  if(  (ProcessingCode == "17/0") or (ProcessingCode == "17/1") or
       (ProcessingCode == "17/2") or (ProcessingCode == "17/20")or
       (ProcessingCode == "17/30")or (ProcessingCode == "17/6") or
       (ProcessingCode == "17/7") or (ProcessingCode == "16/2") or 
       (ProcessingCode == "16/3") or (ProcessingCode == "17/3"))

    msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_REAG);
    if ( valtype(msgParty)!=V_UNDEF )
      /*corr_acc_c*/
      ЗаписатьПолеЛогСПроверкой( corr_acc_c_Field, msgParty.Account );
      /*corr_sec_c*/
      ЗаписатьПолеЛогСПроверкой( corr_sec_c_Field, msgParty.Partition );
    else
      if(ProcessingCode != "17/3")
        RunError( "|Отсутствует информация об идентификаторе стороны (REAG)" );
      end;
    end; 
  end;

  msgParty = GetMessageParty(msginf,DPMSGPAT_TRADEPLACE,OBJTYPE_MSGPATYTP_REAG);
  if ( valtype(msgParty)!=V_UNDEF )
    /*deal_num*/
    ЗаписатьПолеЛог( dep_acc_c_Field, msgParty.ProcessingReference  );
    /*deal_date*/
    ЗаписатьПолеЛог( sec_c_Field, YYYYMMDD(msgParty.ProcessingDate) );
  else
    RunError( "|Отсутствует информация об идентификаторе стороны (REAG)" );
  end; 

  /*con_code*/ 
  if( ProcessingCode == "16/2")
    msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_BUYR);
    if ( valtype(msgParty)!=V_UNDEF )
      ЗаписатьПолеЛогСПроверкой(con_code_Field, msgParty.DepoPartyCode);
    end;
  elif( ProcessingCode == "16/3")
    msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_SELL);
    if ( valtype(msgParty)!=V_UNDEF )
      ЗаписатьПолеЛогСПроверкой(con_code_Field, msgParty.DepoPartyCode);
    end;
  end;

  if((ProcessingCode == "17/20")or (ProcessingCode == "16/2") or (ProcessingCode == "16/3") or 
     (ProcessingCode == "17/0") or (ProcessingCode == "17/1") or (ProcessingCode == "17/2") or
     (ProcessingCode == "17/3") or (ProcessingCode == "17/6") or (ProcessingCode == "17/7") )

    msgParty = GetMessageParty(msginf,DPMSGPAT_CURRENCIES,OBJTYPE_MSGPATYTP_PAYE);
    if ( valtype(msgParty)!=V_UNDEF )
      /*sen_acc*/ 
      ЗаписатьПолеЛог(sen_acc_Field, msgParty.Account);
      /*sen_bic*/ 
      PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_BIC, error );
      if( not error )
        ЗаписатьПолеЛог(sen_bic_Field, PartyCode);
      else
        if((ProcessingCode == "17/20")or (ProcessingCode == "16/2") or (ProcessingCode == "16/3") )
          RunError(string("|Не найден код вида 'Код' для субъекта с ID = ",msgParty.PartyID));
        end;    
      end;    
    else
      if((ProcessingCode == "17/20")or (ProcessingCode == "16/2") or (ProcessingCode == "16/3") )
        RunError( "|Отсутствует информация об идентификаторе стороны (PAYE)" );
      end;    
    end;


    msgParty = GetMessageParty(msginf,DPMSGPAT_CURRENCIES,OBJTYPE_MSGPATYTP_BENM);
    if ( valtype(msgParty)!=V_UNDEF )
      /*rec_acc*/ 
      ЗаписатьПолеЛог(rec_acc_Field, msgParty.Account);
      /*rec_bic*/ 
      PartyCode = ПолучитьКодСубъекта( msgParty.PartyID, PTCK_BIC, error );
      if( not error )
        ЗаписатьПолеЛог(rec_bic_Field, PartyCode);
      else
        if((ProcessingCode == "17/20")or (ProcessingCode == "16/2") or (ProcessingCode == "16/3") )
          RunError(string("|Не найден код вида 'Код' для субъекта с ID = ",msgParty.PartyID));
        end;    
      end;    
    else
      if((ProcessingCode == "17/20")or (ProcessingCode == "16/2") or (ProcessingCode == "16/3") )
        RunError( "|Отсутствует информация об идентификаторе стороны (BENM)" );
      end;    
    end;
  end;

  msgParty = GetMessageParty(msginf,DPMSGPAT_DEPONENT,0);
  if ( valtype(msgParty)!=V_UNDEF )
    /*pay_sum*/ 
    ЗаписатьПолеЛог(pay_sum_Field, msgParty.DealAmount);
    /*pay_curr*/ 
    ClearRecord(f_wlfininstr);
    f_wlfininstr.FIID = msgParty.DealAmountCurrency;
    if( getEQ( f_wlfininstr ))
      ЗаписатьПолеЛог( pay_curr_Field,  f_wlfininstr.Ccy);
    end;
  else
    if((ProcessingCode == "17/2")or (ProcessingCode == "17/20") or (ProcessingCode == "17/3") or 
       (ProcessingCode == "17/30") or (ProcessingCode == "16/2") or (ProcessingCode == "16/3") )
      RunError( "|Отсутствует информация о депоненте" );
    end;
  end;
    
  /*block_securities*/       
  if ( msginf.NeedBlock == "X" )
    ЗаписатьПолеЛог(block_securities_Field, "Y");
  else
    ЗаписатьПолеЛог(block_securities_Field, "N");
  end;

  /*con_bic*/
  if( ProcessingCode == "16/2")
    msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_BUYR);
    if ( valtype(msgParty)!=V_UNDEF )
      ЗаписатьПолеЛог(con_bic_Field, msgParty.PartyCode);
    end;
  elif( ProcessingCode == "16/3")
    msgParty = GetMessageParty(msginf,DPMSGPAT_SECURITIES,OBJTYPE_MSGPATYTP_SELL);
    if ( valtype(msgParty)!=V_UNDEF )
      ЗаписатьПолеЛог(con_bic_Field, msgParty.PartyCode);
    end;
  end;

  if( (ProcessingCode != "17/6") and (ProcessingCode != "17/7") )
    /*Блок seсurities*/
    if( not УстановитьКонтекстБлока( seсurities_Block ) )
      RunError("|Ошибка при установке контекста блока "+seсurities_Block);
    end;
    ЗаписатьПолеЛог( BeginOfBlock, seсurities_Block ) ;
     /*Блок seсurity*/
    if( not УстановитьКонтекстБлока( seсurity_Block ) )
      RunError("|Ошибка при установке контекста блока "+seсurity_Block);
    end;
    ЗаписатьПолеЛог( BeginOfBlock, seсurity_Block ) ;

    /*seсurity_c*/   
    ЗаписатьПолеЛог( security_c_Field, msginf.SecurityCode);
    /*sequrity_q*/      
    ЗаписатьПолеЛог( security_c_Field, msginf.QuantitytoBeSettled);
    /*sequrity_isin*/   
    ЗаписатьПолеЛог( security_isin_Field, FillISIN(msginf));  
   
    ЗаписатьПолеЛог( EndOfBlock, seсurity_Block ) ;
    if( not УстановитьКонтекстБлока( ".." ) )
       RunError("|Ошибка при установке контекста блока ..");
    end; 
    /*Конец блока seсurity*/

    ЗаписатьПолеЛог( EndOfBlock, seсurities_Block ) ;
    if( not УстановитьКонтекстБлока( ".." ) )
       RunError("|Ошибка при установке контекста блока ..");
    end; 
    /*Конец блока seсurities*/
  end;

  ЗаписатьПолеЛог( EndOfBlock, MF170_Block );
  /*Конец блока MF170*/
end;


macro CheckMes( addrMes )
  var field_name, field_value, ProcessingCode = "";
  SetBuff( wlmes, addrMes );

  while( СчитатьПоле( field_name, field_value ) )
    if(field_name == order_t_id_Field)
      ProcessingCode = field_value;
    end;    
    /*контроль на русские буквы*/
    if( (field_name == deposit_c_Field ) or
        (field_name == contrag_c_Field ) or
        (field_name == con_code_Field )  or
        (field_name == dep_acc_c_Field ) or
        (field_name == sec_c_Field )     or
        (field_name == corr_acc_c_Field )or
        (field_name == corr_sec_c_Field ) )
      if(not CheckForRus( field_name, field_value, true))
        return false;
      end;
    elif((field_name == dep_acc_c_Field) or (field_name == sec_c_Field))
      if((ProcessingCode == "17/6") or (ProcessingCode == "17/7"))
        std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' не заполняется"));
        return false;
      end;
    elif( (field_name == sen_acc_Field) or
          (field_name == sen_bic_Field) or
          (field_name == rec_acc_Field) or
          (field_name == rec_bic_Field) )
      if((ProcessingCode == "17/30"))
        std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' не заполняется"));
        return false;
      end;
    elif( field_name == block_securities_Field )
      if((field_name != "Y") and (field_name != "N"))
        std.msg(string("В поле '",field_name,"' недопустимые символы"));
        return false;
      end;
    end;

  end;
    /*контроль по коду операции*/
  if(  (ProcessingCode == "17/0") or (ProcessingCode == "17/2") or
       (ProcessingCode == "17/3") or (ProcessingCode == "17/20")or
       (ProcessingCode == "17/30")or (ProcessingCode == "16/2") )
    if  (not ПолучитьПоле(wlmes.MesID, dep_acc_c_Field, field_value))
      field_name = dep_acc_c_Field;
    elif(not ПолучитьПоле(wlmes.MesID, sec_c_Field, field_value)) 
      field_name = sec_c_Field;
    end;
    if(field_name != "")
      std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' должно быть заполнено"));
      return false;      
    end; 
  end;

  if(  (ProcessingCode == "17/0") or (ProcessingCode == "17/1") or
       (ProcessingCode == "17/2") or (ProcessingCode == "17/20")or
       (ProcessingCode == "17/30")or (ProcessingCode == "17/6") or
       (ProcessingCode == "17/7") or (ProcessingCode == "16/2") or 
       (ProcessingCode == "16/3") )
    if  (not ПолучитьПоле(wlmes.MesID, corr_acc_c_Field, field_value))
      field_name = corr_acc_c_Field;
    elif(not ПолучитьПоле(wlmes.MesID, corr_sec_c_Field, field_value)) 
      field_name = corr_sec_c_Field;
    end;
    if(field_name != "")
      std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' должно быть заполнено"));
      return false;      
    end; 
  end;

  if((ProcessingCode == "17/20")or (ProcessingCode == "16/2") or (ProcessingCode == "16/3") )
    if  (not ПолучитьПоле(wlmes.MesID, sen_acc_Field, field_value))
      field_name = sen_acc_Field;
    elif(not ПолучитьПоле(wlmes.MesID, sen_bic_Field, field_value)) 
      field_name = sen_bic_Field;
    elif(not ПолучитьПоле(wlmes.MesID, rec_acc_Field, field_value)) 
      field_name = rec_acc_Field;
    elif(not ПолучитьПоле(wlmes.MesID, rec_bic_Field, field_value)) 
      field_name = rec_bic_Field;
    end;
    if(field_name != "")
      std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' должно быть заполнено"));
      return false;      
    end; 
  end;

  if((ProcessingCode == "17/2")or (ProcessingCode == "17/20") or (ProcessingCode == "17/3") or 
     (ProcessingCode == "17/30") or (ProcessingCode == "16/2") or (ProcessingCode == "16/3") )
    if  (not ПолучитьПоле(wlmes.MesID, pay_sum_Field, field_value))
      field_name = pay_sum_Field;   
    end;
    if(field_name != "")
      std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' должно быть заполнено"));
      return false;      
    end; 
  end;

  if( (ProcessingCode == "16/2") or (ProcessingCode == "16/3") )
    if  (not ПолучитьПоле(wlmes.MesID, pay_curr_Field, field_value))
      field_name = pay_curr_Field;   
    end;
    if(field_name != "")
      std.msg(string("Для кода операции '",ProcessingCode,"' поле '",field_name,"' должно быть заполнено"));
      return false;      
    end; 
  end;

  return true; 

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;

end;