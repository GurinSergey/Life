/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответов по сообщению ED275                                     */
/*                                                                          */
/*  Имя файла: ufgd275.mac                                                  */
/*  Создан:    15.10.13                                  Chukina T.         */
/****************************************************************************/

import "ufgendoc.mac", "ufgdwlreq.mac";

private class (TGenDocWlreqUFEBS) TGenDocED275()

  InitTGenDocWlreqUFEBS(MESKIND_REQUEST, "ED275", true);

  var Flds : TArray = TArray();

  macro ReadFields() : bool
    var field_name, field_value;  
    if( not СчитатьПоле( field_name, field_value ) )
      std.msg( String("В сообщении по форме: ", FormName, " референс: ", wlmes.TRN, 
                      "|не заполнено поле '", XMLField, "'") );
      return FALSE;
    end;

    var xml:object = ActiveX( "MSXML.DOMDocument" );
    if ( not xml.loadXML(field_value) )
      std.msg( "Неверный формат сообщения по форме " + FormName );
      return FALSE;
    end;

    OriginatorUIS  = ReadAttribute(xml,"EDAuthor");
    RecipientUIS   = ReadAttribute(xml,"EDReceiver");
    EDDate_InitMes = ReadAttribute(xml,"EDDate", "EDRefID");

    var node  : object = xml.DocumentElement,
        child : object, 
        i     : integer = 0;

    if( xml.NodeType == DOCUMENT_NODE )
      while( i < xml.childNodes.length )
        child = xml.childNodes.item(i);
        if( child and (child.nodeType==CHILD_NODE) )
          node = child;
          break;
        end;
        i = i+1;
      end;
    end;

    // Получить список атрибутов узла
    var NodeName : string = "";
    i = 0;
    while( i < node.attributes.length )
      child = node.attributes.item(i);
      if( child and (child.nodeType==ATTR_NODE) )
        NodeName = GetNodeName(child.nodeName);
        if( not InList(NodeName, "EDNo", "EDDate", "EDAuthor", "EDReceiver", "xmlns") )
          Flds[ Flds.size ] = TMesField( NodeName, child.nodeValue );
        end;
      end;
      i=i+1;
    end;

    return TRUE;
  end;

  macro FillQueries() : bool
    for(var fld, Flds)
      wlreq.Queries  = wlreq.Queries + fld.FieldName + ": " + fld.FieldValue + "\n";
    end;
    return TRUE;
  end;

end;

macro GenDoc( addrMes )                       
  
  var gdObj : TGenDocED275 = TGenDocED275();

  return gdObj.GenWlreq(addrMes);

  OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
