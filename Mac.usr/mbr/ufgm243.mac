/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED243 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm243.mac                                                  */
/*  Создан:    15.06.11                                                     */
/*  Адаптация: 12.06.2014 Gurin S. HF 49                                    */
/*             22.12.2015 Gurin S. HotFix 2031.19.25 (Life_1 1.3)_11        */
/****************************************************************************/

import OprInter, BankInter, "ufgenmes.mac", "wluftool.mac", "wlfindpm.mac", "wltools.mac", "pm_const.mac";

var ver_st = 2;

const code01 = "/УТ01/", 
      code02 = "/УТ02/", 
      code03 = "/УТ03/", 
      code04 = "/УТ04/", 
      code05 = "/УТ05/", 
      code06 = "/УТ06/", 
      code07 = "/УТ07/", 
      code08 = "/УТ08/",
      code15 = "/УТ15/"; //12.06.2014 Gurin S. HF 49
 
private macro ParseQueries(Queries: string, EDDefineRequestCode: @string, EDDefineRequestInfo: TArray)
  
  if  ( Index(Queries, code01))
    EDDefineRequestCode = "01";
  elif( Index(Queries, code02))
    EDDefineRequestCode = "02";
  elif( Index(Queries, code03))
    EDDefineRequestCode = "03";
  elif( Index(Queries, code04))
    EDDefineRequestCode = "04";
  elif( Index(Queries, code05))
    EDDefineRequestCode = "05";
  elif( Index(Queries, code06))
    EDDefineRequestCode = "06";
  elif( Index(Queries, code07))
    EDDefineRequestCode = "07";
  elif( Index(Queries, code08))
    EDDefineRequestCode = "08";
  else
    RunError("Не найден код в сообщении");     
  end;

  if (EDDefineRequestCode == "08")    
    var nums08str = substr(wlReq.Queries, Index(wlReq.Queries, code08)+6);
    var comma_pos = 0, numstr = "", num = 0;    
    var i = 0;
    while (nums08str)
      comma_pos = index(nums08str, ",");
      if (comma_pos > 0)
        numstr = substr(nums08str, 1, comma_pos - 1);
        nums08str = substr(nums08str, comma_pos + 1);
      else
        numstr = substr(nums08str, 1);
        nums08str = "";
      end;
      num = int(numstr);
      if( (num!=0) and ( (num==60) or (num==61) or (num>=101) and (num<=110) ) )
        EDDefineRequestInfo(i) = TMesField("Field" + string(num), "1");
        i = i + 1;
      end;
    end;
    // Если номера не указаны, то считается, что указаны номера: 
    // 60, 61, 101, 102, 103, 104, 105, 106, 107, 108, 109, 110
    if(EDDefineRequestInfo.size() == 0)
      EDDefineRequestInfo(0) = TMesField("Field60", "1");
      EDDefineRequestInfo(1) = TMesField("Field61", "1");
      for(i, 101, 110)
        EDDefineRequestInfo(EDDefineRequestInfo.size()) = TMesField("Field" + string(i), "1");
      end;
    end;
  else    
    var RequestInfoFNames : TArray = makeArray("AccDocDate",
                                               "AccDocNo",
                                               "Acc",
                                               "Sum",
                                               "Name"
                                              );
    var fld : TMesField = NULL;
    while( Index( Queries, "\n" ) )
      var curr_line = substr( Queries, 1, Index( Queries, "\n" )-1 );
      fld = getFieldFromQueriesLine(curr_line, RequestInfoFNames);
      if(fld)
        EDDefineRequestInfo[ EDDefineRequestInfo.size ] = fld;
      end;
      Queries = substr( Queries, Index( Queries, "\n" )+1 );
    end;
    // последняя строка из Queries
    fld = getFieldFromQueriesLine(Queries, RequestInfoFNames);
    if(fld)
      EDDefineRequestInfo[ EDDefineRequestInfo.size ] = fld;
    end;
  end;

end;

macro GenMes( addrMes, addrReq )
  var i = 0;

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );
  
  // Разобрать поле wlreq.Queries
  var EDDefineRequestCode = "";    
  var EDDefineRequestInfo : TArray = TArray();
  ParseQueries(wlReq.Queries, @EDDefineRequestCode, EDDefineRequestInfo);  
  
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object;
  
  mes = xml.createElement("ED243");
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  
  var ed_nda = NULL;
  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.TRN);

  mes.setAttribute("EDNo",       ed_nda.EDNo );
  mes.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  mes.setAttribute("EDAuthor",   ed_nda.EDAuthor );

  mes.setAttribute("EDDefineRequestCode",  EDDefineRequestCode);

  // OriginalEPD -------------------------------------------
  if (wlreq.RelatedRef == "")
    RunError("Ошибка: Поле 'Ссылка' должно быть заполнено");     
  end;
  
  var elem:object;
  elem = xml.createElement("OriginalEPD");    

  ed_nda = EDNoDateAuthor();
  ed_nda.ConstructByTrn(wlmes.RelatedRef); 

  elem.setAttribute("EDNo",       ed_nda.EDNo );
  elem.setAttribute("EDDate",     YYYYMMDD(ed_nda.EDDate));
  elem.setAttribute("EDAuthor",   ed_nda.EDAuthor );  
                                           
  mes.appendChild(elem);  
  // OriginalEPD -------------------------------------------

  // EDDefineRequestInfo ----------------------------------------------
  if ( (EDDefineRequestInfo.size > 0) and ( (EDDefineRequestCode == "03") or
                                            (EDDefineRequestCode == "06") or
                                            (EDDefineRequestCode == "08") )
     )    

    var elemInfo:object;
    elemInfo = xml.createElement("EDDefineRequestInfo");

    if ( (EDDefineRequestCode == "03") or (EDDefineRequestCode == "06") )      
      for( var FldItem, EDDefineRequestInfo )
        if(FldItem.FieldName == "Name")
          var subElem:object;
          subElem = xml.createElement(FldItem.FieldName);
          subElem.appendChild( xml.createTextNode(FldItem.FieldValue) );
          elemInfo.appendChild(subElem);
        else
          elemInfo.setAttribute(FldItem.FieldName, FldItem.FieldValue);
        end;
      end;      
    end;

    if (EDDefineRequestCode == "08")
      var elemMask:object;
      elemMask = xml.createElement("FieldQueryMask");
      for(i, 0, EDDefineRequestInfo.size() - 1, 1)
        elemMask.setAttribute( EDDefineRequestInfo(i).FieldName, "1");
      end;
      elemInfo.appendChild(elemMask);
    end;

    mes.appendChild(elemInfo);
  end;
  // EDDefineRequestInfo ----------------------------------------------
   
  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;

macro CheckObj(addrReq, addrRefillReq )

  SetBuff( wlReq, addrReq );

  // Проверить поле wlreq.Queries
  var req_code : string = "";
  var code_index : integer = index_wlcode(wlReq.Queries, 49, @req_code);
  if(code_index == 0)
    RunError("Не найден код в сообщении");
  end;

  // Найти входящий платеж wlpmpaym, wlpmrmprop, на который отсылается запрос
  if( not WldFindPmByTRN_Tp( TRANSP_UFBS, wlReq.RelatedRef, NULL, DIRECT_PAYM_IN ) )
    RunError( String("Не найден входящий платеж|с референсом '", wlReq.RelatedRef, "'") );
  end;  

  // Дозаполнить учетный объект
  RECORD RefillReq(wlreq);
  if(ValType(addrRefillReq) == V_MEMADDR)
    SetBuff( RefillReq, addrRefillReq );
    Copy(RefillReq, wlReq, 1);      
  end;
  var TextAfterCode = SubStr( wlReq.Queries, code_index + StrLen(req_code) );
  
  if( (Trim(TextAfterCode) == "") and (ValType(addrRefillReq) == V_MEMADDR) )
    
    if( (req_code == code03) or (req_code == code06) )

      var AccFld : string = "", NameFld : string = "", Number : string = "";
      if(req_code == code03)
        AccFld = wlpmpaym.ReceiverAccount;
        NameFld = wlpmrmprop.ReceiverName;
      else
        AccFld = wlpmpaym.PayerAccount;
        NameFld = wlpmrmprop.PayerName;
      end;
      var len = strlen(wlpmrmprop.Number);
      if (len <= PM_DOCNO_NONZERO_LEN)
        Number = wlpmrmprop.Number;
      else
        Number = substr(wlpmrmprop.Number, len - PM_DOCNO_NONZERO_LEN + 1);
      end;

      TextAfterCode = "\n" + "AccDocDate: " + YYYYMMDD(wlpmrmprop.ClientDate) + 
                      "\n" + "AccDocNo: " + Number + 
                      "\n" + "Acc: " + AccFld + 
                      "\n" + "Sum: " + string( int( double(wlpmpaym.BaseAmount)*100 ) ) + 
                      "\n" + "Name: " + TransTextField(substr(NameFld,1,160));
      RefillReq.Queries = SubStr( wlReq.Queries, 1, code_index - 1 + StrLen(req_code) ) + TextAfterCode;
    end;
  end;

  return true;

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;