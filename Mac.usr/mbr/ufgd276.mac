/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответов по сообщению ED276                                     */
/*                                                                          */
/*  Имя файла: ufgd276.mac                                                  */
/*  Создан:    16.10.13                                  Chukina T.         */
/****************************************************************************/

import "ufgendoc.mac", "ufgdwlreq.mac";

private class (TGenDocWlreqUFEBS) TGenDocED276()

  InitTGenDocWlreqUFEBS(MESKIND_ANSWER, "ED276", true);

  var InitialED : string = "",
      InfoCode  : string = "",
      EDRefID   : string = "";

  macro ReadFields() : bool
    var field_name, field_value;  
    if( not СчитатьПоле( field_name, field_value ) )
      std.msg( String("В сообщении по форме: ", FormName, " референс: ", wlmes.TRN, 
                      "|не заполнено поле '", XMLField, "'") );
      return FALSE;
    end;

    var xml:object = ActiveX( "MSXML.DOMDocument" );
    if ( not xml.loadXML(field_value) )
      std.msg( "Неверный формат сообщения по форме " + FormName );
      return FALSE;
    end;

    OriginatorUIS  = ReadAttribute(xml,"EDAuthor");
    RecipientUIS   = ReadAttribute(xml,"EDReceiver");
    EDDate_InitMes = ReadAttribute(xml,"EDDate", "EDRefID");

    InitialED = GetRefByEDNoDateAuthor(xml, "InitialED");
    InfoCode  = ReadAttribute(xml,"InfoCode");
    EDRefID   = GetRefByEDNoDateAuthor(xml, "EDRefID");

    return TRUE;
  end;

  macro FillQueries() : bool
    wlreq.Queries  = "InfoCode:"  + InfoCode + "\n" +
                     "InitialED:" + InitialED + "\n" +
                     "EDRefID:"   + EDRefID;
    return TRUE;
  end;

end;

macro GenDoc( addrMes )                       
  
  var gdObj : TGenDocED276 = TGenDocED276();

  return gdObj.GenWlreq(addrMes);

  OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
