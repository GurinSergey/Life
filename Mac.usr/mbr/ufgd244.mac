/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответа по сообщению ED244                                      */
/*                                                                          */
/*  Имя файла: ufgd244.mac                                                  */
/*  Создан:    17.06.11                                  Chukina T.         */
/* Изменен:   10.04.12 Chesnokov D.S. по заявке С-9530 проверка на группу   */
/****************************************************************************/
/*SDA 06.12.2013 адаптация для 31-й */
import "ufgendoc.mac", "diver.mac";

var ver_st = 2;

private macro ToDate( strYYYYMMDD ) 
  return date( int(substr(strYYYYMMDD, 9, 2)), int(substr(strYYYYMMDD, 6, 2)), int(substr(strYYYYMMDD, 1, 4)) );
end;

private macro НайтиКодРежима(strcode: string) :string

  var result = "Неизвестный код режима: " + strcode;
  var rs:object;
  var select:string;
  var params:TArray;

  select = "select t_description from dwlcodes_dbt where t_algnum = 52 and t_code=:code";
  
  params = makeArray( SQLParam("code", strcode));
  rs = execSQLselect( select, params, FALSE );
  if ( rs and rs.MoveNext() )
     result = rs.value(0);
  end;
        
  return result;
end;

macro GenDoc( addrMes )

  if (not ВходитВГруппу({oper},198))
    msgBox("Вы не включены в группу \"198 - Генерация сообщений УФЭБС\"");
    return false;
  end;

  SetBuff( wlmes, addrMes );
  
  PrintLog(2,"Генерация ответа по ED244");

  ClearRecord(wlreq);
  
  var error = 0, i = 0;
    
  /* Заполнение учетных буферов */
  wlreq.Trn                      = wlmes.Trn;
  wlreq.RelatedRef               = wlmes.RelatedRef;
  wlreq.Direct                   = "X";  
  wlreq.Kind                     = MESKIND_ANSWER;
  
  wlreq.InitFormIDMes            = НайтиФормуСообщенияПоРеференсу(wlreq.RelatedRef, TRANSP_UFBS);
  
  wlreq.RecipientCode            = {MFO_Bank};
  wlreq.RecipientCodeKind        = PTCK_BIC;
  wlreq.RecipientID              = {OurBank};
  wlreq.RecipientName            = {Name_Bank};
  
  var fieldname = "", fieldvalue = "", blockname = "", EDDefineAnswerCode = "";
  FILE party(party) key 0;
  while ( СчитатьПоле(fieldname, fieldvalue, blockname) )
    if ( (fieldname == "EDAuthor") and (blockname == "") )
      wlreq.OriginatorID         = ToRespID(fieldvalue);
      wlreq.OriginatorCodeKind   = PTCK_BIC;
      wlreq.OriginatorCode       = ПолучитьКодСубъекта(wlreq.OriginatorID, wlreq.OriginatorCodeKind, error);

      party.PartyID = wlreq.OriginatorID;
      if(GetEQ(party))
        wlreq.OriginatorName     = party.Name;
      end;
    end;

    if ( (fieldname == "EDDate") and (blockname == "OriginalEPD") )
      wlreq.InitDateMes          = ToDate(fieldvalue);
    end;
  
    if (fieldname == "EDDefineAnswerCode")
      EDDefineAnswerCode = fieldvalue;
      wlreq.Queries              = "/ОТ"+EDDefineAnswerCode+"/";
    end;

    // wlreq.Queries ------------------------------------------------- 
    if (blockname == "EDDefineAnswerInfo")
      if ( ( (fieldname == "AccDocDate") or (fieldname == "AccDocNo") or 
             (fieldname == "PayerAcc") or (fieldname == "PayeeAcc") or
             (fieldname == "PayerINN") or (fieldname == "PayeeINN") or (fieldname == "Sum")
           ) and (fieldvalue) 
         )
        wlreq.Queries            = wlreq.Queries + string("\n", fieldname, ": ", fieldvalue);
      end;      
    end;
    if ( ( (fieldname == "PayerLongName") or (fieldname == "PayeeLongName") or
           (fieldname == "Purpose") or (fieldname == "Address") 
         ) and (fieldvalue) 
       )
      wlreq.Queries              = wlreq.Queries + string("\n", fieldname, ": ", fieldvalue);
    end;
                      
    if (blockname == "EDDefineAnswerInfo\\AccDocAddInfo")
      if ( ( (fieldname == "PayerINN") or (fieldname == "PayeeINN") or 
             (fieldname == "DrawerStatus") or (fieldname == "PayerKPP") or 
             (fieldname == "PayeeKPP") or (fieldname == "CBC") or
             (fieldname == "OKATO") or (fieldname == "PaytReason") or 
             (fieldname == "TaxiPeriod") or (fieldname == "DocNo") or
             (fieldname == "DocDate") or (fieldname == "TaxPaytKind")
           ) and (fieldvalue) 
         )
        wlreq.Queries            = wlreq.Queries + string("\n", fieldname, ": ", fieldvalue);
      end;      
    end;
    // wlreq.Queries -------------------------------------------------
    
  end;
  
  var Description                = НайтиКодРежима("/ОТ"+EDDefineAnswerCode+"/");

  if( not ВставитьОтвет( wlreq, Description ) )
    std.msg("Ошибка при вводе ответа");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;