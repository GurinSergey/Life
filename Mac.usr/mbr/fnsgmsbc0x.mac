//-----------------------------------------------------------------------------
//          Автоматизированная банковская система RS-Bank               
//                 Copyright (c) R-Style Software Lab
//
// Подсистема   : Межбанковские расчеты
//
// Описание     : Генерация сообщения SBC0 по транспорту ФНС в формате XML
//
// Программист  : Чукина Т.А.
//
// Создан       : 21.05.2014
//
//-----------------------------------------------------------------------------
import "mnssbcal.mac", FIInter;

private const vers : integer = 510; // версия формата 5.10

private macro WriteAccInfoOpen(Счет : string, Валюта : integer, reqopena)
  StartBlockLogXML("Открыт");

  WriteFieldWithCheckAnnul( "КодСостСч", "1" );
  WriteFieldWithCheckAnnul( "НомерДог", reqopena.SfContrNum );
  WriteFieldWithCheckAnnul( "КодСостДог", "0" );

  if( GetKindActionFNS() == KIND_ACTION_FNS_NULL )
    ЗаписатьПолеДляАннул("ДатаЗаклДог", GetOldfnsMessageID());
  else
    ЗаписатьПоле_ДатаЗаклДог(reqopena.Date, reqopena.UnionContrID, Счет, Валюта);
  end;

  FinishBlockLogXML("Открыт");
end;

// СвСчет
private macro WriteAccInfo(Счет : string, Валюта : integer, GenAcc, reqopena)
  StartBlockLogXML("СвСчет");

  // атрибуты элемента СвСчет
  //zmp 02.10.2014 I-00519522 для уже открытых счетов дата открытия берется из карточки счета
  var rs = RsdRecordset("select t_open_date from daccount_dbt where t_account = '" + Счет + "'");        
  if (rs and rs.movenext())
     GenAcc.Open_Date = date(rs.value("t_open_date"));
  end;

  WriteAccInfoAttrs(Счет, Валюта, GenAcc);

  // Файл\Документ\СвСчет\Открыт
  WriteAccInfoOpen(Счет, Валюта, reqopena);

  FinishBlockLogXML("СвСчет");
end;

macro GenMes( addrMes, addrReq, addrOldMes )
  record wlmes(wlmes);
  SetBuff( wlmes, addrMes );

  record reqopena(reqopena);
  SetBuff(reqopena, addrReq);

  record OldMes(wlmes);
  SetBuff( OldMes, addrOldMes );

  PrintLog(2, "Генерация сообщения по SBC0");

  record GenAcc(account);
  var Счет : string, Валюта : integer, ErrMsg : string = "";

  ПолучитьСчетСВалютой(reqopena, 1 /*Открытие*/, Счет, Валюта);
  GenAcc.Client = reqopena.ClientID;
  GenAcc.Open_Date = reqopena.Date;
  GenAcc.Type_Account = reqopena.Type_Account;
  GenAcc.Department = reqopena.AccountDepartment;

  var CommonSBC : TCommonDataGenSBC = TCommonDataGenSBC( GenAcc.Department, 
                                                         ACCMSG_KIND_OPEN, 
                                                         vers );

  var CorrectMesObj : TCorrectMesFNS = TCorrectMesFNS();

  // Файл
  StartBlockLogXML("Файл");

  // атрибуты элемента "Файл"
  WriteFileAttrs(CommonSBC);

  // Файл \ Документ
  StartBlockLogXML("Документ");

  // атрибуты элемента "Документ"
  WriteDocumentAttrs(CommonSBC, Счет, "", Валюта, GenAcc.Client, OldMes.MesID, CorrectMesObj);

  // Файл \ Документ \ СвБанк
  if( not WriteBankInfo(CommonSBC.MesSender, "СвБанк", @ErrMsg, CorrectMesObj) )
    if(not ErrMsg)
      ErrMsg = "Ошибка при записи сведений о банке (филиале банка)";
    end;
    RunError(ErrMsg);
  end;

  // Файл \ Документ \ СвНП
  WriteTaxPayerInfo(GenAcc.Client, ACCMSG_KIND_OPEN);

  // Файл \ Документ \ СвСчет
  WriteAccInfo(Счет, Валюта, GenAcc, reqopena);

  FinishBlockLogXML("Документ");

  FinishBlockLogXML("Файл");

  if(CommonSBC._ВидРегОргана)
    ЗаписатьПолеЛог("_ВидРегОргана", CommonSBC._ВидРегОргана);
  end;

  if(CommonSBC._СпОбм)
    ЗаписатьПолеЛог("_СпОбм", CommonSBC._СпОбм);
  end;

  if( not CorrectMesObj.Check() )
    return FALSE;
  end;

  return TRUE;

OnError(er)
  ExeptionMessage(er);
  return FALSE;
end;
