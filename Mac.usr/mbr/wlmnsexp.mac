/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                          Экспорт сообщений ГНИ                           */
/*                                                                          */
/*  Имя файла: wlmnsexp.mac                                                 */
/*  Создан:  22.12.04                                    Фомченкова Л.Н.    */
/****************************************************************************/
/*SDA адаптация для 31-й */
import "wlexport.mac", "wlmnstls.mac", PSInter, oralib, likepy;

var out = TArray;

var FormName = ""; // название формы выгружаемого сообщения

var Name1 = "", Name2 = "", Name3 = "", Officer = "", Phone = "", IDFile = " ", IDDoc = " ", Def = " ",SubKind, DateMes = " ";

/* Заполнение списка полей*/
var Fld1  = ТПолеМНС( "ВерсФорм", "ЗаписатьКонецФрагмента" ),
    fld2  = ТПолеМНС( "НомСч",    NULL, "НачатьБлокСчет" ),
    fld3  = ТПолеМНС( "ДатаОпер", NULL, "НачатьБлокОперацииПоСчету" );

macro InitOut()
   out.Size = 0;
end;

macro CorrectCode(Code)
   if(Code == "ДатаПодпис")
      return "ДатаПодписи";
   end;
   if(Code == "ДатаЗаклДо")
      return "ДатаЗаклДог";
   end;
   if(Code == "ДатаРастДо")
      return "ДатаРастДог";
   end;

   return Code;
end;

//Откорректировать значение и заодно обновить его
macro CorrectValue(Code, Value)
   var FullName;
   if (FormName == "PB") // KS 08.12.2011 365-П
     if (Code == "ИмяВхФайла")
       return Value + МНС_КонецБлока;
     else
       return Value + МНС_КонецФрагмента;
     end;
   end;
   if(Code == "ИдФайл")
      ОбновитьПоле(IDFile);
      return IDFile;
   end;
   /* EVG 13/01/2012 Сделал обновление поля "ИдДок" не только при первичной выгрузке.
   if((Code == "ИдДок") and (trim(Value) == "") )*/
   if(Code == "ИдДок")
      ОбновитьПоле(IDDoc);
      return IDDoc;
   end;
   if(Code == "ТелОтпр")
      ОбновитьПоле(Phone);
      return Phone;
   end;
   if(Code == "ДолжнОтпр")
      ОбновитьПоле(Officer);
      return Officer;
   end;
   if(Code == "ФамОтпр")
      FullName = substr( string(Name1), 1, 60 );
      ОбновитьПоле(FullName);
      return FullName;
   end;
   /* EVG 6/06/2012 Сделал обновление поля "ДатаСооб" не только при первичной выгрузке.
   if((Code == "ДатаСооб") and (trim(Value) == ""))*/
   if(Code == "ДатаСооб")
     ОбновитьПоле(DateMes);
     return DateMes;
   end;

   if(trim(Value) == "")
     return "";
   else
     return Value;
   end;
end;

macro РеквизитБлока(Код, Значение)
    if((Код != МНС_КонецБлока) and (Код != МНС_КонецФайла) and (Код != МНС_КонецФрагмента))
       if (FormName == "PB") // KS 08.12.2011 365-П
         if (Код != "ТипОтв")
           out(out.size) = string(CorrectValue(Код, Значение));
         end;
       else
       out(out.size) = string(CorrectCode(Код), ":", CorrectValue(Код, Значение));
       end;
    else
       out(out.size) = string(Код);
    end;
end; 

macro ЗаписатьКонецБлока()
   РеквизитБлока(МНС_КонецБлока, "");
end;


macro ЗаписатьКонецФрагмента()
   РеквизитБлока(МНС_КонецБлока, "");
   РеквизитБлока(МНС_КонецФрагмента, "");
end;

private macro НужноЗаписатьКонецБлока():bool
  var LastString : string = out(out.size - 1);
  if( (LastString == МНС_КонецБлока) or (LastString == МНС_КонецФрагмента) )
    return false;
  else
    return true;
  end;
end;

macro НачатьБлокСчет()
  if ( ( (FormName == "BOS") or (FormName == "BNS") or (FormName == "BV") ) and НужноЗаписатьКонецБлока() )
    ЗаписатьКонецБлока();
  end;
end;

macro НачатьБлокОперацииПоСчету()
  if ( (FormName == "BV") and НужноЗаписатьКонецБлока() )
    ЗаписатьКонецБлока();
  end;
end;

macro СохранитьИнформациюВФайле()
    var count = 0, continue0 = true;

    while( continue0 AND (count<out.size) )
        if ( not ЗаписатьСтроку(out(count)) )
           ErrExport("Ошибка записи строки: " + out(count));
           continue0 = false;
        end;
        count = count + 1;
    end;
    InitOut();
    return continue0;
end;

macro SimpleCopyFromMesVal()
  var field, buff;
  while( СчитатьПоле( field, buff ) )
    out(out.size) = string(buff);
  end;
  out(out.size) = МНС_КонецФайла;
  return true;
end;

macro СохранитьПоляСообщения()
   if( (FormName == "BUV") or ( FormName == "PB" ) )
     return SimpleCopyFromMesVal();
   end;

   var field, buff, str, i, fld;
   private var s; // KS 08.12.2011 365-П Буферная переменная
   while( СчитатьПоле( field, buff ) )   
      
      i = IndexMNS(field);
      if( (i != -1) )
        fld = ПоляМНС.Value(i);
      end;
      
      if( (i != -1) )
        fld.ОбработатьПолеФормы2(buff);
      end;
      if( substr(field,1,1) != "_" )
        РеквизитБлока(field, buff);
      end;
      if( (i != -1) )
        fld.ОбработатьПолеФормы(buff);
      end;

   end;

   if (FormName != "PB") // KS 08.12.2011 365-П
      РеквизитБлока(МНС_КонецБлока, "");
      РеквизитБлока(МНС_КонецФрагмента, "");
   else // Отсортируем поля как надо в PB
     s = out[3];
     out[3] = out[2];
     out[2] = out[1];
     out[1] = s;
   end;
   РеквизитБлока(МНС_КонецФайла, "");
   return true;
end;

/***************************************************************************/
/*  Функция формирования сообщения                                         */
/*  Возвращает:                                                            */
/*             TRUE или FALSE                                              */
/***************************************************************************/
macro ЗаписатьСообщение(IgnorePrevMes, FirstCall)
  var rs:object;
  var select:string;
  var params:TArray;
  var СпОбм = GetFldValue( wlmes.MesID, "_СпОбм" );  

 InitOut();
 select = "select t_Number from dwlobjknd_dbt, dwlmesrls_dbt where t_rlsformid =:RlsFormID "+ 
                  "and t_formid = t_objid and t_objkind = 507 and t_type = 13";
 params = makeArray( SQLParam("RlsFormID", wlmes.RlsFormID));
 rs = execSQLselect( select, params, FALSE );
 if(rs.moveNext())
    SubKind = rs.Value(0);
 else
    ErrExport("Для формы сообщения " + wlmes.TRN + " не задан подвид сообщения");
    return false;
 end;

 if( (СпОбм == "1") or (СпОбм == "4") )
    ErrExport("Выгрузка сообщений, которые должны быть отправлены почтой или курьером, не допускается");
    return false;
 end;

 if ( not СохранитьПоляСообщения() ) return false; end;
 if ( not СохранитьИнформациюВФайле() ) return false; end;
 IgnorePrevMes = false;

 SetParm( 0, IgnorePrevMes );
 SetParm( 1, false );
 return TRUE;
end;

private macro PutPersData_Default(Name1, Name2, Name3, Officer, Phone): integer
  Name1 = Name2 = Name3 = Officer = Phone = "";

  var q = "SELECT persn.t_Name1, persn.t_Name2, persn.t_Name3, "
          "       officer.t_Post, officer.t_PhoneNumber "
          "  FROM dpersn_dbt persn, dofficer_dbt officer, dperson_dbt person "
          " WHERE officer.t_PersonID = person.t_PartyID "
          "   AND persn.t_PersonID   = person.t_PartyID "
          "   AND person.t_Oper      = :Oper "
          "   AND officer.t_PartyID = :OurBank ";

  var rs = execSQLselect(q, makeArray( SQLParam("Oper", {Oper}),
                                       SQLParam("OurBank", {OurBank}) )
                        );

  if(rs and rs.moveNext())
    Name1 = rs.value(0);
    Name2 = rs.value(1);
    Name3 = rs.value(2);
    Officer = rs.value(3);
    Phone = rs.value(4);
  end;

  SetParm(0, Name1);
  SetParm(1, Name2);
  SetParm(2, Name3);
  SetParm(3, Officer);
  SetParm(4, Phone);

  return 0;
end;

/* Макрос экспорта  */
macro ExportMessage(ExportFileName)
  var continue0 = 1, Документов = 0, err, IgnorePrevMes = true, FirstCall = true, stat;  
  if( not СчитатьЗапись( wlmes, err ) )
    if ( not err )
       ErrExport("Не найдено ни одного сообщения для отправки");
    else
      ErrExport("Ошибка чтения сообщения");
    end;
    return false;
  end;
  if( DefineFormExport( wlmes.RlsFormID ) )
    FormName = ФормаЭкспорт.Name;
  else
    return false;
  end;

  if( ( FormName != "PB" ) and ( FormName != "BUV" ) and not Name1 )
     stat = PutPersData_Default(Name1, Name2, Name3, Officer, Phone);
     if( strlen(Phone) == 0 )  
       Phone = " ";     // чтобы поле обязательно присутствовало в файле
     end;
     if( strlen(Officer) == 0 )  
       Officer = " ";   // чтобы поле обязательно присутствовало в файле
     end;
     if( strlen(Name1) == 0 )  
       Name1 = " ";     // чтобы поле обязательно присутствовало в файле
     end;
     if(stat == -27)
       return true;
     end;
  end;
  if(DateMes == " ")
    DateMes = string(date():f);
  end;
  while( continue0 )

    IDFile = MakeIDFile(ExportFileName, FormName);
    if(not IDFile and (FormName != "BUV"))
       ErrExport("Ошибка формирования идентификатора файла");
       return false;
    end;
    IDDoc = IDFile + "000001";
    if( not ЗаписатьСообщение(IgnorePrevMes, FirstCall) )
      return false;
    end;
    if( not СчитатьЗапись( wlmes, err, IgnorePrevMes ) )
      if ( not err )
         continue0 = 0;
      else
         ErrExport("Ошибка чтения сообщения");
         return false;
      end;
    end;

    if( continue0 )
      if( DefineFormExport( wlmes.RlsFormID ) )
        FormName = ФормаЭкспорт.Name;
      else
        return false;
      end;
    end;
    Документов = Документов + 1;
    message( "Идет выгрузка документов. Отправлено: ", Документов );
  end;
  return true;
end;
