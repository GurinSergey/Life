/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED276 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm276.mac                                                  */
/*  Создан:    15.10.13                                Chukina T.           */
/****************************************************************************/

import WldInter, "ufgenmes.mac", "wluftool.mac", "rsberror.mac";

private macro GetEDReceiverForED276(wlReq, RelatedRef : string) : string
   return GetEDReceiver( wlReq.RecipientID, wlReq.RecipientcodeKind, wlReq.RecipientCode, 
                         RelatedRef );
end;

//TAM 13.02.2014 I-00464590-2
private macro GetEDInitDateForRef(Trn) : string
   debugbreak;
   var xml_parent:object = ActiveX("Microsoft.XMLDOM"); 
   var xml_str : string = "";
   var ED_initdate:string = YYYYMMDD(Date());
   var rs = execSQLselect("Select t_MesID, t_RelatedRef " +
                          "  from dwlmes_dbt            " +
                          " where t_Trn = :Trn          " +
                          "   and t_Direct = 'X'        ",
                          makeArray( SQLParam("Trn", Trn) )
                         );
   if(rs and rs.moveNext)
      var InRelatedMesId = rs.value("t_MesID");
      if( ПолучитьПоле(InRelatedMesId, XMLField, xml_str) and xml_parent.loadXML(xml_str) )
         ED_initdate = ReadOptinalAttribute(xml_parent, "EDDate");
      end;
   end;
   return ED_initdate;
 end;

macro GenMes( addrMes, addrReq )
   debugbreak;
   record wlmes(wlmes);
   record wlReq(wlreq);

   SetBuff( wlmes, addrMes );
   SetBuff( wlReq, addrReq );

   var RelatedRef : string = GetRelatedRefFromWlreq(wlReq, "EDRefID", "Не найдена ссылка на исходный ЭПД. Нужно или заполнить поле 'Ссылка', или заполнить EDRefID в поле 'Сообщение'");
   // Заполнение атрибутов поля "xml" сообщения
   var xml:object = ActiveX("Microsoft.XMLDOM");  
   var mes:object = xml.createElement("ED276");
   mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  
   FillEDNoDateAuthorByRef_XMLField(mes, wlmes.TRN);

   mes.setAttribute("EDReceiver", GetEDReceiverForED276(wlReq, RelatedRef));

   var InfoCode : string = getFldValFromQueries(wlReq.Queries, "InfoCode:");
   if(not InfoCode)
      RunError("Не заполнен атрибут InfoCode");
   elif(not InList(InfoCode, "1", "2"))
      RunError("Не корректно заполнен атрибут InfoCode");
   end;
   mes.setAttribute("InfoCode", InfoCode);

   var elem : object = null;      

   var InitialED : string = getFldValFromQueries(wlReq.Queries, "InitialED:");
   if(not InitialED)
      RunError("Не заполнен атрибут InitialED");
   end;
   elem = xml.createElement("InitialED");  
   FillEDNoDateAuthorByRef_XMLField(elem, InitialED);
   //TAM 13.02.2014 I-00464590-2
   var date_init = GetEDInitDateForRef(InitialED);
   elem.setAttribute("EDDate", date_init);
   elem.appendChild(xml.createTextNode(""));
   mes.appendChild(elem);
  
   elem = xml.createElement("EDRefID");  
   FillEDNoDateAuthorByRef_XMLField(elem, RelatedRef);
   //  TAM 13.02.2014 I-00464590-2
   //Gurin S. 05.05.2016 R-646145-2
   //date_init = GetEDInitDateForRef(wlreq.RelatedRef);
   date_init = YYYYMMDD(date( (substr(RelatedRef,5,2))+"."+(substr(RelatedRef,3,2))+"."+(substr(RelatedRef,1,2)) ));
   elem.setAttribute("EDDate", date_init);
   elem.appendChild(xml.createTextNode(""));
   mes.appendChild(elem);

   ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
   return TRUE; // Успешное завершение

   OnError( er ) // обработка ошибок
      std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
      return FALSE; 
end;