/***************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Обработка сообщения УФЭБС ED374                                          */
/*                                                                          */
/* Имя файла: ufgd374.mac                                                   */
/* Создан:    19.05.09                                                      */
/* Изменен:   10.04.12 Chesnokov D.S. по заявке С-9530 проверка на группу   */
/****************************************************************************/

import "ufgendoc.mac", "diver.mac";

macro GenDoc( addrMes )

  if (not ВходитВГруппу({oper},198))
    msgBox("Вы не включены в группу \"198 - Генерация сообщений УФЭБС\"");
    return false;
  end;

  var query:string =  "select * from table(WLD_UFEBS.LogProcessED374)";
  var params:TArray;
  var rs:RsdRecordset;
  var n = 0, n_err:integer = 0;
  var ReportFileName,OldOutPut;
  DebugBreak;
  SetBuff( wlmes, addrMes );
  PrintLog( 2, "Обработка сообщения ED374 " );

  params = makeArray( SQLParam( "MesID", wlmes.MesID ));
  execStoredFunc( "WLD_UFEBS.ProcessED374", V_UNDEF, params );

  rs = execSQLselect( query, params, true );

  [                                                                               ];
  [   Отчет о загрузке справочника БЭСП по форме ED374                            ];
  [   Дата обработки: ##########  ########                                        ](date:f, time);
  [                                                                               ];
  [┌─────────┬─────────┬───────────┬─────────────────────────────────────────────┐];
  [│ БИК ОУР │ БИК ПУР │    УИС    │ Ошибка                                      │];
  [│         │         │           │                                             │];
  [├─────────┼─────────┼───────────┼─────────────────────────────────────────────┤];

  if( rs )
    while( rs.moveNext() )
      [│#########│#########│###########│#############################################│](rs.value(0),rs.value(1),rs.value(2),StrSubst(rs.value(4), "|", " "):w );
      n = n + 1;
      if(rs.value(3) != 0)
        n_err = n_err + 1; 
      end;
    end;
  end;

  [└─────────┴─────────┴───────────┴─────────────────────────────────────────────┘];
  [   Всего обработано записей: ##### ](n);
  [   Ошибочных записей:        ##### ](n_err);


  if( n_err )
    MemoryError(6569);
  end;

  return true;

  OnError(er) /* Обработка ошибок времени выполонения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;
