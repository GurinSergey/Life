/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT200                                       */
/*                                                                          */
/*  Имя файла: swgm200.mac                                                  */
/*  Создан:  24.01.00                                        Алешин А.В.    */
/****************************************************************************/

import "swgenmes.mac";


/* EVG 12.11.09 Реализовано по просьбе Фоменко.
   Tikh 19.11.09 Перенес
   Функция проверяет первый символ полученной строки.
   Если символ содержится в массиве aBadSymbs, то перед ним подставляется
   пробел. */
macro CheckFirstSymbol( str:@string )
   var aBadSymbs = TArray();
       aBadSymbs(0) = ":";
       aBadSymbs(1) = "-";

   var i = 0,
       isBad = false,
       FirstSymbol = subStr(str, 1, 1);

   while ((i < aBadSymbs.Size) and not isBad)
      if (FirstSymbol == aBadSymbs(i))
         isBad = true;
      end;
      i = i + 1;
   end;

   if (isBad)
      str = " " + str;
   end;
end;


macro GenMes( addrMes, addrPm )
  var field_value, DKFlag, Tag, PaymentDetails;
  var RsPaym;
  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );

  PrintLog(2,"Генерация сообщения по МТ200");
  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( RsPaym.OutTransferDate ) +
                FillCurrencyOriginalOrderedAmountField( RsPaym );
  ЗаписатьПолеЛог( ValueDateCurrencyCodeAmountField_A, field_value );

  /* 53B - корреспондент отправителя */
  Tag = SWIFT_Tag_B;
  field_value = FillSenderCorrespondentField(RsPaym, Tag, null, "200" );
  CheckFirstSymbol(@field_value);
  ЗаписатьПолеВБлокКонтекст0( Sender_sCorrespondentField+Tag, "", field_value );

  /* 72 - информация для банка-получателя */
  PaymentDetails = "";
  StrChangeRusXSet(RsPaym.Ground, PaymentDetails);
  field_value = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT200_STANDART, PaymentDetails);    
  CheckFirstSymbol(@field_value);
  ЗаписатьПолеЛог( SenderToReceiverInformationField, field_value );

  /* 56a - банк посредник */
  Tag = "";
  field_value = FillIntermediaryField(RsPaym, Tag );
  CheckFirstSymbol(@field_value);
  ЗаписатьПолеВБлокКонтекст0( IntermediaryField+Tag, "56a", field_value );  

  /* 57a - банк бенефициара */
  Tag = "";
  field_value = FillAccountWithInstitutionField(RsPaym, Tag, true );
  CheckFirstSymbol(@field_value);
  ЗаписатьПолеВБлокКонтекст0( AccountWithInstitutionField+Tag, "57a", field_value );    

  return TRUE; /* успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return false;
end;


