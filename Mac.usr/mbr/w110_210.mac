/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Проверка сквитованности платежа                                         */
/*                                                                          */
/*  Имя файла: w110_210.mac                                                 */
/*  Создан:  17.05.00                                        Алешин А.В.    */
/****************************************************************************/
import InsCarryDoc, BankInter, OprInter, RMInter, "rmconst.mac", "wlglobal.mac", "cbsttls.mac";
import oralib; //Jushmanov 2014-02-24 C-19151

RECORD Corschem( corschem );

var PaymentObj:RsbPayment;

const DEBET  = "Д",
      KREDIT = "К";

const Menu1 = "Поместить в картотеку";
const Menu2 = "Вернуть отправителю";
const Menu3 = "Вернуть на повторную обработку";
const ActionName = "Действие:";

/**********************************/
/* Выполнение шага                */
/**********************************/
macro ExecuteCaseStep( Kind_Operation, Number_Step, first, KindDoc )
    var stat:integer = 0, KvitType:bool, Cancel:string, menu_item;

    Cancel=KVIT_NORMAL;

    if( GetOprStatus(OPR_PAYM_KVT) != OPR_PM_ST_UNKVIT ) /* Переход к шагу "Обработка исходящего" */
        if ( Cancel==KVIT_NORMAL )
            if ( УстановитьСтатусыПлатежа(OPR_PAYM_STATE, OPR_PM_ST_CLOSE) )
                MsgBox("Возникла ошибка при смене статусов операции платежа");
                return 1;
            end;
            PaymentObj.PropStatus = PM_PROP_CLOSED;
            PaymentObj.Paymstatus = PM_FINISHED;
            return "";
        else
            /* Была квитовка отказом */
            if ( Corschem.IsNostro="X" )
                menu_item = Opr_MakeChoice(Menu1, Menu2, Menu3, ActionName);
            else
                menu_item = Opr_MakeChoice(Menu2, Menu3, ActionName);
                if ( menu_item>=0 )
                    menu_item = menu_item + 1;
                end;
            end;
            PaymentObj.PropStatus =  PM_PROP_CORREJECTED; 
            if ( menu_item==0 ) /* Поместить в картотеку */
            elif ( menu_item==1 ) /* Вернуть отправителю */
                if ( УстановитьСтатусыПлатежа(OPR_PAYM_DO, OPR_PM_ST_RETURN) )
                    MsgBox("Возникла ошибка при смене статусов операции платежа");
                    return 1;
                end;
            elif ( menu_item==2 ) /* Вернуть на повторную обработку */
                PaymentObj.PropStatus = PM_PROP_READY;
                if ( УстановитьСтатусыПлатежа(OPR_PAYM_DO, OPR_PM_ST_POS) )
                    MsgBox("Возникла ошибка при смене статусов операции платежа");
                    return 1;
                end;
            else
                return 1;
            end;
            return "";
        end;
    else
        return string(НомерШагаКвитовкаМБРИсходящего);  /* Несквитованный */
    end;
end;

//Jushmanov 2014-02-24 C-19151
macro PostStepAction( message,      /* 1 - выполнение шага; 2 - откат шага;   */
                      errTrn,       /* статус выполнения шага. 0 - ошибки нет */
                      FirstDoc,     /* указатель на первичный документ        */
                      ID_Oper,      /* внутренний идентификатор операции      */
                      Number_Step,  /* Номер шага операции (из настроек)      */
                      KindOper,     /* номер вида операции                    */
                      KindDoc,      /* номер вида первичного документа        */
                      KindStep,     /* вид шага операции                      */
                      ID_Step )     /* внутренний идентификатор шага операции */
    private var logquery, idStep = ID_Step, idOper = ID_Oper;

    if(( message == OP_EXECUTE_STEP ) and (errTrn == 0) and (IsOprMultiExec))
        if ((valtype(idOper) != V_INTEGER) and (valtype(idOper) != V_STRING)) idOper = 0; end;
        if ((valtype(idStep) != V_INTEGER) and (valtype(idStep) != V_STRING)) idStep = 0; end;

        logquery = "UPDATE usr_doprstep_robot_dbt SET t_is_robot_step = chr(88)" +
                   " WHERE t_id_operation = " + idOper + " AND t_id_step = " + idStep;
        ExecSQL(logquery, null, false);
    end;

    return 0;
end;