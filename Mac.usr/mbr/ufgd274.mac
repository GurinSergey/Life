/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация ответов по сообщению ED274                                     */
/*                                                                          */
/*  Имя файла: ufgd274.mac                                                  */
/*  Создан:    17.07.12                                  Chukina T.         */
/****************************************************************************/

import "ufgendoc.mac";

private macro FillPartyByUIS(uis : string, PartyID : @integer, 
                                           PartyCodeKind : @integer,
                                           PartyCode : @string,
                                           PartyName : @string )

  var bic_str = "";
  PartyID        = ToRespID(uis, bic_str);
  PartyCodeKind  = PTCK_BIC;
  PartyCode      = bic_str;

  FILE party(party) key 0;
  party.PartyID = PartyID;
  if(GetEQ(party))
    PartyName    = party.Name;
  end;
  
  OnError(er) // ToRespID не нашла субъекта
    PartyID       = -1;
    PartyCodeKind = PTCK_BIC;
    PartyCode     = "04" + substr( uis, 1, 7);
    PartyName     = "";
end;

macro GenDoc( addrMes )
  SetBuff( wlmes, addrMes );
  
  PrintLog(2,"Генерация ответа по ED274");

  ClearRecord(wlreq);
  
  var field_name, field_value;  
  if( not СчитатьПоле( field_name, field_value ) )
    return FALSE;
  end;

  var xml:object = ActiveX( "MSXML.DOMDocument" );
  if ( not xml.loadXML(field_value) )
    println( string( "Неверный формат сообщения по форме ED274" ) );
    return false;
  end; 
  
  /* Заполнение учетных буферов */
  wlreq.Trn           = wlmes.Trn;
  wlreq.RelatedRef    = wlmes.RelatedRef;
  wlreq.Direct        = "X";  
  wlreq.Kind          = MESKIND_ANSWER;
  
  FillPartyByUIS( ReadAttribute(xml,"EDAuthor"), @wlreq.OriginatorID,
                                                 @wlreq.OriginatorCodeKind,
                                                 @wlreq.OriginatorCode,
                                                 @wlreq.OriginatorName );
  
  FillPartyByUIS( ReadAttribute(xml,"EDReceiver"), @wlreq.RecipientID,
                                                   @wlreq.RecipientCodeKind,
                                                   @wlreq.RecipientCode,
                                                   @wlreq.RecipientName );
  
  wlreq.InitDateMes   = ToDate_(ReadAttribute(xml,"EDDate", "InitialED")); //TAM 19.11.2014 I-00533327-2
  wlreq.InitFormIDMes = НайтиФормуСообщенияПоРеференсу(wlreq.RelatedRef, TRANSP_UFBS);

  wlreq.Queries       = "InfoCode:" + ReadAttribute(xml, "InfoCode") + "\n" +
                        "InitialED:"+ GetRefByEDNoDateAuthor(xml, "InitialED") + "\n" +
                        "EDRefID:"  + GetRefByEDNoDateAuthor(xml, "EDRefID")+ "\n" +
                        //Gurin S. 25.09.2014 HF2031_19_25_83
                        "SumPT:"    + ReadAttribute(xml, "SumPT", "", false) + "\n" +
                        "AcptSum:"  + ReadAttribute(xml, "AcptSum", "", false) + "\n" +
                        "AcptDate:" + ReadAttribute(xml, "AcptDate", "", false);
          
  var Description     = ReadNodeText(xml, "Annotation");


  if( not ВставитьОтвет( wlreq, Description ) )
    std.msg("Ошибка при вводе ответа");
    return FALSE;
  end;

  return TRUE;

  OnError(er) /* обработка ошибок времени выполнения */
    ExeptionMessage(er);
    return FALSE;
end;