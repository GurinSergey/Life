/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация выписок по сообщениям УФЭБС ED207                              */
/*                                                                          */
/* Имя файла: ufgd217.mac                                                   */
/* Создан:    26.10.05                                      Фомченкова Л.Н. */
/* Изменен:   10.04.12 Chesnokov D.S. по заявке С-9530 проверка на группу   */
/****************************************************************************/
import "ufgendoc.mac", "diver.mac";

var ver_st = 1;
file wlinfo2 ("wlinfo.dbt") key 5;

macro GenDoc( addrMes )

  if (not ВходитВГруппу({oper},198))
    msgBox("Вы не включены в группу \"198 - Генерация сообщений УФЭБС\"");
    return false;
  end;

  var xml:object = ActiveX( "MSXML.DOMDocument" );
  var Строка, RelatedRef, Receiver, Error, Part = 0;
  var StatusCode, i = 0, Page, EDDate, j = 0;

  SetBuff( wlmes, addrMes );

  PrintLog( 2, "Генерация информационного сообшения по ED217" );

  ClearRecord( wlinfo );

  if( not СчитатьПоле( XMLField, Строка ) )
    return FALSE;
  end;

  if ( not xml.loadXML(Строка) )
     println( string( "Неверный формат сообщения по форме ED217" ) );
     return false;
  end;

  EDDate = ToDate_(ReadAttribute(xml,"EDDate"));//TAM

  /* Заполнение учетных буферов */
  wlinfo.TRN                = wlmes.Trn;
  wlinfo.OriginatorID       = ToRespID(ReadAttribute(xml,"EDAuthor"));
  wlinfo.OriginatorCodeKind = PTCK_BIC;
  wlinfo.OriginatorCode     = ReadAttribute(xml,"BIC");
  Receiver = ReadOptinalAttribute(xml,"EDReceiver");
  wlinfo.RecipientCodeKind  = PTCK_BIC;
  if(Receiver)
     wlinfo.RecipientID     = ToRespID(Receiver);
  else
     wlinfo.RecipientID     = {OurBank};
  end;
  wlinfo.RecipientCode      = ПолучитьКодСубъекта(wlinfo.RecipientID, wlinfo.RecipientCodeKind, Error);

  RelatedRef = ReadOptinalAttribute(xml,"EDNo", "InitialED");
  if(RelatedRef)
     wlinfo.RelatedRef      = RelatedRef;
  end;
  
  if( ver_st == 1 )
     if( not ВставитьИнфСообщение( wlinfo, "" ) )
       std.msg("Ошибка при вводе информационного сообщения");
       return FALSE;
     end;
  else
     if( (not ПроверитьЭлелементPartInfoНаНаличие(xml, wlinfo.PartAggregateID, Part )) Or (Part == 1))
        if( not ВставитьИнфСообщение( wlinfo, "" ) )
           std.msg("Ошибка при вводе информационного сообщения");
           return FALSE;
        end;
     else
        ClearRecord( wlinfo2 );
        wlinfo2.PartAggregateID = wlinfo.PartAggregateID;
        wlinfo2.Direct = "X";
        if( GetEQ( wlinfo2 ))
           if( not ОбновитьИнфСообщение( wlinfo2, "" ) )
              std.msg("Ошибка при обновлении информационного сообщения");
              return FALSE;
           end;
        else
           std.msg("Не найдена головная часть информационного сообщения");
           return FALSE;
        end;  
     end;
  end;

  return TRUE;

  OnError(er) /* Обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;
end;

macro GenDocV2( addrMes )
   ver_st = 2;
   return GenDoc( addrMes );
end;
