/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED275 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm275.mac                                                  */
/*  Создан:    15.10.13                                Chukina T.           */
/****************************************************************************/

import WldInter, "ufgenmes.mac", "wluftool.mac", "rsberror.mac";

private macro GetEDReceiverForED275(wlReq, RelatedRef : string) : string
  return GetEDReceiver( wlReq.RecipientID, wlReq.RecipientcodeKind, wlReq.RecipientCode, 
                        RelatedRef );
end;

private macro SetAttrFromQueries( elem: object, 
                                  wlreq, 
                                  AttrName: string, 
                                  IsMandatory: bool 
                                )

  var AttrValue : string = getFldValFromQueries(wlreq.Queries, AttrName + ":");

  if(AttrValue)
    elem.setAttribute(AttrName, AttrValue);
  elif(IsMandatory)
    RunError("Не заполнен атрибут " + AttrName);
  end;
end;

macro GenMes( addrMes, addrReq )

  record wlmes(wlmes);
  record wlReq(wlreq);

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );

  var RelatedRef : string = GetRelatedRefFromWlreq(wlReq, "EDRefID", "Не найдена ссылка на исходный ЭПД. Нужно или заполнить поле 'Ссылка', или заполнить EDRefID в поле 'Сообщение'");

  // Заполнение атрибутов поля "xml" сообщения
  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object = xml.createElement("ED275");
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");
  
  FillEDNoDateAuthorByRef_XMLField(mes, wlmes.TRN);

  mes.setAttribute("EDReceiver", GetEDReceiverForED275(wlReq, RelatedRef));
  
  macro SetAttr(AttrName : string, IsMandatory : bool)
    SetAttrFromQueries(mes, wlreq, AttrName, IsMandatory);
  end;
  SetAttr("TransKind",        true );
  SetAttr("Sum",              true );
  SetAttr("PayerPersonalAcc", false);
  SetAttr("PayerBIC",         true );
  SetAttr("PayerCorrespAcc",  false);
  SetAttr("PayeePersonalAcc", false);
  SetAttr("PayeeBIC",         true );
  SetAttr("PayeeCorrespAcc",  false);

  var elem : object = null;      
  
  elem = xml.createElement("EDRefID");  
  FillEDNoDateAuthorByRef_XMLField(elem, RelatedRef);
  elem.appendChild(xml.createTextNode(""));
  mes.appendChild(elem);

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; // Успешное завершение

  OnError( er ) // обработка ошибок
    std.msg(String(RsbGetError(er), "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;
