/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*                  Форма печати файла сеанса связи                         */
/*                                                                          */
/*  Имя файла: wlprsess.mac                                                  */
/*  Создан:  10.01.03                                    Бердюкова И.И.     */
/****************************************************************************/

import WldInter, "wlglobal.mac", "wlmctool.mac";
FILE file_wltpfrmt( wltpfrmt ) key 0;
FILE file_wlmes( wlmes ) key 1; /* SessionID */
FILE file_wlmeslnk( wlmeslnk ) key 0; /* MesID + ObjKind + ObjID */

var file_wlsess:TRecHandler = TRecHandler("wlsess.dbt", "bank.def");

/*Заголовок*/
macro MainHeadline() 
 var SessionAction, Транспорт, ВидФорматаТранспорта;

 if ( file_wlsess.rec.Direct == "X" )
    SessionAction = "Загрузка"
 else
    SessionAction = "Выгрузка"
 end;

 f_wltransp.TpID =  file_wlsess.rec.TpID;
 if ( GetEQ(f_wltransp) )
    Транспорт = f_wltransp.Name;
 else
    std.out(1, "Не найдена запись в файле транспортов");
    return false;
 end;

 file_wltpfrmt.TpFrmtID = file_wlsess.rec.TpFrmtID;
 if ( GetEQ(file_wltpfrmt) )
    ВидФорматаТранспорта = file_wltpfrmt.Description;
 else
    std.out(1, "Не найдена запись в файле форматов транспортов");
    return false;
 end;

 /* ЗАГОЛОВОК ОТЧЕТА - общий для всех видов форматов транспортов */
   println("                           ФАЙЛ СЕАНСА СВЯЗИ №", file_wlsess.rec.Number);
   println("                           РЕЙС №", file_wlsess.rec.NumberRace);
[
    Тип сеанса:                 ########
    Транспорт:                  #####################
    Вид формата транспорта:     #
    Имя файла сеанса:           #
    Дата файла:                 ########## ########
    Дата проведения сеанса:     ########## ########
    Операционный день:          ##########
    Операционист сеанса:        ##########

]
(
   SessionAction:l,
   Транспорт:l,
   ВидФорматаТранспорта:l,
   file_wlsess.rec.FileName:l,
   file_wlsess.rec.FileDate:f:l, file_wlsess.rec.FileTime:l,
   file_wlsess.rec.SysDate:f:l, file_wlsess.rec.SysTime:l,
   file_wlsess.rec.BankDate:f:l,
   file_wlsess.rec.UserID:l
);                                        
end;


macro PrintDocument(ncopy)
 debugbreak;
 FILE SesTmpFile() txt;
 file ss() txt;
 var MacroFile, MacroProc;
 var field_name, field_value;
 var SesTmpFileName, OldName; 
 
/* !!! Пока форма печати файла сеанса связи разработана только для */
/*     транспорта МЦИ */
 /*!!!MEV: Нужно уметь печать сеанс для транспорта "Почта" */
 if (file_wlsess.rec.TpID == TRANSP_POST)
 	DebugBreak;
 	if (open(ss, file_wlsess.rec.FileName))
 		ViewFile(ss);
 		close(ss);
 	else
 		MsgBox("Ошибка при открытии файла сеанса");
 	end;
 else
     if( file_wlsess.rec.TpID != TRANSP_MCI)
       /*std.out(1, "Форма печати файла сеанса связи реализована только для транспорта МЦИ");*/
       MsgBox("Форма печати файла сеанса связи реализована только для транспорта МЦИ");
       return false;
     elif( file_wlsess.rec.Direct != "X");
       /*std.out(1, "Форма печати файла сеанса связи реализована только для сеанса загрузки");*/
       MsgBox("Форма печати файла сеанса связи реализована только для сеанса загрузки");
       return false;
     end;
    /****************************************************************************/

     if( not ПолучитьКопиюФайлаСеансаСвязи(file_wlsess.rec.SessionID) )
    /*    std.out(1,"Не найдена копия файла экспорта/импорта");*/
       exit(1);
     end;  

     /*Перенаправляем вывод сообщения в файл, чтобы при многократной печати использовать данные*/
     SesTmpFileName = GetTxtFileName("tmpssprn");
     OldName = SetOutPut( SesTmpFileName );

     if(not open( SesTmpFile, SesTmpFileName ) )
       std.msg( String("Файл не открыт:", "|", SesTmpFileName) );
       return false;
     end;

     MainHeadline();
     
     if( not ПолучитьДействие( file_wlsess.rec.TpFrmtID, OBJTYPE_TPFRMT, ACTION_FRM_PRINTFILE, MacroFile, MacroProc ) )
       return false;
     end;
      
     ExecMacroFile( MacroFile, MacroProc, ncopy);

     close(SesTmpFile);
     SetOutPut( OldName, true );/*Перенаправляем вывод обратно*/

     while( ncopy !=0 )
       rewind(SesTmpFile);
       while( next(SesTmpFile) )
         println(SesTmpFile.str);
       end;
       ncopy = ncopy - 1;
     end;
 end;

 return true;

 OnError(er) /* обработка ошибок времени выполнения */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE;

end;

