/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения по SWIFT MT202                                       */
/*                                                                          */
/*  Имя файла: swgm202.mac                                                  */
/*  Создан:  24.01.00                                        Алешин А.В.    */
/* Changes:                                                                 */
/* TAM 14.03.2014 I-00471303 - заполняем поля в порядке следования в релизе */
/****************************************************************************/
import "swgenmes.mac";
const  Relise = "202";


/* EVG 12.11.09 Реализовано по просьбе Фоменко.
   Функция проверяет первый символ полученной строки. Если символ содержится в массиве aBadSymbs, 
   то перед ним подставляется пробел. */
macro CheckFirstSymbol( str:@string )
   var aBadSymbs = TArray();
       aBadSymbs(0) = ":";
       aBadSymbs(1) = "-";

   var i = 0,
       isBad = false,
       FirstSymbol = subStr(str, 1, 1);

   while ((i < aBadSymbs.Size) and not isBad)
      if (FirstSymbol == aBadSymbs(i))
         isBad = true;
      end;
      i = i + 1;
   end;

   if (isBad)
      str = " " + str;
   end;
end;


macro GenMes( addrMes, addrPm, addrOldMes )
  var field_value, Tag, PaymentDetails;
  var Recreate = false;
  var RsPaym;
  RECORD rcv(pmroute);
  RECORD ocb(pmroute);
  RECORD irb(pmroute);
  Record oldwlMes (wlmes);

  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );
  if( ValType(AddrOldMes) == V_MEMADDR )
     SetBuff( Oldwlmes,  addrOldMes );
     Recreate = true;
  end;

  PrintLog(2,"Генерация сообщения по МТ202");
  RsPaym = RsbPayment( wlpmpaym.PaymentID );  
  /* 20 - номер транзакции (документа) */
  ЗаписатьПолеЛог( TransactionReferenceNumberField, wlmes.TRN );

  /* 21 - Ссылка на документ или сделку */
  /* DAI по C-20122 добавлена возможность заполнения поля 21 по примечанию платежа */
  field_value = FillRelatedReferenceField(RsPaym);
  if ( wlmes.RelatedRef!="" )
     WriteFieldEx  (RelatedReferenceField, wlmes.RelatedRef, Recreate, OldwlMes );
  elif (field_value != "")
     WriteFieldEx  (RelatedReferenceField, field_value, Recreate, OldwlMes );
  else
     WriteFieldEx  (RelatedReferenceField, "NONREF", Recreate, OldwlMes );
  end;

  /* 32A - дата валютирования, код валюты, сумма */
  field_value = GetSWIFTDate( RsPaym.OutTransferDate ) +
                FillCurrencyOriginalOrderedAmountField( RsPaym );
  WriteFieldEx  (ValueDateCurrencyCodeAmountField_A, field_value, Recreate, OldwlMes );

  if( IsPmCoverMode() == FALSE )

     /* 52a - банк приказодателя */
     /* EVG Передача в функцию FillOrderingInstitutionField() номера формы.
     field_value = FillOrderingInstitutionField(RsPaym, Tag ); */
     field_value = FillOrderingInstitutionField(RsPaym, Tag, null, "202" );

     CheckFirstSymbol(@field_value);
     ЗаписатьПолеВБлокКонтекст0( OrderingInstitutionField+Tag, "52a", field_value, Recreate, OldwlMes );

     /* 53a - корреспондент отправителя */
     Tag = SWIFT_Tag_A;
     field_value = FillSenderCorrespondentField(RsPaym, Tag );     
     CheckFirstSymbol(@field_value);
     ЗаписатьПолеВБлокКонтекст0( Sender_sCorrespondentField+Tag, "53a", field_value, Recreate, OldwlMes );     

     /* 54a - корреспондент получателя */
     Tag = SWIFT_Tag_A;
     field_value = FillReceiverCorrespondentField(RsPaym, Tag );     
     CheckFirstSymbol(@field_value);
     ЗаписатьПолеВБлокКонтекст0( Receiver_sCorrespondentField+Tag, "54a", field_value, Recreate, OldwlMes );     

     /* 56a - банк посредник */
     Tag = "";
     field_value = FillIntermediaryField(RsPaym, Tag );
     CheckFirstSymbol(@field_value);
     ЗаписатьПолеВБлокКонтекст0( IntermediaryField+Tag, "56a", field_value, Recreate, OldwlMes );

     /* 57a - банк бенефициара */
     Tag = "";
     field_value = FillAccountWithInstitutionField(RsPaym, Tag );
     CheckFirstSymbol(@field_value);
     ЗаписатьПолеВБлокКонтекст0( AccountWithInstitutionField+Tag, "57a", field_value, Recreate, OldwlMes );

     /* 58a - банк-бенефициар */
     Tag = "";
     field_value = FillBeneficiaryInstitutionField(RsPaym, Tag );
     CheckFirstSymbol(@field_value);
     ЗаписатьПолеВБлокКонтекст0( BeneficiaryInstitutionField+Tag, "58a", field_value, Recreate, OldwlMes );
  else /* покрытие */
     /* банк рамбурсирующий посредник -> посредник */
     if( ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
         ПолучитьБлижайшийУзелПлатежа(RsPaym.PaymentID, RTDIR_OUT, ocb) AND
         (rcv.RouteID!=ocb.RouteID) AND
         ПерейтиНаБолееДальнийУзел(RTDIR_OUT, Route) AND
         (rcv.RouteID!=Route.RouteID) )
       ЗаписатьПолеМаршрутаКонтекст0Лог( IntermediaryField, "56a", Route, Recreate, OldwlMes );
     end;

     /* корреспондент получателя -> банк бенефициара */
     if( ПолучитьБлижайшийУзелПлатежа( RsPaym.PaymentID, RTDIR_OUT, ocb ) AND
         ПерейтиНаБолееДальнийУзел(RTDIR_OUT, irb) AND
         ПолучитьУзелПолучателяПлатежа(RsPaym.PaymentID, rcv) AND 
         (rcv.RouteID!=ocb.RouteID) AND (rcv.RouteID!=irb.RouteID) AND
         ПерейтиНаБолееБлизкийУзел(RTDIR_OUT, Route) AND (Route.RouteID!=irb.RouteID) )
        ЗаписатьПолеМаршрутаКонтекст0Лог( AccountWithInstitutionField, "57a", Route, Recreate, OldwlMes );
     end;

     /* банк получатель -> банк-бенефициар */
     Tag = "";
     field_value = FillBeneficiaryInstitutionField(RsPaym, Tag );
     ЗаписатьПолеВБлокКонтекст0( BeneficiaryInstitutionField+Tag, "58a", field_value, Recreate, OldwlMes );
  end;

   //TAM 14.03.2014 I-00471303
   //Восстанавливаем нулевой контекст блока
   if( not УстановитьКонтекстБлока())
      RunError("|Ошибка при установке нулевого контекста блока");
   end;
   /* 72 - информация для банка-получателя */
   PaymentDetails = "";
   StrChangeRusXSet(RsPaym.Ground, PaymentDetails);
   field_value = FillInformationOfPaymentField(RsPaym, ALLOWCODES_MT202_STANDART, PaymentDetails);      
   CheckFirstSymbol(@field_value);
   WriteFieldEx  (SenderToReceiverInformationField, field_value, Recreate, OldwlMes );
  
   return TRUE; /* Успешное завершение */

  OnError(er) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;

