// -------------------------------------------------------------------------------------------------
// @filename: xr_category.mac v.1
// @author  : 2013-05-01 zip_z. 
// @desc    : ТК Life - категории
// @changes : none
// -------------------------------------------------------------------------------------------------
import ctinter;
import xr_core;

private macro set_mainattr (p_objecttype, p_objectid, p_groupid, p_attrid, p_uniobjectid)
    var sql = "select 1 from dobjgroup_dbt " + 
              " where t_objecttype = :m_objecttype and t_groupid = :m_groupid and t_type = chr (88)";
    var param = makeArray (SqlParam ("m_objecttype", p_objecttype )
                          ,SqlParam ("m_groupid"   , p_groupid    )
                          );
    sql = execSqlSelect (sql, param, false);
    // категория исключительная - установка главной (основной) категории не требуется
    if (sql.moveNext ()) return; end;
    
    // категория неисключительная - проверяем, есть ли главная (основная) категория
    sql   = "select count ( 1 ) n from dobjatcor_dbt where t_objecttype = :m_objecttype " + 
            "   and t_groupid = :m_groupid and t_object = :m_object and t_general = chr (88)";
    param = makeArray (SqlParam ("m_objecttype", p_objecttype  )
                      ,SqlParam ("m_groupid"   , p_groupid     )
                      ,SqlParam ("m_object"    , p_uniobjectid )
                      );
    sql   = execSqlSelect (sql, param, false);
    if ((sql.moveNext ()) and (sql.value ("n", null, V_INTEGER) == 0))
        // нет основной категории, проставляем :)
        sql = "update dobjatcor_dbt set t_general = chr (88) where t_objecttype = :m_objecttype "
              "   and t_groupid = :m_groupid and t_object = :m_object and t_attrid = :m_attrid";
        param = makeArray (SqlParam ("m_objecttype", p_attrid      )
                          ,SqlParam ("m_groupid"   , p_groupid     )
                          ,SqlParam ("m_object"    , p_uniobjectid )
                          ,SqlParam ("m_attrid"    , p_attrid      )
                          );
        execSql (sql, param, false);
    end;
    
end;

macro usr_set_category (p_objecttype      :integer
                       ,p_objectid        :string
                       ,p_groupid         :integer
                       ,p_attrid          :integer
                        ):TXReturnValueStruct
                        
    var uid  = makeFastUniId (p_objecttype, p_objectid);
    var cat  = RsbObjCategories (p_objecttype, uid);
    var stat = (cat.ConnectAttr(p_groupid, p_attrid, null, null, {curdate}) == EXIT_SUCCESS);
    
    if (stat) 
        stat = cat.Save(uid); 
        set_mainattr (p_ObjectType, p_ObjectID, p_GroupID, p_AttrID, uid);
    end;
    
    return iif (stat, TXReturnValueStruct (p_objectid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_objectid, EXIT_LOGICAL_ERROR, getErrMsg ()));

end;


macro usr_del_category (p_objecttype      :integer
                       ,p_objectid        :string
                       ,p_groupid         :integer
                       ,p_attrid          :integer
                        ):TXReturnValueStruct
                        
    var uid  = makeFastUniId (p_objecttype, p_objectid);
    var cat  = RsbObjCategories (p_objecttype, uid);
    var stat = (cat.DisconnectAttr (p_groupid, p_attrid) == EXIT_SUCCESS);
    
    if (stat) stat = cat.Save(uid); end;
    // подозрение на грегерадские костыли. Пока убрал.
    // set_mainattr (p_ObjectType, p_ObjectID, p_GroupID, p_AttrID, uid);
    
    return iif (stat, TXReturnValueStruct (p_objectid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_objectid, EXIT_LOGICAL_ERROR, getErrMsg ()));

end;