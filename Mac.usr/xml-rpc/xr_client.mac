// -------------------------------------------------------------------------------------------------
// @filename: xr_client.mac v.1
// @author  : 2013-05-01 zip_z. 
// @desc    : ТК Life - субъекты экономики
// @changes : none
// -------------------------------------------------------------------------------------------------
import ptinter, bankinter;
import xr_core;

/*** ВНУТРЕННИЕ ИНТЕРФЕЙСНЫЕ МЕТОДЫ (исключаем дублирование кода в бизнес-логике) ***/

// @desc    : заполнить и обновить структуру клиента
// @return  : V_OBJECT TXReturnValueStruct (определено в xr_core.mac)
private macro PT_FillClientParameters (p_partyid          :integer // dparty_dbt.t_partyid
                                      ,p_legalform        :integer // форма собственности
                                      ,p_shortname        :string  // краткое наименование
                                      ,p_fullname         :string  // полное наименование
                                      ,p_addname          :string  // дополнительное наименование
                                      ,p_lastname         :string  // фамилия
                                      ,p_firstname        :string  // имя
                                      ,p_secondname       :string  // отчество
                                      ,p_birthdate        :date    // дата рождения
                                      ,p_birthplace       :string  // место рождения
                                      ,p_ismale           :bool    // is мужчина
                                      ,p_nationality      :string  // национальность
                                      ,p_isemployer       :bool    // is работотатель (ЧП)
                                      ,p_workplace        :string  // место работы и должность
                                      ,p_okpo             :string  // код ОКПО
                                      ,p_country          :string  // страна
                                      ,p_superiorid       :integer // id вышестоящей организации
                                      ,p_charterdate      :date    // {нет документации}
                                      ,p_capitalfi_iso    :string  // {нет документации}
                                      ,p_declarecapital   :money   // {нет документации}
                                      ,p_realcapital      :money   // {нет документации}
                                      ):TXReturnValueStruct
    /** организационно-правовые формы субъектов (dparty_dbt.t_legalform) **/
    private const PT_PARTYKIND_LEGAL    = 1; // юрик
    private const PT_PARTYKIND_PHYSICAL = 2; // физик
    
    // заполнение
    var pt = RsbParty (nvl_ex (p_partyid, 0));
    
    if   ( p_legalform == PT_PARTYKIND_LEGAL )
        if (notnull (p_charterdate    )) pt.charterdate    = p_charterdate;    end;
        if (notnull (p_capitalfi_iso  )) pt.capitalfi      = p_capitalfi_iso;  end;
        if (notnull (p_declarecapital )) pt.declarecapital = p_declarecapital; end;
        if (notnull (p_realcapital    )) pt.realcapital    = p_realcapital;    end;
        if (notnull (p_okpo           )) pt.okpo           = p_okpo;           end;
    
    elif ( p_legalform == PT_PARTYKIND_PHYSICAL )
        if (p_isemployer == true) 
            if (notnull (p_okpo       )) pt.okpo       = p_okpo;               end;
        end;
        if (notnull (p_isemployer     )) pt.isemployer = p_isemployer;         end;
        if (notnull (p_firstname      )) pt.firstname  = p_firstname;          end;
        if (notnull (p_secondname     )) pt.patronymic = p_secondname;         end;
        if (notnull (p_lastname       )) pt.lastname   = p_lastname;           end;
        if (notnull (p_birthdate      )) pt.birthdate  = p_birthdate;          end;
        if (notnull (p_birthplace     )) pt.birthplace = p_birthplace;         end;
        if (notnull (p_ismale         )) pt.ismale     = p_ismale;             end;
        if (notnull (p_nationality    )) pt.ethnos     = p_nationality;        end;
        if (notnull (p_workplace      )) pt.placework  = p_workplace;          end;
    end;
    
    // общее для всех
    if (notnull (p_legalform )) pt.legalform  = p_legalform; end;
    if (notnull (p_shortname )) pt.shortname  = p_shortname; end;
    if (notnull (p_fullname  )) pt.fullname   = p_fullname;  end;
    if (notnull (p_addname   )) pt.addname    = p_addname;   end;
    
    // если внешняя система не передала параметры, заполняем их значениями по умолчанию
    pt.nrcountry  = nvl_ex (p_country, "RUS");
    pt.superiorid = nvl_ex (p_superiorid, -1);
    
    // обновление, возврат результата 
    var stat = pt.update ();
    return iif (stat, TXReturnValueStruct (pt.partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (pt.partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;

/*** КЛИЕНТЫ (usr_create_client; usr_update_client; usr_delete_client) ***/

// @desc    : создать клиента
// @return  : V_OBJECT TXReturnValueStruct (определено в xr_core.mac)
macro usr_create_client (p_legalform        :integer // форма собственности
                        ,p_shortname        :string  // краткое наименование
                        ,p_fullname         :string  // полное наименование
                        ,p_addname          :string  // дополнительное наименование
                        ,p_lastname         :string  // фамилия
                        ,p_firstname        :string  // имя
                        ,p_secondname       :string  // отчество
                        ,p_birthdate        :date    // дата рождения
                        ,p_birthplace       :string  // место рождения
                        ,p_ismale           :bool    // is мужчина
                        ,p_nationality      :string  // национальность
                        ,p_isemployer       :bool    // is работотатель (ЧП)
                        ,p_workplace        :string  // место работы и должность
                        ,p_okpo             :string  // код ОКПО
                        ,p_country          :string  // страна
                        ,p_superiorid       :integer // id вышестоящей организации
                        ,p_charterdate      :date    // {нет документации}
                        ,p_capitalfi_iso    :string  // {нет документации}
                        ,p_declarecapital   :money   // {нет документации}
                        ,p_realcapital      :money   // {нет документации}
                        ):TXReturnValueStruct
    return PT_FillClientParameters ( null, p_legalform, p_shortname, p_fullname, p_addname, p_lastname, 
                                     p_firstname, p_secondname, p_birthdate, p_birthplace, p_ismale, 
                                     p_nationality, p_isemployer, p_workplace, p_okpo, p_country, 
                                     p_superiorid, p_charterdate, p_capitalfi_iso, p_declarecapital, 
                                     p_realcapital 
                                   );
end;

// @desc    : обновить параметры клиента
// @return  : V_OBJECT TXReturnValueStruct (определено в xr_core.mac)
macro usr_update_client (p_partyid          :integer // dparty_dbt.t_partyid
                        ,p_legalform        :integer // форма собственности
                        ,p_shortname        :string  // краткое наименование
                        ,p_fullname         :string  // полное наименование
                        ,p_addname          :string  // дополнительное наименование
                        ,p_lastname         :string  // фамилия
                        ,p_firstname        :string  // имя
                        ,p_secondname       :string  // отчество
                        ,p_birthdate        :date    // дата рождения
                        ,p_birthplace       :string  // место рождения
                        ,p_ismale           :bool    // is мужчина
                        ,p_nationality      :string  // национальность
                        ,p_isemployer       :bool    // is работотатель (ЧП)
                        ,p_workplace        :string  // место работы и должность
                        ,p_okpo             :string  // код ОКПО
                        ,p_country          :string  // страна
                        ,p_superiorid       :integer // id вышестоящей организации
                        ,p_charterdate      :date    // {нет документации}
                        ,p_capitalfi_iso    :string  // {нет документации}
                        ,p_declarecapital   :money   // {нет документации}
                        ,p_realcapital      :money   // {нет документации}
                        ):TXReturnValueStruct
    return PT_FillClientParameters ( p_partyid, p_legalform, p_shortname, p_fullname, p_addname, p_lastname, 
                                     p_firstname, p_secondname, p_birthdate, p_birthplace, p_ismale, 
                                     p_nationality, p_isemployer, p_workplace, p_okpo, p_country, 
                                     p_superiorid, p_charterdate, p_capitalfi_iso, p_declarecapital, 
                                     p_realcapital 
                                    );
end;

// @desc    : заблокировать клиента
// @return  : V_OBJECT TXReturnValueStruct (определено в xr_core.mac)
macro usr_delete_client (p_partyid:integer):TXReturnValueStruct
    var pt = RsbParty (p_partyid);
    // обновление, возврат результата 
    var stat = pt.lock ();
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;

/*** ОБСЛУЖИВАНИЕ КЛИЕНТОВ ***/

// @desc    : установить вид обслуживания клиента
// @return  : V_OBJECT TXReturnValueStruct (определено в xr_core.mac)
macro usr_set_client_service (p_partyid       :integer // dparty_dbt.t_partyid
                             ,p_servkind      :integer // вид обслуживания
                             ,p_oper          :integer // опер
                             ,p_startdate     :date    // дата начала обслуживания
                             ,p_department    :integer // филиал
                             ,p_branch        :integer // ВСП
                             ):TXReturnValueStruct
    // дозаполняем непереданные параметры
    p_oper       = nvl_ex (p_oper      , {oper}      );
    p_startdate  = nvl_ex (p_startdate , {curdate}   );
    p_department = nvl_ex (p_department, {operdprt}  );
    p_branch     = nvl_ex (p_branch    , p_department);
    
    var stat = PT_BindClientWithBranch(p_partyid, p_servkind, p_branch, p_oper, p_startdate);
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;

// @desc    : удалить вид обслуживания клиента
// @return  : V_OBJECT TXReturnValueStruct (определено в xr_core.mac)
macro usr_del_client_service (p_partyid:integer, p_servkind:integer):TXReturnValueStruct
    var stat = PT_CloseClientService(p_partyid, p_servkind, {curdate}, true);
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;

/*** КОДЫ КЛИЕНТОВ ***/

// @desc    : установить клиенту p_partyid код вида p_codekind со значением p_codevalue
// @return  : V_OBJECT TXReturnValueStruct (определено в xr_core.mac)
macro usr_set_client_code (p_partyid   :integer  // dparty_dbt.t_partyid
                          ,p_codekind  :integer  // вид кода
                          ,p_codevalue :variant  // значение кода
                          ):TXReturnValueStruct
    // дозаполняем непереданные параметры
    p_codekind = nvl_ex (p_codekind, PTCK_CONTR);
    
    // инициализируем RsbParty, закрываем старый код, устанавливаем новый (если p_codevalue) задан
    // если прилетает p_codekind == 1 (PTCK_CONTR), зовем нашу собственноручно писанную установку RS-кода,
    // на переданное значение p_codevalue не обращаем внимания (ничего страшного, таке життя)
    var pt   = RsbParty (p_partyid);
    var code = pt.code;
    
    code.closeCode (p_codekind); // обработки ошибок нет и не надо
    if (notnull (p_codevalue)) code.setCode (p_codekind, p_codevalue); end;
    
    if (p_codekind == PTCK_CONTR) 
        execMacroFile ("set_rscode.mac", "SetRScode", pt); // обработки ошибок нет и не надо
    else
        code.setCode (p_codekind, p_codevalue); // обработки ошибок нет и не надо
    end;
    
    var stat = pt.update ();
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;

// @desc    : удалить у клиента p_partyid код вида p_codekind
// @return  : V_OBJECT TXReturnValueStruct (определено в xr_core.mac)
macro usr_del_client_code (p_partyid:integer, p_codekind:integer):TXReturnValueStruct
    var pt = RsbParty (p_partyid);
    var code = pt.code;
    
    code.DeleteCode (p_codekind); // обработки ошибок нет и не надо
    
    var stat = pt.update ();
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;


/*** АДРЕСА КЛИЕНТОВ ***/

macro usr_set_client_address (p_partyid         :integer 
                             ,p_addresstype     :integer
                             ,p_country         :string
                             ,p_address         :string
                             ,p_phonenumber     :string
                             ,p_phonenumberad   :string
                             ,p_faxnumber       :string
                             ,p_email           :string
                             ):TXReturnValueStruct
    
    var pt   = RsbParty(p_partyid);
    var addr = pt.address(p_addresstype);
    
    addr.country       = p_country;
    addr.address       = p_address;
    addr.phonenumber   = p_phonenumber;
    addr.phonenumberad = p_phonenumberad;
    addr.faxnumber     = p_faxnumber;
    addr.email         = p_email;
    
    var stat = pt.update ();
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;

macro usr_del_client_address (p_partyid:integer, p_addresstype:integer):TXReturnValueStruct
    var pt   = RsbParty(p_partyid);
    var addr = pt.address(p_addresstype);
    addr.delete ();
    var stat = pt.update ();
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;


/*** ДАННЫЕ РЕГИСТРАЦИИ КЛИЕНТОВ ***/

macro usr_set_client_paper (p_partyid         :integer
                           ,p_paperkind       :integer
                           ,p_series          :string
                           ,p_number          :string
                           ,p_issueddate      :date
                           ,p_issuer          :string
                           ,p_issuercode      :string
                           ,p_is_main         :bool
                           ):TXReturnValueStruct
    var pt   = RsbParty(p_partyid);
    var p    = pt.personPaper (p_paperkind);
    
    if (notnull (p_series     )) p.series     = p_series;     end;
    if (notnull (p_number     )) p.number     = p_number;     end;
    if (notnull (p_issueddate )) p.issueddate = p_issueddate; end;
    if (notnull (p_issuer     )) p.issuer     = p_issuer;     end;
    if (notnull (p_issuercode )) p.issuercode = p_issuercode; end;
    if (notnull (p_is_main    )) p.ismain     = p_is_main;    end;
    
    var stat = pt.update ();
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;

macro usr_del_client_paper (p_partyid         :integer
                           ,p_paperkind       :integer
                           ):TXReturnValueStruct
    var pt   = RsbParty (p_partyid);
    var p    = pt.personPaper (p_paperkind);
    p.delete ();
    var stat = pt.update ();
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;

/*** РЕГИСТРАЦИОННЫЕ ДОКУМЕНТЫ КЛИЕНТОВ ***/

// @desc   : установка данных о регистрации клиента
// @return : V_OBJECT TXReturnValueStruct
macro usr_set_client_regdoc (p_partyid         :integer
                            ,p_regpartykind    :integer
                            ,p_regdockind      :integer
                            ,p_regpartyid      :integer
                            ,p_regcode         :string
                            ,p_startdate       :date
                            ,p_series          :string
                            ,p_number          :string
                            ,p_docdate         :date
                            ,p_ismain          :bool
                            ):TXReturnValueStruct
    
    var pt = RsbParty (p_partyid);
    var doc = pt.partyRegDoc(p_regpartykind, p_regdockind);
    
    doc.regpartyid = p_regpartyid;
    doc.startdate  = p_startdate;
    doc.series     = p_series;
    doc.number     = p_number;
    doc.docdate    = p_docdate;
    doc.ismain     = p_ismain;
    
    var stat = pt.update ();
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;

// @desc   : удаление данных о регистрации клиента
// @return : V_OBJECT TXReturnValueStruct
macro usr_del_client_regdoc (p_partyid       :integer
                            ,p_regpartykind  :integer
                            ,p_regdockind    :integer
                            ):TXReturnValueStruct
    var pt = RsbParty (p_partyid);
    var doc = pt.partyRegDoc(p_regpartykind, p_regdockind);
    doc.delete ();
    var stat = pt.update ();
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;

/*** СОТРУДНИКИ КЛИЕНТА ***/
macro usr_set_client_officer (p_partyid          :integer
                             ,p_personid         :integer
                             ,p_isfirstperson    :bool
                             ,p_issecondperson   :bool
                             ,p_post             :string
                             ,p_datefrom         :date
                             ,p_dateto           :date
                             ):TXReturnValueStruct
    var pt   = RsbParty (p_partyid);
    var off  = pt.officer();
    var pers = TRecHandler ("party.dbt");
    
    off.new();
    off.personid       = p_personid;
    off.isfirstperson  = p_isfirstperson;
    off.issecondperson = p_issecondperson;
    off.post           = p_post;
    off.datefrom       = p_datefrom;
    off.dateto         = p_dateto;
    
    // добавляем RS-код сотруднику, если его нет (сомнительная доработка, т.к. используется 110-й референс)
    var pers_code = "";
    var p         = RsbParty (p_personid);
    var pcode     = p.code;
    var stat      = true;
    
    if (ПолучитьКодСубъекта (p_personid, PTCK_CONTR) == EMPTY_STRING)
        pers.rec.partyid = p_personid;
        if ( stat = (GenerateReference(110, pers_code, OBJ_PARTY, pers) == EXIT_SUCCESS));
            if (stat = pcode.setCode (PTCK_CONTR, pers_code))
                stat = p.Update ();
            end;
        end;
    end;

    if (stat == true) stat = pt.update (); end;
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;


macro usr_del_client_officer (p_partyid:integer, p_personid:integer):TXReturnValueStruct
    var pt   = RsbParty (p_partyid);
    var off  = pt.officer();
    
    off.deletePerson (p_personid);
    
    var stat = pt.update ();
    return iif (stat, TXReturnValueStruct (p_partyid, EXIT_SUCCESS      , EMPTY_STRING ),
                      TXReturnValueStruct (p_partyid, EXIT_LOGICAL_ERROR, getErrMsg ()));
end;