// -------------------------------------------------------------------------------------------------
// @filename: form_ssud_rest.mac
// @author  : Гурин С.
// @desc    : Процедура формирования остатков ссудных средств
// @request : C-22396 
// @modify  : none
// -------------------------------------------------------------------------------------------------
import rcw, rsexts, treport ;
import globals, oralib, likePy, lib_account;
var XL_WorkBook_Path;
var XL_Sheet;

var rec_success = TArray (); // вставленные записи  
var rec_err     = TArray (); // отбракованные записи

private class TRecord (_code, _acc, _rest, _oper, _desc)
    var m_code   = _code;
    var m_acc    = _acc;
    var m_rest   = _rest;
    var m_oper   = _oper;
    var m_desc   = _desc;
end;

private macro viewProtocol ()
    var table_success = CTableReport ();
    var table_err = CTableReport ();
    var i;
   [##########################################################################################] ("Протокол загрузки остатков ссудных средств":c:40);
   [                                                                       Пользователь: #####] ({oper});
   [                                                       Дата: ########### Время: ##########] (date (), time ());
   [                                                                                          ];

    i = 0;
    if (rec_success.size ())
        table_success.addColumn ("RS-код",                   10,  AL_LEFT); 
        table_success.addColumn ("Счет",                     25,  AL_LEFT);
        table_success.addColumn ("Остаток ссудных средств",  25,  AL_CENTER);
        table_success.addColumn ("Операционист",             15,  AL_CENTER);
        table_success.printHead (string ("Успешно загружены записи":c:50));

        while (i < rec_success.size ())
            table_success.printStringTransferByWord (rec_success[i].m_code, 
                                                     rec_success[i].m_acc,
                                                     rec_success[i].m_rest,
                                                     rec_success[i].m_oper); 
            i = i + 1;
        end;

        table_success.printBottom ();
        println("ИТОГО: ", i);
    else
        println("Не было загружено ни одной записи");
    end;
   
    i = 0;
    if (rec_err.size ())
        table_err.addColumn ("RS-код",                   10,  AL_LEFT); 
        table_err.addColumn ("Счет",                     25,  AL_LEFT);
        table_err.addColumn ("Остаток ссудных средств",  25,  AL_CENTER);
        table_err.addColumn ("Операционист",             15,  AL_CENTER);
        table_err.addColumn ("Описание",                 45,  AL_CENTER);
        table_err.printHead (string ("Не загружены записи":c:50));

        while (i < rec_err.size ())
            table_err.printStringTransferByWord (rec_err[i].m_code, 
                                                 rec_err[i].m_acc,
                                                 rec_err[i].m_rest,
                                                 rec_err[i].m_oper,
                                                 rec_err[i].m_desc); 
            i = i + 1;                                                                       
        end;

        table_err.printBottom ();
        println("ИТОГО: ", i);
    else
        println("Ошибок при загрузки нет");
    end;
    
end;

private macro ConfirmData(p_acc, p_valdate, p_rest)
    var sql = "select 1 from usr_ssud_rest_dbt where t_account = :acc " 
              "  and t_date_value = :valdate and t_rest = :rest"; 
    var rs:RsdRecordset = execSQLselect(sql, makeArray (SqlParam ("acc", p_acc),SqlParam ("valdate", p_valdate),SqlParam ("rest", p_rest)));
    return (rs.movenext());
end;

private macro ConfirmExistsAcc(p_acc)
    //Gurin S. 31 патч адаптация
    var sql = "select 1 from daccount_dbt where t_account = :acc and t_open_close != 'З'"; 
    var rs:RsdRecordset = execSQLselect(sql, makeArray (SqlParam ("acc", p_acc)));
    return (not rs.movenext());
end;


class TXLData ()
   var m_acc, m_code, code_cur, m_valdate = {Curdate}, m_oper = {oper}, m_rest;
   macro insert ()
       if (m_code != m_acc)
           if(not ConfirmData(m_acc, m_valdate, m_rest))
               if (not ConfirmExistsAcc(trim(m_acc)))
                   var sql = "insert into usr_ssud_rest_dbt values (null, :acc, :code_cur, :valdate, :rest, :oper)";
                   sql = execSQL (sql, makeArray (SQLParam ("acc", m_acc),           SQLParam ("code_cur", Acc_GetFiidByAccount(m_acc)),
                                                  SQLParam ("valdate", m_valdate),   SQLParam ("rest", m_rest), SQLParam ("oper", m_oper)));
                   rec_success(rec_success.size()) = TRecord(int(m_code), m_acc, m_rest, m_oper);
               else
                   rec_err(rec_err.size()) = TRecord(int(m_code), m_acc, m_rest, m_oper, "Счета " + m_acc + " в Рс-Банке не существует");
               end;
               
           else
               rec_err(rec_err.size()) = TRecord(int(m_code), m_acc, m_rest, m_oper, "Данные ранее загружались");
           end;
       else
           rec_err(rec_err.size()) = TRecord(int(m_code), m_acc, m_rest, m_oper, "Недостаточно данных");
       end;
   onerror (err)
        MsgBox(Err.Message,"| at ",Err.Line,"|in ",Err.Module); 
   end;
end;

macro XL_ReadCell ( _Row, _Col, _RSLDataType)
        if ( ValType ( _RSLDataType ) == V_UNDEF)
            return XL_Sheet.cells (_Row, _Col).value;
        elif ( _RSLDataType  == V_INTEGER)
            return int (XL_Sheet.cells (_Row, _Col).text);
        elif (  _RSLDataType == V_DOUBLE)
            return double (XL_Sheet.cells (_Row, _Col).text);
        elif (  _RSLDataType == V_MONEY)
            return money (XL_Sheet.cells (_Row, _Col).value);
        elif (  _RSLDataType == V_STRING)
            return string (XL_Sheet.cells (_Row, _Col).value);
        else
            return trim (XL_Sheet.cells (_Row, _Col).text);
        end;
    
OnError(Err)
    MsgBox(Err.Message,"| at ",Err.Line,"|in ",Err.Module); 
end;

var XL = CreateObject ("rsax", "TRsAxServer", "RsBankAxServer", false).CreateComObject ("Excel.Application",true);
//XL.application.defaultfilepath = "//rsapp9/d$/TEST_PRBB_2030/mac.usr/DebugMacro/Gurin/";
XL_WorkBook_Path = xl.application.GetOpenFileName ("Все файлы (*.xls),*.*", null, "--"); 
XL_Sheet  = XL.Workbooks.Open (XL_WorkBook_Path).ActiveSheet (); 
var i = 3;
var data:string = " ";
var XLData = TXLData ();

while (1 == 1)

  data = XL_ReadCell (i, 1, V_INTEGER); 

  if ((data == 0) or (data == V_UNDEF))
     if (XL_ReadCell (i+1, 1, V_INTEGER) == 0)
       break;
     else
       i = i + 1;
       continue;
     end;
  end; 

  XLData.m_code = XL_ReadCell (i, 2, V_STRING);
  XLData.m_rest = XL_ReadCell (i, 4, V_MONEY);
  XLData.m_acc  = XL_ReadCell (i, 5, V_STRING); 

  XLData.insert ();

  i = i + 1;
end;

//msgbox("Все");
XL.Quit ();

viewProtocol();

OnError
    msgbox("Файл не выбран. Процедура импорта прервана");
