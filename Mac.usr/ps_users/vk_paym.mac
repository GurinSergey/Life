import "globals", RsbDataSet, Rsd, "cb_sql.mac", FIInter, BankInter, "spserv.mac", "sprepfun.mac", Календарь;

var startdate, enddate, node;
var dp = TRecHandler("dp_dep");
record dl (vk_paym, "..\\mac.usr\\lbr\\vk.lbr") dialog;

/* Печать заголовка отчета */
macro Заголовок()
var Head = "┌──────────┬──────────┬────────────┬──────────────────────┬──────────────────────┬───────────────┬──────┬──────────────────────────────────────────────────┐\n"+
           "│ID платежа│   Дата   │    Номер   │         Дебет        │        Кредит        │     Сумма     │Валюта│                   Назначение платежа             │\n"+
           "├──────────┼──────────┼────────────┼──────────────────────┼──────────────────────┼───────────────┼──────┼──────────────────────────────────────────────────┤\n";

return Head;
end;


/* Получить данные для отчета */
macro GetData()

var SQL, cmd, rs;

SQL = "INSERT INTO usr_vkpaym_info SELECT * FROM usr_vkpaym WHERE  pmdate >= ? AND pmdate <= ?";

cmd = RsdCommand(SQL);
cmd.AddParam("startdate", RSDBP_IN, startdate);
cmd.AddParam("enddate", RSDBP_IN, enddate);
cmd.Execute();

SQL = "SELECT * FROM usr_vkpaym_info WHERE node = ? ORDER BY id ASC";

cmd = RsdCommand(SQL);
cmd.AddParam("node", RSDBP_IN, node);
cmd.Execute();

rs = RsdRecordSet(cmd);

return rs;

end;


/* Напечатать отчет */
macro PrintRep()

var Header, Rep;
var cmd, rs;

var i = 0;

rs = GetData();

Header = Заголовок();
Rep = CMakeReport(Header);

Rep.AddPrintCell("Платежи на Валютном контроле в подразделении \"" + dl.nodename + "\"  за период с " + dl.startdate + " по " + dl.enddate, 155, 0, "l:ex_FS(b)", Rep_Elem_Str);
Rep.AddEmptyStr();
Rep.AddEmptyStr();

  while(rs.MoveNext)
    i = i + 1;
    Rep.AddPrintCell(int(rs.Value("id")), 10, 0, "l");
    Rep.AddPrintCell(date(rs.Value("pmdate")), 10, 0, "c");
    Rep.AddPrintCell(string(rs.Value("num")), 12, 0, "r");
    Rep.AddPrintCell(string(rs.Value("debet")), 22, 0, "c");
    Rep.AddPrintCell(string(rs.Value("credit")), 22, 0, "c");
    Rep.AddPrintCell(string(rs.Value("sum")), 15, 0, "r");
    Rep.AddPrintCell(string(rs.Value("cur")), 6, 0, "c");
    Rep.AddPrintCell(string(rs.Value("ground")), 50, 0, "l");
    Rep.AddStr();
  end;

cmd = RsdCommand("truncate table usr_vkpaym_info");
cmd.Execute();

if (i > 0)
  Rep.PrintWinRep("Платежи на шаге ВК");
  Rep.ShowWinRep();
else
  MsgBox("Нет данных для отчета");
end;

end;

/* ---------------------------------------------------------------------- */
macro ProcessDialog(dlg, cmd, id, key)

var pr = TBFile("party");

  if(cmd == DLG_KEY)

     if(key == 317) /* F3 */
       if  (id == 0) /* Дата начала периода */
         dlg.startdate = GetDateByCalendar({curdate});
       elif  (id == 1) /* Дата окончания периода */
         dlg.enddate = GetDateByCalendar({curdate});
       elif  (id == 2) /* Узел ТС */
         ListDepartment(dp);
         dlg.node = dp.rec.code;
         pr.KeyNum = 0;
         pr.rec.partyid = dp.rec.partyid;
         pr.GetEQ();
         dlg.nodename = dp.rec.name + " - " + pr.rec.name;
         UpdateFields(dlg);
       end;
     elif  (key == 316) /* F2 */
       return CM_SAVE;
     elif(key == 27)  /* ESC */
       exit(1);
     end;

  end;

end;

/* ---------------------------------------------------------------------- */
/*  ОСНОВНАЯ ЧАСТЬ                                                        */
/* ---------------------------------------------------------------------- */

/* Запросить дату у пользователя */
macro ИсходнаяИнформация();

var pr = TBFile("party",0,"R");
var dp = TBFILE("dp_dep",0,"R");

  dl.startdate = {curdate};
  dl.enddate = {curdate};
  dl.node = {OperDprtNode};

  dp.rec.code = {OperDprtNode};
  dp.GetEQ();
  pr.rec.partyid = dp.rec.partyid;
  pr.GetEQ();

  dl.nodename = dp.rec.name + " - " + pr.rec.name;

  RunDialog(dl, @ProcessDialog);

  startdate = dl.startdate;
  enddate = dl.enddate;
  node = dl.node;

end;


/* Запрашиваем исходые данные */
ИсходнаяИнформация();

/* Печатаем отчет */
PrintRep();

/* Завершаем работу */
exit(1);

