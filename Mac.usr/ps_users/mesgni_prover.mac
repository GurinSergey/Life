/*
Help Desk IT № A41577
Для проверки полноты отправки в ИФНС сообщений об открытии/закрытии счетов юр.лиц необходим отчет "Журнал проверки".
(c) Diver
*/

import ptinter,rsd,rsbdataset,  bankinter , FIinter;
import globals;
import "lib_lang.mac";

private const  KEY_F2      =316;
private const  KEY_F3      =317;
private const KEY_ESC      = 27;
private const KEY_SPACE    = 32;
private const KEY_ENTER    = 13;


private var Genmesl, outl, outputl="Diver.lbr";                    

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,outl);
Genmesl = FindPath(outputl, outl);
	if (not Genmesl)
		msgbox("Не найдена LBR");
		exit();
	end;

private var dlg = TRecHandler("O_ZhOtpr", Genmesl, TRUE); 
record dpdep(dp_dep);
file party (party);

var ДатаС, ДатаПо, ВСП, ВСП_название;
var TxtAccMask, eCode;
GetRegistryValue("PS\\REQOPENACC\\СЧЕТА КЛИЕНТОВ", V_STRING, TxtAccMask, eCode);
If (eCode > 0)
 TxtAccMask = "Ошибка при считывании параметра";
 msgbox(TxtAccMask);
End;


/*Найдем имя по Partyid*/ 
private MACRO GetClientName(id)
var  sl=" select part.t_name from dparty_dbt part where part.t_partyid="+id;
var  DataS=TRsbDataSet(sl);

  if( DataS.moveNext())
    return DataS.name;
  else
    if (id !=0)
       msgbox("Субъект не найден в party.dbt");
       return 0;
    else
      return "ОАО АКБ Пробизнесбанк";
    end;
  end;

END;


MACRO Шапочка;
   [                                                                  ЖУРНАЛ
                                                          проверки открытых/закрытых счетов
                                                             с ########## по ##########
    Подразделение: #
    
    ┌────┬──────────┬──────────┬────────────────────┬────────────────────────────────────────────┬────────────────────┬──────────┬──────────────────────────────────────────────┬──────────┬────┬───────────┐
    │    │   Дата   │   Дата   │                    │                                            │       Номер        │  Дата    │                                              │  Дата    │ N  │Примечание │
    │ N  │ открытия │ закрытия │  N лицевого счета  │           Наименование клиента             │     сообщения      │сообщения │    Наименование электронного сообщения       │электрон. │сооб│           │
    │п/п │   счета  │   счета  │                    │                                            │      в ИФНС        │ в ИФНС   │                                              │сообщения │ по │           │
    │    │          │          │                    │                                            │                    │          │                                              │ в ИФНС   │сч-у│           │
    ├────┼──────────┼──────────┼────────────────────┼────────────────────────────────────────────┼────────────────────┼──────────┼──────────────────────────────────────────────┼──────────┼────┼───────────┤]
    (ДатаС, ДатаПо, Ternary(ВСП == 1, "Все", ВСП_название));

END;

MACRO Данные(i,do,dc,acc,AccountName,ref,dmes,ns,ds,nta);
   [│####│##########│##########│####################│############################################│####################│##########│##############################################│##########│##  │ ######### │]
    ( i,
      do:10,
      dc:10,
      acc:20,
      SubStr(AccountName,1,44):w,
      ref:r,
      dmes:10,
      Ternary(ValType(ns) == 26, "", ns:w),
      ds:r,
      nta,
      ""
    );
END;

MACRO ЗакрытьТаблицу;
   [└────┴──────────┴──────────┴────────────────────┴────────────────────────────────────────────┴────────────────────┴──────────┴──────────────────────────────────────────────┴──────────┴────┴───────────┘];
END;

MACRO ЩаОтчетикВыведем
 Шапочка;
 var do,dc,dmes,ds;
 var cmd, rs;
 var i = 0;
 //Мегаселектег
 var select = "" + 
  " SELECT acc.T_ACCOUNT, acc.T_NAMEACCOUNT, acc.T_CLIENT, acc.T_OPEN_DATE, acc.T_CLOSE_DATE, " +
  "      wlval.t_value, mes.t_mesid, mes.t_sessionid, mes.t_outsideabonentdate, mes.t_outsideabonenttime, mes.t_bankdate, " +
  "      mes.t_trn, wlses.t_filename \n" +
  " FROM DACCOUNTS_VIEW ACC, DREQOPENA_DBT REQOP, doproper_dbt oprop, doprdocs_dbt oprdoc, dwlmes_dbt mes, " +
  "      DWLMESFLD_DBT WLMESFLD, DWLTPFLD_DBT  WLTPFLD, DWLMESVAL_DBT wlval, -- три таблички что бы достать всего одно поле \n" +
  "      dwlsess_dbt wlses \n" +
  " WHERE ACC.T_ACCOUNT = REQOP.T_ACCOUNT AND ACC.T_CODE_CURRENCY  = REQOP.T_CODE_CURRENCY " +
  "  AND acc.T_CHAPTER = 1 " +
  "  AND oprop.t_dockind = 230 -- Вид документа: Заявл на откр. счета \n" +
  "  AND oprop.t_documentid = lpad(reqop.t_requestid,34,'0') " +
  "  AND oprop.t_id_operation = oprdoc.t_id_operation " +
  "  AND oprdoc.t_dockind = 370 -- Вид документа: Сообщение в МНС \n" +
  "  AND to_number(oprdoc.t_documentid) = mes.t_mesid " +
  "  AND mes.t_mesid  = wlval.t_mesid (+) AND WLTPFLD.T_NAME = 'НомСооб' \n" +
  "  AND (wlval.T_FIELDID = WLMESFLD.T_FIELDID(+) AND wlval.T_TPFIELDID = WLTPFLD.T_TPFIELDID(+)) " +
  "  AND mes.t_sessionid = wlses.t_sessionid (+) \n" +
  "  AND REQOP.T_DATE >= TO_DATE('" + ДатаС +"','DD.MM.YYYY') AND REQOP.T_DATE <= TO_DATE('" + ДатаПо + "','DD.MM.YYYY')";
//AND acc.T_ACCOUNT = '40702840200000082745'
  If (ВСП != 1)
   select = select + " AND acc.T_BRANCH = " + ВСП;
  End;
  select = select + " \n" + 
  " UNION ALL \n" +

  " SELECT acc.T_ACCOUNT, acc.T_NAMEACCOUNT, acc.T_CLIENT, acc.T_OPEN_DATE, acc.T_CLOSE_DATE, " +
  "      wlval.t_value, mes.t_mesid, mes.t_sessionid, mes.t_outsideabonentdate, mes.t_outsideabonenttime, mes.t_bankdate, " +
  "      mes.t_trn, wlses.t_filename \n" +
  " FROM DACCOUNTS_VIEW ACC, DREQCLOSA_DBT REQCL, doproper_dbt oprop, doprdocs_dbt oprdoc, dwlmes_dbt mes, " +
  "      DWLMESFLD_DBT WLMESFLD, DWLTPFLD_DBT  WLTPFLD, DWLMESVAL_DBT wlval, -- три таблички что бы достать всего одно поле \n" +
  "      dwlsess_dbt wlses \n" +
  " WHERE ACC.T_ACCOUNT  = REQCL.T_ACCOUNT AND ACC.T_CODE_CURRENCY  = REQCL.T_CODE_CURRENCY  " +
  "   AND acc.T_CHAPTER = 1 " +
  "   AND oprop.t_dockind = 231 -- Вид документа: Заявл на закр. счета \n" +
  "   AND oprop.t_documentid = lpad(reqcl.t_requestid,34,'0') " +
  "   AND oprop.t_id_operation = oprdoc.t_id_operation " +
  "   AND oprdoc.t_dockind = 370 -- Вид документа: Сообщение в МНС \n" +
  "   AND to_number(oprdoc.t_documentid) = mes.t_mesid " +
  "   AND mes.t_mesid  = wlval.t_mesid AND WLTPFLD.T_NAME = 'НомСооб' " +
  "   AND (wlval.T_FIELDID = WLMESFLD.T_FIELDID(+) AND wlval.T_TPFIELDID = WLTPFLD.T_TPFIELDID(+)) " +
  "   AND mes.t_sessionid = wlses.t_sessionid (+) \n" +
  "   AND REQCL.T_DATE >= TO_DATE('" + ДатаС +"','DD.MM.YYYY') AND REQCL.T_DATE <= TO_DATE('" + ДатаПо + "','DD.MM.YYYY')";
//AND acc.T_ACCOUNT = '40702840200000082745'
  If (ВСП != 1)
   select = select + " AND acc.T_BRANCH = " + ВСП;
  End;
  select = select + " \n" + 
  " ORDER BY t_ACCOUNT, t_mesid ";
//  println(select);
  cmd = RSDCommand( select );
  rs = RsdRecordset( cmd );
debugbreak;
  initprogress(rs.RecCount,""," Вывод данных... ");
  while( rs.moveNext() )
   useprogress(i = i + 1);
   If (CompareStrWithMasks(TxtAccMask, rs.value("T_ACCOUNT")) == 0)
     if (rs.value("T_OPEN_DATE") == " 1.01.0001 (00:00:00.00)")
       do = "";
      else
       do = DATE(rs.value("T_OPEN_DATE"));
     end;
     if (rs.value("T_CLOSE_DATE")== " 1.01.0001 (00:00:00.00)")
       dc = "";
      else
       dc = DATE(rs.value("T_CLOSE_DATE"));
     end;
     if (rs.value("T_BANKDATE") == " 1.01.0001 (00:00:00.00)")
       dmes = "";
      else
       dmes = DATE(rs.value("T_BANKDATE")); 
     end;
     if (rs.value("t_outsideabonentdate") == " 1.01.0001 (00:00:00.00)")
       ds = "";
      else
       ds = DATE(rs.value("t_outsideabonentdate"));
     end;
     Данные(i,
            do,
            dc,
            rs.value("T_ACCOUNT"),
            rs.value("T_NAMEACCOUNT"),
            rs.value("T_VALUE"), //rs.value("T_TRN")
            dmes,
            Ternary(Index(rs.value("T_FILENAME"),"SBC") == 0,rs.value("T_FILENAME"),SubStr(rs.value("T_FILENAME"),Index(rs.value("T_FILENAME"),"SBC"),StrLen(SubStr(rs.value("T_FILENAME"),Index(rs.value("T_FILENAME"),"SBC")))-4)),
            ds,
            "?");
   End;
  end;
  remprogress;

  ЗакрытьТаблицу;

END;


MACRO ОпределитьВСПОпера(oper, vid);
var select, cmd, rs;  
var rez = "";

    select = "select dp.t_code, opr.t_oper, dp.t_name as t_kod, p.t_name from dperson_dbt opr, ddp_dep_dbt dp, dparty_dbt p " + 
             " where opr.t_codedepart = dp.t_code and p.t_partyid = dp.t_partyid " + 
             " and opr.t_oper = " + oper;
    cmd = RSDCommand(select);
    rs = rsdRecordSet(cmd);
    If (rs and rs.MoveNext())
        If (STRLWR(vid) == "name")
          rez = rs.value("t_name");
          elif(STRLWR(vid) == "kod")
          rez = rs.value("t_kod");
          else
          rez = rs.value("t_code");
        End;
    End;

    return rez;

END;


MACRO EventDlg (dlg, cmd, id, key) 
   var code="";
   var const_mess = "~F2~ Продолжить ~ESC~ Выход ";

   /*Первоначальная инициализация полей*/
   if(cmd == DLG_INIT)
      dlg.rec.filial  = "000";
      dlg.rec.filial_n = "ОАО АКБ \"ПРОБИЗНЕСБАНК\"";
/*
      dlg.rec.VSP = ОпределитьВСПОпера({oper},"kod");
      dlg.rec.VSP_n = ОпределитьВСПОпера({oper},"name");
      ВСП = ОпределитьВСПОпера({oper},"id");
*/
      dlg.rec.VSP =  "000";
      dlg.rec.VSP_n =  "ОАО АКБ \"ПРОБИЗНЕСБАНК\"";
      ВСП = 1;
      dlg.rec.data_s = {CurDate};
      dlg.rec.data_po = {CurDate};
      dlg.rec.znach_nastr = TxtAccMask;
      UpdateFields(dlg);
   end;
   
   /*Установка подсказок в строке состояния*/
   if (cmd==DLG_SETFOCUS)

   end;
   
   if (cmd == DLG_REMFOCUS)
     /*Проверка корректности */
//      UpdateFields(dlg); 
   end;

   if (cmd == DLG_KEY)
     /*Выход из диалогового окна*/
     if (KEY == KEY_ESC)
       return exit(1);//CM_CANCEL;
     /*Выбор данных из списка*/
     elif ( KEY == KEY_F3)
        if (FldName(dlg,id) == "VSP")
          if (ListDepartment (dpdep))
           dlg.rec.VSP = dpdep.name;
           dlg.rec.VSP_n = GetClientName(dpdep.partyid);
           ВСП = dpdep.code;
           UpdateFields(dlg);
          end;
        end;
     elif (KEY == KEY_SPACE)

     elif (( KEY == KEY_F2 )  /*or ( KEY == KEY_ENTER )*/)         //Проверки при вводе
           ДатаС = dlg.rec.data_s;
           ДатаПо = dlg.rec.data_po;
           ВСП_название = dlg.rec.VSP_n;
           If (ДатаС > ДатаПо) 
               msgbox("Начальная дата меньше конечной.");
               return CM_IGNORE;
           End;
           Return CM_SAVE;
     end;
   end;
        
END;



If (RunDialog(dlg, "EventDlg"))
  ЩаОтчетикВыведем;
End;

