/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6.0                     */
/****************************************************************************/
/*                         Подсистема "Финансовый мониторинг"               */
/*  Функционал проверки соответствия платежа критериям для отбора на шаг    */
/*  "Комплаенс-контроль"                                                    */
/*                                                                          */
/*  Имя файла:  Compl_contr_rule.mac                                        */
/*  Создан   :  01.11.2014                                   Чесноков Д.С.  */
/*  Заявка   :  C-33929                                                     */
/*  Описание :  Функционал вызывается из макроса psprepro.mac в проверке на */
/*              условия попадания на шаг "Комплаенс-контроля"               */
/*  Изменения:  Gurin S. 25.06.2015 C-40459 Добавлено поле                  */
/*                                USR_COMPILAENS_RULE_DBT.T_RECEIVERINN     */
/****************************************************************************/
import PaymInter, CTInter, FIInter, "lib_compare", globals, op_lib;

macro CheckRule(Paym)
  var Client_risk = 0, rs, str, amount_curr, sql, parm;
  var attr    :TRecHandler = TRecHandler("objattr.dbt" , "bank.def");
  var riskCat = RsbObjcategories(3, RSL_LPAD(paym.Payer, 10, "0"));

  if (riskCat.GetMainAttr(300, {curdate}, attr) == 0)
    Client_risk = attr.rec.numinlist;
  else
    Client_risk = 0;
  end;

  if (Paym.payerFiid == 0)
    amount_curr = Paym.PayerAmount;
  else
    ConvSum(amount_curr, Paym.PayerAmount, {curdate},  Paym.payerFiid);
  end;

  str = "select * from usr_compilaens_rule_dbt";
  rs = RsdRecordset(str);
  while(rs and rs.movenext)
    sql = " select 1 from dual where :m_Client_risk " + rs.value("t_cond_level") + "  :m_level " + 
          "                      and :m_amount_curr " + rs.value("t_cond_sum")   + "  :m_sum " + 
          "                      and RSI_Rsb_Mask.compareStringWithMask(:m_recinn,    :m_paymRecInn) = 1" +
          "                      and RSI_Rsb_Mask.compareStringWithMask(:m_ground,    :m_paymGround) = 1";

    parm = MakeArray(SqlParam(":m_Client_risk", Client_risk), SqlParam(":m_level",  rs.value("t_level")),
                     SqlParam(":m_amount_curr", amount_curr), SqlParam(":m_sum",    rs.value("t_sum")), 
                     SqlParam(":m_recinn", rs.value("t_receiverinn")), SqlParam(":m_paymRecInn", paym.ReceiverINN),
                     SqlParam(":m_ground", rs.value("t_ground")), SqlParam(":m_paymground", paym.Ground)
                    );
    sql = execSqlSelect (sql, parm, true);
    if (sql.MoveNext)
      return true;
    end;
  end;

  return false;
end;