// -------------------------------------------------------------------------------------------------
// @filename: closeFrontDocs.mac
// @author  : Гурин С.
// @desc    : Процедура закрытия документов без проводок
// @request : R-620685-2 
// @modify  : none
// -------------------------------------------------------------------------------------------------
import rcw, rsexts, treport;
import globals, oralib, likePy, lib_account, lib_pm_check, lib_fg;
var XL_WorkBook_Path;
var XL_Sheet;

var rec_success = TArray ();
var rec_err     = TArray ();

private class TRecord (_paymentid, _error)
    var m_paymentid = _paymentid;
    var m_error = _error;
end;

private macro viewProtocol ()
    var table_success = CTableReport ();
    var table_err = CTableReport ();
    var i;
   [##########################################################################################] ("Протокол работы процедуры":c:40);
   [                                                                       Пользователь: #####] ({oper});
   [                                                       Дата: ########### Время: ##########] (date (), time ());
   [                                                                                          ];

    i = 0;
    if (rec_success.size ())
        table_success.addColumn ("ID документа",         10,  AL_LEFT); 
        table_success.printHead (string ("Закрыты документы":c:50));

        while (i < rec_success.size ())
            table_success.printStringTransferByWord (rec_success[i].m_paymentid); 
            i = i + 1;
        end;

        table_success.printBottom ();
        println("ИТОГО: ", i);
    else
        println("Не было закрыто ни одного документа");
    end;
   
    i = 0;
    if (rec_err.size ())
        table_err.addColumn ("ID документа",             10,  AL_LEFT); 
        table_err.addColumn ("Описание",                 65,  AL_CENTER);
        table_err.printHead (string ("Не закрыты документы":c:50));

        while (i < rec_err.size ())
            table_err.printStringTransferByWord (rec_err[i].m_paymentid, 
                                                 rec_err[i].m_error); 
            i = i + 1;                                                                       
        end;

        table_err.printBottom ();
        println("ИТОГО: ", i);
    else
        println("Ошибок при закрытии документов нет");
    end;
    
end;

class TXLData ()
   var m_paymid;

   macro checkDocState()
      var sql = execSqlSelect ("select 1 from dpmpaym_dbt where t_paymentid = :id and t_paymstatus != 32000", makeArray(SQLParam("", m_paymid)));
      return (sql and sql.movenext());
   end;

   macro checkExistsDoc()
      var sql = execSqlSelect ("select 1 from dpmpaym_dbt where t_paymentid = :id", makeArray(SQLParam("", m_paymid)));
      return (sql and sql.movenext());
   end;

   macro checkDocStep()
      return (PM_IsStepExist(m_paymid, 11000120, 20, "R"));
   end;

       
   macro insert ()
       if(checkExistsDoc())
          if (checkDocState())
             if(checkDocStep())
                var sql = "insert into usr_paymid_frontclose values (:id)";
                sql = execSQL (sql, makeArray (SQLParam ("id", m_paymid)));
             else
                rec_err(rec_err.size()) = TRecord(m_paymid, "На момент выполнения процедуры этот документ находится НЕ на шаге \"Зачисление (инициатор-Фронт)\"");
             end;
          else
             rec_err(rec_err.size()) = TRecord(m_paymid, "На момент выполнения процедуры этот документ уже закрыт");
          end;
       else
          rec_err(rec_err.size()) = TRecord(m_paymid, "На момент выполнения процедуры этого документа в базе нет");
       end;
   onerror (err)
        MsgBox(Err.Message,"| at ",Err.Line,"|in ",Err.Module); 
   end;
end;

macro XL_ReadCell ( _Row, _Col, _RSLDataType)
        if ( ValType ( _RSLDataType ) == V_UNDEF)
            return XL_Sheet.cells (_Row, _Col).value;
        elif ( _RSLDataType  == V_INTEGER)
            return int (XL_Sheet.cells (_Row, _Col).text);
        elif (  _RSLDataType == V_DOUBLE)
            return double (XL_Sheet.cells (_Row, _Col).text);
        elif (  _RSLDataType == V_MONEY)
            return money (XL_Sheet.cells (_Row, _Col).value);
        elif (  _RSLDataType == V_STRING)
            return string (XL_Sheet.cells (_Row, _Col).value);
        else
            return trim (XL_Sheet.cells (_Row, _Col).text);
        end;
    
OnError(Err)
    MsgBox(Err.Message,"| at ",Err.Line,"|in ",Err.Module); 
end;

macro checkFormatFile(id)
   var data:string = " ";
   var i = 1;
   while (1 == 1)
      data = XL_ReadCell (1, i,  V_STRING);
      if (data ==  "Undefined")
         if (XL_ReadCell (1, i+1, V_STRING) == "Undefined")
            return False;
         else
            i = i + 1;
            continue;
         end ;
      end; 

      if (data == "ID платежа")
         SetParm(0,i);
         return True;
      else
         i = i + 1;
      end;
   end;
   return False;
end;

if (not _bank.is_PRBB ())
    msgbox ("Функционал реализован только для ПРББ банка");
    return 0;
end;

execSql("truncate table usr_paymid_frontclose");

var XL = CreateObject ("rsax", "TRsAxServer", "RsBankAxServer", false).CreateComObject ("Excel.Application",true);
XL_WorkBook_Path = xl.application.GetOpenFileName ("Все файлы (*.xls),*.*", null, "--"); 
XL_Sheet  = XL.Workbooks.Open (XL_WorkBook_Path).ActiveSheet (); 
var id;
var i = 2, n = 0;;
var data:string = " ";

var XLData = TXLData ();

if (checkFormatFile(id))
   InitProgress(-1," ~CtrlBreak~ Прервать", "Загружаются данные с файла ..");
   while (1 == 1)
      UseProgress (i);
      data = XL_ReadCell (i, id, V_INTEGER); 

      if ((data == 0) or (data == V_UNDEF))
         debugbreak;
         if (XL_ReadCell (i+1, id, V_INTEGER) == 0)
            break;
         else
            i = i + 1;
            continue;
         end ;
      end; 

      XLData.m_paymid = data;

      XLData.insert ();

      i = i + 1;
   end;
   RemProgress (i);
else
   msgbox("Неверный формат файла");
   return 0;
end;

XL.Quit (); 

//Закрываем документы без проводки
var sql = execSqlSelect("SELECT   usr.t_paymentid id, D.T_DOCKIND dockind, rownum n"+
                        "  FROM   usr_paymid_frontclose usr, dpmpaym_dbt d "+
                        " WHERE   D.T_PAYMENTID = USR.T_PAYMENTID");
InitProgress(-1," ~CtrlBreak~ Прервать", "Закрываются документы .. ");
while (sql and sql.movenext())
    n = int(sql.value("n"));
    SetDialogFlag(0);
    if (PM_ExecuteOperation(sql.value("id"), sql.value("dockind")))
       rec_success(rec_success.size()) = TRecord(sql.value("id"), "ok");
    else
       rec_err(rec_err.size()) = TRecord(sql.value("id"), getErrMsg ()); 
    end;
    SetDialogFlag(1);
    UseProgress (n);
end;
RemProgress (n);

viewProtocol();

OnError
    msgbox("Файл не выбран. Процедура импорта прервана");
