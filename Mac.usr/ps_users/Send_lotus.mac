//--------------------------------------------------------------------------
// @filename: Send_Lotus.mac
// @desc    : Отправка сообщений в лотус
// @author  : 2010-12-01 I-092662 Тихомиров А.Н.
// @changes : 2013-10-02 C-23747 Гурин С.(добавил функцию SendLetterSogl)
//--------------------------------------------------------------------------
import RSD, PaymInter, BankInter, CTInter, globals, Календарь;
import "frontcommon.mac", "likepy.mac", lib_const;

private var oConn_lib = ActiveX("ADODB.Connection");

macro Send_l(Theme, Text, ID, mail)
    var oConn = oConn_lib, connstr, rsd_conn, rsd_proc;
    array aInpM, aOutM;

    /* Инициализация параметров */
    aInpM(0) = 1;
    //aInput(1) = ;
    aInpM(2) = ID; /*ID из фронта*/
    //aInput(3) = "";
    //aInput(4) = "";
    //aInput(5) = "";
    aInpM(6) = 0;
    aInpM(7) = {curdate};
    aInpM(8) = theme;
    aInpM(9) = text;
    aInpM(10) = mail;
    asize(aOutM,0);
   
    rsd_conn = rsdrecordset(rsdcommand("select connstring from usr_route_parm where rule_id = 4"));
    if (rsd_conn.movenext())
        connstr = rsd_conn.value(0);
    else
        msgbox("Строка соединения не найдена");
        return 0;
    end;
    conn2front(oConn, connstr);

    rsd_proc = RSADORecordset(ExecuteFrontProcEx("iud_f_notice", aInpM, oConn));

    /* Если осталось открытое соединение, закрываем */
    if (oConn and (oConn.State == 1) )
        oConn.Close();
    end;
   return 0;

   //LAO добавил обработку ошибок, даже если отправка письма не прошла, она не должна влиять на исход работы процедур.
onerror(x)
    if (oConn and (oConn.State == 1) )
        oConn.Close();
    end;
    return 0;
END;

private macro SplitAddresat(addresat, except)
    var a = split(addresat, ",");
    var str = "", i = 0;
    while(i < a.size)
        if((trim(a[i])!= except) and (trim(a[i])!=""))
            str = str + a[i]+ ",";
        end;
        i = i + 1;
    end;
    return "\n\nОТВЕТ НА ПИСЬМО ПРОСИМ ОТПРАВИТЬ НА ОБЩИЙ АДРЕС : " + trim(substr(str,1,strlen(str)-1));
end;

macro SendLetterSogl(_id, _addresat):integer
    var pm = RSBPayment (_id);
    var letter, theme, addresat, f, i = 0, PS;
    letter  = string (
            "Добрый день! ", "\n", 
            "Необходимо согласование платежных поручений, принятых по системе Интернет-Клиент.", "\n", 
            "№ п/п               : ", pm.number          , "\n", 
            "Счёт плательщика    : ", pm.payerAccount    , "\n", 
            "Счёт получателя     : ", pm.receiverAccount , "\n", 
            "Сумма платежа       : ", pm.baseAmount      , "\n", 
            "Назначение платежа  : ", pm.ground
            );
    theme = String("Банк ",{NAME_BANK},". Согласование п/п по клиенту : '",pm.payerAccount,"' ,", pm.payerName);
    addresat = trim(string(ReadNoteForObject(OBJTYPE_ACCOUNT, "010000000" + pm.payerAccount, 155)));
    //addresat = strSubst (addresat, " ", "_");
    if (addresat == "") return RSL_EXIT_FAILURE; end;
    f = split(addresat, ",");
    while (i < f.size(f))
        if (trim(f(i)) != "")
            PS = "";
            PS = SplitAddresat(addresat, trim(f(i)));
            Send_l(theme, letter + PS, 73, trim(f(i)));
        end;
        i = i + 1;
    end;
    SetParm(1,addresat);
    return RSL_EXIT_SUCCESS;

onError()
    return RSL_EXIT_FAILURE;
end;


//Send_l("Уведомление в лотус", "Текст уведомления. все прекрасно", 73, "Aleksey S Tihomirov/IT/Probusiness Bank");
