// -------------------------------------------------------------------------------------------------
// @filename: navk.mac
// @author  : unknown
// @desc    : планирование шага валютного контроля
// @changes: 2012-02-22 TAM      I-00156390, I-00156412 - изменила шаблоны паспортов и маски счетов
//           2012-11-01 TAM      C-14674
//           2013-11-31 TAM      C-15761
//           2013-03-19 Gurin S. I-00342700-2
//           2013-08-27 zip_z.   C-19006 epic change под ЕИ
// KS 22.11.2013 Предварительная адаптация под 31ю сборку
// -------------------------------------------------------------------------------------------------
import globals, vk_router, lib_compare, lib_const;

const VK_STEP_EXECUTE = 1; // идёт на ВК
const VK_STEP_SKIP    = 0; // не идёт на ВК

// Dex заявка I-077919
// Автоматически ставятся категории 3 (Перечень видов услуг для 402ф) и 119(Код страны нерезидента-получателя)
// TODO: zip_z. - Это побочный функционал. Вынести на предобработку!
macro setPaymentCategoryForVK (PmObj)
    // получить страну для нерезидента
    macro getCountryForForeign(acc:string):string
        var ret = "";
        var sql = "select t_nrcountry from dparty_dbt where t_partyid = (select t_client from daccount_dbt where t_account= :acc )";
        sql = execSQLselect (sql, makeArray (SqlParam ("acc", acc)));
        if (sql.moveNext ())
            ret = sql.value ("t_nrcountry", null, V_STRING);
        end;
        return ret;
    end;
    // получить attrid по известному codelist для заполнения 119-й категории платежа
    macro getPMAttributeByCodeList (CC:string):integer
        var ret = -1;
        var sql = "select t_attrid from dobjattr_dbt where t_groupID = 119 and t_objecttype = 501 and t_codelist = :codelist";
        sql = execSQLselect (sql, makeArray (SqlParam ("codelist", cc )));
        if (sql.moveNext ())
            ret = sql.value ("t_attrid", null, V_INTEGER);
        end;
        return ret;
    end;
    
    if (stringEqMask (PmObj.PayerAccount   , "401??810*,402??810*,403??810*,404??810*,405??810*,406??810*,407??810*,40802810*") and stringEqMask (PmObj.ReceiverAccount, "30114*" ))
        PmObj.categories.ConnectAttr (3, 27, null,null,{curdate});
        PmObj.categories.Save();
        PmObj.categories.ConnectAttr (PM_USR_CAT_COUNTRY_RECV, getPMAttributeByCodeList ( getCountryForForeign (PmObj.ReceiverAccount)), null, null, {curdate});
        PmObj.categories.Save();
    end;
onerror
end;

// @desc  : пойдёт ли платёж на ВК?
// @return: V_INTEGER (VK_STEP_*)
macro naVK ( PmObj:object ):integer
   setPaymentCategoryForVK (PmObj); 
   return iif (VK_RouteParm (PmObj).isVkPayment () == true, VK_STEP_EXECUTE, VK_STEP_SKIP);
onerror return VK_STEP_SKIP;
end;
