// -------------------------------------------------------------------------------------------------
// @filename: ps_reqclose_reason.mac v.1
// @author  : 2013-05-05 zip_z. 
// @desc    : C-19478 аналитика по закрытым счетам (ПРББ, ВУЗ, ГЭБ, ЭВ) - БИБЛИОТЕКА
// @changes : 2014-07-25 LVV R-417349 добавлена константа PSRQ_SUBKIND_FINMONORDER для приведения в соответствие с системным справочником
// -------------------------------------------------------------------------------------------------
import lib_types, lib_sqltools, ZUBRunScroll;

/*** Подвиды документов закрытия счета (системный справочник 1684) ***/
const PSRQ_SUBKIND_ANY         = -1; // любой вид
const PSRQ_SUBKIND_DECLARATION =  1; // заявление
const PSRQ_SUBKIND_ORDER       =  2; // распоряжение банка
const PSRQ_SUBKIND_JUDJEMENT   =  3; // решение суда
const PSRQ_SUBKIND_FINMONORDER =  4; // Распоряжение банка из-за подозрений в причастности к финансированию терроризма и легализации преступных доходов

/*** Виды скроллинга причин закрытия счета ***/
const PSRQ_SCROLL_READ_ONLY  = 1; // только чтение
const PSRQ_SCROLL_READ_WRITE = 2; // чтение-запись

const PSRQ_EMPTY_REASON = ""; // пустая причина закрытия счета

// @desc  : обработчик скроллинга
private var ret = PSRQ_EMPTY_REASON;
macro returnCurrentValue (cmd, rsd, id, key)
    ret = rsd.value("t_reason_name", null, V_STRING);
    return CM_SELECT;
end;

// @desc  : выводим скроллинг причин закрытия счёта. Разный в зависимости от режима и подвида заявления
// @return: V_STRING - строка с причиной (из скроллинга)
macro USR_GetCloseReason (p_mode:integer, p_kind:integer):string
    var s = ZUBScroll;
    
    if (p_mode == PSRQ_SCROLL_READ_ONLY)
        s.ScrollReadOnly = true;
        s.scrollPrompt = "~Esc~ Выход";
    else 
        s.ScrollReadOnly = false;
        s.scrollPrompt = "~F8~ Удалить запись; ~F9~ Добавить запись; ~Esc~ Выход";
    end;
    
    s.sqltext = "select t_reason_id, t_reason_name, t_oper, t_datetime from usr_ps_reqclose_reason " + 
                " where t_document_subkind = any ( :subkind_document, :subkind_any )";
    s.params = makeArray (p_kind, PSRQ_SUBKIND_ANY);
    
    var edit_mode = iif (s.ScrollReadOnly, ZUB_SCR_COL_NONEDITABLE, ZUB_SCR_COL_EDITABLE);
    s.columns.add ( "t_reason_id"   ,"ID"                , 5 , edit_mode );
    s.columns.add ( "t_reason_name" ,"Причина закрытия"  ,60 , edit_mode );
    s.columns.add ( "t_oper"        ,"Опер"              , 5 , edit_mode );
    s.columns.add ( "t_datetime"    ,"Время"             ,10 , edit_mode );
    
    s.ScrollHead = "Справочник причин закрытия счёта " + iif (s.ScrollReadOnly, " (только чтение)", " ");
    
    if (s.ScrollReadOnly) s.setMacroOnKeys (13 /*VK_ENTER*/, "returnCurrentValue"); end;
    
    s.scroll ();
    return ret;
end;