/************************************************************************/
/* Процедура формирования сообщений РВ2 о постановке и/п в Картотеку №2 */
/*                                                                      */
/* По заявке C-18264                                                    */
/* 07.08.2013 Chesnokov D.S. По заявке C-22478 добавлено логгироание    */
/*             созданных PB2 в протокол обработки.                      */
/************************************************************************/

import RsbFormsInter, BankInter, Rsd, rcw;
import "fns_gmPB.mac", "lib_const.mac", "KeyCodes.mac", "wlreq.mac", "lib_fg.mac";
import "globals.mac", "prreplib.mac", "wlmnstls.mac", "lib_access.mac";

private const	FT_INTEGER = 0;
private const	FT_LONG    = 1;
private const	FT_FLOAT   = 2;
private const	FT_DOUBLE  = 4;
private const	FT_DOUBLEM = 6;
private const	FT_STRING  = 7;
private const	FT_SNR     = 8;
private const	FT_DATE    = 9;
private const	FT_TIME    = 10;
private const	FT_CHR     = 12;
private const	FT_UCHR    = 13;
private const	FT_LDMON   = 14;
private const	FT_LDMONR  = 15;
private const	FT_DOUBLER = 16;
private const	FT_LDOUBLE = 17;
private const	FT_NUMSTR  = 18;

private const   RK_BY_DAY     = "За день";
private const   RK_BY_MONTH   = "За месяц";
private const   RK_BY_PERIOD  = "За период";

FILE f_rep_out() txt;
var f_rep_name = GetTXTFileName( "365P_RB2_K2_report" );
var extDir     = ""; 

var BankDate, OriginatorID;

macro PB2_summ( summ )

     return strSubst( string( money( summ )/100 ), ".", "-" ) + " " + "рублей";

end;

macro GetReqParam(reqid, BankDate, OriginatorID)
  var rs, str;
  
  str = " select t_bankdate, t_originatorid from dwlreq_dbt where t_reqid = " + reqid;
  
  rs = RsdrecordSet(str);
  if (rs and rs.MoveNext)
    SetParm(1, rs.value(0));
    SetParm(2, rs.value(1));
  else
    SetParm(1, date(0,0,0));
    SetParm(2, 0);
  end;

onerror  
  return false;
end;

// Сообщение PNO
class MnsMessageFormPNO_easy( m_reqid, m_mesid )

    var  reqid     = m_reqid;
    var  mesid     = m_mesid;
    var  bankDate  = date;
    var  Error     = TRUE;
    var  ИдФайл    = "", // Идентификатор файла
         НомПоруч  = "", // Номер Поручения
         ДатаПоруч = "", // Дата Поручения
         СумПоруч  = "", // Сумма
         ИНННП     = "", // ИНН плательщика
         Плательщ  = "", // Плательщик
         НомСчПл   = ""; // Сч. N="", плательщика 

    macro MnsMessageFormPNO_easy()
         
        var  rs, str;
        if( reqid == NULL ) 
            str = " SELECT     F.T_NAME, T.T_VALUE, M.T_MANDATORYFLAG, "
                  "            (SELECT T_BANKDATE FROM dwlmes_dbt WHERE t_mesid = T.T_MESID) bankdate "
                  "     FROM   DWLMESVAL_DBT T, DWLTPFLD_DBT F, DWLMESFLD_DBT M "
                  "    WHERE       T.T_MESID = " + mesid +
                  "            AND T.T_TPFIELDID = F.T_TPFIELDID "
                  "            AND M.T_FIELDID = T.T_FIELDID "
                  " ORDER BY   T_INDEX ";

        elif( reqid != NULL )
            str = "   SELECT   F.T_NAME, T.T_VALUE, M.T_MANDATORYFLAG, "
                  "            (select T_BANKDATE from dwlreq_dbt where t_reqid = L.T_OBJID ) bankdate "
                  "     FROM   DWLMESVAL_DBT T, "
                  "            DWLMESLNK_DBT L, "
                  "            DWLTPFLD_DBT F, "
                  "            DWLMESFLD_DBT M "
                  "    WHERE       L.T_OBJKIND = 505 "
                  "            AND L.T_OBJID = " + reqid +
                  "            AND T.T_MESID = L.T_MESID "
                  "            AND T.T_TPFIELDID = F.T_TPFIELDID "
                  "            AND M.T_FIELDID = T.T_FIELDID "
                  " ORDER BY   T_INDEX ";
        end;

        if( ( reqid != null ) or ( mesid != null ) )
            rs = RsdRecordset( str );
         
            while( rs and rs.movenext )
                 if( rs.value(0) == "ИдФайл"    ) ИдФайл    = rs.value(1); end;
                 if( rs.value(0) == "НомПоруч"  ) НомПоруч  = rs.value(1); end;
                 if( rs.value(0) == "ДатаПоруч" ) ДатаПоруч = rs.value(1); end;
                 if( rs.value(0) == "СумПоруч"  ) СумПоруч  = rs.value(1); end;
                 if( rs.value(0) == "ИНННП"     ) ИНННП     = rs.value(1); end;
                 if( rs.value(0) == "Плательщ"  ) Плательщ  = rs.value(1); end;
                 if( rs.value(0) == "НомСчПл"   ) НомСчПл   = rs.value(1); end;

                 bankDate = rs.value(3);
                 Error    = FALSE;
            end;
        end;    

        if( ( reqid == NULL ) and ( mesid != null ))
            str = " SELECT t_objid "
                  "   FROM dwlmeslnk_dbt "
                  "  WHERE t_objkind = 505 AND t_mesid = " + mesid;
                  
            rs = RsdRecordset( str );

            if (rs and rs.movenext)
                reqid = rs.value(0);  // 21.10.2013 Golovkin исправил mesid на reqid
            end;
        end;
        
    end;

    MnsMessageFormPNO_easy();

end;

macro altFindMes( paymentGround, paymentNumber )
    private var cmd, rs;

    cmd = RsdCommand( " SELECT   * "
                      "   FROM   DWLMESVAL_DBT T, DWLMESVAL_DBT T2 "
                      "  WHERE       T.T_VALUE = :GROUND "
                      "          AND T2.T_VALUE = :NUMDOC "
                      "          AND T2.t_mesid = T.t_mesid "
                      "          AND EXISTS "
                      "                (SELECT   * "
                      "                   FROM   DWLMESFLD_DBT fld "
                      "                  WHERE   fld.t_fieldid = t.t_fieldid "
                      "                          AND fld.t_description = 'Назначение платежа') "
                      "          AND EXISTS "
                      "                (SELECT   * "
                      "                   FROM   DWLMESFLD_DBT fld "
                      "                  WHERE   fld.t_fieldid = t2.t_fieldid "
                      "                          AND fld.t_description = 'Номер поручения') "
                      "          AND EXISTS "
                      "                (SELECT   tp.t_tpid, frm.t_formid, rls.t_rlsformid "
                      "                   FROM   DWLTRANSP_DBT tp, "
                      "                          DWLMESFRM_DBT frm, "
                      "                          DWLMESRLS_DBT rls, "
                      "                          dwlmes_dbt m "
                      "                  WHERE       tp.t_name = 'ФНС_365-П' "
                      "                          AND tp.t_tpid = frm.t_tpid "
                      "                          AND frm.t_name = 'PNO' "
                      "                          AND rls.t_formid = frm.t_formid "
                      "                          AND rls.t_rlsformid = m.t_rlsformid "
                      "                          AND T.t_mesid = m.t_mesid) " );

    cmd.addparam( "GROUND", RSDBP_IN, paymentGround );
    cmd.addparam( "NUMDOC", RSDBP_IN, paymentNumber );

    rs = RsdRecordset( cmd );
    if( rs.movenext )
        return rs.value( "t_mesid" );
    end;

    return 0;
end;

// секретная функция
macro selectDir( Folder )

    var ShellObject = CreateObject( "rsax", "TRsAxServer", "RsAxServer", false ).CreateComObject( "Shell.Application", true );
    var browseFoldr = ShellObject.BrowseForFolder( 0, "Выбор папки", 529 );

    if( browseFoldr != null )
        Folder = browseFoldr.Self.Path;

        if( SubStr( Folder, 1, 1 ) == "\\" )
            setparm( 0, Folder );
        else
            setparm( 0, "$" + Folder );
        end;

        return true;
    else
        return false;
    end;

onerror return false;
end;

// Получить директорию выгрузки
macro getExpPath
    
    var expPath, operNumber, Error = 0;
    
    GetRegistryValue("PS\\REQOPENACC\\OPERATION\\СОЗДАНИЕ ИНФ СООБЩЕНИЙ\\ДИРЕКТОРИЯ ВЫГРУЗКИ", V_STRING, expPath, error);
    
    if( error > 0 ) return "";
    end;

    operNumber = string( {oper} );

    if( strLen( operNumber ) < 5 )
        while( ( 5 - strLen( operNumber ) ) != 0 )
            operNumber = "0" + operNumber;
        end;
    end;

    if( subStr( expPath, strLen( expPath ), 1 ) != "\\" )
        expPath = expPath + "\\";
    end;
    
    expPath = expPath + operNumber;

    return expPath;

end;

// Переписал createPB, чтобы можно было задавать свою директорию выгрузки
macro createPB2File( inFaleName, errText, path, fileName, createError )

    var expPath;
    var fullfilename, name, dir;

    expPath      = path;
    name         = inFaleName;
    dir          = SplitFile( name, name );
    fullFileName = GetTXTFileName( "PB2_" + name );

    SetOutPut( fullFileName, false );

    println( name                + "###" );
    println( errText             + "@@@" );
    println( ToDateStr()         + "@@@" );
    println( TimeToStr( Time() ) + "@@@" );
    println(                       "===" );
  
    SetOutput( null, true );

    Name = "PB2_" + Name + ".txt";

    removefile( expPath + "\\" + Name );

    if( CopyFile( fullFileName, expPath + "\\" + Name ) )
        setparm( 3, Name );
        return 0;
    else
        setparm( 4, "Не удалось скопировать файл " + Name + " в каталог " + expPath + "\\" ); 
        return 1;
    end;

end;

// Получить имя клиента по счету
macro clientNameAccount( account )

    var cmd, rs;
        cmd = RsdCommand( " SELECT   UPPER(T_SHORTNAME) SHORTNAME "
                          "   FROM   DPARTY_DBT CLIENT, DACCOUNT_DBT ACCOUNT "
                          "  WHERE   ACCOUNT.T_CLIENT = CLIENT.T_PARTYID AND ACCOUNT.T_ACCOUNT = :ACCOUNT " );

        cmd.addparam( "Account", RSDBP_IN, account );

        rs = RsdRecordSet( cmd );

        if( rs.movenext ) return rs.value( "SHORTNAME" );
        else              return "";
        end;

end;

// Протокол
macro initReport( DateIn, DateOut, DepCode, DepName, Oper, OperName, Account )

    if( Oper == 0 ) OperName = "не выбран";
    end;

    if( Account == "" ) Account = "не выбран";
    end;

    DateIn  = StrSubst( string( DateIn  ), " ", "0" );
    DateOut = StrSubst( string( DateOut ), " ", "0" );

    SetOutput( f_rep_name, false );

    [   
                                                                          Протокол формирования сообщений РВ2
                                                                            о постановке и/п в Картотеку №2                                                                            
    ];
    [                                      ];
    [   Параметры процедуры:               ];
    [   Филиал: #### #                     ]( DepCode, DepName );
    [   Период: с ########## по ########## ]( DateIn , DateOut );
    [   Операционист: #                    ]( OperName         );
    [   Счет: #                            ]( Account          );
    [                                      ];
    [ ┌────────────┬────────┬─────────────────────────────────────────────┬──────────────┬──────────────────────┬────────────┬────────────┬──────────────────────────────────────────┐
      │            │        │                                             │              │                      │            │            │                                          │
      │ ID платежа │    №   │                 Плательщик                  │     ИНН      │         Счет         │   Сумма    │    Дата    │                 Результат                │
      │            │        │                                             │              │                      │            │            │                                          │
      ├────────────┼────────┼─────────────────────────────────────────────┼──────────────┼──────────────────────┼────────────┼────────────┼──────────────────────────────────────────┤ ];

end;

macro printLine( PaymentID: integer, PNOmessage: MnsMessageFormPNO_easy, Result: string )

    SetOutput( f_rep_name, true );

    [ │ ########## │ ###### │ ########################################### │ ############ │ #################### │ ########## │ ########## │ ######################################## │]

    (PaymentID, PNOmessage.НомПоруч:r, clientNameAccount(PNOmessage.НомСчПл), PNOmessage.ИНННП:c, PNOmessage.НомСчПл, Money(PNOmessage.СумПоруч)/100:r, PNOmessage.ДатаПоруч, Result);

end;

macro endReport( count, mesCount )
   
    var currentDay = StrSubst( string( date ), " ", "0" );

    [ └────────────┴────────┴─────────────────────────────────────────────┴──────────────┴──────────────────────┴────────────┴────────────┴──────────────────────────────────────────┘ ];
    [                                           ];
    [   Директория выгрузки файлов: #           ]( extDir     );
    [   Количество обработанных платежей: ##### ]( count:l    );
    [   Количество созданных сообщений: #####   ]( mesCount:l );
    [                                           ];
    [   Дата: ##########                        ]( currentDay ); 

    SetOutput( null, false );

    viewFile( f_rep_name );

end;

// Создание РВ2 по платежу
private macro createPB2_K2( paymentID, pmGround, pmNumber )
    
    private file pspayord( pspayord ) key 0;
    private var reqid, file_name, result, error = 1, stat = 1;
    private var mesPNO, PB2_text, mesid, _inFileName;

    pspayord.orderid = paymentID;
    if( GetEQ( Pspayord ) )     
        reqid  = int( ReadNoteForObject( OBJTYPE_PSPAYORD, UniID( Pspayord, OBJTYPE_PSPAYORD, PS_PAYORDER ), 101 ) );

        if( reqid == 0 )
            mesid = altFindMes( pmGround, pmNumber );
            if( mesid == 0 )
                result = "Не найдено входящее сообщение";
            else
                mesPNO = MnsMessageFormPNO_easy( null, mesid );
            end;
        else
            mesPNO = MnsMessageFormPNO_easy( reqid );
        end;

        if( ( reqid != 0 ) or ( mesid != 0 ) )
            if( not mesPNO.error )            

                PB2_text    = "35;И/п "               + mesPNO.НомПоруч + " от " + mesPNO.ДатаПоруч + " на сумму " + PB2_summ( mesPNO.СумПоруч ) + ", "
                              "выставленное к счету " + mesPNO.НомСчПл  + " "    + mesPNO.Плательщ  + " ИНН "      + mesPNO.ИНННП                + " "  
                              "помещено в Картотеку №2 в связи с недостаточностью свободных средств на счете"; 

                if( reqid == 0)
                    _inFileName = ПолучитьИмяВходФайлаПоСообщ( mesid );
                else
                    _inFileName = ПолучитьИмяВходФайла( reqid );
                end;

                stat = createPB2File( _inFileName, PB2_text, extDir, file_name, result );

                if( stat == 0 )
                    /*Если удалось создать PB2 сделаем запись в протокол. По C-22478*/
                    BankDate     = mesPNO.bankDate;
                    OriginatorID = 0;
                    GetReqParam(mesPNO.reqid, BankDate, OriginatorID);
                    insert_MES365_LOG(mesPNO.reqid, ОтрезатьПутьОтИмениФайла(_inFileName), BankDate, file_name, {curdate}, OriginatorID);
                    result = file_name;
                    error  = 0;
                else 
                    error = 1;
                end;
            else
                result = "Не найдено входящее сообщение";
            end;    
        end;
    else
        result = "Платеж не найден";
    end;
    
    printLine( PaymentID, mesPNO, result );

    return error;

end;

macro create_365P_K2_Docs( pDepCode: string, pDepName: string, pOperName: string, pDateIn: date, pDateOut: date, pDepNum: integer, pOper: integer, pAccount: string )

    // Данные панели параметров отчета  
    var DateIn  :date    = pDateIn;
    var DateOut :date    = pDateOut;
    var DepNum  :integer = pDepNum;
    var Oper    :integer = pOper;
    var Account :string  = pAccount;

    // Запрос
    var params  :TArray  = TArray();
    var select  :string  = "", selectCount:string = "";
    var _from   :string  = "FROM dpmhist_dbt pmh, dpmpaym_dbt pm, dpmrmprop_dbt rm ";
    var rsCount :RsdRecordset, count:integer = 0, i:integer = 1, stat = 0, mesCount = 0;

    ПлатежБылПомещенВ_К2( @select, params, DateIn, DateOut );

    select = select + " AND pm.t_paymentid = rm.t_paymentid ";

    select = select + ПлатежКлиентскийОрдер();

    ДобавитьУсловиеПоСчетуПлательщика     ( @select, params, Account );
    ДобавитьУсловиеПоФилиалуПлатежа       ( @select, params, DepNum  );
    ДобавитьУсловиеПоОперационистуПлатежа ( @select, params, Oper    );
   
    // Golovkin 17.03.2015 R-557839 для Пойдема пачку не учитываем
    if( _bank.is_GO )
        select = select + " AND pm.t_origin = 3300 ";
    else
        select = select + " AND (pm.t_numberpack = 1002 and pm.t_origin = 3300) ";
    end;

    select = select + " ORDER BY pm.t_I2PlaceDate ";             

    selectCount = " select count(*) " + _from /*FromPMHIST_PMPAYM()*/ + " WHERE " + select;
    select      = " SELECT pm.t_paymentid AS paymentid, rm.t_ground AS ground, rm.t_number AS \"number\" " /*ВыбратьPaymentID()*/  + _from /*FromPMHIST_PMPAYM()*/ + " WHERE " + select;

    begAction;

    rsCount = execSQLselect( selectCount, params, FALSE );

    if( rsCount.movenext )
        Count = rsCount.value(0);
    end;

    endAction; 

    initReport( pDateIn, pDateOut, pDepCode, pDepName, pOper, pOperName, pAccount );
    initProgress( count );

    var rs:RsdRecordset = execSQLselect( select, params, FALSE );

    if( rs )
        rs.Command.NullConversion = true;
        while( ( rs.MoveNext() ) and ( rs.value(0) > 0 ) )
            stat = createPB2_K2( rs.value(0), rs.value(1), rs.value(2) );

            if( stat == 0 ) mesCount = mesCount + 1;
            end;

            useProgress( i );

            i = i + 1;
        end;
    end;    
    
    remProgress;
    endReport( count, mesCount );

end;

Class ( TRsbPanel ) Panel_365P_RB2_K2
    
    InitTRsbPanel();
    SetSize( 50, 16 );    

    var Labels_position        = 5;
    var First_Fields_Position  = Labels_position       + 10;
    var Second_Fields_position = First_Fields_Position + 6;

    // получить имя подразделения по его коду
    macro DepartmentName ( Code: string )
    
        var rs,cmd = RsdCommand( " SELECT   PARTY.T_SHORTNAME "
                                 "   FROM   DPARTY_DBT PARTY, DDP_DEP_DBT DEPARTMENT "
                                 "  WHERE   DEPARTMENT.T_NAME = :NAME AND PARTY.T_PARTYID = DEPARTMENT.T_PARTYID " );

        cmd.addparam( "Code", RSDBP_IN, Code );

        rs = RsdRecordSet( cmd );

        if( rs.movenext ) return rs.value( "T_SHORTNAME" );
        else              return "";
        end;
       
    End;

    macro DepartmentNumber ( Code: string )
    
        var rs,cmd = RsdCommand( " SELECT   T_CODE "
                                 "   FROM   DDP_DEP_DBT "
                                 "  WHERE   T_NAME = :NAME " );

        cmd.addparam( "Code", RSDBP_IN, Code );

        rs = RsdRecordSet( cmd );

        if( rs.movenext ) return rs.value( "T_CODE" );
        else              return "";
        end;
       
    End;

    macro OperName( Oper )
        var rs,cmd = RsdCommand( " SELECT   T_NAME "
                                 "   FROM   DPERSON_DBT "
                                 "  WHERE   T_OPER = :OPER " );

        cmd.addparam( "Oper", RSDBP_IN, Oper );

        rs = RsdRecordSet( cmd );

        if( rs.movenext ) return rs.value( "T_NAME" );
        else              return "";
        end;
    end;

    macro ListRepKind( val, x, y)

        private var m;
        private array RepKind;
        private var fx, fy;
        
        getPosition( fx, fy );

        RepKind( asize( RepKind ) ) = RK_BY_DAY;
        //RepKind(asize(RepKind)) = RK_BY_MONTH;
        RepKind( asize( RepKind ) ) = RK_BY_PERIOD;

        m = menu( RepKind, null, null, x + fx, y + fy);

        if( m >= 0 ) return RepKind( m );
        else         return val;
        end;

    end;

    class ( TRsbEditField ) EditField( type: integer, x: integer, y: integer, width: integer, height: integer, bindVal, active: bool )
        
        var bindString = bindVal;
        
        InitTRsbEditField( type );
        SetPosition( x, y );
        SetSize( width, height );
        
        if( active == false ) editable = focusable = false;
        end;

        if  ( type == FT_STRING ) bindValue( this, "bindString", 100 );
        elif( bindVal != null ) value = bindVal;
        end;    

    end;

    //macro getMonthPeriod(month, date1: @date, date2: @date)
    //end;
    
    // Labels
    var DepLabel:TRsbLabel     = TRsbLabel( Labels_position, 2 , "Филиал"       );
    var RepkindLabel:TRsbLabel = TRsbLabel( Labels_position, 4 , "Вид отчета"   );
    var PeriodLabel:TRsbLabel  = TRsbLabel( Labels_position, 6 , "Период"       );
    var OperLabel:TRsbLabel    = TRsbLabel( Labels_position, 10, "Операционист" );
    var AccountLabel:TRsbLabel = TRsbLabel( Labels_position, 12, "Лицевой счет" );
    var extDirLabel:TRsbLabel  = TRsbLabel( Labels_position, 14, "Директория"   );

    // EditFields   
    var DepField      = EditField( FT_STRING , First_Fields_Position , 2 , 5 , 1, "000"                        ); // подразделение
    var DepNameField  = EditField( FT_STRING , Second_Fields_position, 2 , 20, 1, DepartmentName("000"), false ); // название подразделения
    var RepkindField  = EditField( FT_STRING , First_Fields_Position , 4 , 11, 1, "За день"                    ); // вид отчета  
    var Period1Field  = EditField( FT_DATE   , First_Fields_Position , 6 , 11, 1, {curdate}                    ); // поля для задания периода
    var Period2Field  = EditField( FT_DATE   , First_Fields_Position , 7 , 11, 1, {curdate}            , false );
    var Period3Field  = EditField( FT_STRING , First_Fields_Position , 8 , 11, 1, ""                   , false );
    var OperField     = EditField( FT_INTEGER, First_Fields_Position , 10, 5 , 1                               ); // операционист
    var OperNameField = EditField( FT_STRING , Second_Fields_position, 10, 20, 1, ""                   , false ); // имя
    var AccountField  = EditField( FT_STRING , First_Fields_Position , 12, 26, 1, ""                           ); // счет
    var extDirField   = EditField( FT_STRING , First_Fields_Position , 14, 26, 1, getExpPath                   ); // директория выгрузки
    
    // добавление обработчика нажатия клавиш
    DepField.addEventHandler     ( RSB_EV_KEY_PRESSED, R2M( this, "onDepFieldKeyPressed"     ) );
    RepkindField.addEventHandler ( RSB_EV_KEY_PRESSED, R2M( this, "onRepkindFieldKeyPressed" ) );
    OperField.addEventHandler    ( RSB_EV_KEY_PRESSED, R2M( this, "onOperFieldKeyPressed"    ) );
    AccountField.addEventHandler ( RSB_EV_KEY_PRESSED, R2M( this, "onAccountFieldKeyPressed" ) );
    extDirField.addEventHandler  ( RSB_EV_KEY_PRESSED, R2M( this, "onExtDirFieldKeyPressed"  ) );
    addEventHandler              ( RSB_EV_KEY_PRESSED, R2M( this, "onActionKeyPressed"       ) );
    
    // добавление Label на форму 
    addLabel( DepLabel     );
    addLabel( RepkindLabel );
    addLabel( PeriodLabel  );
    addLabel( OperLabel    );
    addLabel( AccountLabel );
    addLabel( extDirLabel  );

    // добавление полей на форму    
    addControl( DepField      );
    addControl( DepNameField  );
    addControl( RepkindField  );
    addControl( Period1Field  );
    addControl( Period2Field  );
    //addControl( Period3Field  );
    addControl( OperField     );
    addControl( OperNameField );
    addControl( AccountField  );
    addControl( extDirField   );

    // обработка нажатия клавиш
    macro onDepFieldKeyPressed( RsbEvent:object )
        record Department( dp_dep );
        if  ( rsbEvent.KeyCode == KEY_F3 )
            if( ListDepartment( Department ) )
                DepField.value     = Department.Name;
                DepNameField.value = DepartmentName( Department.Name );
            end;    
        elif( ( rsbEvent.KeyCode == KEY_DOWN  ) or
              ( rsbEvent.KeyCode == KEY_UP    ) or
              ( rsbEvent.KeyCode == KEY_TAB   ) or
              ( rsbEvent.KeyCode == KEY_ENTER ) )
            DepNameField.value = DepartmentName( DepField.value );
        end;
    end;

    macro onRepkindFieldKeyPressed( RsbEvent:object )
        private var RepKind;
        private var x,y,width,height;

        if( rsbEvent.KeyCode == KEY_F3 )
            RepkindField.GetPosition( x, y ); 
            RepkindField.GetSize( width, height );
            RepKind = ListRepKind( RepkindField.value, x, y + height );
            RepkindField.value = RepKind;
            if   ( RepKind == RK_BY_PERIOD )
                Period1Field.editable = Period1Field.focusable = true;                
                Period2Field.editable = Period2Field.focusable = true;                
                Period3Field.editable = Period3Field.focusable = false;
                redraw();
            elif ( RepKind == RK_BY_MONTH )
                Period1Field.editable = Period1Field.focusable = false;                
                Period2Field.editable = Period2Field.focusable = false;                
                Period3Field.editable = Period3Field.focusable = true;
                redraw();
            elif ( RepKind == RK_BY_DAY )
                Period1Field.editable = Period1Field.focusable = true;
                Period2Field.editable = Period2Field.focusable = false;
                Period3Field.editable = Period3Field.focusable = false;
                redraw();
            end;    
        end;
    end;

    macro onOperFieldKeyPressed( RsbEvent:object )        
        private record oper( person );
        if  ( rsbEvent.KeyCode == KEY_F3 )
            if( ListOper( oper, true, OperField.value ) )
                OperField.value     = oper.oper;
                OperNameField.value = oper.name;
            end;
        elif( ( rsbEvent.KeyCode == KEY_DOWN  ) or
              ( rsbEvent.KeyCode == KEY_UP    ) or
              ( rsbEvent.KeyCode == KEY_TAB   ) or
              ( rsbEvent.KeyCode == KEY_ENTER ) )            
            OperNameField.value = OperName( OperField.value );
        end;
    end;

    macro onAccountFieldKeyPressed( RsbEvent:object )
        private record account( account );
        if( rsbEvent.KeyCode == KEY_F3 )
            if( ListAccount( account, 1, 0, AccountField.value, true, null, DepartmentNumber( DepField.value ), true ) )
                AccountField.value = account.account;
            end;
        end;
    end;

    macro onExtDirFieldKeyPressed( RsbEvent:object )
        var folder;
        if( rsbEvent.KeyCode == KEY_CTRL_F3 )
            if( selectFolder( folder, null, null, true ) )                 
                ExtDirField.value = folder;
            end;
        elif( rsbEvent.KeyCode == KEY_F3 )
            if( selectDir( folder ) )
                ExtDirField.value = folder;
            end;
        end;
    end;

    macro onActionKeyPressed( RsbEvent:object )
        
        private var DateIn,DateOut,DepNum,Oper,Account,DepCode,DepName,OperName;
        
        if( rsbEvent.KeyCode == KEY_F2 )
            extDir   = extDirField.value;
            DepCode  = DepField.value;
            DepName  = DepNameField.value;
            OperName = OperNameField.value;
            DateIn   = Period1Field.value;

            if( RepkindField.value == RK_BY_DAY ) DateOut = Period1Field.value;
            else                                  DateOut = Period2Field.value;
            end;
            
            DepNum   = DepartmentNumber( DepField.value );
            Oper     = OperField.value;
            Account  = AccountField.value;

            makeDir( extDir ); // пытаемся создать папку

            create_365P_K2_Docs( DepCode, DepName, OperName, DateIn, DateOut, DepNum, Oper, Account );
        end;    

    end;

End;

if( not ACS_CheckGroupOper( 96, {oper}, true ) ) exit( 1 ); end;

var panel = Panel_365P_RB2_K2;
    panel.setPosition(30, 10);
    panel.setCaption("Формирование РВ2");
    panel.setStatus("~F2~ Запуск процедуры ~F3~ Выбор из справочника");
    panel.run;
    exit(1);
