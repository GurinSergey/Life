/*Печать уведомления/извещения для валюты/нерезидента                    */
/*                                                                       */
/*                                                                       */
/*                                                                       */
/*                                                                       */
/* Changes:                                                              */
/* Тихомиров А.Н. Стручков А. 10.07.2009                    Версия 1.0   */
/* Seleznev 16.07.2009                                                   */
/* Lavrenov: 29.05.2012 I-00200565-2                                     */
/* Жаворонкова Н. 21.01.2013 I-00306892-3 добавлены маски счетов         */
/* TAM 29.07.2013 R-223747-2                                             */
/* RR  21.07.2015 Доработки согласно С-40831                            */

import globals,rsd, Календарь, BankInter, PaymInter;
import cb_sql;
import "fg_Life_parm.mac";
import "print_vk_text.mac";

private const fdBank = fg_life_subject({OurBank});

private CONST KEY_STRBREAK  :string = "/STRBREAK"; //zmp 12.05.2014  I-00486005
CONST REF_NUM = 1000009;

var needAskForDate = true,
    ntfDateEntered = false,
    newNtfDate = {CurDate},
    printNtfDate = {CurDate},
    existingNtfDate = {CurDate} ;

private var out, fulloutput;
GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR",2,out);
Fulloutput = out + "\\print_vk.txt";                    


macro ISPayerNerezPtID(partyid)

   private var cmd, rs;
   cmd = RSDCommand("select 1 from dparty_dbt where t_partyid =  ? and t_notresident = 'X'");
   cmd.addParam("id",RSDBP_IN,   partyid );
   rs = RSDRecordset(cmd);
   return (rs.MoveNext);

end;

macro ISTransitAcc(acc, fiid)

   private var cmd, rs;
   //Gurin S. 29.01.2014 R-322082-1 Адаптация 2031
   cmd = RSDCommand("select 1 from daccount_dbt where t_account =  ? and t_chapter = 1 and t_code_currency = ? and t_type_account like '%Y%'");
   cmd.addParam("acc", RSDBP_IN, acc  );
   cmd.addParam("fiid",RSDBP_IN, fiid );
   rs = RSDRecordset(cmd);
   return (rs.MoveNext);

end;


macro IsCarry(paymentid)

   private var cmd, rs;
   cmd = RSDCommand("select 1 from dpmdocs_dbt where t_paymentid =  ?");
   cmd.addParam("id",RSDBP_IN, paymentid);
   rs=RSDRecordset(cmd);
   return (rs.MoveNext);

end;

/* A.Gregeradsky - 09.12.2009 - Доработка по запросу пользователя */
Macro ТранзитныйСчетЗачисленияИзПроводки(paymentid, acnt)
   var SQL, cmd, rs;

   cmd = RsdCommand(
                   "select trn.t_account_receiver" +
                   "  from dacctrn_dbt trn, dpmdocs_dbt pm, daccount_dbt acnt"+
                   " where pm.t_paymentid = ?"+ 
                   "   and trn.t_acctrnid = pm.t_acctrnid"+
                   "   and trn.t_account_receiver = acnt.t_account"+
                   "   and acnt.t_chapter = 1"+
                   "   and acnt.t_code_currency > 0"+
                   "   and instr(upper(acnt.t_type_account),'Y') != 0"
                   );

   cmd.AddParam("pm", RSDBP_IN, paymentid);
   rs = RsdRecordSet(cmd);

   if(rs.MoveNext())
      return rs.Value(0);
   else
      return acnt;
   end;
end;

/* EVG 5/03/2011 Получить номер уведомления по референсу */
Macro GetRefNumber( pmId )
  debugbreak;
  var stat, val;
  var rs, query;
  var isCur = false;

  // Определим валюту
  query = " Select t_BaseFiid From DPmPaym_dbt " +
          "  Where t_PaymentId = " + pmId;
  rs = rsdRecordSet( query );
  if ( rs and rs.MoveNext() )
     if ( rs.Value(0, null, V_INTEGER) > 0 )
        isCur = true;
     end;
  end;
  
  //Lavrenov: 29.05.2012 I-00200565-2 для ПРББ используем только 1 референс
  if(fdBank.is_PRBB)
     stat = GenerateReference( 1000030, val );
     val = val;
  else
     // Рубли
     if ( not isCur )
        stat = GenerateReference( 1000030, val );
        val = val;
     // Валюта
     else
        stat = GenerateReference( 1000031, val );
     end;
  end; 
  if ( stat != 0 )
     msgbox ("Ошибка формирования номера уведомления (референс № 1000023 или 1000024)");
     val = "xxx";
  end;

  return val;
end;



/* EVG 8/08/2012 Вставка уведомления вынесена в отдельную процедуру */
Macro InsTrnsfNotify( paymentId, isCur )
  var cmd, rs;

   /* EVG 23/03/2011 Возникла проблема: номера валютных и рублёвых уведомлений
      потенциально могут пересекаться (пересекутся сразу, если нумерация будет с 1).
      Поэтому для рублёвых уведомлений я создал отдельную таблицу usr_trnsf_notify_rur,
      полностью идентичную usr_trnsf_notify. Она будет использоваться только для сохране-
      ния номера уведомления, в других процессах она не задействована. */
   var tblName = "usr_trnsf_notify_rur";

   if ( isCur )
      tblName = "usr_trnsf_notify";
   end;

   //Lavrenov: 29.05.2012 I-00200565-2 для ПРББ используем только usr_trnsf_notify
   if((fdBank.is_PRBB) or (fdBank.is_VUZ))
      tblName = "usr_trnsf_notify";
   end;
   
   cmd = RSDCommand("select 1 from " + tblName + " where payment_id = ?");
   cmd.addparam("pmid", RSDBP_IN, paymentid);
   rs = RSDRecordset(cmd);

   if (not(rs.movenext))
      /* EVG 2.12.2010 Запрос даты уведомления */
      if ( needAskForDate )
         if ( getDate( newNtfDate, "Пожалуйста, введите дату формирования уведомлений:" ) )
            needAskForDate = false;
         end;
      end;
      cmd = RSDCommand("insert into " + tblName + " (notify_num,notify_date,payment_id) values (?,?,?)");
      cmd.addparam("notify_num",  RSDBP_IN,  GetRefNumber( paymentid ) );
      cmd.addparam("notify_date", RSDBP_IN,  newNtfDate);
      cmd.addparam("payment_id",   RSDBP_IN, paymentid );
      cmd.execute();

      printNtfDate = newNtfDate;
   else
      /* EVG Если уведомление уже есть, получим его дату */
      cmd = RSDCommand(" Select notify_date From " + tblName + " where payment_id = ? ");
      cmd.addparam("payment_id", RSDBP_IN, paymentid);
      rs = RSDRecordset(cmd);
      if ( rs and rs.MoveNext() )
         existingNtfDate = rs.Value(0, null, V_DATE);
      end;

      printNtfDate = existingNtfDate;
   end;

end;

//TAM 19.10.2012 I-00270572-2
macro GetClientNameByAcc(acnt)
   var SQL, cmd, rs;
   cmd = RsdCommand(  "SELECT PT.T_NAME " +
                      "  FROM daccount_dbt ac, dparty_dbt pt " +
                      " WHERE AC.T_ACCOUNT = ? AND AC.T_CLIENT = PT.T_PARTYID "
                  );
   cmd.AddParam("acc", RSDBP_IN, acnt);
   rs = RsdRecordSet(cmd);
   if(rs.MoveNext())
      return rs.Value(0);
   else
      return "";
   end;
end;


Macro ПечатьУведомления(Doc_date,Account_num,Client_Name,Value,Currency_str,Value_str,Reply_date, BankName, paymentid, ismassprint, NeedPrintLog)
   debugbreak;
   if ((NeedPrintLog) or (valtype(NeedPrintLog) == V_UNDEF))
     setoutput(fulloutput,ismassprint);
   end;
   debugbreak;
   var rs, cmd;
   var PmObj = RsbPayment(paymentid);

   /* A.Gregeradsky - 09.12.2009 - Поиск транзитного счета для зачисления валюты резиденту по платежу. Доработка выполнена по запросу пользователя от 09.12.2009 */
   if((PmObj.dockind == 320) and (PmObj.BaseFIID != 0))
      Account_Num = ТранзитныйСчетЗачисленияИзПроводки(paymentid, Account_Num);
   end;
   /* End Gregeradsky */

   InsTrnsfNotify( paymentId, true );
   if ((NeedPrintLog) or (valtype(NeedPrintLog) == V_UNDEF))
     println(ТекстУведомления(Client_name, paymentid, Currency_str, Value, Value_str, Account_Num, Doc_date, Reply_date ));
     if (ismassprint)
        println("");
     end;

     SetOutput(null,true);
     if (not(ismassprint))
        viewfile(fulloutput);
     end;
   end;
end;  

macro ПечатьИзвещения(Account_num,Client_Name,Value,Currency_str,Value_str, paymentid, ismassprint, pdate, NeedPrintLog)
   debugbreak;
   if ((NeedPrintLog) or (valtype(NeedPrintLog) == V_UNDEF))
     setoutput(fulloutput,ismassprint);
   end;
   InsTrnsfNotify( paymentid );
   println (ТекстИзвещения(client_name, paymentid, value, currency_str, value_str, account_num, pdate));

   /* EVG 8/08/2012 По поступлению средств на р/с от нерезидентов теперь 
      также создаётся уведомление. */
   InsTrnsfNotify( paymentid );
   if ((NeedPrintLog) or (valtype(NeedPrintLog) == V_UNDEF))
     if (ismassprint)
        println("");
     end;

     SetOutput(null,true);

     if (not(ismassprint))
       viewfile(fulloutput);
     end;
   end;
end;

macro ПечатьВсехУведомлений(for_send, NotifyDate, NeedAskDepartment, NeedPrintLog)
   debugbreak;
   var pdate = {curdate};
   var rub, kop,CURtext, case="";
   var depi = "";
   var rs1, cm1, dep = "0";
   
   if (valType(NotifyDate) == V_UNDEF)
     if (not(getDate(pdate,"Укажите дату зачисления на счет")))
       exit(1);
     end;
   else
     pdate = NotifyDate;
   end;
   
   if ((NeedAskDepartment) or (valType(NeedAskDepartment) == V_UNDEF))
     if (not(getstring(dep,"Укажите подразделение (0 - Весь банк)", 3)))
       exit(1);
     else
       if (dep > 0)
        cm1 = rsdcommand("select t_code from ddp_dep_dbt where t_name = ?");
        cm1.addparam("name", RSDBP_IN, dep);
        rs1 = rsdrecordset(cm1);
        if (rs1.movenext())
          depi = "and ac.t_branch = "+rs1.value(0);
        end;
       end;
     end;
   end;
   
   if (for_send)
//      case = "  and ac.t_usertypeaccount like '%Ё%' ";
   end;

   private var rs, cmd, srok;

   cmd = RSDCommand("select   pm.t_paymentid, trn.t_date_carry t_valuedate, " +
                    "         trn.t_account_receiver t_receiveraccount, " +
                    "         pt.t_name as t_receivername, " +       // Тихомиров А.Н. Имя для получателя не из платежа а из party_dbt
                    "         pm.t_payamount, " +
                    "         fi.t_iso_number, " +
                    "         pmr.t_receiverbankname " +
                    "  from   dpmpaym_dbt pm, " +
                    "         dpmrmprop_dbt pmr, " +
                    "         dpmdocs_dbt pmd, " +
                    "         dacctrn_dbt trn, " +
                    "         daccount_dbt ac, " +
                    "         dparty_dbt pt, " +
                    "         dfininstr_dbt fi " +
                    " where       pm.t_dockind in (320, 322, 27, 202, 70) " + //Gurin S. 01.04.2015 R-565267-2 Добавил МО
                    "         and pm.t_FIID_FutureRecAcc <> 0 " +
                    "         and pm.t_paymentid = pmr.t_paymentid " +
                    "         and trn.t_date_carry = ? " +
                    "         and trn.t_fiid_receiver = pm.t_FIID_FutureRecAcc " +
                    "         and trn.t_chapter = 1 " +
                    "         and ac.t_type_account like '%Y%' " + case +
                    "         and fi.t_fiid = pm.t_FIID_FutureRecAcc " + depi +
                    "         and pt.t_partyid = ac.t_client " +
                    "         and pt.t_notresident = chr (0) " +
                    "         and pmd.t_paymentid = pm.t_paymentid " +
                    "         and trn.t_acctrnid = pmd.t_acctrnid " +
                    "         and trn.t_account_receiver = ac.t_account " +
                    "         and trn.t_chapter = 1 " +
                    "         and trn.t_fiid_receiver = pm.t_FIID_FutureRecAcc "
                    );
   cmd.addparam("pdate", RSDBP_IN, pdate);
   rs = RSDRecordset(cmd);
   
   if ((NeedPrintLog) or (valType(NeedPrintLog) == V_UNDEF))
     setoutput(fulloutput,false);
     setoutput(null,true);
   end;
   
   while (rs.moveNext())

      debugbreak; //DAI по I-00012216
      srok = date(rs.value("t_valuedate"));
      if (srok > date("26.02.2011"))
         srok = DateAfterWorkDays(srok, 15);//TAM R-50392
      else
         srok = DateAfterWorkDays(srok, 8);
      end;

      CURtext    = CurToStrAlt(money(rs.value("t_payamount")), rub, kop, int(rs.value("t_iso_number")), false); 
      ПечатьУведомления(date(rs.value("t_valuedate")),rs.value("t_receiveraccount"),rs.value("t_receivername"),money(rs.value("t_payamount")),
                    SubStr(CURtext, StrLen(rub)+1, Index(CURtext,kop)-Strlen(rub)-1), CURtext,
                    srok, rs.value("t_receiverBankname"), rs.value("t_paymentid"), true, NeedPrintLog);
                    //DateAfterWorkDays(date(rs.value("t_valuedate")),8) 
   end;
   
   if ((NeedPrintLog) or (valType(NeedPrintLog) == V_UNDEF))
     viewfile(fulloutput);
   end;
   
end;


macro ПечатьВсехИзвещений(for_send, NotifyDate, NeedAskDepartment, NeedPrintLog)
   debugbreak;
   var pdate = {curdate};
   var rub, kop,CURtext, case = "";
   var depi = "";
   var rs1, cm1, dep = "0";

   if (valType(NotifyDate) == V_UNDEF)
     if (not(getDate(pdate,"Укажите дату зачисления на счет")))
       exit(1);
     end;
   else
     pdate = NotifyDate;
   end;

   if ((NeedAskDepartment) or (valType(NeedAskDepartment) == V_UNDEF))
     if (not(getstring(dep,"Укажите подразделение (0 - Весь банк)", 3)))
       exit(1);
     else
        if (dep > 0)
        cm1 = rsdcommand("select t_code from ddp_dep_dbt where t_name = ?");
        cm1.addparam("name", RSDBP_IN, dep);
        rs1 = rsdrecordset(cm1);
        if (rs1.movenext())
          depi = "and ac.t_branch = "+rs1.value(0);
        end;
       end;
     end;
   end;
   
   if (for_send)
//      case = "  and ac.t_usertypeaccount like '%Ё%' ";
   end;
   private var rs, cmd;
   /* EVG 28/1/2014 Адаптация под 2031 */
   cmd = RSDCommand("select   pm.t_paymentid, trn.t_date_carry t_valuedate,  " +
                    "         pm.t_payeraccount,  " +
                    "         trn.t_account_receiver t_receiveraccount,  " +
                    "         pmr.t_receivername,  " +
                    "         pm.t_amount,          " +
                    "         pmr.t_receiverbankname,  " +
                    "         pm.t_receiver,  " +
                    // EVG 16/08/2012 Для анализа основания
                    "         pmr.t_ground " +
                    "  from   dpmpaym_dbt pm,  " +
                    "         daccount_dbt ac, " +
                    "         dacctrn_dbt trn, " +
                    "         dpmrmprop_dbt pmr,  " +
                    "         dpmdocs_dbt pmd       " +
                    " where   pm.t_dockind              in (320, 322, 16, 201)  " +
                    "         and pm.t_FIID_FutureRecAcc = 0  " +
                    "         and trn.t_account_receiver = ac.t_account " +
                    "         and pm.t_paymentid         = pmr.t_paymentid  " + case +
                    "         and ac.t_chapter           = 1 " + depi +
                    "         and pmd.t_paymentid        = pm.t_paymentid " +
                    "         and trn.t_acctrnid         = pmd.t_acctrnid " +
                    "         and trn.t_date_carry       = ?  " +
                    "         and pmd.t_paymentid        = pm.t_paymentid  "
                    );

   cmd.addparam("pdate", RSDBP_IN, pdate);
   rs = RSDRecordset(cmd);

   if ((NeedPrintLog) or (valType(NeedPrintLog) == V_UNDEF))
     setoutput(fulloutput,false);
     setoutput(null,true);
   end;
   
   var  ReciverMask = "401*-407*, 40802*"; /*21-01-2013 joy I-00306892-3 добавлены маски, согл. с Севостьяновой О.Б.*/
   while (rs.moveNext())
   //debugbreak;
      //if (not ISPayerNerezAcc(rs.value("t_receiveraccount")))
      //TAM 18.10.2012 I-00270572-2
      /* 21-01-2013 Жаворонкова Н. (joy) I-00306892-3 добавлены маски, согл. с Севостьяновой О.Б.
      if( ( SubStr(rs.value("t_receiveraccount"),1,5) == "40802") or ( SubStr(rs.value("t_receiveraccount"),1,3) == "407")) */
      if ((CompareStrWithMasks (ReciverMask,  rs.value("t_receiveraccount")  ) == 0) )
      /* ----end joy*/
         if ( ISPayerNerezAcc(rs.value("t_payeraccount"))
           // EVG 16/08/2012 Для того, чтобы извещения печатались по документам с Дт. 30302 и кодом ВО в основании
         or ( index( rs.value("t_ground"), "{VO" ) > 0 ) )
            CURtext    = CurToStrAlt(money(rs.value("t_amount")), rub, kop, 810); 
            ПечатьИзвещения(rs.value("t_receiveraccount"),rs.value("t_receivername"),money(rs.value("t_amount")), SubStr(CURtext, StrLen(rub)+1, Index(CURtext,kop)-Strlen(rub)-1), CURtext, rs.value("t_paymentid"), true, pdate, NeedPrintLog);
         end;
      end;
      /* EVG 19/10/2011 Если счёт нерезидента - получатель, то просто добавим 
        уведомление в базу без вывода на печать (это нужно для оплаты комиссий). */
      if( ISPayerNerezAcc( rs.value("t_receiveraccount") ) )
         InsTrnsfNotify( rs.value("t_paymentid"));
      end;
   end;

   if ((NeedPrintLog) or (valType(NeedPrintLog) == V_UNDEF))
     viewfile(fulloutput);
   end;
   
end;