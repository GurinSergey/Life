/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   EVG Макрос выполнения шага валютного контроля для рублёвых платежей подсистемы
       АРМ Позиционера.

++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/
/*SDA 20.12.2013 адаптация дельты дистрибутива для 31-й сборки */
import CommissLib , "fg_Life_parm.mac";
import OprInter, oralib; //Jushmanov 2014-02-20 C-19151

var PaymentObj:RsbPayment;

private const  fgBank = fg_life_subject({OurBank}); //I-00426189 18.09.13

MACRO ExecuteStep( doc, paymDoc, p1, p2,  Id_step )
    var stat:integer = 0;

    /* EVG Выбор тарифов комиссий за ВК и сохранение в примечании */
    if ( not fgBank.is_go )
        stat = Execute_CC_CommissionChoice( PaymentObj, Id_step );
    end;

    /* EVG */
    if (stat == 0)
        msgbox ("Документ проконтролирован ВК++");
    end;

    /*Восстановлени FuturePayerAccount после исправлений в списке открытых*/
    var cmd = RSDCommand("select ad.t_account_receiver" +
                         "  from dpmdocs_dbt pm, dacctrn_dbt ad " +
                         " where pm.T_ACCTRNID = ad.T_ACCTRNID " +
                         "   and pm.t_paymentid = ? " 
                        );
    cmd.addparam("pmid", RSDBP_IN, PaymentObj.PaymentID);
    var rs = RSDRecordset(cmd);

    if (rs.movenext)
        PaymentObj.FuturePayerAccount = rs.value(0);
    end;

    return stat;
END;


macro PostStepAction( message,     /* 1 - выполнение шага; 2 - откат шага;  */
                      errTrn,      /* статус выполнения шага. Если параметр */
                                   /* не равен 0, произошла ошибка          */
                      FirstDoc,    /* указатель на первичный документ       */
                      ID_Oper,     /* внутренний идентификатор операции     */
                      Number_Step, /* номер шага операции                   */
                      KindOper,    /* номер вида операции                   */
                      KindDoc,     /* номер вида первичного документа       */
                      KindStep,    /* вид шага операции                     */
                      ID_Step )    /* идентификатор шага операции           */
    var SQL, cmd;
    private var logquery;

    /*FIV при откате удаляем комиссии за ВК*/
    if((message== OP_BACKOUT_STEP) AND (errTrn == 0))
        SQL = " DELETE   usr_trnsf_comiss utc ";
        SQL = SQL + " WHERE   utc.id_step = ? ";
        SQL = SQL + "         AND utc.notify_num = (SELECT   LTRIM (oo.t_documentid, '0') ";
        SQL = SQL + "                                 FROM   doproper_dbt oo ";
        SQL = SQL + "                                WHERE   oo.t_id_operation = ?) ";
        cmd = rsdcommand(sql);
        cmd.AddParam("", RSDBP_IN,ID_Step);
        cmd.AddParam("", RSDBP_IN,ID_Oper);
        cmd.Execute;
    end;

    //Jushmanov 2014-02-20 C-19151
    if(( message == OP_EXECUTE_STEP ) and (errTrn == 0) and (IsOprMultiExec))
        if ((valtype(ID_Oper) != V_INTEGER) and (valtype(ID_Oper) != V_STRING)) ID_Oper = 0; end;
        if ((valtype(ID_Step) != V_INTEGER) and (valtype(id_Step) != V_STRING)) ID_Step = 0; end;

        logquery = "UPDATE usr_doprstep_robot_dbt SET t_is_robot_step = chr(88)" +
                   " WHERE t_id_operation = " + ID_Oper + " AND t_id_step = " + ID_Step;
        ExecSQL(logquery, null, false);
    end;
    
    return 0;
end;
