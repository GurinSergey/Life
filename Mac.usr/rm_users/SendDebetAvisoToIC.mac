// -------------------------------------------------------------------------------------------------
// @filename: SendDebetAvisoToIC.mac
// @author  : 2013-04-25 Teleshova A. 
// @desc    : Отправка дебетовых авизо (SW MT103) из примечания 151 к платежу в Интернет-Клиент
// @changes : none
// -------------------------------------------------------------------------------------------------

import CTInter;
import "lib_iclient.mac", "lib_compare.mac", "lib_const.mac"; 

macro SendAllAviso (send_date)
   var sql_info, sql_cnt, sql_where, rsdcmd, rsl, cnt = 0;
   record pm(pmpaym);
   //Отбираются платежи, для которых категория "Выгружено авизо СВИФТ в ИК" не равна "Выгружено" - исключаем дублирование выгрузки.
   sql_cnt =  " SELECT   COUNT(1) as amount ";
   sql_info = " SELECT   PM.T_PAYMENTID as payment, PM.T_PAYER AS payer, UTL_RAW.cast_to_varchar2 (nt.t_text) AS MESSAGE ";
   sql_where =    " FROM   dnotetext_dbt nt, dpmpaym_dbt pm " +
                  " WHERE       PM.T_VALUEDATE = ? " +
                  "         AND PM.T_DOCKIND = 202 " +
                  "         AND nt.t_documentid = LPAD (TO_CHAR (PM.T_PAYMENTID), 10, '0') " +
                  "         AND nt.t_objecttype = 501 " +
                  "         AND t_notekind = 151 " +
                  "         AND NOT EXISTS " +
                                 " (SELECT   1 FROM   dobjatcor_dbt OBJ" +
                                 "  WHERE   OBJ.T_OBJECTTYPE = 501 " +
                                 " AND OBJ.T_OBJECT = LPAD (TO_CHAR (PM.T_PAYMENTID), 10, '0') " +
                                 " AND OBJ.T_GROUPID = 1017 " +
                                 " AND OBJ.T_ATTRID = 1) ";
   sql_cnt = sql_cnt + sql_where;
   sql_info = sql_info + sql_where;
   rsdcmd = RSDCommand(sql_cnt);
   rsdcmd.AddParam("date",  RSDBP_IN, send_date);
   rsl = RSDRecordset(rsdcmd, null, RSDVAL_STATIC);
   if( rsl and rsl.movenext())
      InitProgress (int(rsl.Value("amount")),"Отправка дебетовых авизо SWIFT в Интернет-Клиент","Отправка дебетовых авизо SWIFT в Интернет-Клиент... Ждите...");
      rsdcmd = RSDCommand(sql_info);
      rsdcmd.AddParam("date",  RSDBP_IN, send_date);
      rsl = RSDRecordset(rsdcmd, null, RSDVAL_STATIC);
      while(rsl.movenext())
         IC_SendMessage(rsl.value("payer"),"Сообщение банка",PutDoubleQoute(rsl.value("message")));
         //После выгрузки устанавливаем категорию "Выгружено авизо СВИФТ в ИК" как "Выгружено"
         pm.PaymentId = rsl.value("payment");
         ConnectObjAttr(null, OBJTYPE_PAYMENT, UniID(pm,OBJTYPE_PAYMENT), PM_USR_CAT_IC_AVISO_UPLOAD, 1);
         cnt = cnt + 1;
         UseProgress (cnt);
      end;
      RemProgress (cnt);
   end;
   msgbox("Всего отправлено ", cnt, " сообщений");
end;


var send_dt = {curdate} - 1;
GetDate(send_dt, "Введите дату, за которую необходимо отправить дебетовые AVISO SWIFT");
SendAllAviso(send_dt);