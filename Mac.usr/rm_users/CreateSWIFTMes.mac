// -------------------------------------------------------------------------------------------------
// @filename: CreateSWIFTMes.mac
// @author  : Teleshova 15.11.2013 C-22504
// @desc    : Создание SWIFT сообщения на основе валютного документа и примечания 122 (сообщение RS-Payments)
// @changes : none
// -------------------------------------------------------------------------------------------------
import "swiftout.mac";
//Константы
const SARATOV_BIC   = "046311808";
const VOLGOGRAD_BIC = "041806835";
const VORONEZH_BIC  = "042007755";
const STAVROPOL_BIC = "040702756";
const ULYANOVSK_BIC = "047308902";

//Сформировать первую часть сообщения: Basic Header Block  
macro GenerateBasicHeaderBlock()
   var BankCode, Destination, BranchCode, НомерТерминала, НомерСессии, ISN, BasicHeaderBlock;
   var error; 
   BankCode = ПолучитьКодСубъекта( {OurBank}, PTCK_SWIFT, error );
   if( error ) ErrExport( "Не найден SWIFT код отправителя сообщения! " );
      return FALSE;
   end;
   Destination = SubStr(BankCode,1,Len_BIC_Destination);
   if(StrLen(Destination) != Len_BIC_Destination)
      ErrExport( "Неправильно указан Отправитель сообщения (BIC)!" );
      return FALSE;
   end;
   BranchCode = SubStr(BankCode,Len_BIC_Destination+1,Len_BIC_BranchCode);
   if( (BranchCode == "")AND
       (StrLen(BranchCode) != Len_BIC_BranchCode) ) 
      BranchCode = MkStr(CodeFor(СимволBICПоУмолчанию),Len_BIC_BranchCode);
   end;
   НомерТерминала = НомерТерминалаПоУмолчанию;
   НомерСессии = НомерСессииПоУмолчанию;
   ISN = ISNПоУмолчанию;
   BasicHeaderBlock = КодНачалаБлока     + НомерБлокаBasicHeader + КодРазделительНомера + КодПриложенияFIN + 
                      НомерПриложенияFIN + Destination           + НомерТерминала       + BranchCode       + 
                      НомерСессии        + ISN                   + КодКонцаБлока;
   return BasicHeaderBlock;
end;

// Сформировать блок 2 заголовка Application Header Block
macro GenerateApplicationHeaderBlockMIR(ReceiverBankId)
   var BankCode, Destination, BranchCode, Delivery, Priority, НомерФормы, Категория, ApplicationHeaderBlockMIR;
   var error;
   BankCode = ПолучитьОткрытыйКодСубъекта( ReceiverBankId, PTCK_SWIFT, error );
   Destination = SubStr(BankCode,1,Len_BIC_Destination);
   if(StrLen(Destination) != Len_BIC_Destination) 
      ErrExport( "Неправильно указан Получатель сообщения (BIC)!" ); 
      return FALSE;
   end;
   BranchCode = MkStr(CodeFor(СимволBICПоУмолчанию),Len_BIC_BranchCode);
   /*BranchCode = SubStr(BankCode,Len_BIC_Destination+1,Len_BIC_BranchCode);
   if((BranchCode ==  "") AND
      (StrLen(BranchCode) != Len_BIC_BranchCode)) 
      BranchCode = MkStr(CodeFor(СимволBICПоУмолчанию),Len_BIC_BranchCode);
   end;*/
   Priority = КодПриоритетНормальный;
   Delivery = "";
   НомерФормы = "103";
   ApplicationHeaderBlockMIR = КодНачалаБлока       + НомерБлокаApplicationHeader + КодРазделительНомера   + 
                КодВходящего + НомерФормы           + "0000"                      + "000000"               +
                Destination  + СимволBICПоУмолчанию + BranchCode                  + НомерСессииПоУмолчанию +
              ISNПоУмолчанию + "000000"             + "0000"                      + Priority               +
              Delivery       + КодКонцаБлока;
   return ApplicationHeaderBlockMIR;
end;

// Сформировать блок 3 заголовка User Header Block 
macro GenerateUserHeaderBlock()
   var BlockStandart = "", UserHeaderBlock;
   UserHeaderBlock = КодНачалаБлока + НомерБлокаUserHeader + КодРазделительНомера   + КодНачалаБлока + 
                     НомерПоляMUR   + КодРазделительНомера + ЗначениеMURПоУмолчанию + КодКонцаБлока  +
                     BlockStandart  + КодКонцаБлока;
   return UserHeaderBlock;
end;

// Сформировать блок 4 - текст сообщения 
macro GenerateTextBlock(PaymentId)
   var TextBlock = КодНачалаБлока + НомерБлокаText + КодРазделительНомера + "\n";
   record pmpaym(pmpaym);
   ClearRecord(pmpaym);
   pmpaym.PaymentID = PaymentID;
   TextBlock = TextBlock + ReadNoteForObject( OBJTYPE_PAYMENT, makeObjectID(OBJTYPE_PAYMENT, NULL, pmpaym), 122);
   TextBlock = TextBlock + "\n" + КодКонцаТекстовогоБлока + КодКонцаБлока;
   return TextBlock;
end;

macro GenerateSwiftMessage(ReceiverBankId_, BICBankRecCode, PaymentId_)
   debugbreak;
   var result = GenerateBasicHeaderBlock() +
                GenerateApplicationHeaderBlockMIR(ReceiverBankId_) +
                GenerateUserHeaderBlock() + 
                GenerateTextBlock(PaymentId_) +
                "{5:{MAC:0301C818}{CHK:22194F0FE4F0}}";
   var timestapm;
   var path = RSL_GetRegistryValue ("PRBB/SWIFT_EXV");
   timestapm = strsubst(string(date(),"_",time()),":","_");
   timestapm = strsubst(timestapm,".","_");
   //определяем каталог выгрузки
   if (BICBankRecCode == VOLGOGRAD_BIC)
      path = path + "\\exv_volg\\";
   elif (BICBankRecCode == VORONEZH_BIC)
      path = path + "\\exv_vor\\";
   elif (BICBankRecCode == STAVROPOL_BIC)
      path = path + "\\exv_stav\\";
   elif (BICBankRecCode == ULYANOVSK_BIC)
      path = path + "\\exv_ul\\";
   end;
   SetOutPut(path + timestapm +".txt", false);
   println(result);
   SetOutput(null, true);
end;