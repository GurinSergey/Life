//--------------------------------------------------------------------------------------------------
// Печать кредитового авизо (SW MT103) из примечания 150 к платежу
// 2011-06-15 zip_z.
// TAM 29.12.2011 I-00137181-2 - испарвлены неверные параметры, которые передаются в функцию ОтправкаСообщения
// TAM 10.01.2012 I-00137181-4 - форматирование строки сообщения: экранирование одинарный кавычек
//--------------------------------------------------------------------------------------------------

import prpm, lib_compare;
import oralib, likepy;
import BankInter;
import "Send_IC_text.mac"; // 2011-07-07 zip_z. C-3501 

macro Note150Exists ( objectID, note, noteKind )
    var sql = "select t_noteKind, utl_raw.cast_to_varchar2 (t_text) as message from dnotetext_dbt where t_objecttype = 501"
                "  and t_notekind in (150, 151) and t_documentid = lpad (to_char (:paymentID), 10, '0') and rownum = 1";
    sql = ExecSQLSelect (sql, makeArray (SQLParam ("paymentID", objectID)));
    if (sql and sql.moveNext ())
        setparm (1, sql.value ("message"));
        setparm (2, sql.value ("t_notekind"));
        return true;
    end;
    
    return false;
end;

/* Более быстрый аналог дистрибутивного ПолучитьКодСубъекта */ 
macro getPTCode (partyid :integer)
    var sql = ExecSQLSelect ("select t_code from dobjcode_dbt  where t_codeKind = 1 and t_objectType = 3  and t_objectID = :partyID and t_state = 0",
                            makeArray (SQLParam ("partyid", partyid)));
    if (sql and sql.moveNext ())
        return sql.value ("t_code");
    end;

    return "";
end;

macro PrintDocument (ncopy)
    var note;
    var noteKind;
    if (note150Exists ( pr_pmpaym.rec.paymentID, note, noteKind ))
        while( ncopy )
            println (note);
            ncopy = ncopy - 1;
        end;

        // 2011-07-07 zip_z. C-3501 | begin --------------------------------------------------------
        if (StrBrk (strfor (GetIdentProgram ()), "ЧНУ") != 0) /* !!! отправляем сообщения только при вызове макроса печати из п/с "РКО", "ББ", "АРМ" */
            var needSendIBNotify = false;
            GetTrue (needSendIBNotify, "Отправить сообщение в Интернет-банк?");
            if (needSendIBNotify)
            //TAM 10.01.12 I-00137181 - необходимо заэкранировать одинарные кавычки в тексте сообщения
            note = PutDoubleQoute(note);
                
                if (noteKind == 150) /* входящий платёж: отправляем сообщение по получателю */
                    //TAM 29.12.2011 I-00137181-2
                    //ОтправкаСообщения(note, pr_pmpaym.rec.paymentID, 1, getPTCode (pr_pmpaym.rec.receiver));
                    ОтправкаСообщения(note, pr_pmpaym.rec.receiver, 1, getPTCode (pr_pmpaym.rec.receiver));
                end;

                if (noteKind == 151) /* исходящий платёж: отправляем сообщение по плательщику */
                    //TAM 29.12.2011 I-00137181-2
                    //ОтправкаСообщения(note, pr_pmpaym.rec.paymentID, 1, getPTCode (pr_pmpaym.rec.payer));
                    ОтправкаСообщения(note, pr_pmpaym.rec.payer, 1, getPTCode (pr_pmpaym.rec.payer));
                end;

            end;

        end;
        // 2011-07-07 zip_z. C-3501 | end ----------------------------------------------------------

    else
        println ("Кредитовое авизо не было сформировано");
    end;
    return true;
end;