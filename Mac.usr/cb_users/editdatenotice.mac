/*********************************************************************/
/*  Изменение даты и основания открытых мем. ордеров                 */
/*  Коркин Иван                                								*/
/*  14.09.2010                                 								*/
/*  Реализовано по заявке I-059189.                                  */
/*********************************************************************/

import rsbdataset, bankinter, globals, календарь, "KeyCodes.mac", rsd;

var beginDate, newNotice;
var documentid, datee, notice1 = "";

var Fulloutputl, outl, outputl="datenotice.lbr";                    
GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,outl);
Fulloutputl = FindPath(outputl, outl);
if (not Fulloutputl)
	msgbox("Не найдена LBR");
	exit();
end;

var dlg = TRecHandler("date", fulloutputl, TRUE); 

 /*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key) 
   var const_mess = " ~F3~ Выбор значения из списка ~F2~ Продолжить ~ESC~ Выход ",
   SQLQuery, SQLQuery1, rsrec, cmd1;
   /*Первоначальная инициализация полей*/
   if(cmd == DLG_INIT)
      dlg.rec.Dates = datee;
      BeginDate  = dlg.rec.Dates;
      dlg.rec.text1 = notice1;
      newNotice = dlg.rec.text1;
      UpdateFields(dlg); 
   end;
   /*Установка подсказок в строке состояния*/
   if (cmd==DLG_SETFOCUS)
      message(const_mess);
   end;
   if (cmd == DLG_REMFOCUS)
      /*Проверка корректности дат отчета*/
      UpdateFields(dlg); 
   end;
   if (cmd == DLG_KEY)
      /*Выход из диалогового окна формирования отчета*/
      if (KEY == KEY_ESC)
         //return exit;//
         return CM_CANCEL;
      /*Выбор данных из списка*/
      elif ( KEY == KEY_F3)
         if (FldName(dlg,id) == "Dates")
            dlg.rec.Dates = GetDateByCalendar ({curDate}-1);
         end;
      elif (( KEY == KEY_F2 ) or (KEY == KEY_ENTER))         //Проверки при вводе
         if ( dlg.rec.Dates > {curDate} )
            MsgBox("Дата начала периода должна быть больше текущей даты");
            return CM_IGNORE;
         end;
         if ( dlg.rec.text1 == "" )
            MsgBox("Основание не должно быть пустым");
            return CM_IGNORE;
         end;
         BeginDate  = dlg.rec.Dates;
         newNotice = dlg.rec.text1;
         Return CM_SAVE;
      end;
   else
      Return CM_IGNORE;
   end;
END;

macro calculate(documentid, datee, notice)
   var query, data, rs, edit;
   edit = false;
   if (newNotice != notice)
      query = "update dpmrmprop_dbt set t_ground=? where t_paymentid=?";   
      rs = rsdcommand(query);
      rs.AddParam("", RSDBP_IN, newNotice);
      rs.AddParam("", RSDBP_IN, documentid);
      rs.execute();
      edit = true;
	end;
	if (beginDate != datee)
      query = "update dpmpaym_dbt set t_valuedate = ?"
      + "\n" + "where t_paymentid = ?";
      rs = rsdcommand(query);
      rs.AddParam("", RSDBP_IN, beginDate);
      rs.AddParam("", RSDBP_IN, documentid);
      rs.execute();
      edit = true;
   end;
   if (edit)
      rs = rsdcommand("commit");
      rs.execute();
   end;
end;

/*Точка входа в макрос*/
macro editDateNotice(vdocumentid, vdatee, vnotice)
	documentid = vdocumentid;
	datee = vdatee;
	notice1 = vnotice;
   if (RunDialog(dlg, "Event"))                  
      calculate(documentid, datee, notice1);
   end; 
end;




