/* Вспомогательные функции
    
    CLASS RslScroll - для построения скроллинга
        Class (наследник RslScroll'a) clPrimScr //Скроллинг примечаний платежа

    ИсправитьРеференсы; - Вызывается из bilsfgen.mac
        Class (наследник RslScroll'a) clGenRefScr //Скролинг счетов фактур без номера
  (с) Гончарук Серж (Diver)

  02.07.2013 GSP R-212539 Добавлена обработка ошибки при чтении реестровой настройки
// KS 22.11.2013 Предварительная адаптация
*/

import rslx;
import oralib,likepy;
import BankInter;
import "bilsfnum.mac";

var Sobitie = ""; //UpdateScroll работает крайне фигово. Приходиться при обновлении закрывать окно и открывать его вновь -(
var TekMestoKur = "";

/*Базовый класс*/
CLASS RslScroll
   private var colInfo;

   private macro numCol
      if (colInfo)
         return (colInfo.size / 6)
      end
   end;    


   macro AddCol (ind, fld, head, width, fldType,decPoint)
      if (not colInfo)
         colInfo = TArray;
      end;
      colInfo.value (ind * 6)     = fld;
      colInfo.value (ind * 6 + 1) = head;
      colInfo.value (ind * 6 + 2) = width;
      colInfo.value (ind * 6 + 3 ) = fldType; 
      colInfo.value (ind * 6 + 4 ) = decPoint;
      colInfo.value (ind * 6 + 5 ) = 0;   // reserv
   end;
        
   
   macro ObrabScr (rs, cmd, id, key)
   end;
   
   macro Run (rs,name,Head, StLn, rdOnly)
      return RunScroll (rs, numCol, colInfo,name, R2M(this,"ObrabScr"), Head, StLn, rdOnly);
   end;
      
END;

Class (RslScroll) clPrimScr //Скроллинг примечаний Платежа

   AddCol(0,"T_NOTEKIND","N",null,2);
   AddCol(1,"T_NAME","Вид примечания",null,2);
   AddCol(2,"T_TEXT","Примечание");
   InitRslScroll; 

   
   MACRO ObrabScr (rs, cmd, id, key)
    var TextNote, TextPrompt, len = null;
    var upd_q, command;

//     msgbox("cmd=" + cmd +" id="+id+" key="+key);
     If ((TekMestoKur != "") and (cmd == 16) and (id ==0) and (key == 0) )//спозиционируемся на записи, которая изменялася при прошлом вызове рунскролла
       /*
         Опытным путем было установлено что cmd=16,id=0,key=0 возникает однократно и при запуске окна скроллинга.
         На самом деле изврщаться с позиционированием на нужной записи пришлось из-за некоректной работы updatescroll.
       */
       while (rs.MoveNext()) 
         if (rs.value("T_NOTEKIND") == TekMestoKur)
          break;
         end;
       end;
       GoToScroll(rs);
       TekMestoKur = ""; 
     End;


     If (cmd == DLG_KEY)
        if (key == 27) //ESC                                                                                                              
                Sobitie = "ESC"; //Выйти из ранскрлла навсегда!                                                                                  
                return CM_CANCEL;                                                                                                                
           elif (key == 13) //Enter                                                                                                         
                 //Подготовка перед запросом примечания...                                                                                       
                 TextNote = rs.value(2);                                                                                                         
                 if (trim((rs.value(2)) == "") or (ValType(rs.value(2)) == 26)) //26 - SPECVAL                                                   
                    TextPrompt = "Добавление примечания: |" + rs.value(0) + "." + rs.value(1);                                                    
                    else                                                                                                                          
                     TextPrompt = "Редактирование примечания: |" + rs.value(0) + "." + rs.value(1);                                               
                 end;                                                                                                                            
                 if (rs.value(4) != 0)                                                                                                           
                    len = rs.value(4); //Ограничить по длине(и кол-ву вводимых символов) поля                                                      
                 end;                                                                                                                            
                 //Получить новое значение                                                                                                       
                 if (rs.value(3) == 18) //Тип поля NUMSTR                                                                                        
                        GETSTRINGR(TextNote,TextPrompt,len);                                                                                          
                    elif (rs.value(3) == 9) //Тип поля DATE                                                                                        
//                        msgbox("Изменение примечаний типа DATE по CTRL+Z не предусмотрено");
                      TextNote = Date(TextNote);
                      GETDATE(TextNote,TextPrompt);                                                                                           
//                      msgbox(valtype(""+ TextNote +""));
                    elif (rs.value(3) == 0) //Тип поля INT
                        if (ValType(rs.value(2)) != 26)
                          TextNote = Int(TextNote);
                        end;
                        GETINT(TextNote,TextPrompt,len);                                                                                         
                    elif (rs.value(3) == 4) //Тип поля DOUBLE
                        if (ValType(rs.value(2)) != 26)
                          TextNote = Double(TextNote);
                        end;
                        GETDOUBLE(TextNote,TextPrompt,len);                                                                                         
                    elif (rs.value(3) == 25) //Тип поля MONEY
                        if (ValType(rs.value(2)) != 26)
                          TextNote = Money(TextNote);
                        end;
                        GETMONEY(TextNote,TextPrompt,len);                                                                                         
                     else                                                                                                                           
                       GETSTRING(TextNote,TextPrompt,len);                                                                                           
                 end;                                                                                                                            
//------------------------------------------
                 //-----добавить------
                 IF ((TextNote != "") and (ValType(rs.value(2)) == 26)) //26 - SPECVAL
//                   msgbox("Пробую добавлять=" + TextNote + " Тип: " + ValType(TextNote) + "/" + rs.value(3));
                   addNoteForObject(501,rs.value("T_DOCUMENTID"),rs.value("T_NOTEKIND"), TextNote);
                 END;
                 //-----изменить------ 
                 IF ( TextNote != rs.value("T_TEXT") ) 
                   addNoteForObject(501,rs.value("T_DOCUMENTID"),rs.value("T_NOTEKIND"), TextNote);
                 END;
//------------------------------------------
                                                                                                                                        
//          UpdateFields(rs);                                                                                                             
            TekMestoKur = rs.value("T_NOTEKIND");                                                                                           
            return CM_CANCEL; //Выйдем из скроллинга, но он вновь запустится                                                                
          elif (key == 322) //F8                                                                                                         
//--------------------------------------------
                 //-----удалить
                 IF ( GetTrue(false,"Удалить примечание: " + rs.value("T_TEXT")+ "?") );
                   RemoveNoteForObject(501,rs.value("T_DOCUMENTID"),rs.value("T_NOTEKIND"));                                                      
                 END;
//--------------------------------------------
            TekMestoKur = rs.value("T_NOTEKIND");                                                                                           
            return CM_CANCEL; //Выйдем из скроллинга, но он вновь запустится                                                                
         end;                                                                                                                              
    End; 
   END;

   macro Run (rs, IDPaym)
     return Run (rs,"Имя окна прокрутки", "Примечания платежа (DOCUMENTID = "+IDPaym+")", "~Enter~ Редактировать ~ESC~ Выход ~F4~ Поиск ~F8~ Удалить ~Alt+ +/-~ Показать кол-во", true );
   end;

End;



/*
 Проверить или входит опер в заданную группу СУД
*/
Macro ВходитВГруппу(Oper,IdGroup)
 var stat = true; 
 var select, recSet, command;
//msgbox({oper});
/*
 If ({oper} == 10246) //Для теста
   return true;
 End;
*/
 select = "SELECT t_name FROM DACSGROUP_DBT WHERE T_GROUPID = " + IdGroup;
 command = RSDCommand(select);
 command.execute();
 recSet = RSDRecordSet(command, RSDVAL_CLIENT, RSDVAL_STATIC);
 if (recSet and recSet.moveNext())
   select = "select * from DACSGROUPOPER_DBT where T_GROUPID = "+ IdGroup +" and T_OPER = " + Oper;
   command = RSDCommand(select);
   command.execute();
   recSet = RSDRecordSet(command, RSDVAL_CLIENT, RSDVAL_STATIC);
   if (not(recSet and recSet.moveNext()))
    stat = false;
   end;
  else 
   msgbox("Группа с номером " + IdGroup + " не найдена");
   stat = false;
 end;
 return stat;
End;

/*Позвать скроллинг на редактирование примечаний платежа*/
Macro ВывестиПримечанияНаРедактирование(IDPaym)
        var ID_PRIM = "";
        var select, where, orderby;
        var cmd, recSet;
        var col = TArray;
        private var errorcode;
 GetRegistryValue("PRBB\\ПРИМЕЧАНИЯ ДЛЯ РЕДАКТИРОВАНИЯ", V_STRING, ID_PRIM, errorcode);  //GSP по R-212539-2 добавил обработку кода ошибки

 WHILE (Sobitie != "ESC") //Будем переоткрывать сроллинг, что бы на экране были новые значения при изменении (UpdateScroll не применял так как NOTETEXT и чота глючит)
/* select = "select NK.T_NOTEKIND, NK.T_NAME, " + 
          " case " +
          "  when NK.T_NOTETYPE = 0 and NT.T_TEXT is not null then " +
          "   to_char(utl_raw.cast_to_binary_integer(utl_raw.substr(NT.t_text,1,4), 3)) " +
          "  when NK.T_NOTETYPE = 4 and NT.T_TEXT is not null then " +
          "   trim(to_char(power(2, (to_number(substr(utl_raw.reverse(utl_raw.substr(NT.t_text, 1, 8)),1,3),'FMXXXXXXXXXXXXXXXX')-1075))*to_number('1'||substr(utl_raw.reverse(utl_raw.substr(NT.t_text, 1, 8)),4,13),'FMXXXXXXXXXXXXXXXX'),'9999999999999D999999')) " +
          "  when NK.T_NOTETYPE = 9 and NT.T_TEXT is not null then " +
          "   lpad(UTL_RAW.cast_to_binary_integer(UTL_RAW.SUBSTR(t_text, 1, 1)),2,'0')||'.'||lpad(UTL_RAW.cast_to_binary_integer(UTL_RAW.SUBSTR(t_text, 2, 1)),2,'0')||'.'||UTL_RAW.cast_to_binary_integer(UTL_RAW.SUBSTR(t_text, 3, 2),2) " +
          "  else  " +
          "   utl_raw.cast_to_varchar2(NT.T_TEXT) " +
          " end as T_TEXT, " +
          "        NK.T_NOTETYPE, NK.T_MAXLEN, NT.T_ID, lpad('" + IDPaym + "',10,'0') as T_DOCUMENTID " + 
          " from dnotekind_dbt nk, dnotetext_dbt nt ";
*/

 select = "select NK.T_NOTEKIND, NK.T_NAME, USR_NOTETEXT.ConvertRAWtoSTRING(NT.T_TEXT, NK.T_NOTETYPE) T_TEXT, NK.T_NOTETYPE, NK.T_MAXLEN, NT.T_ID, lpad('" + IDPaym + "',10,'0') as T_DOCUMENTID " + 
          " from dnotekind_dbt nk, dnotetext_dbt nt ";

 where  = " WHERE NK.T_NOTEKIND = NT.T_NOTEKIND (+) and nk.T_OBJECTTYPE = 501" +
          " and NT.T_DOCUMENTID (+) = lpad('" + IDPaym + "',10,'0') ";

// if ((ID_PRIM != "") and (ID_PRIM))                                 //GSP по R-212539-2 добавил обработку кода ошибки и вывод инфо по проблеме
 if((errorcode == 0) and (ID_PRIM != ""))
  where = where + "and NK.T_NOTEKIND in (" + ID_PRIM + ")";
 else
   msgbox("обратитесь к администратору АБС для заполнения \n           настройки реестра: \n PRBB\\ПРИМЕЧАНИЯ ДЛЯ РЕДАКТИРОВАНИЯ ");
   return 0;
 end;

 orderby = " order by NK.T_NOTEKIND";

 cmd = RsdCommand(select + where + orderby);
 recSet = RSDRecordset( cmd , RSDVAL_CLIENT, RSDVAL_STATIC );

 clPrimScr.Run(recSet, IDPaym);//Вызвать скроллинг по отбираемым записям.
 END;
 Sobitie = "";
 TekMestoKur = "";
End;


Class (RslScroll) clGenRefScr //Скролинг счетов фактур без номера
   AddCol(0,"T_FACTURANUMBER","Присвоенный номер",null,2);
   AddCol(1,"T_CREATIONDATE","Дата создания",null,2);
   AddCol(2,"T_TOTALAMOUNT","Сумма",null,2);
   AddCol(3,"t_fi_code", "Вал",null,2);
   AddCol(4,"t_receivername", "Контрагент", null,2);
   InitRslScroll; 

   
   MACRO ObrabScr (rs, cmd, id, key)
       If (cmd == DLG_KEY)
//        msgbox("cmd=" + cmd +" id="+id+" key="+key);
        if (key == 27) //ESC                                                                                                              
                return CM_CANCEL;                                                                                                                
        end;
       End;
   END;

   macro Run (rs)
     return Run (rs,"Имя окна прокрутки", "Счета фактуры, которые не имели номеров", "~ESC~ Выход ~F4~ Поиск ~Alt+ +/-~ Показать кол-во", true );
   end;

End;


MACRO  ИсправитьРеференсы; 
     var select, where, orderby, upd;
     var where_id = "", where_fnumb = "";
     var found = false;
     var cmd, recSet;
     var i=0, col_factur;
     var arrFid:TArray = TArray();
     var arrFdate:TArray = TArray();
     var TxtPath, eCode;
     GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, TxtPath, eCode);
     if ( eCode > 0 )
        TxtPath = "..\\TxtFile";
     end;
     var NameFile_monValueForRef = TxtPath + "\\monValueForRef." + UserNumber;
     FILE monValueForRef() txt write;
//     msgbox(NameFile_monValueForRef);

     select = "SELECT t.t_facturaid, T.T_FACTURANUMBER, T.T_CREATIONDATE, T.T_TOTALAMOUNT, fi.t_fi_code, t.t_receivername " +
              " FROM DBILFACTURA_DBT T, DFININSTR_DBT FI ";

     where  = " WHERE t.t_fiid = fi.t_fiid AND t.t_sftypeid = 1  ";
     where_fnumb = " AND t.t_facturanumber = chr(1) ";
     orderby = " ORDER BY t.t_creationdate ";

     cmd = RsdCommand(select + where + where_fnumb + orderby);
     recSet = RSDRecordset( cmd , RSDVAL_CLIENT, RSDVAL_STATIC );

     where_fnumb = " AND t.t_facturaid in (-1";
     while (recSet.moveNext)
      found = true;  
      where_fnumb = where_fnumb + "," + recSet.value("t_facturaid");
      arrFid[i] = recSet.value("t_facturaid");
      arrFdate[i] = recSet.value("t_creationdate");
      i = i + 1;
     end; 
     col_factur = i;
     i = 0;
     if (found)
           If (gettrue(true, "Обнаружены с/ф, которые не имеют номеров. \nСгенерировать для таких фактур номера?"))
            while (i < col_factur)
                  /*Через текстовый файл передадим дату вводимую в форме (поле Дата счета-фактуры)*/
                  /* Заявка A51181*/
                  open(monValueForRef,NameFile_monValueForRef);
                  insert(monValueForRef,substr(string(arrFdate[i]),1,10)); 
                  close(monValueForRef);
                  /**/
                upd =     "UPDATE DBILFACTURA_DBT SET t_facturanumber = '"
                          + GetRefSF()  +"'" + 
                          " WHERE t_facturanumber = chr(1) AND t_facturaid = " 
                          + arrFid[i];
//              msgbox(GetRefSF());
                i = i + 1;
                cmd = RsdCommand(upd);
                cmd.execute();
                DelFile(NameFile_monValueForRef); // KS 03.05.2011 Удаляю буферный файл
            end;
           End;
           where_fnumb = where_fnumb + ")";
           cmd = RsdCommand(select + where + where_fnumb + orderby);
           recSet = RSDRecordset( cmd , RSDVAL_CLIENT, RSDVAL_STATIC );
           clGenRefScr.Run(recSet);//Вызвать скроллинг по отбираемым записям.
     end;
  return found;

END;

/*ОЮФ доработал по заявкам I-049836, I-059899*/
/*После вставки перед печатью проверяем КАЖДУЮ фактуру на наличие номера*/
/*Если номера нет - генерим и update'им, а потом показываем в скроллинге*/

MACRO  ИсправитьРеференс(FacID); 
     var select, where, upd, where_fnumb = "";
     var cmd, recSet;
     var found = false;
     var TxtPath, eCode;
     GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, TxtPath, eCode);
     if ( eCode > 0 )
        TxtPath = "..\\TxtFile";
     end;
     var NameFile_monValueForRef = TxtPath + "\\monValueForRef." + UserNumber;
     FILE monValueForRef() txt write;

     select = "SELECT t.t_facturaid, T.T_FACTURANUMBER, T.T_CREATIONDATE, T.T_TOTALAMOUNT, fi.t_fi_code, t.t_receivername " +
              " FROM DBILFACTURA_DBT T, DFININSTR_DBT FI ";
                                                                                    /*ОЮФ*/
     where  = " WHERE t.t_fiid = fi.t_fiid AND t.t_sftypeid = 1 AND t.t_facturaid = " + FacID;
     where_fnumb = " AND t.t_facturanumber = chr(1) ";

     cmd = RsdCommand(select + where + where_fnumb);
     recSet = RSDRecordset( cmd , RSDVAL_CLIENT, RSDVAL_STATIC );
     
     while (recSet.moveNext)
      found = true;  
      If (gettrue(true, "Обнаружена с/ф, которая не имеет номера. \nСгенерировать номер?"))
             /*Через текстовый файл передадим дату вводимую в форме (поле Дата счета-фактуры)*/
             /* Заявка A51181*/
             open(monValueForRef,NameFile_monValueForRef);
             insert(monValueForRef,substr(string(recSet.value("t_creationdate")),1,10)); 
             close(monValueForRef);
             /**/
           upd =     "UPDATE DBILFACTURA_DBT SET t_facturanumber = '"
                     + GetRefSF()  +"'" + 
                     " WHERE t_facturanumber = chr(1) AND t_facturaid = " 
                     + recSet.value("t_facturaid");
//              msgbox(GetRefSF());
           cmd = RsdCommand(upd);
           cmd.execute();
           DelFile(NameFile_monValueForRef); // KS 03.05.2011 Удаляю буферный файл
      End;

     end; 
     if (found)
           cmd = RsdCommand(select + where);
           recSet = RSDRecordset( cmd , RSDVAL_CLIENT, RSDVAL_STATIC );
           clGenRefScr.Run(recSet);//Вызвать скроллинг по отбираемым записям.
     end;
  return found;

END;
/*ОЮФ*/


/* EVG 15.02.2011 Функция перенесена из bilsffiziki.mac */
macro UpdateRefOnMem( DocKind, PaymID )
  /*
   Что бы при попытке нажать на мемике Ctrl+F сразу сообщалось что фактура уже есть
   Система при этом ожидает что в dbilreconcile.t_isauto находиццо 'X', но используемая 
   системная функция ставит тута 'Z'
  */
  var updsql = "UPDATE DBILRECONCILE_DBT SET t_isauto = chr(88) " +
               "WHERE t_dockind = ? AND t_docid = ? ";
  var cmd;
  cmd =  RsdCommand( updsql );
  cmd.addParam( "", RSDBP_IN, DocKind );
  cmd.addParam( "", RSDBP_IN, PaymID );
  cmd.execute();
end;


//ВывестиПримечанияНаРедактирование("2054711");
//ИсправитьРеференсы; 