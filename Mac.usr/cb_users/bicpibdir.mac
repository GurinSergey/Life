/*
 * Импорт BICPlusIBAN Directory
 * 2012-08-07 zip_z. Адаптация к реалиям 2030.45.09
 */
Import BankInter, PTInter, rsd, "globals.mac";

file FBICDir("") txt;  /* Текстовый файл импорта */

var fullFileName : string;
var regFileName  : string;
var file_name    : string;
var file_ext     : string;
var file_path    : string;

var nCount : integer;

var stat : bool;

var LoadOnlyBank : integer;
var LoadBiDelta  : integer;

var ImportStringArray : TArray;

var RecompilePackage = false;

/* Очистить временную таблицу */
macro ClearTempTable() : bool
  var cmd : RsdCommand;
  var sqlstr;
  cmd = RsdCommand("TRUNCATE TABLE dbicimp_tmp");
  cmd.execute();
  cmd = RsdCommand("TRUNCATE TABLE dbicplusiban_tmp");
  cmd.execute();
end;

/* Добавить запись во временную таблицу */
macro AddToTempTable(ImportString : string )
  ImportStringArray(ImportStringArray.Size) = ImportString;
end;

/* Добавить запись во временную таблицу */
macro InsertIntoTempTable( Flush : bool ) : bool
  var ins : RsbSQLInsert;
  var stat = true;
  if( Flush == null ) Flush = false; end;
  if((ImportStringArray.Size == 5000) or ((Flush) and (ImportStringArray.Size > 0)))
    ins = RsbSQLInsert("INSERT INTO dbicplusiban_tmp (t_id, t_importstring) VALUES (0, ?)", 1, ImportStringArray.Size );
    ins.AddParam( V_STRING, ImportStringArray, 1100 );
    stat = ins.Insert();
    ImportStringArray.Size = 0;
  end;
  return stat;
end;

/* получить общее количество записей во временной таблице */
private macro GetAllRecCount()
  var rs : RsdRecordset;
  var RecCount : integer;
  RecCount = 0;
  rs = RsdRecordset("SELECT count(1) FROM dbicplusiban_tmp WHERE t_processrecord IN (0, 1)" );
  if( rs.MoveNext() )
    RecCount = rs.value(0);
  end;
  return RecCount;
end;

/* получить количество записей определеннго вида */
private macro GetRecCount( ModificationFlag : string )
  var rs : RsdRecordset;
  var RecCount : integer;
  RecCount = 0;
  rs = RsdRecordset("SELECT COUNT(1) FROM dbicplusiban_tmp WHERE t_processrecord <> -2 AND t_modification_flag = '" + ModificationFlag + "'" );
  if( rs.MoveNext() )
    RecCount = rs.value(0);
  end;
  return RecCount;
end;

/* получить количество добавленных субъектов */
private macro GetNewRecCount()
  var rs : RsdRecordset;
  var RecCount : integer;
  RecCount = 0;
  rs = RsdRecordset("SELECT COUNT(1) FROM dbicplusiban_tmp WHERE t_processrecord = 1" );
  if( rs.MoveNext() )
    RecCount = rs.value(0);
  end;
  return RecCount;
end;

/* получить количество измененных субъектов */
private macro GetCngRecCount()
  var rs : RsdRecordset;
  var RecCount : integer;
  RecCount = 0;
  rs = RsdRecordset("SELECT COUNT(1) FROM dbicplusiban_tmp WHERE t_processrecord = 0 AND t_partyid > 0 AND t_modification_flag IN ('A', 'U', 'M')" );
  if( rs.MoveNext() )
    RecCount = rs.value(0);
  end;
  return RecCount;
end;

/* получить количество измененных субъектов */
private macro GetDelRecCount()
  var rs : RsdRecordset;
  var RecCount : integer;
  RecCount = 0;
  rs = RsdRecordset("SELECT COUNT(1) FROM dbicplusiban_tmp WHERE t_partyid > 0 AND t_modification_flag IN ('D', 'E')" );
  if( rs.MoveNext() )
    RecCount = rs.value(0);
  end;
  return RecCount;
end;

private macro GetAbsRecCount()
  var rs : RsdRecordset;
  var RecCount : integer;
  RecCount = 0;
  rs = RsdRecordset("SELECT COUNT(1) FROM dbicplusiban_tmp WHERE t_partyid > 0 AND t_processrecord = 10" );
  if( rs.MoveNext() )
    RecCount = rs.value(0);
  end;
  return RecCount;
end;

/* печать блока "Введены новые субъекты - участники SWIFT" */
private macro PrintInsertedParty()
  var rs : RsdRecordset;
  var RecCount = 0;
  rs = RsdRecordset("SELECT bic.t_record_key, bic.t_partyid, pt.t_name " +
                    "  FROM dbicplusiban_tmp bic JOIN dparty_dbt pt ON (bic.t_partyid = pt.t_partyid) " +
                    " WHERE bic.t_processrecord = 1 " +
                    " ORDER BY bic.t_record_key"
                    );
  while( rs.MoveNext() )
    if( RecCount == 0 )
[-------------------------------------------------------                           ];
[Введены новые субъекты - участники SWIFT:                                         ];
[                                                                                  ];
    end;
    RecCount = RecCount + 1;
[######## ########## #                                                             ] (rs.value(0), rs.value(1), rs.value(2));
  end;
[                                                                                  ];
end;

/* печать блока "Изменены данные субъектов экономки" */
private macro PrintChangedParty()
  var rs : RsdRecordset;
  var RecCount = 0;
  rs = RsdRecordset("SELECT bic.t_record_key, bic.t_partyid, pt.t_name " +
                    "  FROM dbicplusiban_tmp bic JOIN dparty_dbt pt ON (bic.t_partyid = pt.t_partyid) " +
                    " WHERE bic.t_partyid > 0 AND bic.t_modification_flag IN ('A', 'M', 'U') AND bic.t_processrecord = 0 " +
                    " ORDER BY bic.t_record_key"
                    );
  while( rs.MoveNext() )
    if( RecCount == 0 )
[-------------------------------------------------------                           ];
[Изменены данные субъектов экономки:                                               ];
[                                                                                  ];
    end;
    RecCount = RecCount + 1;
[######## ########## #                                                             ] (rs.value(0), rs.value(1), rs.value(2));
  end;
[                                                                                  ];
end;

/* печать блока "Закрыто участие в SWIFT субъектов экономики" */
private macro PrintClosedParty()
  var rs : RsdRecordset;
  var RecCount = 0;
  rs = RsdRecordset("SELECT bic.t_record_key, bic.t_partyid, pt.t_name " +
                    "  FROM dbicplusiban_tmp bic JOIN dparty_dbt pt ON (bic.t_partyid = pt.t_partyid) " +
                    " WHERE bic.t_partyid > 0 AND bic.t_modification_flag IN ('D', 'E') AND bic.t_processrecord = 0 " +
                    " ORDER BY bic.t_record_key"
                    );
  while( rs.MoveNext() )
    if( RecCount == 0 )
[-------------------------------------------------------                           ];
[Закрыто участие в SWIFT субъектов экономики:                                      ];
[                                                                                  ];
    end;
    RecCount = RecCount + 1;
[######## ########## #                                                             ] (rs.value(0), rs.value(1), rs.value(2));
  end;
[                                                                                  ];
end;

/* печать блока "Отсутствуют в справочнике субъекты экономики (участие в SWIFT закрыто)" */
private macro PrintAbsentParty()
  var rs : RsdRecordset;
  var RecCount = 0;
  rs = RsdRecordset("SELECT bic.t_partyid, pt.t_name " +
                    "  FROM dbicplusiban_tmp bic JOIN dparty_dbt pt ON (bic.t_partyid = pt.t_partyid) " +
                    " WHERE bic.t_partyid > 0 AND bic.t_processrecord = 10 " +
                    " ORDER BY bic.t_partyid"
                    );
  while( rs.MoveNext() )
    if( RecCount == 0 )
[-------------------------------------------------------                           ];
[Отсутствуют в справочнике субъекты экономики (участие в SWIFT закрыто):           ];
[                                                                                  ];
    end;
    RecCount = RecCount + 1;
[######## #                                                                        ] (rs.value(0), rs.value(1));
  end;
[                                                                                  ];
end;

macro GetLoadMode()
  if(LoadOnlyBank)
    return "Только банки";
  else
    return "Все";
  end;
end;

private macro PrintErrDupPartyDetail(PtID, PtName, RecKey)
  array _RecKey;
  var j = 0;
  StrSplit(RecKey, _RecKey, 50, 50, 10 );
  while(StrLen(_RecKey(j)) > 0)
    if(j == 0)
[######## #####################: ################################################## ] (int(PtID), PtName, _RecKey(j));
    else
[                                ################################################## ] (_RecKey(j));
    end;
    j = j + 1;
  end;
end;

/* печать блока "Предупреждение: одному субъекту экономики в БД соответствуют несколько записей файла, применена только последняя:" */
private macro PrintErrDupParty()
  var rs : RsdRecordset;
  var RecCount = 0;
  var PtID = 0;
  var PtName, RecKey;
  rs = RsdRecordset("SELECT bic.t_record_key, ABS(bic.t_partyid), pt.t_name, bic.t_modification_flag " +
                    "  FROM dbicplusiban_tmp bic JOIN dparty_dbt pt ON (ABS(bic.t_partyid) = pt.t_partyid) " +
                    " WHERE bic.t_partyid < 0 AND bic.t_processrecord IN (-1, -2) " +
                    " ORDER BY ABS(bic.t_partyid), bic.t_processrecord DESC "
                    );
  while( rs.MoveNext() )
    if( RecCount == 0 )
[-------------------------------------------------------                           ];
[Предупреждение: одному субъекту экономики в БД соответствуют                      ];
[несколько записей файла, применена только последняя:                              ];
[                                                                                  ];
    end;
    RecCount = RecCount + 1;
    if(PtID == rs.value(1))
      RecKey = RecKey + ", " + rs.value(0) + "(" + rs.value(3) + ")";
    end;
    if(PtID == 0)
      RecKey = rs.value(0) + "(" + rs.value(3) + ")";
      PtID = rs.value(1);
      PtName = rs.value(2);
    end;
    if((PtID != 0) and (PtID != rs.value(1)))
      PrintErrDupPartyDetail(PtID, PtName, RecKey);
      RecKey = rs.value(0) + "(" + rs.value(3) + ")";
      PtID = rs.value(1);
      PtName = rs.value(2);
    end;
  end;
  if(PtID != 0)
    PrintErrDupPartyDetail(PtID, PtName, RecKey);
  end;
[                                                                                  ];
end;

/* Печать протокола */
private macro PrintProtocol( StartTime : time, EndTime : time )
[Протокол выполнения процедуры импорта справочника BICPlusIBAN Directory SWIFT     ];
[                                                                                  ];
[Дата:         ########## г.                                                       ] (date:f);                
[Время:        ######## - ########                                                 ] (StartTime:f, EndTime:f);
[Пользователь: ##### #                                                             ] ({oper}, {Name_Oper});   
[                                                                                  ];
[-------------------------------------------------------                           ];
[Обобщенные результаты импорта:                                                    ];
[                                                                                  ];
[Имя импортированного файла: #                                                     ] (fullFileName:l);
[Всего записей в файле:  ######                                                    ] (nCount:l);
[Вариант импорта: #                                                                ] (GetLoadMode():l);
[Количество обработанных записей:  ######                                          ] (GetAllRecCount():l);
[Количество обработанных записей по типам: A:##### M:###### D:##### U:###### E:####] (GetRecCount("A"):l, GetRecCount("M"):l, GetRecCount("D"):l, GetRecCount("U"):l, GetRecCount("E"):l);
[Созданы новые субъекты экономики: ######                                          ] (GetNewRecCount():l);
[Изменены существующие субъекты экономики: ######                                  ] (GetCngRecCount():l);
[Закрыто участие в SWIFT субъектов экономики: ######                               ] (GetDelRecCount():l);
[Отсутствуют в справочнике субъекты экономики (участие в SWIFT закрыто): ######    ] (GetAbsRecCount():l);
[                                                                                  ];
  PrintInsertedParty();
  PrintChangedParty();
  PrintClosedParty();
  PrintAbsentParty();
  PrintErrDupParty();
[------------------- КОНЕЦ ПРОТОКОЛА -------------------                           ];
end;

/* Запустить импорт */
macro RunImport()
  var i = 0;
  var cmd : RsdCommand;
  cmd = RsdCommand("BEGIN RsbBicImport.ImportBICPlusIBANDirectory(?, ?, ?); END;");
  cmd.addParam( "LoadOnlyBank", RSDBP_IN );
  cmd.addParam( "LoadBiDelta" , RSDBP_IN );
  cmd.addParam( "Oper"        , RSDBP_IN );
  cmd.value("LoadOnlyBank") = LoadOnlyBank;
  cmd.value("LoadBiDelta")  = LoadBiDelta;
  cmd.value("Oper")         = {oper};
  cmd.execute();
  OnError(err);
  PrintLn( "Строка: ", err.line );
  PrintLn( err.message );
  while( i < cmd.connection.environment.ErrorCount )
    PrintLn( cmd.connection.environment.Error(i).Descr);
    i = i + 1;
  end;
  RunError();
end;

macro FillFieldsBICPlusIBAN()
  var cmd : RsdCommand;

  if(RecompilePackage)
    cmd = RsdCommand("ALTER PACKAGE RsbBicImport COMPILE");
    cmd.execute();
  end;

  cmd = RsdCommand("BEGIN RsbBicImport.FillFieldsBICPlusIBAN; END;");
  cmd.execute();
end;

private macro IsRightFileName(importPathName)
  var strFile : string;
  var strExt : string;
  var IsRight = true;

  SplitFile(importPathName, strFile, strExt);

  if(IsRight and (index(strupr(strFile), "BI_") == 0) and (index(strupr(strFile), "BIDELTA_") == 0))
    IsRight = false;
  end;
  if(IsRight and (strupr(strExt) != ".TXT"))
    IsRight = false;
  end;

  return IsRight;
end;

macro CheckFileNameDate()
  var fullOnlyFileName : string;
  var regOnlyFileName  : string;
  var fullDateFileName : string;
  var regDateFileName  : string;

  SplitFile(fullFileName, fullOnlyFileName);
  SplitFile(regFileName , regOnlyFileName );

  if(regOnlyFileName != "")
    if(IsRightFileName(regFileName))
      fullDateFileName = substr(fullOnlyFileName, index(fullOnlyFileName, "_") + 1, 8);
      regDateFileName  = substr(regOnlyFileName , index(regOnlyFileName, "_") + 1 , 8);
      if(fullDateFileName < regDateFileName)
        msgbox("Импорт BI-файла ", file_name, " прерван: дата активации выбранного файла меньше даты ранее импортированного файла");
        exit(1);
      elif(fullDateFileName == regDateFileName)
        msgbox("Импорт BI-файла ", file_name, " прерван: файл уже импортирован");
        exit(1);
      end;
    else
      msgbox("Импорт BI-файла ", file_name, " прерван: имя файла, содержащееся в значении настройки реестра \"CB\\PARTY\\SWIFT\\ИМПОРТИРОВАН BI ФАЙЛ\", имеет формат, не соответствующий BICPlusIBAN Directory");
      exit(1);
    end;
  end;
  if(fullOnlyFileName != "")
    if(not IsRightFileName(fullFileName))
      msgbox("Импорт BI-файла ", file_name, " прерван: имя выбранного файла имеет формат, не соответствующий BICPlusIBAN Directory");
      exit(1);
    end;
  end;  
end;

macro Main()
  var StartTime : time;
  var EndTime   : time;

  fullFileName = "";
  regFileName  = "";
  LoadOnlyBank = 0;
  LoadBiDelta  = 0;

  GetRegistryValue( "CB\\PARTY\\SWIFT\\ИМПОРТИРОВАН BI ФАЙЛ", V_STRING, regFileName, stat );
  if(regFileName != "")

    file_path = SplitFile(regFileName, file_name, file_ext);
    if(file_path != "")
      file_path = file_path + "\\" + "*.txt";
    else
      file_path = "*.txt";
    end;
  else
    file_path = "*.txt";
  end;

  /* Выбираем текстовый файл с данными для импорта */
  stat = SelectFile( fullFileName, file_path, "Выбор файла импорта" );

  StartTime = time;

  if( stat )
    stat = Open( FBICDir, fullFileName );
    if( not stat )
      msgbox( "Ошибка открытия файла импорта " + fullFileName );
    else
      SplitFile(fullFileName, file_name, file_ext);
      file_name = file_name + file_ext;
      CheckFileNameDate();

      if(strupr(substr(file_name, 1, 8)) == "BIDELTA_")
        LoadBiDelta = 1;
      end;
      ImportStringArray = TArray;
    
      ClearTempTable();
    
      nCount = 0;
      InitProgress(-1, " ~Ctrl-Brk~ Прервать", "Обработка файла импорта");
    
      next(FBICDir);
      next(FBICDir);
    
     //Lavrenov: грузим все!!! 
     // if( (stat) and (GetTrue(null, "Загружать только банки?")) ) LoadOnlyBank = 1; end;
    
      while( stat )
        nCount = nCount + 1;
    
        if(substr(FBICDir.str, 1, 2) != "BI")
          msgbox("Импорт BI-файла ", file_name, " прерван: структура выбранного файла имеет формат, не соответствующий BICPlusIBAN Directory");
          exit(1);
        end;
        AddToTempTable(FBICDir.str);
        stat = InsertIntoTempTable();
        UseProgress( nCount );
        if(stat)
          stat = next(FBICDir);
        end;
/*    
        if(nCount == 2000) stat = false; end;
*/    
      end;
      RemProgress();
      stat = InsertIntoTempTable( true );
    end;

    FillFieldsBICPlusIBAN();

  end;

  /* если пользователь отказался от выбора, выходим без сообщений */
  if( not stat ) exit(1); end;

  /* запуск процедуры импорта */
  RunImport();

  SetDefaultRegistryValue( "CB\\PARTY\\SWIFT\\ИМПОРТИРОВАН BI ФАЙЛ", fullFileName);

  EndTime = time;
  PrintProtocol( StartTime, EndTime );
end;

//<Lavrenov 
var rs,str;
//Lavrenov/>

Main();

//<Lavrenov 
str = "INSERT INTO dpartyown_dbt " +
      "   SELECT t.t_partyid, 2, t.t_superior, t.t_subkind, t.t_branch, " +
      "          t.t_numsession " +
      "     FROM dpartyown_dbt t " +
      "    WHERE t.t_partykind = 10 " +
      "      AND NOT EXISTS (SELECT * " +
      "                        FROM dpartyown_dbt " +
      "                       WHERE t_partykind = 2 AND t_partyid = t.t_partyid); " ;

rs = rsdcommand(str);
rs.execute();
// 2012-08-07 zip_z. Вот так мы ставит крест на транзакционной целостности, 
// впендюрив свой коммит посреди чужой сишной транзакции. Закомментировал.
// rs = rsdcommand("commit");
// rs.execute();
//Lavrenov/>
