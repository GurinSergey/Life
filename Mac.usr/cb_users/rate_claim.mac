/*29.04.2015 LAO C-37350 Процедура пересчета рублевого ареста по валютному счету*/
/*В случае поступления на валютный счет ареста от судебных приставов в рублях, 
  необходимо арест в программе хранить в рублях usr_dacclaimRUR_dbt 
  и при изменение курса по валюте счета, производить пересчет ареста 
  на основание первоначального рублевого эквивалента                            */
   private macro getSumm(Fiid, sum,sumv)
       record rate (ratedef); 
       private var ConvSuma ;    
     //  var FIID = Get_Fiid(Account), ConvSuma ;      
       if(  FIID != 0 )        
           if( ConvSum(ConvSuma , Money(Sum), {curdate}, 0,FIID) != 0 )
              return money(sumv);
           end;   
           return money(ConvSuma);     
       end;
       return money(sum);     
   end;
   
    private  macro updateclaimVAL(claimid,amount)
     private var cmd_upd;
        if (money(amount)!=0)
             cmd_upd= RSDCommand(" update dacclaim_dbt set T_STARTAMOUNT = ? where T_CLAIMID =?");
             cmd_upd.addparam("mmm", RSDBP_IN, money(amount));
             cmd_upd.addparam("pid", RSDBP_IN, claimid);
             cmd_upd.execute();
             cmd_upd = RSDCommand(" update dacclaimstate_dbt set T_CURRENTAMOUNT = ? where T_CLAIMID =?");
             cmd_upd.addparam("mmm", RSDBP_IN, money(amount));
             cmd_upd.addparam("pid", RSDBP_IN, claimid);
             cmd_upd.execute();
        else 
            return 1;
        end; 
        
        return 0;
        OnError 
            return 1;
    end;
    
  
  macro changeArestClaim(datechange :date)
  var valsum =$0;
    [ ];
    [ ];
    [ ];
    [Процедура переоценки ареста по валютным счетам ];
    [+---------+-----------------------+---------------------+---------------------+------------------+];
    [|    №    |        Счет           |       Сумма         |         Сумма       |      Сумма       |];
    [|  докум  |                       |   в валюте(старая)  |    в валюте(новая)  |     в рублях     |];
    [+---------+-----------------------+---------------------+---------------------+------------------+];
    
    private var  cmd_acclaim = RsdCommand("SELECT   CLAIM.T_CLAIMID,"+
            " claim.t_fiid,"+
            " claim.T_DOCNUMBER,"+
            " claim.t_account,"+
            " claim.t_startdate,"+
            " claim.T_STARTAMOUNT valamount,"+
            " RURCLAIM.T_STARTAMOUNT ruramount"+
            " FROM   DACCLAIM_DBT claim, USR_DACCLAIMRUR_DBT rurclaim"+
            " WHERE   "+
            "      claim.t_claimid = rurclaim.t_claimid  and claim.t_initiator = 3 and (claim.t_finishdate =  to_date('01010001','ddmmyyyy') or claim.t_finishdate > ?)");
        cmd_acclaim.addparam("mmm", RSDBP_IN, date({curdate}));
        cmd_acclaim.execute();
        private var rs_acclaim = rsdrecordset (cmd_acclaim);
        While (rs_acclaim.movenext())
           valsum=$0;
           valsum = getSumm(rs_acclaim.value("t_fiid"),rs_acclaim.value("ruramount"),rs_acclaim.value("valamount"));
            if (updateclaimVAL(rs_acclaim.value("T_CLAIMID"),valsum) ==0)
                [|#########|#######################|#####################|#####################|##################|]
                (rs_acclaim.value("T_DOCNUMBER"):r, rs_acclaim.value("t_account"):f, rs_acclaim.value("valamount"):f,valsum:f,rs_acclaim.value("ruramount"));
            end;
        end;
            [+---------+-----------------------+---------------------+---------------------+------------------+];
  //[|#########|#######################|#######################|##################|]
    return 0;
    OnError return 0;
  end;