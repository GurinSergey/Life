// -------------------------------------------------------------------------------------------------
// @filename: RepCheckRevs.mac
// @author  : Зленко М.П. 
// @desc    : Отчет по денежным чекам за период
// @request : C-12502 
// @modify  : 
// -------------------------------------------------------------------------------------------------
import rsexts, KeyCodes,Календарь;
import PaymInter, PTInter, rmcmptl;
import activeX, lib_sqltools;

private var TermFileName_Excel;
private var TermFileName_Txt;
private var NameTxtFile = "CheckRevs_txt." + UserNumber;
private var output = "CheckRevs.xlsx";
private var ClientID, Account;
private var filen = "CheckRevs.lbr";
private var RowCount, dlg;



private macro NewExcelWorkbook2 (Visible, ExcelFileName)
   if (ExcelApplication == null)
      if (isStandAlone())
         ExcelApplication = ActiveX("Excel.Application", null, true);
      else
         if (startAX == null)
            startAX = CreateObject("rsax", "TRsAxServer", "LoansAxServer", isStandalone());
         end;
         ExcelApplication = startAX.CreateComObject("Excel.Application");
      end;
   end;
   ExcelApplication.Workbooks.Add (ExcelFileName);
   if (ValType (Visible) == V_BOOL)
      ExcelApplication.Visible = Visible;
   end;
   ActiveSheet = ExcelApplication.ActiveSheet;

   return true;
   onError (err)
      ExcelError(err, false);
      Exit(1);
END;/*NewExcelWorkbook2*/

private macro GetWorkDirName() : string
   var DirName:string = "", /* Строка с именем пути                         */
       StrErr :string = ""; /* Строка с ошибкой определения временной папки */

   GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, DirName, StrErr );
   if( DirName ) /* Если в настройке указан каталог для временных файлов, работаем в этом направлении */
       //msgbox(string("В реестре найден каталог текстовых файлов: ", DirName));
       /* Если первый символ в DirName - это точка, то значит путь относительный, */
       /* тогда приклеем к нему CurDir терминала, иначе путь абсолютный */
       if( SubStr(DirName, 1, 1) == "." )
           DirName = "$" + GetCurDir(TRUE) + replace (DirName, "..","");
       end;
       //msgbox(string("А это его модификация для терминала: ", DirName));
       if (existFile(DirName))
          //msgbox(string("Он существует на терминале, поэтому его и возвращаем: ", DirName));
          return DirName;
       end;
   end;
   DirName = "$" + GetEnv("TEMP");
   if( NOT DirName )
       DirName = "$" + GetEnv("TMP");
   end;
   if (existFile(DirName))
      //msgbox(string("Он существует на терминале, поэтому его и возвращаем: ", DirName));
      return DirName;
   end;
   msgbox("Ошибка поиска папки для временных файлов на терминале");

   return "";
end; /*GetWorkDirName*/

private macro Copy_text (Name_file)
   var Templs_Dir_Term = GetWorkDirName;
   var TermFileName = toAnsi(Name_file);
   var SPFileName;

   if (Templs_Dir_Term=="")
      return "";
   end;
   SPFileName = "..\\txtfile\\" + NameTxtFile;
   if (not existFile(SPFileName))
      Msgbox(string("Транспортный временный файл не найден: |",SPFileName));
      return "";
   end;
   if( not CopyFile(SPFileName, Templs_Dir_Term+"\\"+TermFileName) )
     Msgbox("Ошибка при передаче файла на терминал\n" + Templs_Dir_Term+"\\"+TermFileName);
   end;

   return replace(Templs_Dir_Term+"\\"+TermFileName,"$","");
End;/*Copy_text*/



private Macro Check_templ (Name_file_templ)
   var Templs_Dir_Term = GetWorkDirName();
   var TermFileName = toAnsi(Name_file_templ);
   var SPFileName;

   if (Templs_Dir_Term == "")
      return "";
   end;
   GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\USERTEMPLSDIR",2,SPFileName);
   SPFileName = findpath (Name_file_templ,SPFileName);
   if (not existFile(SPFileName))
      Msgbox(string("Шаблон отсутствует в папке СП: |",SPFileName));
      return "";
   end;
   if( not CopyFile(SPFileName, Templs_Dir_Term+"\\"+TermFileName) )
     Msgbox("Ошибка при передаче файла на терминал\n" + Templs_Dir_Term+"\\"+TermFileName);
   end;

   return replace(Templs_Dir_Term+"\\"+TermFileName,"$","");
End;


macro OpenTempl()
var qt;
    ExcelApplication.Sheets("Лист1").Select;
    ActiveSheet = ExcelApplication.ActiveSheet; 
    qt = ActiveSheet.QueryTables.Add("TEXT;"+TermFileName_Txt,ActiveSheet.Range("$A$3"));
    qt.Name = "earep";
    qt.FieldNames = True;
    qt.RowNumbers = False;
    qt.FillAdjacentFormulas = False;
    qt.PreserveFormatting = True;
    qt.RefreshOnFileOpen = False;
    qt.RefreshStyle = 0; //xlOverwriteCells        1;  //xlInsertDeleteCells;
    qt.SavePassword = False;
    qt.SaveData = True;
    qt.AdjustColumnWidth = False;
    qt.RefreshPeriod = 0;
    qt.TextFilePromptOnRefresh = False;
    qt.TextFilePlatform = 866;
    qt.TextFileStartRow = 1;;
    qt.TextFileParseType = 1; //xlDelimited;
    qt.TextFileTextQualifier = 1; //xlTextQualifierDoubleQuote;
    qt.TextFileConsecutiveDelimiter = False;
    qt.TextFileTabDelimiter = True;
    qt.TextFileSemicolonDelimiter = True;
    qt.TextFileCommaDelimiter = False;
    qt.TextFileSpaceDelimiter = False;
//    qt.TextFileColumnDataTypes = TArray(1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1, 1);
    qt.TextFileTrailingMinusNumbers = True;
    qt.Refresh(False);
end;

private macro getPartyName(ClientID)
var cmd, rs; 

cmd = RSDCommand("select PT.T_NAME from dparty_dbt pt where  PT.T_PARTYID = :ClientID ");
cmd.AddParam("ClientID", RSDBP_IN, ClientID);
rs = RsdRecordset (cmd);
    if (rs and rs.moveNext ())
        return String(rs.value(0));
    end;  
end;

macro RepHead(ClientID)
   ActiveSheet.Range("A1:D1").Value = String("Список проведенных денежных чеков по счету № ",Account," ",getPartyName(ClientID)," от ",dlg.rec.DateBegin," по ",dlg.rec.DateEnd);
   ActiveSheet.Range("A3:D"+(RowCount+2)).Borders.Weight = 2;
end; 

macro Out(acc, date1, date2)
startQueryCapture ();
[
SELECT   RMPROP.T_NUMBER num,
         PM.T_AMOUNT summ,
         to_char(PM.T_VALUEDATE,'dd.mm.yyyy') valdate,
          (  'Выдано по чеку №'
         || RMPROP.T_NUMBER
         || ' '
         || CH.T_SERIES
         || ' от '
         || to_char(RMPROP.T_DATE,'dd.mm.yyyy')
         || ' '
         || (  SELECT   translate(wm_concat(s.t_name),0,0)
                                        FROM   dlistsymb_dbt s, dsymbcash_dbt c
                                       WHERE   S.T_SYMB_CASH = C.T_SYMBOL
                                               AND C.T_KIND = 2
                                               AND S.T_DOCKIND = C.T_DOCKIND
                                               AND C.T_APPLICATIONKEY =
                                                     LPAD (pm.T_PAYMENTID,
                                                           34,
                                                           0)
                                               
                                    GROUP BY   C.T_APPLICATIONKEY) 
         || ' '
         || CH.T_FIOCLIENT
         ||' '
         || (select  P.T_NAME from dpaprkind_dbt p where P.T_PAPERKIND = CH.T_PAPERKIND)
         || CH.T_PAPERSERIES
         || ' '
         || CH.T_PAPERNUMBER
         || ' '
         || CH.T_PAPERISSUER
         || ' '
         || to_char(CH.T_PAPERISSUEDDATE,'dd.mm.yyyy')) ground
  FROM   dpmpaym_dbt pm,
         dpscshdoc_dbt ch,
         dpmrmprop_dbt rmprop
 WHERE   PM.T_PAYMENTID = RMPROP.T_PAYMENTID
         AND RMPROP.T_SHIFROPER = '03'
         AND SUBSTR (PM.T_RECEIVERACCOUNT, 1, 5) = '20202'
         AND CH.T_STATUS = 3
         and CH.T_AUTOKEY = PM.T_PAYMENTID
         and CH.T_DOCKIND = 420
         and PM.T_PAYERACCOUNT =:acc                                
         AND PM.T_VALUEDATE between :date1 and :date2

];
var    osql = endQueryCapture ();
       osql = execSqlSelect (osql, makeArray (SqlParam ("acc",     acc),
                                              SqlParam ("date1",   date1),
                                              SqlParam ("date2",   date2 )
                                              ));

RowCount = 0;
setoutput("..\\txtfile\\" + NameTxtFile);
while(osql.moveNext ())
   println(String(osql.value("num"),";",osql.value("summ"),";",osql.value("valdate"),";",osql.value("ground")));
   RowCount = RowCount + 1;
end;
setoutput(null,true);
TermFileName_Txt = Copy_text (NameTxtFile);
if (TermFileName_Txt == "")
   exit(1);
end;
TermFileName_Excel = Check_templ (Output);
if (TermFileName_Excel == "")
   exit(1);
end; 
if(not NewExcelWorkbook2(false, TermFileName_Excel))
   exit(0, "Не открыт файл шаблона отчета");
end;
if (RowCount)  
    OpenTempl();
end;
RepHead(ClientID);
ExcelApplication.Visible = true; 

end;/*Out*/ 


MACRO CheckRevs_event (dlg, cmd, id, key)     
var rs;
 
   if(cmd == DLG_INIT)
     message("~F2~ продолжить, ~F3~ Выбрать дату, ~ESC~ выход");
     dlg.rec.DateBegin = {curdate};
     dlg.rec.DateEnd   = {curdate};
     dlg.rec.Account   = Account;
     setFocus(dlg,1);
     UpdateFields(dlg);

   elif(cmd==DLG_KEY)
      if(KEY==KEY_F3)
         if (FldName(dlg, id) == "DateBegin")
            dlg.rec.DateBegin = GetDateByCalendar({curdate});           
         elif (FldName(dlg, id) == "DateEnd")
            dlg.rec.DateEnd   = GetDateByCalendar({curdate});
         end;
         UpdateFields(dlg);
      elif (KEY==KEY_F2)
         if ( dlg.rec.DateBegin > dlg.rec.DateEnd ) 
            MsgBox("Дата начала больше даты окончания  периода");
            return CM_IGNORE;
         else
            return CM_save;
         end;
      elif ( KEY == KEY_ESC )                                                                                                                   
         return CM_CANCEL;  
      elif ((KEY == KEY_ENTER) AND ((FldName(dlg,id) == "Acoount") or (FldName(dlg,id) == "DateEnd")))
          setFocus(dlg,1);
          return CM_IGNORE;
      end;
   elif (cmd == DLG_REMFOCUS)
      if ( dlg.rec.DateBegin > dlg.rec.DateEnd ) 
          MsgBox("Дата начала больше даты окончания  периода");
          return CM_IGNORE;
      end;
   end;
END;

macro CheckRevs(_acc, _ClientID)  
GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",V_STRING,path);
var pathfile = FindPath(filen,path);
	if (not pathfile)
		msgbox("Не найдена LBR");
		exit();                                  
	end;
   dlg = TRecHandler("check",pathfile,True);  
   Account = _acc;
   ClientID = _ClientID;
   if ( RunDialog(dlg, "CheckRevs_event"))
      Out(_acc,dlg.rec.DateBegin,dlg.rec.DateEnd);
   end;
                  
end;

