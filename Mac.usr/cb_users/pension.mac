/*******************************************************************************/
/*  RS-Bank 6.0                                          R-Style Software Lab  */
/*                                                                             */
/*  File Name   : pension.mac                                                  */
/*  Created     : 16.05.2012                                                   */
/*  Programmer  : Chesnokov D.                                                 */
/*  Description : Макрос заполения/редактирования списка счетов, по которым    */
/*                производится отправка письма в LotusNotes с оповещением.     */
/*                Заявка С-10370                                               */
/*                                                                             */
/*******************************************************************************/
import BankInter, PTInter, RSD, "KeyCodes.mac", "diver.mac";

var rs, str;
var Col = TArray(), NumCol = 0;
record acc("account.dbt");

macro AddCol( ar,ind, fld, head, wdth, edit )
  ar.value( ind * 6 + 0 ) = fld;  // fieldName            
  ar.value( ind * 6 + 1 ) = head; // header               
  ar.value( ind * 6 + 2 ) = wdth; // width                
  ar.value( ind * 6 + 3 ) = edit; // fldType (2 = FBT)    
  ar.value( ind * 6 + 4 ) = null; // decPoint             
  ar.value( ind * 6 + 5 ) = 0;    // reserv               
  NumCol = NumCol + 1;
end;

macro PensEvent(rs, cmd, id, key)
 
  var rs_ac;
  var CM_FLAG = CM_DEFAULT;  /*Возвращаемое значение по умолчанию*/

  if (cmd == DLG_KEY)
    if (key == KEY_ESC)
      CM_FLAG = CM_DEFAULT;
    elif (key == KEY_F8)
      if (rs.RecCount > 0)
        if (ВходитВГруппу({oper}, 212))
        if (GetTrue(true, "Вы действительно хотите удалить запись?"))
          rs.Delete();
          CM_FLAG = CM_SELECT;
        else
          CM_FLAG = CM_IGNORE;
        end;
        else
          msgbox("Вы не входите в группу \"212 - Настройка таблицы счетов КДЦ-Вклад\".|Удаление счетов запрещено.");
          CM_FLAG = CM_IGNORE;
        end;
      end;
    elif (key == KEY_F9)
      if (ВходитВГруппу({oper}, 212))
      if (ListAccount(acc, 1, 0, "", NULL, "47422", null, true))
        
        rs_ac = RsdRecordSet("select 1 from dpension_dbt where t_account = '" + acc.account + "'");
        
        if (not rs_ac.MoveNext)
          rs.AddNew();
          rs.value("t_account") = acc.account;
          rs.value("t_balance") = acc.balance;
          rs.value("t_client") = acc.client;
          rs.value("t_branch") = acc.branch;
          rs.value("t_nameaccount") = acc.nameaccount;
          rs.value("t_nameclient") = GetNameClient("", acc.account, 1, 0);
          rs.value("t_oper") = acc.oper;

          rs.Update ();
          GoTOScroll (rs);
          UpdateScroll (rs);
          CM_FLAG = CM_IGNORE;
        else
          msgbox("Выбранный Вами счет " + acc.account + " уже присутствует в выборке");
          CM_FLAG = CM_DEFAULT;
        end;
      end;
      else
        msgbox("Вы не входите в группу \"212 - Настройка таблицы счетов КДЦ-Вклад\".|Добавление новых счетов запрещено.");
        CM_FLAG = CM_DEFAULT;
      end;
    elif (key == KEY_ENTER)
      CM_FLAG = CM_IGNORE;
    end;
  end;
  
  return CM_FLAG;

end;

/****************************************************************/
/*                       ТОЧКА ВХОДА                            */
/****************************************************************/

  str = " SELECT t_account, t_balance, " +
        "        t_client, t_branch, " +
        "        t_nameaccount, t_nameclient, t_oper " +
        "   FROM dpension_dbt ORDER BY t_account";

  rs = RsdRecordSet(RsdCommand(str) ,RSDVAL_CLIENT, RSDVAL_STATIC );

  AddCol(Col, NumCol, "t_account",     "Счет",                 25);
  AddCol(Col, NumCol, "t_balance",     "Балансовый",            5);
  AddCol(Col, NumCol, "t_oper",        "Операционист",          5);
  AddCol(Col, NumCol, "t_branch",      "ВСП",                   4);
  AddCol(Col, NumCol, "t_nameaccount", "Наименование счета",   40);
  AddCol(Col, NumCol, "t_nameclient",  "Наименование клиента", 40);

  while(RunScroll(rs, NumCol, Col, "pension", "PensEvent", "Список счетов для оповещения", "~F8~ Удалить счет ~F9~ Добавить счет ", true, 15, 8, 100, 25))
    rs = RsdRecordSet(RsdCommand(str) ,RSDVAL_CLIENT, RSDVAL_STATIC );
  end;
  rs.Close();