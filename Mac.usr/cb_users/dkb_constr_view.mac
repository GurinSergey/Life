/************************************************************************************/
/*  RS-Bank 6.0                                              R-Style Software Lab   */
/*                                                                                  */
/*  File Name   : dkb_constr_view.mac                                               */
/*  Created     : 10.08.2012                                                        */
/*  Programmer  : Chesnokov D.                                                      */
/*  Description : Макрос обработки панели редактирования клиента для конструктора ТП*/
/*  Заявка      : C-12956                                                           */
/************************************************************************************/

import BankInter, RSD, Календарь;

var constrID;
private var path, resname = "ConstrTP.lbr", pathfile, ErrCode, dlg_p;
var cntPM;

class Obj_ConstrCom(m_id)

  var ID = m_id; // id записи из dconstrcom_dbt
  
  var  Object         = "",
       Client         = -1,
       NameObject     = "",
       DepositFlag    = "",
       DepositSum     = $0,
       CreditFlag     = "",
       CreditSum      = $0,
       OverFlag       = "",
       OverSum        = $0,
       RateFlag       = "",
       RateSum        = $0,
       TypePacket     = "0",
       NamePacket     = "",
       Numpacket      = 0,
       SumPacket      = $0,
       DatePack       = Date(0,0,0),
       SumPayment     = $0,
       SumAfter16     = $0,
       SumBESP        = $0,
       DistinctFlag   = " ",
       DistinctProc   = " ",
       DateIns        = Date(0,0,0),
       State          = 0,
       DateClose      = Date(0,0,0),
       TypeName       = "",
       FormatDatePack = Date(0,0,0);
       
  macro ParseFlagBack(flag)
    if (flag == -1)
      return "-";
    elif(flag == 0)
      return "0";
    elif(flag == 1)  
      return "+";
    end;
  end;
       
  macro init()
    
    var rs;
      rs = RsdrecordSet("select * from dconstrcom_dbt where t_id = " + ID);
      rs.NullConversion = true;
      if (rs and rs.movenext)
        Object       = rs.value("t_object");
        Client       = rs.value("t_client");
        NameObject   = rs.value("t_nameobject");
        DepositFlag  = ParseFlagBack(rs.value("t_depositflag"));
        DepositSum   = rs.value("t_depositsum");
        CreditFlag   = ParseFlagBack(rs.value("t_creditflag"));
        CreditSum    = rs.value("t_creditsum");
        OverFlag     = ParseFlagBack(rs.value("t_overflag"));
        OverSum      = rs.value("t_oversum");
        RateFlag     = ParseFlagBack(rs.value("t_rateflag"));
        RateSum      = rs.value("t_ratesum");
        TypePacket   = rs.value("t_typepacket");
        NamePacket   = rs.value("t_namepacket");
        Numpacket    = rs.value("t_numpacket");
        SumPacket    = rs.value("t_sumpacket");
        Datepack     = rs.value("t_datepack");
        SumPayment   = rs.value("t_sumpayment");
        SumAfter16   = rs.value("t_sumafter16");
        SumBESP      = rs.value("t_sumbesp");
        DistinctFlag = rs.value("t_distinctflag");
        DistinctProc = rs.value("t_distinctproc");
        DateIns      = rs.value("t_date");
        State        = rs.value("t_state");
        DateClose    = rs.value("t_dateclose");
        TypeName     = rs.value("t_typename");
        DtTmSplit(rs.value("t_datepack"), FormatDatePack, null);
        //TypePacketNew = 0;
      end;
  end;
  
  if(valType(ID) != V_Undef)
    init();
  end;
end;

var mes:Obj_ConstrCom;

private macro ПрочитатьНастройки

  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",V_STRING, path, errCode);
  pathfile = FindPath(resname, path);
  if (not pathfile)
    msgbox("Не найдена LBR с именем " + resname);
    return false;
  end;
  if ( errCode > 0 )
    msgbox("Проверьте наличие настройки: BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR");
    return false;
  end;
    
  dlg_p = TRecHandler("p_clpack", pathfile, True);
  
  return true;

end;

macro HandleEvent_View (dlg, cmd, id, key)

  var str, rs_list, rs_upd;
  var const_message = "~ESC~ Выход ~F9~ Сохранить";

  const rs = RsdrecordSet("select * from dconstrcom_dbt where t_id = "  + constrID);

  if (cmd == DLG_INIT)
    dlg.rec.Object = mes.Object;
    DisableFields(dlg, fldindex(dlg, "Object"));
    dlg.rec.NameObject   = mes.NameObject;
    dlg.rec.DepositFlag  = mes.DepositFlag;
    dlg.rec.DepositSum   = mes.DepositSum;
    dlg.rec.CreditFlag   = mes.CreditFlag;
    dlg.rec.CreditSum    = mes.CreditSum;
    dlg.rec.OverFlag     = mes.OverFlag;
    dlg.rec.OverSum      = mes.OverSum;
    dlg.rec.RateFlag     = mes.RateFlag;
    dlg.rec.RateSum      = mes.RateSum;
    dlg.rec.TypePacket   = mes.TypeName;
    dlg.rec.NamePacket   = mes.NamePacket;
    cntPM                = mes.NumPacket;
    dlg.rec.SumPacket    = mes.SumPacket;
    dlg.rec.SumPayment   = mes.SumPayment;
    if (dlg.rec.TypePacket == "Статусный")
      dlg.rec.SumBESP      = $0;
      dlg.rec.SumAfter16   = $0;
      DisableFields(dlg, fldindex(dlg, "SumAfter16"));
      DisableFields(dlg, fldindex(dlg, "SumBESP"));
    else
      dlg.rec.SumBESP      = mes.SumBESP;
      dlg.rec.SumAfter16   = mes.SumAfter16;
    end;
    dlg.rec.DistinctFlag = mes.DistinctFlag;
    dlg.rec.DistinctProc = mes.DistinctProc;
    
    UpdateFields(dlg);
    
  elif (cmd == DLG_SETFOCUS)

    if(FldName(dlg, id) == "DepositFlag")
      message(const_message + " ~Space~ Установить");
    elif(FldName(dlg, id) == "CreditFlag")
      message(const_message + " ~Space~ Установить");
    elif(FldName(dlg, id) == "OverFlag")
      message(const_message + " ~Space~ Установить");
    elif(FldName(dlg, id) == "RateFlag")
      message(const_message + " ~Space~ Установить");
    elif(FldName(dlg, id) == "TypePacket")
      message(const_message + " ~Space~ Установить");
    elif(FldName(dlg, id) == "NamePacket")
      message(const_message + " ~F3 Список~");
    elif(FldName(dlg, id) == "DistinctFlag")
      message(const_message + " ~Space~ Установить");
    elif(FldName(dlg, id) == "DistinctProc")
      message(const_message + " ~Space~ Установить");
    else
      message(const_message);
    end;
  
  elif(cmd == DLG_KEY)
    if(key == 27) //KEY_ESC
      if (GetTrue(TRUE,"Вы действительно хотите выйти без сохранения?"))
        //exit(1);
        return CM_CANCEL;
      else
        return CM_IGNORE;
      end;
    elif(key == 32) //KEY_SPACE
      if (FldName(dlg, id) == "DepositFlag")
        if(dlg.rec.DepositFlag == "0")
          dlg.rec.DepositFlag = "+";
        elif(dlg.rec.DepositFlag == "+")
          dlg.rec.DepositFlag = "-";
        elif(dlg.rec.DepositFlag == "-")
          dlg.rec.DepositFlag = "0";
        end;
      elif(FldName(dlg, id) == "CreditFlag")
        if(dlg.rec.CreditFlag == "0")
          dlg.rec.CreditFlag = "+";
        elif(dlg.rec.CreditFlag == "+")
          dlg.rec.CreditFlag = "-";
        elif(dlg.rec.CreditFlag == "-")
          dlg.rec.CreditFlag = "0";
        end;
      elif(FldName(dlg, id) == "OverFlag")
        if(dlg.rec.OverFlag == "0")
          dlg.rec.OverFlag = "+";
        elif(dlg.rec.OverFlag == "+")
          dlg.rec.OverFlag = "-";
        elif(dlg.rec.OverFlag == "-")
          dlg.rec.OverFlag = "0";
        end;
      elif(FldName(dlg, id) == "RateFlag")
        if(dlg.rec.RateFlag == "0")
          dlg.rec.RateFlag = "+";
        elif(dlg.rec.RateFlag == "+")
          dlg.rec.RateFlag = "-";
        elif(dlg.rec.RateFlag == "-")
          dlg.rec.RateFlag = "0";
        end;
      elif(FldName(dlg, id) == "TypePacket")
        if(dlg.rec.TypePacket == "Простой")
          dlg.rec.TypePacket = "Статусный";
          dlg.rec.NamePacket = "";
          dlg.rec.SumPacket = $0;
          rs_list = RsdRecordSet("select t_note from dllvalues_dbt where t_list = "+ List6002 +" and t_element = 6");
          if(rs_list and rs_list.movenext)
            dlg.rec.SumPayment = rs_list.value(0);
          else
            dlg.rec.SumPayment = $0;
          end;
          
          dlg.rec.SumAfter16 = $0;
          dlg.rec.SumBESP = $0;
          DisableFields(dlg, fldindex(dlg, "SumAfter16"));
          DisableFields(dlg, fldindex(dlg, "SumBESP"));
          
        elif(dlg.rec.TypePacket == "Статусный")
          dlg.rec.TypePacket = "Простой";
          dlg.rec.NamePacket = "";
          dlg.rec.SumPacket = $0;
          rs_list = RsdRecordSet("select t_note from dllvalues_dbt where t_list = "+ List6002 +" and t_element = 5");
          if(rs_list and rs_list.movenext)
            dlg.rec.SumPayment = rs_list.value(0);
          else
            dlg.rec.SumPayment = $0;
          end;
          
          EnableFields(dlg, fldindex(dlg, "SumAfter16"));
          EnableFields(dlg, fldindex(dlg, "SumBESP"));
          
          rs_upd = RsdRecordSet("select t_note from dllvalues_dbt where t_list = "+ List6002 +" and t_element = 7");
          if(rs_upd and rs_upd.movenext)
            dlg.rec.SumAfter16 = rs_upd.value(0);
          else
            dlg.rec.SumAfter16 = $0;
          end;
          
          rs_upd = RsdRecordSet("select t_note from dllvalues_dbt where t_list = "+ List6002 +" and t_element = 8");
          if(rs_upd and rs_upd.movenext)
            dlg.rec.SumBESP = rs_upd.value(0);
          else
            dlg.rec.SumBESP = $0;
          end;
          
        end;
      elif(FldName(dlg, id) == "DistinctFlag")
        debugbreak;
        if(dlg.rec.DistinctFlag == "")
          dlg.rec.DistinctFlag = "X";
        elif(dlg.rec.DistinctFlag == "X")
          dlg.rec.DistinctFlag = "";
        end;
      elif(FldName(dlg, id) == "DistinctProc")
        if(dlg.rec.DistinctProc == "")
          dlg.rec.DistinctProc = "X";
        elif(dlg.rec.DistinctProc == "X")
          dlg.rec.DistinctProc = "";
        end;
      end;
    elif(key == 317) //KEY_F3
      if (FldName(dlg, id) == "NamePacket")
        if(dlg.rec.TypePacket == "Простой")
          ClearRecord(llvalues);
          if(LL_ListLLValues(List6003, llvalues))
            dlg.rec.NamePacket = llvalues.name;
            dlg.rec.SumPacket = llvalues.note;
            cntPM = llvalues.flag;
          else
            //dlg.rec.NamePacket = "";
            //dlg.rec.SumPacket = $0;
            //cntPM = 0;
          end;
        elif(dlg.rec.TypePacket == "Статусный")
          ClearRecord(llvalues);
          if(LL_ListLLValues(List6004, llvalues))
            dlg.rec.NamePacket = llvalues.name;
            dlg.rec.SumPacket = llvalues.note;
            cntPM = llvalues.flag;
          else
            //dlg.rec.NamePacket = "";
            //dlg.rec.SumPacket = $0;
            //cntPM = 0;
          end;
        end;
      end;

    elif (key == 323) //KEY_F9
      debugbreak;
      if(mes.State == 1)
        msgbox("Редактирование закрытого пакетного предложения запрещено. | Изменения сохранены не будут!");
        return CM_IGNORE;
      elif((dlg.rec.NamePacket == "") and (dlg.rec.TypePacket == "Простой"))
        msgbox("Не заполнено обязательное поле \"Наименование пакета\"");
        return CM_IGNORE;
      elif((dlg.rec.SumPacket == $0) and (dlg.rec.TypePacket == "Простой"))
        msgbox("Не заполнено обязательное поле \"Стоимость пакета\"");
        return CM_IGNORE;
      elif((({curdate} - mes.FormatDatePack) <= 29) and ((mes.Numpacket != cntPM) or (mes.TypeName != dlg.rec.TypePacket) or (mes.SumPacket != dlg.rec.SumPacket)))
        msgbox("Дата возможного изменения "+ DateAfterCalenDays(mes.FormatDatePack, 30));
        return CM_IGNORE;
      end;
        
      return CM_SAVE;
      
    end;
    
    UpdateFields(dlg);

  end;

end;


macro View_sfconstr(id, parm1, parm2)

  if(ПрочитатьНастройки)
    mes = Obj_ConstrCom(id);
    if (RunDialog(dlg_p, "HandleEvent_View"))
      debugbreak;
      Setparm(1, dlg_p);
      Setparm(2, cntPM);
    else
      Setparm(1, null);
      Setparm(2, null);
    end;
  end;
end; 
