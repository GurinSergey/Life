/* ──────────────────────────────────────────────────────────────────────────
  RS-Bank 6.00                              (C) АКБ "Пробизнесбанк"
  Module     : RS-Bank Core
  File Name  : edit_rnko.mac (pl_centr.mac в 5.0)
  Description: Редактирование пунктов РНКО 
  Comment    : 
  History    : 22.03.10 Братчиков В.В. (pl_centr.mac в 5.0 на VCH)
               17.02.11  Зубко С. -  переписал полностью под RS-Bank 6.0
└───────────────────────────────────────────────────────────────────────────*/
import FIInter;
import rsd, ZUBRunScroll;

/*19.03.2013 Chesnokov D.S. логгирование запуска для поиска неиспользуемых макросов*/
ExecmacroFile("lib_log.mac", "LogProcedure", "edit_rnko.mac");

var path = "",
    pathfile = "",
    filen = "rnko.lbr";
var dlg;

var isupdate:bool = false;  /*Обновляем или вставляем*/
var ischanged:bool = false; /*Если обновляем и запись изменили*/

private var v_type:integer = 0; /*Код*/ 
private var v_type_name:string = ""; /*Наименование*/ 

private var v_code_currency:integer = 0; /*Код валюты*/ 
private var v_code_currency_id:integer = 0; /*Для рублей отражвться должно 810, но код 0. Для запоминания 0 и вводим переменную*/ 

private var v_name:string = ""; /*Наименование пункта*/ 
private var v_accountp:string = ""; /*Счет плательщика*/ 
private var v_nameaccp:string = ""; /*Плательщик*/ 
private var v_accountr:string = ""; /*Счет получателя*/ 
private var v_nameaccr:string = ""; /*Получатель*/
private var v_corrscheme:integer = 0; /*Получатель*/
private var v_agent:string = ""; /*Код агента*/

array LstType_Name;
array LstType_Id;

/*Выбранный тип */
var choise_type:integer;


/*──────────────────────────────────────────────────────────────────────────*/
/* Процедура заполняет массив для MENU
   Делаем так, потому что мы не имеем таблицы. Будем харкодить
   !!!ДОБАВЛЯЯ ЗДЕСЬ ДОБАВЬ И В SELECT!!! в SetOptions
*/
MACRO Init_Type()

  LstType_Name(0) = "Золотая корона вх.";   LstType_Id(0) = 0;
  LstType_Name(1) = "Золотая корона исх.";  LstType_Id(1) = 1;
  LstType_Name(2) = "Город входящие";       LstType_Id(2) = 2;
  LstType_Name(3) = "Город исходящие";      LstType_Id(3) = 3;
  LstType_Name(4) = "Контакт входящие";     LstType_Id(4) = 4;
  LstType_Name(5) = "Контакт исходящие";    LstType_Id(5) = 5;

END;


/*Переводим наименование в ID*/
MACRO TypeNameToID (p_type_name)

var lc_type:integer = -1;
var i:integer = 0;

  /*пробежимся по массиву*/
  while( i < asize(LstType_Name) )
    /*если в массиве нашли*/
    if(LstType_Name(i) == p_type_name)
      /*позиция и равна ID*/
      lc_type = i;
    end;
    i = i + 1;
  end;

  if (lc_type == -1)
    msgbox("Имя типа документа ", p_type_name, " не опознано!");
  end;
  return lc_type;
END;

/*Переводим наименование в ID. Делаем так, потому что мы не имеем таблицы. Будем харкодить*/
MACRO TypeIDToName (p_type_id)

var lc_type_name:string = "";

  lc_type_name = LstType_Name(p_type_id);

  if (StrLen(lc_type_name) == 0)
    msgbox("Тип документа ", lc_type_name, " не опознан!");
  end;

  return lc_type_name;
END;

/*Обновление записи в таблице ddoc_dprt_dbt*/
MACRO Update_Points(p_type, p_code_currency_id, p_name, p_accountp, p_nameaccp, p_accountr, p_nameaccr, p_corrscheme, p_agent )
   var str;
   var cmd:RsdCommand;

   str = "UPDATE ddoc_dprt_dbt " +
         "   SET t_accountp = nvl(?, chr(1)) , " +
         "       t_nameaccp = nvl(?, chr(1)) , " +
         "       t_accountr = nvl(?, chr(1)) ," +
         "       t_nameaccr = nvl(?, chr(1)) ," +
         "       t_corrscheme = nvl(?, chr(1)) ," +
         "       t_agent = nvl(?, chr(1)) ," +
         "       t_reserve = chr(1) " +
         " WHERE t_type = ? " +
         " AND t_code_currency = ? " +
         " AND t_name = ? ";

   cmd = RsdCommand(str);

   cmd.addParam( "", RSDBP_IN, p_accountp );
   cmd.addParam( "", RSDBP_IN, p_nameaccp );
   cmd.addParam( "", RSDBP_IN, p_accountr );
   cmd.addParam( "", RSDBP_IN, p_nameaccr );
   cmd.addParam( "", RSDBP_IN, p_corrscheme );
   cmd.addParam( "", RSDBP_IN, p_agent );
   cmd.addParam( "", RSDBP_IN, p_type );
   cmd.addParam( "", RSDBP_IN, p_code_currency_id );
   cmd.addParam( "", RSDBP_IN, p_name );
  
   cmd.execute;

   return true;
END;

/*Обновление записи в таблице ddoc_dprt_dbt*/
MACRO Insert_Points(p_type, p_code_currency_id, p_name, p_accountp, p_nameaccp, p_accountr, p_nameaccr, p_corrscheme, p_agent )
   var str;
   var cmd:RsdCommand;


   str = "INSERT INTO ddoc_dprt_dbt " +
         " (t_type, " +
         "  t_code_currency, " +
         "  t_name, " +
         "  t_accountp, " +
         "  t_nameaccp, " +
         "  t_accountr, " +
         "  t_nameaccr, " +
         "  t_corrscheme, " +
         "  t_agent, " +
         "  t_reserve) VALUES ( " +
         " ?,?,?, " +
         " nvl(?, chr(1)) , " +
         " nvl(?, chr(1)) , " +
         " nvl(?, chr(1)) , " +
         " nvl(?, chr(1)) , " +
         " nvl(?, chr(1)) , " +
         " nvl(?, chr(1)) , " +
         " chr(1))";
   
   cmd = RsdCommand(str);

   cmd.addParam( "", RSDBP_IN, p_type );
   cmd.addParam( "", RSDBP_IN, p_code_currency_id );
   cmd.addParam( "", RSDBP_IN, p_name );
   cmd.addParam( "", RSDBP_IN, p_accountp );
   cmd.addParam( "", RSDBP_IN, p_nameaccp );
   cmd.addParam( "", RSDBP_IN, p_accountr );
   cmd.addParam( "", RSDBP_IN, p_nameaccr );
   cmd.addParam( "", RSDBP_IN, p_corrscheme );
   cmd.addParam( "", RSDBP_IN, p_agent );
  
   cmd.execute;

   return true;
END;


/* Наличие записи в справочнике пунктов */
MACRO Is_Point_in_Table ( p_type, p_code_currency_id, p_name )

  var str:string;
  var rs:RsdRecordset;
  var cmd:RsdCommand;

  str = " SELECT COUNT (*) cnt " +
        " FROM ddoc_dprt_dbt "
        " WHERE t_type = ? " +
        " AND t_code_currency = ? " +
        " AND t_name = ? ";

  cmd = RsdCommand(str);

  cmd.addParam( "", RSDBP_IN, p_type );
  cmd.addParam( "", RSDBP_IN, p_code_currency_id );
  cmd.addParam( "", RSDBP_IN, p_name );

  rs = RsdRecordSet(cmd);

  if(rs and rs.movenext)
     if(rs.value(0) > 0)
        return true;
     end;
  end;
  return false;

END;


/*Обработчик для панели*/
MACRO RNKO_Point_dlg (dlg, cmd, id, key)
   var st;
   var rs:RsdRecordset;
   var rscmd:RsdCommand;

   var curr_get_err:integer = 0;

   if ( cmd == DLG_INIT )

      dlg.rec.type       =  v_type_name;
      dlg.rec.code_currency = string(v_code_currency);
      dlg.rec.name       =  v_name;
      dlg.rec.accountp   =  v_accountp;
      dlg.rec.nameaccp   =  v_nameaccp;
      dlg.rec.accountr   =  v_accountr;
      dlg.rec.nameaccr   =  v_nameaccr;
      dlg.rec.corrscheme =  v_corrscheme;
      dlg.rec.agent      =  v_agent;
                                                                                       
      UpdateFields(dlg);
      
   elif ( cmd == DLG_REMFOCUS ) 
      return CM_IGNORE;
   elif( cmd == DLG_KEY ) //Проверки на нажатие клавиш
      if( key == 27 ) //Esc
         ischanged = false;
         return CM_CANCEL;
      elif ( key == 317 ) /*F3*/         
         /*Если встали в поле Тип*/
         if (FldName(dlg,id)=="type")
           choise_type =  menu(LstType_Name, "~Enter~ - выбор ~ESC~ - выход", "  Тип платежа  ");
           if(choise_type >= 0 )
             dlg.rec.type = LstType_Name(choise_type);
           end;
           UpdateFields(dlg);
         end;
         return CM_IGNORE;
      elif (key == 323) //F9

         if(StrLen(trim(dlg.rec.type)) == 0 )
           msgbox("Поле ТИП пустое!");
           return CM_IGNORE;
         end;  

         if(StrLen(trim(dlg.rec.corrscheme)) == 0 )
           msgbox("Поле ПУНКТ пустое!");
           return CM_IGNORE;
         end;  

         if(StrLen(trim(dlg.rec.code_currency)) == 0 )
           msgbox("Поле Валюта пустое!");
           return CM_IGNORE;
         end;  

         /*Проверка заполненности полей*/
         if(TypeNameToID(trim(dlg.rec.type)) == -1)
           msgbox("Название типа некорректное! Выберите по F3!");
           return CM_IGNORE;
         end;  

         ПолучитьКодФинИн(string(trim(dlg.rec.code_currency)), curr_get_err) == "";
         if (curr_get_err != 0)
           msgbox("Код валюты некорректный!");
           return CM_IGNORE;
         end; 

         /*Разберемся с рублями. Отображается 810, а в таблице код 0*/
         v_code_currency_id = int(v_code_currency);
         if (v_code_currency_id == 810)
           v_code_currency_id = 0;
         end;

         if(isupdate) /*Обновляем запись*/
            /*Если меняем одно из ключевых полей, то обновить не сможем.*/
            if((dlg.rec.type    !=  v_type_name) or
               (dlg.rec.code_currency != string(v_code_currency)) or
               (dlg.rec.name       !=  v_name)
              )
               msgbox("Невозможно обновить поле ТИП, ВАЛЮТА и ПУНКТ! Удалите и вводите заново!");
               return CM_IGNORE;
            end;

            /*Обновляем запись*/
            if (Update_Points( TypeNameToID(dlg.rec.type), v_code_currency_id, dlg.rec.name, dlg.rec.accountp, dlg.rec.nameaccp, dlg.rec.accountr, dlg.rec.nameaccr, dlg.rec.corrscheme, dlg.rec.agent ))
               ischanged = true;
            else
               msgbox("Не обновилось :(!!!");
               ischanged = false;
               return CM_IGNORE;
            end;
         else//Вставляем запись 
            /*Смотрим нет ли в таблице уже такой записи с таким  типом, кодом валютой, пунктом*/
            if (Is_Point_in_Table(TypeNameToID(dlg.rec.type), v_code_currency_id, dlg.rec.name))
              msgbox("Уже есть запись для типа: " + dlg.rec.type + "; Для валюты: " + v_code_currency + "; Пункта: " + dlg.rec.name);
              ischanged = false;
              return CM_IGNORE;
            end;

            if (insert_Points(TypeNameToID(dlg.rec.type), v_code_currency_id, dlg.rec.name, dlg.rec.accountp, dlg.rec.nameaccp, dlg.rec.accountr, dlg.rec.nameaccr, dlg.rec.corrscheme, dlg.rec.agent))
              ischanged = true;
            else
              msgbox("Не Вставилось :(!!!");
              ischanged = false;
              return CM_IGNORE;
            end;
         end;

         return CM_SAVE;

      end;

   end;
   return CM_DEFAULT;
END;


/*Если нажимаем Enter*/
MACRO press_F5(cmd, rsd, id, key)

  if(rsd.RecCount == 0)
    return CM_IGNORE;
  end;

  v_type_name = rsd.value("t_type"); /*Тип*/ 
  v_type = TypeNameToID(v_type_name); /*Тип*/ 

  v_code_currency = rsd.value("t_code_currency"); /*Код валюты*/ 

  if( v_code_currency == 0)
     v_code_currency = 810;
  end;

  v_name = rsd.value("t_name"); /*Наименование пункта*/ 
  v_accountp = rsd.value("t_accountp"); /*Счет плательщика*/ 
  v_nameaccp = rsd.value("t_nameaccp"); /*Плательщик*/ 
  v_accountr = rsd.value("t_accountr"); /*Счет получателя*/ 
  v_nameaccr = rsd.value("t_nameaccr"); /*Получатель*/
  v_corrscheme = rsd.value("t_corrscheme"); /*Получатель*/
  v_agent = rsd.value("t_agent"); /*Код агента*/

  /*Флаг, что обновляем*/
  isupdate = true;

  //Панель изменения записи
  RunDialog (dlg, "RNKO_Point_dlg");

  if(ischanged)
     return CM_SELECT;
  else
     return CM_IGNORE;
  end;

  return CM_SELECT;
END;

/*Если нажимаем F9 в скролинге*/
MACRO press_F9(cmd, rsd, id, key)

  /*Флаг, что вставляем*/
  isupdate = false;

  v_type_name = "НАЖМИТЕ F3!"; /*Тип*/ 
  v_type = -1; /*Тип*/ 

  v_code_currency = "810";

  v_name = ""; /*Наименование пункта*/ 
  v_accountp = ""; /*Счет плательщика*/ 
  v_nameaccp = ""; /*Плательщик*/ 
  v_accountr = ""; /*Счет получателя*/ 
  v_nameaccr = ""; /*Получатель*/
  v_corrscheme = -1; /*Получатель*/
  v_agent = ""; /*Код агента*/

  /*Очистим поля, если перед этим нажали F5*/
/*
  dlg.rec.type       =  v_type_name;
  dlg.rec.code_currency = v_code_currency;
  dlg.rec.name       =  v_name;
  dlg.rec.accountp   =  v_accountp;
  dlg.rec.nameaccp   =  v_nameaccp;
  dlg.rec.accountr   =  v_accountr;
  dlg.rec.nameaccr   =  v_nameaccr;
  dlg.rec.corrscheme =  v_corrscheme;
  dlg.rec.agent      =  v_agent;
*/


  //Панель изменения/ввода записи
  RunDialog (dlg, "RNKO_Point_dlg");
  
  if(ischanged)
     return CM_SELECT;
  else
     return CM_IGNORE;
  end;

  return CM_SELECT;
END;


/*Нажимаем delete в скроллинге*/
MACRO press_del(cmd, rsd, id, key)

   var rs_del, str_del;

   if(rsd.RecCount == 0)
     return CM_IGNORE;
   end;

   if (GetTrue(false, "!!! Удалить запись?"))

      v_type = TypeNameToID(rsd.value("t_type"));
      
      v_code_currency = rsd.value("t_code_currency");
      /*В таблице хранится не 810, а 0*/
      v_code_currency_id = int(v_code_currency);
      if(v_code_currency == "810")
        v_code_currency_id = 0;
      end;

      str_del = "DELETE ddoc_dprt_dbt WHERE t_type = " + v_type + " AND t_code_currency = " + v_code_currency_id + " AND t_name = '" + rsd.value("t_name") + "'";

      rs_del = rsdcommand(str_del);
      rs_del.execute;
/*
      rsd_del.Delete();
      
      UpdateScroll(rsd, 5);

      if(rsd.RecCount == 0)
        return CM_SELECT;
      end;
*/
   end;

   return CM_SELECT;
END;


MACRO SetOptions ()

  var select;

  /*Объявляем экземпляр класса глобальным*/
  var scroll = ZUBScroll;


  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,path);
  pathfile = FindPath(filen,path);
  if (not pathfile)
    msgbox("Не найдена LBR: rnko.lbr");
    exit();
  end;

  dlg = TRecHandler("PL_CENTR",pathfile,True);

  //Обработчик для скроллинга

  Select = "SELECT decode(t_type, " +
            " 0, 'Золотая корона вх.', " +
            " 1, 'Золотая корона исх.', " +
            " 2, 'Город входящие', " +
            " 3, 'Город исходящие', " +
            " 4, 'Контакт входящие', " +
            " 5, 'Контакт исходящие', " +
            " t_type) t_type, " +
            " decode (t_code_currency, 0, '810', t_code_currency) t_code_currency, " +
            " t_name, t_accountp, t_nameaccp, t_accountr, " +
            "       t_nameaccr, t_corrscheme, t_agent " +
            "  FROM ddoc_dprt_dbt " +
            "  order by t_type, t_code_currency, t_name "; 

  /*Разрушаю объект*/
  scroll = null;

  scroll = ZUBScroll;

  scroll.sqltext = select;

  scroll.ScrollPrompt = "~Esc~ Выход ~F5~ Редактирование ~F8~ Удалить ~F9~ Ввод";

  scroll.SetMacroOnKeys( 323, "press_F9"); //F9
  scroll.SetMacroOnKeys( 322, "press_del"); //del
  scroll.SetMacroOnKeys( 319, "press_F5"); //F5

  scroll.ScrollHead = "Центры РНКО";

  scroll.Columns.Count = 0;
  scroll.Columns.CArray.Size = 0;

  scroll.Columns.Add ("t_type", " Тип ", 20, null);
  scroll.Columns.Add ("t_code_currency", "Вал.", 3, null);
  scroll.Columns.Add ("t_name", "Пункт", 15, null);
  scroll.Columns.Add ("t_accountp", "Счет плательщик", 20, null);
  scroll.Columns.Add ("t_nameaccr", "Плательщик", 35, null);
  scroll.Columns.Add ("t_accountr", "Счет получатель", 20, null);
  scroll.Columns.Add ("t_nameaccr", "Плучатель", 35, null);
  scroll.Columns.Add ("t_corrscheme", "Корсх", 6, null);
  scroll.Columns.Add ("t_agent", "Агент", 10, null);

  /* Инициализируем массив. Нужен будет дальше */
  Init_Type();

  while (scroll.Scroll)
    scroll.CreateRecordSet;
  end;

END;

exit(1);