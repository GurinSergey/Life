//-----------------------------------------------------------------------------
// Блок     : "Контроль п/п/к"
// Шаг      : "Контроль"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, PSInter, psbccomn, cbsttls, PTInter;
import OprInter, oralib; //Jushmanov 2014-01-21 C-19151

var PaymentObj:RsbPayment;

MACRO ExecuteStep( doc, paymDoc )

  var note_text:string = "";

  if( PM_NeedRejectControl() == true )

    if( not GetMultNoteReject( note_text ))
      SetNoteReject( note_text );
      if(not strlen(note_text))
        note_text = "Контроль не выполнен";
      end;
    end;

    // Установить статус платежа
    PaymentObj.PaymStatus = PM_REJECTED;

    // Установить статус первички
    BC_SetPrimDocumentState( PaymentObj, PSBCORD_ST_REJECTED );

    // Заполнить статусы сегментов операции
    if( УстановитьСтатусыПлатежа( OPR_BC_CONTROL, OPR_BC_ST_CTRL_REJECTED ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
   
    // Заполнить примечание
    if( PaymentObj.Notes.AddNote( PM_NOTEKIND_DENIALGROUND, note_text ) != 0 )
      msgbox( "Ошибка при вставке примечания платежа" );
      return 1;
    end;

  else
    
    if( УстановитьСтатусыПлатежа( OPR_BC_CONTROL, OPR_BC_ST_CTRL_CONTROL ) )
      msgbox("Ошибка при установке сегментов статуса экземпляра операции");
      return 1;
    end;
  
  end;

  return 0;
END;


//Jushmanov 2014-01-21 C-19151
macro PostStepAction( message,  /* данный параметр может принимать следующие       */
                                /* значения: 1 - выполнение шага; 2 - откат шага;  */
                      errTrn,   /* статус выполнения шага. Если параметр не равен  */
                                /* 0, то произошла ошибка                          */
                      FirstDoc, /* указатель на первичный документ                 */
                      ID_Oper,  /* внутренний идентификатор операции               */
                      Num_Step, /* Номер шага операции (из настроек)               */
                      KindOper, /* номер вида операции                             */
                      KindDoc,  /* номер вида первичного документа                 */
                      KindStep, /* вид шага операции                               */
                      ID_Step ) /* внутренний идентификатор шага операции          */

private var query;

    if(( message == OP_EXECUTE_STEP ) and (errTrn == 0) and (IsOprMultiExec))
        if ((valtype(ID_Oper) != V_INTEGER) and (valtype(ID_Oper) != V_STRING)) ID_Oper = 0; end;
        if ((valtype(ID_Step) != V_INTEGER) and (valtype(id_Step) != V_STRING)) ID_Step = 0; end;

        query = "UPDATE usr_doprstep_robot_dbt SET t_is_robot_step = chr(88)" +
                " WHERE t_id_operation = " + ID_Oper + " AND t_id_step = " + ID_Step;
        ExecSQL(query, null, false);
    end;

  return 0;
end;