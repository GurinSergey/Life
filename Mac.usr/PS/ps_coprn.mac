/* ps_coprn.mac                                   Бурак В.Ю.             */
// KS 22.11.2013 Перенесены доработки из 30й сборки
/* ============= Печать платежного поручения ==============================


 =========================================================================*/
import prpm, likepy, "adress.mac";

CONST ПутьНастройкиИспользованияVRSLВалютномПоручении = "PS\\CPORDER\\ORDER\\USE_VRSL";

macro joinAddress(PostIndex:string, Address:string):string
  return join(map(filter(makeArray(PostIndex, Address)), @Trim), ", ");
end;



//Tikh Время платежа. Сначала обработчик, затем фискальный журнал

private macro gettimeP (ID, date1)
   // zmp 01.10.2014 I-00519083 usr_payments_log => dpmpaym_dbt
   var cmdT = rsdcommand("select to_char(T_CREATIONTIME,'HH24:MI:SS') from  dpmpaym_dbt where t_paymentid = ?");
       cmdT.addparam("Id",RSDBP_IN, ID);    
   var rsdT = rsdrecordset(cmdT);
   if (rsdT.movenext())
       return ("Время поступления платежа: "+rsdT.value(0));
   end;
   return "";
end;


macro DrawReport( Number, Date,
                  Client_Name, Client_Address, INN,
                  Currency, Amount, Amount_str,
                  Payer_Account,
                  ComissCharges,
                  BankCorr_Name, BankCorr_Address, BankCorr_SWIFT, AccCorrBank,
                  BankBen_Name, BankBen_Address, BankBen_SWIFT, Beneficiary,
                  Beneficiary_Account,
                  Details )

  var
    SPOT, TOD,
    BEN, OUR, C_BEN, C_OUR;

  SPOT = "X";
  TOD  = "";

  if(ComissCharges == PM_CHRG_OUR)
     BEN = "";
     OUR = "X";
     C_BEN = "";
     C_OUR = "X";
  end;
  if(ComissCharges == PM_CHRG_BEN)
     BEN = "X";
     OUR = "";
     C_BEN = "X";
     C_OUR = "";
  end;
  if(ComissCharges == PM_CHRG_SHA)
     BEN = "";
     OUR = "X";
     C_BEN = "X";
     C_OUR = "";
  end;

  replace( Details, "\n", "" );
  replace( Beneficiary, "\n", "" );

  ARRAY ClN, ClA, Am, D, B;

  strsplit( Client_Name,    ClN,50,50,2 ); /* Имя клиента */
  strsplit( Client_Address, ClA,50,50,3 ); /* Адрес клиента */
  strsplit( Amount_str,      Am,50,50,4 ); /* Сумма прописью */
  strsplit( Details,          D,97,97,2 ); /* Детали платежа*/
  strsplit( Beneficiary,      B,97,97,2 ); /* Реквизиты бенефициара */

[
 +-----------------------------------------------------+-----------------------------------------------------+
 |                             +---------------+       |+---------------------------------------------------+|
 | ЗАЯВЛЕНИЕ НА ПЕРЕВОД НОМЕР  |###############|       ||  ЗАПОЛНЯЕТСЯ БАНКОМ                               ||
 | PAYMENT ORDER NUMBER        +---------------+       ||- - - - - - - - - - - - - - - - - - - - - - - - - -||
 |                  +----------+                       ||                                 +---------------+ ||
 | Дата/Date        |##########|                       ||  Комиссия за перевод            |               | ||
 |                  +----------+                       ||                                 +---------------+ ||
 |                                                     ||  Комиссия за Паспорт сделки     |               | ||
 |                                                     ||                                 +---------------+ ||
 |                                                     ||  НДС                            |               | ||
 |                                                     ||                                 +---------------+ ||
 +-----------------------------------------------------||- - - - - - - - - - - - - - - - - - - - - - - - - -+|
 |  :50: КЛИЕНТ/CLIENT                                 ||                         +-----------------------+ ||
 |  Имя клиента / Client's name                        ||  Сумма к переводу       |                       | ||
 |  ################################################## ||                         +-----------------------+ ||
 |  ################################################## ||  Банк-корреспондент                               ||
 |  Адрес/Address                                      ||                                                   ||
 |  ################################################## ||  :30: Дата валютирования                          ||
 |  ################################################## ||                                                   ||
 |  ################################################## ||- - - - - - - - - - - - - - - - - - - - - - - - - -+|
 |            +------------------------------------+   ||                                                   ||
 |  ИНН       |####################################|   || Подписи ответственных лиц                         ||
 |            +------------------------------------+   ||                                                   ||
 +-----------------------------------------------------|+---------------------------------------------------+|
 |                                                     +-----------------------------------------------------+
 |  :32:ПЛАТИТЬ/PAY                                       Сумма прописью / Amount in words                   |
 |  +--------------+-----------------------------+                                                           |
 |  |      ###     |#############################|        ################################################## |
 |  +--------------+-----------------------------+        ################################################## |
 |  Валюта платежа  Сумма цифрами                         ################################################## |
 |  Paymentcurrency Numerical Amount                      ################################################## |
 +--------------------------------------------------------------------+--------------------------------------+
 |                                    +-----------------------------+ | Перевод на условиях:/Payment terms:  |
 |  Платеж осуществить с нашего счета |#############################| |              +---+             +---+ |
 |  Please debit our account No       +-----------------------------+ | обычный/SPOT |###| срочный/TOD |###| |
 |                                                                    |              +---+             +---+ |
 +--------------------------------------------------------------------+--------------------------------------+
 |                                                     +--+                                        +--+      |
 |  : :  Расходы по переводу нашего банка за наш счет  |##|             за счет получателя         |##|      |
 |       Charges of our bank for our account           +--+             for beneficiary's account  +--+      |
 +-----------------------------------------------------------------------------------------------------------+
 |                                                     +--+                                        +--+      |
 |  :71: Комиссии банков корреспондентов за наш счет   |##|             за счет получателя         |##|      |
 |       Correspondent`s charges for our account       +--+             for beneficiary's account  +--+      |
 +-----------------------------------------------------------------------------------------------------------+
 |  :56: БАНК-КОРРЕСПОНДЕНТ БАНКА ПОЛУЧАТЕЛЯ                                                                 |
 |       CORRESPONDENT BANK OF BENEFICIARY'S BANK                                                            |
 |       Наименование банка / Bank name                                                                      |
 |       ################################################################################################### |
 |       Адрес / Address                                                                                     |
 |       ################################################################################################### |
 |  S.W.I.F.T. код/code          +-----------------------------+        <CHIPS UID No, ABA No,               |
 |  или/or <...>                 |#############################|        Bank Code, Sort Code etc.>           |
 +-------------------------------+-----------------------------+---------------------------------------------+
 |  :57: БАНК ПОЛУЧАТЕЛЯ ПЛАТЕЖА / BENEFICIARY'S BANK                                                        |
 |       Наименование банка / Bank name                                                                      |
 |       ################################################################################################### |
 |       Адрес / Address                                                                                     |
 |       ################################################################################################### |
 |  Счет в банке-корресп.        +-----------------------------+                                             |
 |  Acc with corresp. Bank       |#############################|                                             |
 |                               +-----------------------------+                                             |
 |  S.W.I.F.T. код/code          +-----------------------------+        <CHIPS UID No, ABA No,               |
 |  или/or <...>                 |#############################|        Bank Code, Sort Code etc.>           |
 +-------------------------------+-----------------------------+---------------------------------------------+
 |  :59: ПОЛУЧАТЕЛЬ ПЛАТЕЖА / BENEFICIARY                                                                    |
 |       Имя получателя / Beneficiary's name                                                                 |
 |       Адрес / Address                                                                                     |
 |       ################################################################################################### |
 |       ################################################################################################### |
 |  Счет получателя номер        +-----------------------------+                                             |
 |  Beneficiary's Acc No         |#############################|                                             |
 +-------------------------------+-----------------------------+---------------------------------------------+
 |  :70: ДЕТАЛИ ПЛАТЕЖА / PAYMENT DETAILS                                                                    |
 |                                                                                                           |
 |       ################################################################################################### |
 |       ################################################################################################### |
 +-----------------------------------------------------------------------------------------------------------+
 |  В случае недостаточной информации для платежа Банк не несет ответственности за сроки прохождения платежа |
 |In case of incomplete or incorrect information in payment instructions the Bank incurs no responsibility   |
 |                                      for processing of payment                                            |
 +-----------------------------------------------------------------------------------------------------------+


                 М.П.                    Подписи / Signatures                   
                 
                 
                                                            ##################################################


]


 (Number:l,
  Date:c,
  ClN(0):l, ClN(1):l,
  ClA(0):l, ClA(1):l, ClA(2):l,
  INN:c,
  Currency:c, Amount:r, Am(0):l,
  Am(1):l, Am(2):l, Am(3):l,
  Payer_Account:c, SPOT:c, TOD:c,
  OUR:l, BEN:l, C_OUR:l, C_BEN:l,
  BankCorr_Name:l, BankCorr_Address:l, BankCorr_SWIFT:c,
  BankBen_Name:l, BankBen_Address:l,
  AccCorrBank:c, BankBen_SWIFT:c,
  B(0):l, B(1):l,
  Beneficiary_Account:c,
  D(0):l, D(1):l,
  gettimeP(pr_pmpaym.rec.paymentid, pr_pmpaym.rec.valuedate)
 );
 return true;
end;


/* ================= Печать платежного поручения ============================= */
macro PrintDocument(ncopy:integer):bool
 debugbreak;
 var
   Client_Address, BankBen_Address, BankBen_SWIFT, INN,
   Currency, Name_Country,
   sumstr, cur,
   BankCorr_Name, BankCorr_Address, BankCorr_SWIFT, AccCorrBank,
   Use_VRSL, err, VRSL, report, stat;

   FILE party    (party) key 0;
   FILE partcode (partcode) key 1;
   FILE country  (country,"rsrfdb.def") key 2;

   record RecordAdress ( adress );

   var DocKind:integer = pr_pmpaym.rec.DocKind;
  
   if( DocKind != PS_CPORDER )
     MsgBox("Данный вид документа не поддерживается макросом");
     return FALSE;
   end;

   GetRegistryValue( ПутьНастройкиИспользованияVRSLВалютномПоручении, V_INTEGER, Use_VRSL, err );
   if ( err != 0 )
      MsgBox( "Не определен ключ ", ПутьНастройкиИспользованияVRSLВалютномПоручении, " в настройках банка" );
      return FALSE;
   end;

   Currency = ПолучитьКодФинИН(pr_pmpaym.rec.FIID);

   party.PartyID = pr_pmpaym.rec.Payer;   
   if(GetEq(party))
      ClearRecord(RecordAdress);
      НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress);
      Client_Address = joinAddress(RecordAdress.PostIndex, RecordAdress.Adress);
      INN = GetPartyINN( party.PartyID, 1 );
   else
      Client_Address = "";
      INN = "";
   end;

   party.PartyID = pr_pmpaym.rec.ReceiverBankID;   
   if(НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress))
      BankBen_Address = joinAddress(RecordAdress.PostIndex, RecordAdress.Adress);
   else
      BankBen_Address = "";
   end;

   BankBen_SWIFT = ПолучитьКодСубъекта( pr_pmpaym.rec.ReceiverBankID, PTCK_SWIFT, err );
   if(err)
      BankBen_SWIFT   = "";
   end;

   if ( (pr_credit.rec.PaymentID==pr_pmpaym.rec.PaymentID) AND (pr_credit.rec.IsSender!="X") )
      AccCorrBank    = pr_credit.rec.CorrAcc;
      BankCorr_SWIFT = pr_credit.rec.CorrCode;

      partcode.CodeKind = pr_credit.rec.CorrCodeKind;
      partcode.Code     = pr_credit.rec.CorrCode;
      if(getEQ(partcode))
         party.PartyID = partcode.PartyID;
         if(getEQ(party))
            ClearRecord(RecordAdress);
            НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress);
            BankCorr_Address = joinAddress(RecordAdress.PostIndex, RecordAdress.Adress);
            BankCorr_Name = pr_pmrmprop.rec.ReceiverCorrBankName;
         else
            BankCorr_Address = "";
            BankCorr_Name  = "";
         end;
      else
         BankCorr_Name    = "";
         BankCorr_Address = "";
      end;
   else
      AccCorrBank    = pr_debet.rec.CorrAcc;
      BankCorr_SWIFT = pr_debet.rec.CorrCode;

      partcode.CodeKind = pr_debet.rec.CorrCodeKind;
      partcode.Code     = pr_debet.rec.CorrCode;
      if(getEQ(partcode))
         party.PartyID = partcode.PartyID;
         if(getEQ(party))
            ClearRecord(RecordAdress);
            НайтиЮридическийАдресСубъекта(party.PartyID,RecordAdress);
            BankCorr_Address = joinAddress(RecordAdress.PostIndex, RecordAdress.Adress);
            BankCorr_Name  = party.ShortName;
         else
            BankCorr_Address = "";
            BankCorr_Name  = "";
         end;
      else
         BankCorr_Name    = "";
         BankCorr_Address = "";
      end;
   end;

   sumstr = string(MoneyL(pr_pmpaym.rec.Amount));
   cur = CurToStrAlt(pr_pmpaym.rec.Amount , null, null, getISOCode(pr_pmpaym.rec.FIID) );

   if( not Use_VRSL)
     if( pr_PrintEA )
       PrintEAHeader();
     end;
     while(ncopy > 0)
         stat =    DrawReport(
                   pr_pmrmprop.rec.Number, string(pr_pmrmprop.rec.Date),
                   SubStr(pr_pmrmprop.rec.PayerName, 1, 100), SubStr(Client_Address, 1, 150 ), INN,
                   Currency, sumstr, cur,
                   pr_pmpaym.rec.PayerAccount,
                   pr_pmrmprop.rec.ComissCharges,
                   BankCorr_Name, BankCorr_Address, BankCorr_SWIFT, AccCorrBank,
                   pr_pmrmprop.rec.ReceiverBankName, BankBen_Address, BankBen_SWIFT, pr_pmrmprop.rec.ReceiverName,
                   pr_pmpaym.rec.ReceiverAccount,
                   pr_pmrmprop.rec.Ground
                   );
       ncopy = ncopy - 1;
     end;
   else

      VRSL = ActiveX("RSGENOLE.VRslRun.1");
      report = VRSL.RunRpt("ВалютноеПлатежноеПоручение");

      report.Number.text              = pr_pmrmprop.rec.Number;
      report.Date_Document.text       = string(pr_pmrmprop.rec.Date);
      report.Client_Name.text         = pr_pmrmprop.rec.PayerName;
      report.Client_Address.text      = Client_Address;
      report.INN.text                 = INN;
      report.Currency.text            = Currency;
      report.Amount.text              = sumstr;
      report.Amount_Str.text          = cur;
      report.Details.text             = pr_pmrmprop.rec.Ground;
      report.Payer_Account.text       = pr_pmpaym.rec.PayerAccount;
      report.Beneficiary_Account.text = pr_pmpaym.rec.ReceiverAccount;
      if(pr_pmrmprop.rec.ChargesOfBank)
         report.BEN.text = "X";
      else
         report.OUR.text = "X";
      end;
      if(pr_pmrmprop.rec.CorrespondentCharges)
         report.C_BEN.text = "X";
      else
         report.C_OUR.text = "X";
      end;
      report.BankBen_Name.text        = pr_pmrmprop.rec.ReceiverBankName;
      report.BankBen_Address.text     = BankBen_Address;
      report.Ben_SWIFT.text           = BankBen_SWIFT;
      report.AccCorrBank.text         = AccCorrBank;
      replace(pr_pmrmprop.rec.ReceiverName, "\n", "" );
      report.Beneficiar.text          = pr_pmrmprop.rec.ReceiverName;
      report.Corr_Name.text           = BankCorr_Name;
      report.Corr_Address.text        = BankCorr_Address;
      report.Corr_SWIFT.text          = BankCorr_SWIFT;

      /* пока так за не имением данных*/
      report.SPOT.text                = "X";
      report.TOD.text                 ="";

      report.runReport ();
      if (IsStandAlone)
       if( ViewRep (report) )
         exit( 1 );
       end;
      else
       if( TransferRep (report) )
         exit( 1 );
       end;
      end;
   end;
  return TRUE;
end;
