/***********************************************************************
          Автоматизированная банковская система RS-Bank v6.0           
                  Copyright (c) R-Style Software Lab                   
   Имя файла        : psrqdoc.mac
   Описание         : Макродействия ИПВС
   Создан           : 01.03.2011                                       
***********************************************************************/
// KS 22.11.2013 Перенесены доработки из PS_CP.MAC

import BankInter, payments, globals, pm_opr, pmchoper, pm_tools, pm_common, pmbuff, pm_chksave, "pm_syscont";

/* номера полей в панели */
const fld_Number = 1,  /* номер */
      fld_PaymKind = 3,  /* вид платежа*/
      fld_NumberPay = 7, /* номер поля поручения на продажу*/
      fld_Ground = 24; /* основание */


/* Проверка на числовой номер */
macro IsDigitNumber( Number )
  var stat = 0, i = 1, ch, DigitString = "0123456789";

  while( (not stat) and (i <= strlen(Number)) )
    ch = SubStr( Number, i, 1 );
    if( not Index( DigitString, ch ))
      stat = 1;
    end;
    i = i + 1;
  end;

  return stat;
end;

/*  Определение, что две записи(имеющие одинаковое описание в bank.def)
    совпадают, или отличаются только одним полем с заданным именем.
    Если записи совпадают, возвращается true */
MACRO ПроверкаИдентичностиИПВС(Запись1, Запись2, НаименованиеНепроверяемогоПоля)

  var НомерНепроверяемогоПоля = -1;
  var i = 0;

  if( (ValType(НаименованиеНепроверяемогоПоля) == V_STRING) and (НаименованиеНепроверяемогоПоля != "") )
    НомерНепроверяемогоПоля = FldIndex(Запись1,НаименованиеНепроверяемогоПоля);
  end;

  while(i < FldNumber(Запись1))
    if( (i != НомерНепроверяемогоПоля) and (Запись1(i) != Запись2(i)) ) return false; end;
    i = i + 1;
  end;

  return true;
END;

macro Проверить_Номер(number:string, str_docname:string):integer

  if( StrLen( Number ) == 0 )
    MsgBox("Не задан номер "+str_docname);
    return 1;
  end;

  if( IsDigitNumber( Number ) )
    MsgBox("Номер "+str_docname+" нечисловой");
    return 1;
  else
     if( int( Number ) == 0 )
        MsgBox("Номер "+str_docname+" не может быть нулевым");
        return 1;
     end;
  end;

  return 0;

end;


macro PS_ReqOrderCheckDoc( Режим )
  var stat:integer = 0,
      OldDoc:integer = 0;


  /* EVG Проверка номера пачки */
  if (r_pmpaym.NumberPack < 0)
      msgbox ( "Номер пачки не может быть отрицательным!" );
      return 42;
  end;

  if ( (Режим == 2 ) or (Режим == 3) or (Режим == 8) )

    if (Проверить_Номер(r_pmrmprop.Number, "документа"))
          return fld_Number;
       end;

    if( StrLen( r_pmrmprop.Ground ) == 0 )
      MsgBox("Введите назначение платежа");
      return fld_Ground;
    end;
    // Документ не устарел?
    if( OldDoc = GetRegValueOldDoc( r_pmpaym.DocKind ) )
      if((r_pmpaym.ValueDate - r_pmrmprop.Date) > OldDoc ) 
        MsgBox("Документ устарел и не может быть сохранён");
        return 1;
      end;
    end;

    if( PM_CheckPayments( r_pmpaym, NULL, r_credit, r_pmrmprop, 1 ) )
      return 1;
    end;

    /* Общие проверки по списку */
    stat = INRQ_ScrolMacroCommonChecks( TPanelFields(), r_pmpaym, r_debet, r_credit, r_pmrmprop );
    /* Проверка длин атрибутов*/
    if( stat == NOTERROR )
      stat = CheckSave383PLength( r_pmpaym, r_debet, r_credit, r_pmrmprop );
    end;
    if( stat != NOTERROR )
      return stat;
    end;

    /*проверка реквизитов валютной операции*/
    if( PM_CheckCO(r_pmpaym,r_pmrmprop,0,0))
       return 1;
    end;

  end;

  /* Вид платежа "Срочно" запрещено устанавливать для ИПВС */
  if( r_pmrmprop.PaymentKind == "С" )
     MsgBox("Недопустимый вид платежа");
     return fld_PaymKind;
  end;

  if(Режим == 3)  /* РЕДАКТИРОВАНИЕ ДОКУМЕНТА */
    /* Запрещаем кооректировку, если внешний платеж уже выгружен в Корсчета/МБР */
    if( ( r_credit.Group == PAYMENTS_GROUP_EXTERNAL ) and (r_pmpaym.PaymStatus > PM_READY_TO_SEND)
          and ( r_credit_old.Group == PAYMENTS_GROUP_EXTERNAL ) )
       msgbox("Платеж выгружен. Корректировка реквизитов запрещена.");
       stat = 1;
    elif ( not EditFromHistScrol )
       /* Запрет редактирования проконтролированного документа */
       if( (Opr_IsStepExecuteSymb( r_pmpaym.DocumentID, r_pmpaym.DocKind, "К" ) or 
            Opr_IsStepExecuteSymb( r_pmpaym.DocumentID, r_pmpaym.DocKind, "Е" ) or
            Opr_IsStepExecuteSymb( r_pmpaym.DocumentID, r_pmpaym.DocKind, "е" )) and (r_pmpaym.PaymStatus != PM_REJECTED) )

         if( not (ПроверкаИдентичностиИПВС(r_psinrq  , r_psinrq_old  ) and
                  ПроверкаИдентичностиИПВС(r_pmpaym  , r_pmpaym_old, "FUTURERECEIVERACCOUNT") and
                  ПроверкаИдентичностиИПВС(r_credit  , r_credit_old  ) and
                  ПроверкаИдентичностиИПВС(r_pmrmprop, r_pmrmprop_old)) )
           stat = CHANG_NOTKEEP;
         elif( (not Opr_IsStepExecuteSymb( r_pmpaym.DocumentID, r_pmpaym.DocKind, "U", "R")) and
               (not Opr_IsStepExecuteSymb( r_pmpaym.DocumentID, r_pmpaym.DocKind, "З", "R", 29029)) and
               (r_pmpaym.FutureReceiverAccount != r_pmpaym_old.FutureReceiverAccount ) )
           stat = CHANG_NOTKEEP;
         end;

       /*При редактировании производим проверку важности внесенных изменений */
       /* Константы важности внесенных изменений:           */
       /* CHANG_NOTIMPORTANT   - изменения неважные (сохранение без отката операции)*/
       /* CHANG_IMPORTANT      - изменения важны (сохранение изменений возможно только при откате) */
       /* CHANG_NOTKEEP        - не сохранять изменения */
       /* Если возвращаемое значение  > 0, то это оно интерпритируется как номер поля с ошибочным параметром*/
       /* Если возвращаемое значение  = 0, то cчитается, что проверка прошла успешно
          и сохранение изменений можно производить без отката операции      */
       elif( r_psinrq.State != PSPO_ST_DEFERRED /*!!*/)
         if(  (r_pmpaym.PayAmount           != r_pmpaym_old.PayAmount) or
              (r_pmpaym.PayerAccount        != r_pmpaym_old.PayerAccount) or
              (r_pmpaym.ReceiverAccount     != r_pmpaym_old.ReceiverAccount) or
//              (r_pmpaym.NumberPack          != r_pmpaym_old.NumberPack) or
              (r_pmrmprop.PayerINN          != r_pmrmprop_old.PayerINN) or
              (r_pmrmprop.ReceiverINN       != r_pmrmprop_old.ReceiverINN) or
              (r_pmrmprop.PayDate           != r_pmrmprop_old.PayDate) or
              (r_credit.CodeKind            != r_credit_old.CodeKind) or
              (r_credit.BankCode            != r_credit_old.BankCode) or
              (r_credit.Corschem            != r_credit_old.Corschem) or
              (r_credit.RlsFormID           != r_credit_old.RlsFormID) or
              (r_pmrmprop.TaxAuthorState    != r_pmrmprop_old.TaxAuthorState) or
              (r_pmrmprop.BttTICode         != r_pmrmprop_old.BttTICode) or
              (r_pmrmprop.OKATOCode         != r_pmrmprop_old.OKATOCode) or
              (r_pmrmprop.TaxPmGround       != r_pmrmprop_old.TaxPmGround) or
              (r_pmrmprop.TaxPmPeriod       != r_pmrmprop_old.TaxPmPeriod) or
              (r_pmrmprop.TaxPmNumber       != r_pmrmprop_old.TaxPmNumber) or
              (r_pmrmprop.TaxPmDate         != r_pmrmprop_old.TaxPmDate) or
              (r_pmrmprop.TaxPmType         != r_pmrmprop_old.TaxPmType)
           )
//            stat = CHANG_IMPORTANT; 
            // KS 13.06.2012 I-00198430 Сохраняем
            stat = CHANG_NOTIMPORTANT
         end;
       end;
    end;
  elif(Режим == 2)  /* ВВОД ДОКУМЕНТА */
  elif(Режим == 1)  /* УДАЛЕНИЕ ДОКУМЕНТА */
     if(CheckDeletePayment(r_pmpaym.PaymentID))
       return 1;
     end;
  elif(Режим == 7) /*ПОМЕЩЕНИЕ ДОКУМЕНТА В ОТЛОЖЕННЫЕ, ОТКАТ ОПЕРАЦИИ*/
  elif(Режим == 8)  /*ВВОД В ОТЛОЖЕННЫЕ*/
  elif(Режим == OBJ_AFTEREDIT) // Проверка важности изменений влияющей на необходимость смены опера документа
                               // (изменения в буферах не сохраняются)
    return IsImportantChangeForOperRequestOrder(r_pmpaym, r_pmpaym_old, r_pmrmprop, r_pmrmprop_old, r_credit, r_credit_old);
  end;

  if( (not EditFromHistScrol) and ((stat == CHANG_NOTIMPORTANT) or (r_psinrq.State == PSPO_ST_DEFERRED)
                                                                or (r_psinrq.State == PSPO_ST_REJECTED)) )
    if( IsEditFields_PmInHist(r_pmpaym, r_pmpaym_old, r_credit, r_credit_old, r_pmrmprop, r_pmrmprop_old) )
      msgbox( "Не будут сохранены данные, которые можно редактировать через список для изменения реквизитов" );
      return 1;
    end;
  end;

  return stat;
end;

macro PS_ReqOrderNewDoc()
  return 0;
end;

macro PS_ReqOrderUserFunc( Режим:integer )
  return 1;
end;
