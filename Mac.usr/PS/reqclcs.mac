/* -------------------------------------------------------------------- */
/*         Автоматизированная банковская система RS-Bank v6.0           */
/*                 Copyright (c) R-Style Software Lab                   */
/*                                                                      */
/*  Имя файла  : rqchcs.mac                                             */
/*                                                                      */
/*  Описание   : Операция - 3035 - "Закрытие лицевого счета"            */
/*               Шаг      - 20   - "Закрывать дополнительные счета?"    */
/*                                                                      */
/*  Программист: Андреева Е.П.                                          */
/*                                                                      */
/*  Создан     : 11.11.2004                                             */
/*                                                                      */
/* -------------------------------------------------------------------- */
import reqinter, PSInter, BankInter;
import lib_account;

var ReqCloseAccObj:RsbReqCloseAcc;

MACRO USR_SetLinkedObject (account, client, code_cur, chapter)
    record current_acc("account.dbt");
    record trans_acc  ("account.dbt");
    ClearRecord( current_acc ); 
    ClearRecord( trans_acc );
    var m: TArray = TArray();

    var sql = "SELECT t_account acc, count (1) OVER (PARTITION BY NULL) cnt " +
              "  FROM daccount_dbt " +
              " WHERE t_client = :client AND t_code_currency = :code_cur AND INSTR (t_type_account, 'Y'/*Транзитный*/) > 0 ";
    var parm = makeArray (SQLParam ("client", client), SQLParam ("code_cur", code_cur));
    sql = execSqlSelect (sql, parm);
    while (sql.movenext())
        current_acc.account = account; current_acc.chapter = chapter; current_acc.code_currency = code_cur; 
        if (GetLinkedObject(OBJROLE_ACC_TRANSIT, OBJTYPE_ACCOUNT, current_acc, OBJTYPE_ACCOUNT, trans_acc))
            if (sql.value("cnt") > 1)
                if (not IsOprMultiExec())
                    m(m.size()) = sql.value("acc");
                    while (sql.movenext())
                        m(m.size()) = sql.value("acc");
                    end;

                    var n = menu (m, "Выберите один с транзитных счетов клиента, который надо закрыть", "Выберите транзитный счет:");
                    if (n < 0) exit(); end;

                    trans_acc.account = m(n); trans_acc.chapter = chapter; trans_acc.code_currency = code_cur;
                    SetLinkedObject( OBJROLE_ACC_TRANSIT, OBJTYPE_ACCOUNT, current_acc, OBJTYPE_ACCOUNT, trans_acc);
                else
                    msgbox("У клиента ", int(sql.value("cnt")), " транзитных счета. Нужно обработать заявление вручную");
                    exit();
                end;
            else
                trans_acc.account = sql.value("acc"); trans_acc.chapter = chapter;    trans_acc.code_currency = code_cur;            
                SetLinkedObject( OBJROLE_ACC_TRANSIT, OBJTYPE_ACCOUNT, current_acc, OBJTYPE_ACCOUNT, trans_acc);
            end;
        end;
    end;
END;

/* Выполнение шага */
MACRO ExecuteCaseStep( Kind_Operation, Number_Step )

  var NumStep:integer = 0;
  array Text;
  array Buttons;
  if ( ReqCloseAccObj.AccCloseDate > {curdate} )
    Text(0) = string("Дата закрытия счета ", ReqCloseAccObj.AccCloseDate, " еще не наступила. Закрыть счет текущим операционным днем?");
    Buttons(0) = "Текущим опер. днем";
    Buttons(1) = "Прервать";
    var stat = ConfWin(Text, Buttons, 1);
    if (stat == 1)
      return 1;
    end;
    ReqCloseAccObj.AccCloseDate = {curdate};
    var sc = TBFile("sfcontr", "W", 1);
    sc.Clear();
    sc.rec.ObjectType = SF_ACCOUNT;
    sc.rec.FIID = ReqCloseAccObj.Code_Currency;
    sc.rec.Object = ReqCloseAccObj.Account;
    sc.rec.ServKind = PTSK_PAY;
    if (sc.GetEQ())
      sc.rec.DateClose = {curdate};
      sc.Update();
    end;
  end;

  var accbuf = GetAccount( 1, ReqCloseAccObj.Account, ReqCloseAccObj.Code_Currency );

  //Gurin S. 08.08.2014 C-31467-8
  var _acc = RSL_Account(accbuf.rec.account,accbuf.rec.code_currency);
  if ( (ReqCloseAccObj.Code_Currency != NATCUR) and (index(_acc.rec.type_account,"X")>0))
     USR_SetLinkedObject(accbuf.rec.account,accbuf.rec.client,accbuf.rec.code_currency,accbuf.rec.chapter);
  end;

  if( 
      ( ReqCloseAccObj.Code_Currency == NATCUR ) or 
      ( Req_IsCliringFIID( ReqCloseAccObj.Code_Currency ) ) or 
      ( not ( Index( accbuf.rec.Type_Account, "X" ) ) ) 
    )
    NumStep = 50;
  else
    NumStep = 30;
  end;

  return string( NumStep );

END;
