/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 5.1                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Генерация сообщения по SBRF3 0B1-0B5                                    */
/*  Вынес из МБР, теперь для формирования БЦИ и генерации используется      */
/*  единый код. Формирование строки сообщения теперь происходит с           */
/*  использованием класса RsbPayment.                                       */
/*                                                                          */
/*  Имя файла: cbsbmes.mac                                                  */
/*  Создан:  18.02.03                                        Алешин А.В.    */
/****************************************************************************/

import OprInter, PaymInter, PTInter, BankInter, FIInter, "globals.mac", "cbsbtls.mac", "likepy.mac", oralib;

file f_sbpaym    ( pmpaym )   key 1;
file f_sbfininstr( fininstr ) key 0;
FILE f_cbdprt       ( bankdprt ) key 0;

FILE cb_payord (pspayord) key 0;
FILE cb_namealg(namealg) key 0;
FILE pspaydem( pspaydem ) key 0;


var cb_sbparty = TRecHandler("party.dbt");

const SB_ДоходыРаспредОрганамиФедКазначейства = "40101",
      SB_ДоходыФедБюджета = "40102";

//Преобразует строку в последовательность 16-ричных символов (для поля ME, например)
private macro ConvertToHEXSequence(Str: string): string
  var len = StrLen(Str);
  var i = 1;
  var res = "";
  while( i<=len )
    res = res + string(CodeFor(substr(str, i, 1)):x);
    i = i + 1;
  end;

  return res;
end;

/* Проверяет пустой счет или нет */
macro IsEmptyAcc( Acc )
   var i=1, n = strlen(Acc);
   while( i<=n )
      if ( substr(Acc,i,1)!="0" )
         return false;
      end;
      i = i+1;
   end;
   return true;
end;

/* Если ИНН == ИНН/КПП, то оставляем только ИНН */
macro SB_RemoveKPP(INN)
   var ind_slash = Index(INN,"/");
   if ( ind_slash != 0 ) return SubStr(INN, 1, ind_slash-1); end;
   return INN;
end;

macro SB_RemoveINN(INN)
   var ind_slash = Index(INN,"/");
   if ( ind_slash != 0 ) return SubStr(INN, ind_slash+1); end;
   return "";
end;

macro strLeft( str, maxLen )
  return substr(str,1,maxLen );
end;

/*Зубко Удаление запрещенных символов*/
macro removeForbid( p_str )
  var v_str:string = "";

  v_str = p_str;
  v_str = StrSubst(v_str, "ё", "е");
  v_str = StrSubst(v_str, "Ё", "Е");
  v_str = StrSubst(v_str, "№", "N");

  return v_str;
end;

/* Формирование сообщения в формате СБРФ (формы 0B1,..,0B5) по платежу 
   TypeDoc   - тип сообщения (0B1, 0B2 и др.)
   CPaym     - сконструированный объект класса TRsbPayment   
   MacroFill - макропроцедура обработки заполненных полей сообщения
   Parm      - дополнительный параметр для передачи в макропроцедуру обработки */
macro GenMesSBRF3Str( TypeDoc, CPaym, MacroFill:string, Parm )

  var field_value, ВидКодаСтр, error, CodeKind, PartyID, SubTypeDoc, ИскатьВышестоящих,
      
      Payer,
      INN_Payer,
      KPP_Payer,
      Bank_Payer,
      KindCode_Payer,
      Code_Payer,
      CorAcc_Payer,
    
      Receiver,
      INN_Receiver,
      KPP_RECEIVER,
      Bank_Receiver,
      KindCode_Receiver,
      Code_Receiver,
      CorAcc_Receiver,
      ReceiverBalanseAcc,
      PayerChargeOffDate, 
      PlaceDate,
      DateAkkr,
      CondAkkr,
      InDate,
      len_field_value,
      PayerMesCode, 
      ReceiverMesCode;
  
  Payer             = CPaym.PayerName;
  Bank_Payer        = CPaym.PayerBankName;
  KindCode_Payer    = CPaym.PayerBankCodeKind;
  Code_Payer        = CPaym.PayerBankCode;

  var IsPaydem: bool = false; //Клиентское требование?

  ClearRecord(pspaydem);

  pspaydem.OrderId = CPaym.DocumentID;

  IsPaydem = getEQ(pspaydem); //Определяется по наличию записи pspaydem

  /*Зубко Сделал так, так как требования не выгружаем
    потом сделаю и CPaym.ShifrOper = "01";*/
  IsPaydem = false;

  if ( Code_Payer != "" )
     CorAcc_Payer      = CPaym.PayerCorrAccNostro;
  else
     CorAcc_Payer      = {CORAC_Bank};
  end;

  INN_Payer      = SB_RemoveKPP(CPaym.PayerINN);
  KPP_Payer      = SB_RemoveINN(CPaym.PayerINN);

  Receiver          = CPaym.ReceiverName;
  Bank_Receiver     = CPaym.ReceiverBankName;
  KindCode_Receiver = CPaym.ReceiverBankCodeKind;
  Code_Receiver     = CPaym.ReceiverBankCode;

  if ( Code_Receiver != "" )
     CorAcc_Receiver   = CPaym.ReceiverCorrAccNostro;
  else
     CorAcc_Receiver   = {CORAC_Bank};
  end;

  INN_Receiver      = SB_RemoveKPP(CPaym.ReceiverINN);
  KPP_Receiver      = SB_RemoveINN(CPaym.ReceiverINN);  
  
  if( CPaym.ReceiverFIID != 0 ) /* Валюта */    
    /* Для валютных ИНН не выгружаем */
    INN_Payer = "";
    INN_Receiver = "";
  end;

  /* Заполняем параметры нашего банка в платежной системе контрагента */
  ИскатьВышестоящих = 1;
  if ( ( Code_Payer == "" ) AND ( Code_Receiver != "" ) ) /* Кредитовка */
      KindCode_Payer = KindCode_Receiver;
      Code_Payer = ПолучитьКодСубъекта( {OurBank}, KindCode_Payer, error, ИскатьВышестоящих);
      if ( error != 0 )
         RunError("|Не найден код нашего банка вида " + string(KindCode_Payer));
      end;
  end;

  if ( ( Code_Receiver == "" ) AND ( Code_Payer != "" ) ) /* Дебетовка */
      KindCode_Receiver = KindCode_Payer;
      Code_Receiver = ПолучитьКодСубъекта( {OurBank}, KindCode_Receiver, error, ИскатьВышестоящих);
      if ( error != 0 )
         RunError("|Не найден код нашего банка вида " + string(KindCode_Receiver));
      end;
  end;  
  
  /* Тип документа */
  ExecMacro( MacroFill, "DT", TypeDoc, Parm );

  /*Зубко С. Так как при выгрузке документов с шифром 02, 16 возникали ошибки, то с SDA принято решение выгружать простые платежки, чтобы избавится от неприятностей*/
  CPaym.ShifrOper = "01";
  /*Так как правил "нагорячую", то вставил заглушку, если свойство не переопределится*/
  if( CPaym.ShifrOper != "01" )
    RunError( "|Попытка выгрузить документ с шифром операции не 01 !" );
  end;

  /* Подтип документа, определяем по шифру операции (только для типов 001, 003, 005) */
  SubTypeDoc = 0;
  if( (TypeDoc == type_001) OR (TypeDoc == type_005) )    
    if ( int(substr(CPaym.ShifrOper, 1, 2))==6 )
       SubTypeDoc = 8; /* Инкассовое поручение */ 
    elif ( int(substr(CPaym.ShifrOper, 1, 2))==8 )
       SubTypeDoc = 5; /* аккредитив */ 
    elif ( int(substr(CPaym.ShifrOper, 1, 2))==16 )
       SubTypeDoc = 4; /* платежный ордер */ 
    elif ( int(substr(CPaym.ShifrOper, 1, 2))==9 ) 
       SubTypeDoc = 6; /* мемориальный ордер */ 
    end;
  elif( TypeDoc == type_003 )
    if ( int(substr(CPaym.ShifrOper, 1, 2))==16 )
       SubTypeDoc = 4; /* платежный ордер */ 
    end;
  end;

  if(IsPaydem)
      ExecMacro( MacroFill, "UT", sub_type_9, Parm );
  else
    /* Ненулевой подтип документа записываем в сообщение */
    if( SubTypeDoc != 0 )
      ExecMacro( MacroFill, "UT", String(SubTypeDoc), Parm );
    end;
  end;

  if( (TypeDoc != type_003) OR (SubTypeDoc != 4) )
     ExecMacro( MacroFill, "SH", CPaym.ShifrOper, Parm );
  end;

  CodeKind = PTCK_CLIRING;

  /* Участник-отправитель */
  /*
  if( CPaym.PayerMesBankID == {OurBank} )
    PartyID = {OurBank};
    ИскатьВышестоящих = 1;
  else
    PartyID = CPaym.PayerMesBankID;
    ИскатьВышестоящих = 0;
  end;
  */

  /*Зубко С. Так как PartyID определялось 0, то сделл вот так*/
  PartyID = {OurBank};
  ИскатьВышестоящих = 1;

  PayerMesCode = ПолучитьКодСубъекта( PartyID, CodeKind, error, ИскатьВышестоящих );
  if( error != 0 )
    /* Не найден такой код */
    RunError( "|Не найден код клиринга банка участника-отправителя" );
  end;
  ExecMacro( MacroFill, "PA", CB_StrSubst0( String(PayerMesCode:10) ), Parm );

  /* Участник-получатель */
/*
  if( CPaym.ReceiverMesBankID == {OurBank} )
    PartyID = {OurBank};
    ИскатьВышестоящих = 1;
  else
    PartyID = CPaym.ReceiverMesBankID;
    ИскатьВышестоящих = 0;
  end;
*/

  /*Зубко С. Так как PartyID определялось 0, то сделал так, чтобы код получаетеля определялся по коду банка-получателя*/
  PartyID = CPaym.ReceiverBankID;
  ИскатьВышестоящих = 1;

  ReceiverMesCode = ПолучитьКодСубъекта( PartyID, CodeKind, error, ИскатьВышестоящих);

  /*Зубко 
    Нашли при возвратах
    Если отправляем, например, на ВОЛГО-ВЯТСКИЙ БАНК СБЕРБАНКА РФ г. НИЖНИЙ НОВГОРОД, то кода клиринга этого банка нет в справочнике
    В этом случае нам необходимо подставить САРАТОВСКОЕ ОСБ N 8622
    В панеле по F11 определяется правильно, поэтому берем оттуда: CPaym.ReceiverMesBankID
  */
  if( error != 0 )
    /* Не найден такой код */
    ReceiverMesCode = ПолучитьКодСубъекта( CPaym.ReceiverMesBankID, CodeKind, error, ИскатьВышестоящих);
  end;

  if( error != 0 )
    //Gurin S. 09.02.2015 Берем код клиринга с единственного подразделения, где он существует, ищем по БИКу
    var id = execSqlSelect("select t_objectid id from dobjcode_dbt where t_objecttype = 3 and t_codekind = 3 and t_code = '046311649'");
    id.movenext();
    ReceiverMesCode = ПолучитьКодСубъекта( id.value("id"), CodeKind, error, ИскатьВышестоящих);
  end;

  if( error != 0 )
    /* Не найден такой код */
    RunError( "|Не найден код клиринга банка участника-получателя" );
  end;

  ExecMacro( MacroFill, "RC", CB_StrSubst0( String(ReceiverMesCode:10) ), Parm );
  
  /* Участник отправиель не должен совпадать с участником-получателем */
  if( PayerMesCode == ReceiverMesCode )
    RunError( "|Код участника-отправителя не может совпадать с кодом участника-получателя" );
  end;

  /* Сумма */
  ExecMacro( MacroFill, "AM", CB_StrSubst0( String(double(CPaym.ReceiverAmount)*100:17:0)), Parm ); 

  if(IsPaydem) //Заполнение полей, специфичных для требования
  
    ExecMacro( MacroFill, "AX", CB_StrSubst0( String(double(pspaydem.ReqSum)*100:17:0)), Parm ); 

    if ( CPaym.PayDate!=date(0,0,0) )
       ExecMacro( MacroFill, "DP", ДатаДДММГГ(CPaym.PayDate), Parm );
    end;

    if ( CPaym.DocDispatchDate!=date(0,0,0) )
       ExecMacro( MacroFill, "DS", ДатаДДММГГ(CPaym.DocDispatchDate), Parm );
    end;

    ExecMacro( MacroFill, "ME", ConvertToHEXSequence(pspaydem.PayCondition),  Parm );
    
  end;

  if( ((TypeDoc != type_003) OR (SubTypeDoc != 4)) AND ((TypeDoc != type_005) OR (SubTypeDoc != 6)) )
     /* Дата валютирования */
     /* Если в отвтном сообщении не был указан таг VD, то дату валютирования не заполняем */
     if( CPaym.IsExternal() AND CPaym.IsExternalIncoming() AND
         CPaym.ReadNote( PM_NOTEKIND_SBRFVALUEDATEAUTO, field_value) AND (field_value != "") )
       /* Не заполняем дату валютирования, так как она не указана в ответном сообщении */
     else
       /* Запрос 48593 */
       if ( CPaym.ValueDate!={curdate} )
          /*Сделал как в Кубань-Кредите*/
          /*ExecMacro( MacroFill, "VD", ДатаДДММГГ( CPaym.ValueDate ), Parm );*/
       end;
     end;
  end;

  /* Валюта */
  f_sbfininstr.FIID = CPaym.ReceiverFIID;
  if( not getEQ( f_sbfininstr ) )
    RunError( "|Код финансового инструмента "+CPaym.ReceiverFIID+" не найден в справочнике" );
  end;
  ExecMacro( MacroFill, "CU", f_sbfininstr.Ccy, Parm );

  /* Тип сообщения */
  if( ((TypeDoc != type_003) OR (SubTypeDoc != 4)) AND ((TypeDoc != type_005) OR (SubTypeDoc != 6)) )
     if ( CPaym.Instancy )
       if ( CPaym.NeedNotify ) 
         ExecMacro( MacroFill, "MT", 139, Parm );
       else
         ExecMacro( MacroFill, "MT", 137, Parm );
       end;
     else
        ExecMacro( MacroFill, "MT", CPaym.MessageType, Parm );
     end;
  end;

  /* Наименовние плательщика */
  /* Зубко Заменил  Payer на removeForbid(Payer) */
  ExecMacro( MacroFill, "PN", strLeft(StrSubst( removeForbid(Payer), "\n", " " ),160), Parm );

  /* ИНН плательщика */
  if( ((TypeDoc != type_003) OR (SubTypeDoc != 4)) AND ((TypeDoc != type_005) OR (SubTypeDoc != 6)) )
    len_field_value = StrLen( INN_Payer );
    /* Длина ИНН должна быть 10 или 12 символов */
    if( (len_field_value != 10) AND (len_field_value != 12) AND (len_field_value != 0) )
      RunError( "|Поле ИНН плательщика должно содержать 10 или 12 символов" );
    end;
    /* Если поле не задано - записываем символ "0" */
    if( len_field_value == 0 )
      INN_Payer = "0";
    end;
    ExecMacro( MacroFill, "SI", INN_Payer, Parm );
  end;

  /* Номер счета плательщика */
  ExecMacro( MacroFill, "SA", strLeft(CPaym.PayerAccount,35), Parm );

  if( ((TypeDoc != type_003) OR (SubTypeDoc != 4)) AND ((TypeDoc != type_005) OR (SubTypeDoc != 6)) )
     /* Номер филиала плательщика, считываем из примечания */
     if( CPaym.ReadNote( PM_NOTEKIND_SBRFRESERVEFIELD1, field_value) AND (field_value != "") )
       ExecMacro( MacroFill, "SF", SubStr(field_value,1,15), Parm );
     end;
  end;

  /* Банк-плательщик, заполняется по банку-создателю */
  /* Код банка - только для внешних, так как ЭЦП может накладываться на внутренние платежи */
  if( CPaym.IsExternal() )
     /* Переопределяем наименование банка и корсчет только для исходящего платеж, для входящего оставляем как есть */
     if( not CPaym.IsExternalIncoming() )
       /* Ищем БИК по ЦБ с учетом вышестоящих организаций */
       Code_Payer = ПолучитьКодСубъекта( CPaym.PayerBankID, PTCK_BIC, error, 1 );
       if( error != 0 )
         /* Не найден такой код */
         RunError( "|Для банка-плательщика не найден код с видом кода: " + PTCK_BIC );
       end;
       /* Ищем субъекта */
       PartyID = ПолучитьКодСубъекта( Code_Payer, PTCK_BIC, Error );
       if( Error != 0 )
         RunError( "|Не найден субъект с кодом: " + Code_Payer );
       end;
       /* Если банк-плательщик и вышестоящая организация это один и тот же субъект */
       if( PartyID == CPaym.PayerBankID )
          Bank_Payer  = CPaym.PayerBankName;
       else
         if( ПолучитьСубъекта(PartyID, cb_sbparty) != 0 )
           RunError( "|Не найден субъект с идентификатором: " + PartyID );
         end;
         Bank_Payer = cb_sbparty.rec.Name;
       end;  
       /* Корсчет определяем по справочнику отделений банков */
       if ( (CorAcc_Payer=="") OR (PartyID!=CPaym.PayerBankID) )
          /* Для поиска преобразуем в SelfID */
          if( PartyID == {OurBank} )
            PartyID = {OurBank};
          end;
          f_cbdprt.PartyID = PartyID;
          if( not GetEQ(f_cbdprt))
            RunError( "|Не найдена запись в справочнике отделений банков с ID = " + PartyID );
          end;
          CorAcc_Payer = f_cbdprt.CorAcc;
       end;
     end;

     ExecMacro( MacroFill, "SB", strLeft(StrSubst( Bank_Payer, "\n", " " ),140), Parm );
     ExecMacro( MacroFill, "SN", strLeft(Code_Payer,35), Parm );
     ExecMacro( MacroFill, "SK", strLeft(CorAcc_Payer,35), Parm );
     ВидКодаСтр = ПолучитьВидКодаSBRF3( PTCK_BIC );
     if( ВидКодаСтр != "" )
       ExecMacro( MacroFill, "SS", ВидКодаСтр, Parm );
     else /* Не найден такой вид кода */      
       RunError( "|Невозможно определить платежную систему банка-создателя|для вида кода " + string(PTCK_BIC) );
     end;
  end;

  /* Информация о первичном документе */
  if( (TypeDoc != type_003) OR (SubTypeDoc != 4) )
    if( (TypeDoc != type_005) OR (SubTypeDoc != 6) )
       if( CPaym.Reference != "" )
         ExecMacro( MacroFill, "ST", CPaym.Reference, Parm );
       end;
    end;

    /* Реквизиты получателя */
    /* Наименование получателя */
    /* Зубко Заменил  Receiver на removeForbid(Receiver) */
    ExecMacro( MacroFill, "RN",strLeft( StrSubst( removeForbid(Receiver), "\n", " " ), 160), Parm );
    if( (TypeDoc != type_005) OR (SubTypeDoc != 6) )
       /* ИНН получателя */
       len_field_value = StrLen( INN_Receiver );
       /* Длина ИНН должна быть 10 или 12 символов */
       if( (len_field_value != 10) AND (len_field_value != 12) AND (len_field_value != 0) )
         /*Зубко С. Платежи с нулевым "0" мы можем пропускать*/
         if((len_field_value == 1 ) and (INN_Receiver != "0"))
           RunError( "|Поле ИНН получателя должно содержать 10 или 12 символов" );
         end;
       end;
       /* Если поле не задано - записываем символ "0" */
       if( len_field_value == 0 )
         INN_Receiver = "0";
       end;
       ExecMacro( MacroFill, "RI", INN_Receiver, Parm );
    end;
  end;

  /* Счет */
  if ( not IsEmptyAcc(CPaym.ReceiverAccount) ) 
     ExecMacro( MacroFill, "RA", strLeft(CPaym.ReceiverAccount,35), Parm );
  end;

  if( (TypeDoc != type_003) OR (SubTypeDoc != 4) )
     if( (TypeDoc != type_005) OR (SubTypeDoc != 6) )
        /* Номер филиала получателя, считываем из примечания */
        if( CPaym.ReadNote( PM_NOTEKIND_SBRFRESERVEFIELD2, field_value) AND (field_value != "") )
          ExecMacro( MacroFill, "RF", SubStr(field_value,1,15), Parm );
        end;
     end;

     /* Банк - получатель */
     if( CPaym.IsExternal() )
        /* Переопределяем наименование банка и корсчет только для исходящего платеж, для входящего оставляем как есть */
        if( not CPaym.IsExternalIncoming() )
          /* Ищем БИК по ЦБ с учетом вышестоящих организаций */
          Code_Receiver = ПолучитьКодСубъекта( CPaym.ReceiverBankID, PTCK_BIC, error, 1 );
          if( error != 0 )
            /* Не найден такой код */
            RunError( "|Для банка-получателя не найден код с видом кода: " + PTCK_BIC );
          end;
          /* Ищем субъекта */
          PartyID = ПолучитьКодСубъекта( Code_Receiver, PTCK_BIC, Error );
          if( Error != 0 )
            RunError( "|Не найден субъект с кодом: " + Code_Receiver );
          end;
          /* Если банк-получатель и вышестоящая организация это один и тот же субъект */
          if( PartyID == CPaym.ReceiverBankID )
             Bank_Receiver  = CPaym.ReceiverBankName;
          else
            if( ПолучитьСубъекта(PartyID, cb_sbparty) != 0 )
              RunError( "|Не найден субъект с идентификатором: " + PartyID );
            end;
            Bank_Receiver = cb_sbparty.rec.Name;
          end;  
          /* Корсчет определяем по справочнику отделений банков */
          if ( (CorAcc_Receiver=="") OR (PartyID!=CPaym.ReceiverBankID) )
             /* Для поиска преобразуем в SelfID */
             if( PartyID == {OurBank} )
               PartyID = {OurBank};
             end;
             f_cbdprt.PartyID = PartyID;
             if( not GetEQ(f_cbdprt))
               RunError( "|Не найдена запись в справочнике отделений банков с ID = " + PartyID );
             end;
             CorAcc_Receiver = f_cbdprt.CorAcc;
          end;
        end;
        ExecMacro( MacroFill, "BN", strLeft(StrSubst( Bank_Receiver, "\n", " " ),140), Parm );
        ExecMacro( MacroFill, "BC", strLeft(Code_Receiver,35), Parm );
        ExecMacro( MacroFill, "BK", strLeft(CorAcc_Receiver,35), Parm );
        ВидКодаСтр = ПолучитьВидКодаSBRF3( PTCK_BIC );
        if( ВидКодаСтр != "" )
          ExecMacro( MacroFill, "RS", ВидКодаСтр, Parm );
        else /* Не найден такой вид кода */      
          RunError( "|Невозможно определить платежную систему банка-получателя|для вида кода " + string(PTCK_BIC) );
        end;
     end;

     /* Назначение платежа */
     /* Зубко Заменил  CPaym.Ground на removeForbid(CPaym.Ground) */
     ExecMacro( MacroFill, "PP", strLeft(StrSubst( removeForbid(CPaym.Ground), "\n", " " ),210), Parm );
  end;

  /* Информация участника 7 - Оплата клиентского платежного документа */
  if( (TypeDoc != type_005) OR (SubTypeDoc != 6) )
     /* Заполняется только при передачи платежных ордеров внутри Сбербанка, для комбанков запрещено для заполнения */
     if( IsBankType( CPaym.ReceiverBankID, PT_KIND_SAVINGS_BANK ) )
       field_value = "";
       if( (CPaym.Purpose == 7) and (CPaym.PartPaymNumber > 0) and (CPaym.ReceiverFIID == 0) )
         field_value = CB_StrSubst0( String( CPaym.PartPaymNumber:2:r ) ) +
                       CB_StrSubst0( String( double( CPaym.PartPaymRestAmountMain )*100:17:0) ) +
                       CB_StrSubst0( String( CPaym.Number:6:r ) ) +
                       ДатаДДММГГ( CPaym.ClientDate ) +
                       CB_StrSubst0( String( CPaym.ShifrOper:2:r ) );
       else 
         field_value = MkStr( " ", 33 );
       end;
       ExecMacro( MacroFill, "MP", strLeft(Trim( field_value + SubStr( CPaym.PartyInfo, 1, 210 - 33 ) ),210),  Parm );
     end;
  end;

  /* Дата последней обработки, считываем из примечания, если не задано - опердень */
  if( not CPaym.ReadNote( PM_NOTEKIND_SBRFLASTDATE, field_value )
      or (ValType(field_value) != V_DATE) or (field_value == date(0,0,0)) )
    field_value = {curdate};
  end;
  ExecMacro( MacroFill, "LD", ДатаДДММГГ( field_value ), Parm );

  ///* Условия перевода */
  //if( CPaym.IsExternal() AND CPaym.IsExternalIncoming() )
  //  if( CPaym.ChargesOfBank == PM_CHRG_BEN )
  //    field_value = "0";
  //  else
  //    field_value = "1";
  //  end;
  //else
  //  field_value = "0";    /* Расходы за счет плательщика */
  //end;
  field_value = "1"; //Теперь всегда "1" (#149971) 

  if( (TypeDoc != type_003) OR (SubTypeDoc != 4) )
     if( (TypeDoc != type_005) OR (SubTypeDoc != 6) )
        /* Вид связи */
        if( CPaym.PaymentKind == "П" )     /* Почтой */
          field_value = field_value + "1";
        elif( CPaym.PaymentKind == "Т" )   /* Телеграфом */
          field_value = field_value + "2";
        elif( CPaym.PaymentKind == "Э" )   /* Электронно */
          field_value = field_value + "3";
        else
          field_value = field_value + "3"; /* По умолчанию - Электронно */
        end;
        ExecMacro( MacroFill, "SC", field_value, Parm );
     end;

     /* ДПП */
     if( CPaym.IsExternal ) /* Если платеж внешний */
       if( CPaym.IsExternalIncoming() )
         field_value  = CPaym.InTransferDate;
       else
         field_value  = CPaym.OutTransferDate;
       end;
     else /* Платеж внутренний */
       field_value  = CPaym.ValueDate;
     end;

     if ( field_value != date(0,0,0) )
        ExecMacro( MacroFill, "SD", ДатаДДММГГ( field_value ), Parm );
     else
        ExecMacro( MacroFill, "SD", ДатаДДММГГ( CPaym.PayDate ), Parm );
     end;
  end;

  /* Тип обслуживания, считываем примечание, если не задано - то по умолчанию */
  if( CPaym.ReadNote( PM_NOTEKIND_SBRFSERVICETYPE, field_value ) )
    field_value = SubStr( String(field_value), 1, 4 );
  else
    field_value = ТипОбслуживанияПоУмолчанию;
  end;

  if ( ((TypeDoc!=type_003) OR (SubTypeDoc!=4)) AND ((TypeDoc != type_005) OR (SubTypeDoc != 6)) )
     ExecMacro( MacroFill, "PT", field_value, Parm );
  end;

  /* Информация о внесистемном документе */
  /* Пока нет информации о содержании тега IN в валютных документах */
  if( ((TypeDoc != type_003) OR (SubTypeDoc != 4)) AND ((TypeDoc != type_005) OR (SubTypeDoc != 6)) )
     if( CPaym.ReceiverFIID == 0 )  /* выгружаем его только для рублевых */
       field_value = "000" + CB_StrSubst0( String( CPaym.Number:6:r) ) /* N клиентского документа */
                           + ДатаДДММГГ( CPaym.Date )         /* Дата приема документа от клиента */
                           + String( CPaym.Priority );              /* Очередность платежа */
        ExecMacro( MacroFill, "IN", field_value, Parm );
     end;
  end;

  /* Поступило в банк плательщика */
  if( CPaym.IsExternal() AND CPaym.IsExternalIncoming() ) /* Для внешних ответных не заполняем */
    if( CPaym.ReadNote( PM_NOTEKIND_SBRFCLIENTDATEAUTO, field_value) AND (field_value != "") )
      /* Поле не было заполнено в ответном сообщении */
    else
      ExecMacro( MacroFill, "ED", ДатаДДММГГ(CPaym.PayerBankEnterDate), Parm ); /* сделано по запросу № 58571 */
    end;
  else
    if ( (TypeDoc==type_001) OR (TypeDoc==type_003) OR (TypeDoc==type_005) )
      if ( ((TypeDoc==type_003) AND (SubTypeDoc==4)) OR 
           ((TypeDoc!=type_003) AND ((SubTypeDoc==0) OR (SubTypeDoc==8)) ) )
         /* Зубко С. Сделал как в Кубань-Кредите*/
         /* Заполняем поле и для банковских платежей */
         /*if ( not SB_IsOwnerAccOwnBank(CPaym.PayerAccount) ) */
            if(CPaym.PayerBankEnterDate != date( 0, 0, 0 ))             
               ExecMacro( MacroFill, "ED", ДатаДДММГГ(CPaym.PayerBankEnterDate), Parm ); /* сделано по запросу № 58571 */
            elif( CPaym.ClientDate == date( 0, 0, 0 ) )
               if ( CPaym.I2PlaceDate !=date(0,0,0) )  
                  ExecMacro( MacroFill, "ED", ДатаДДММГГ(CPaym.I2PlaceDate), Parm ); 
               else
                  ExecMacro( MacroFill, "ED", ДатаДДММГГ(CPaym.ValueDate), Parm ); 
               end;
            else
               ExecMacro( MacroFill, "ED", ДатаДДММГГ(CPaym.ClientDate), Parm ); 
            end ;       
         /*end;*/
      end;
    end;
  end;

  if ( ((TypeDoc==type_001) OR (TypeDoc==type_005)) AND 
       (SubTypeDoc==8) )
    if ( CPaym.ReceiverBankMarkDate!=date(0,0,0) )
      ExecMacro( MacroFill, "DD", ДатаДДММГГ(CPaym.ReceiverBankMarkDate), Parm );
    end;
  end;

  if ( ((TypeDoc==type_001) OR (TypeDoc==type_005)) AND ((SubTypeDoc==0) OR (SubTypeDoc==8)) )
     /*Зубко Сделал как в Кубань-Кредите*/
     /*if ( not SB_IsOwnerAccOwnBank( CPaym.PayerAccount) ) */
        PayerChargeOffDate = PM_GetPayerChargeOffDate(CPaym.PaymentID);
        if ( PayerChargeOffDate!=date(0,0,0) )
           ExecMacro( MacroFill, "DH", ДатаДДММГГ(PayerChargeOffDate), Parm );
        else
           ExecMacro( MacroFill, "DH", ДатаДДММГГ({curdate}), Parm );
        end;

        if ( CPaym.I2PlaceDate!=date(0,0,0) )
           ExecMacro( MacroFill, "DK", ДатаДДММГГ(CPaym.I2PlaceDate), Parm );
        end;
     /*end; */
  end;

  if ( ((TypeDoc==type_001) OR (TypeDoc==type_005)) AND 
       ((SubTypeDoc==0) OR (SubTypeDoc==4) OR (SubTypeDoc==5) OR (SubTypeDoc==8)) )
     if ( CPaym.PayerBankMarkDate!=date(0,0,0) )
        ExecMacro( MacroFill, "DO", ДатаДДММГГ(CPaym.PayerBankMarkDate), Parm );
     end;
  end;

  if ( ((TypeDoc==type_001) OR (TypeDoc==type_005)) AND 
       ((SubTypeDoc==0) OR (SubTypeDoc==4) OR (SubTypeDoc==8)) )

     if ( KPP_Payer!="" )
        ExecMacro( MacroFill, "KP", KPP_Payer, Parm );
     end;

     if ( KPP_Receiver!="" )
        ExecMacro( MacroFill, "KR", KPP_Receiver, Parm );
     end;

     if ( CPaym.TaxAuthorState!="" )
       field_value = substr(CPaym.TaxAuthorState,1,2);
       if ( strlen(field_value)<2 )
          field_value = "0"+field_value;
       end;
       ExecMacro( MacroFill,  "NS", field_value, Parm );
     end;

     if ( CPaym.BttTICode!="" )
       ExecMacro( MacroFill, "NB", CPaym.BttTICode, Parm );
     end;

     if ( CPaym.OKATOCode!="" )
       ExecMacro( MacroFill, "NC", CPaym.OKATOCode, Parm );
     end;

     if ( CPaym.TaxPmGround!="" )
       ExecMacro( MacroFill, "NO", CPaym.TaxPmGround, Parm );
     end;

     if ( CPaym.TaxPmPeriod!="" )
       if ( (strlen(CPaym.TaxPmPeriod)!=1) AND
            (strlen(CPaym.TaxPmPeriod)!=8) AND
            (strlen(CPaym.TaxPmPeriod)!=10) )
          RunError( "|Неверно задан налоговый период" );
       end;
       ExecMacro( MacroFill,  "NP", CPaym.TaxPmPeriod, Parm );
     end;

     if ( CPaym.TaxPmNumber!="" )
       ExecMacro( MacroFill, "NN", CPaym.TaxPmNumber, Parm );
     end;

     if (CPaym.TaxPmDate!="")
       if ( (strlen(CPaym.TaxPmDate)!=1) AND
            (strlen(CPaym.TaxPmDate)!=10) )
          RunError( "|Неверно задана дата налогового документа" );
       end;
       if (CPaym.TaxPmDate!="0") 
         field_value = substr(CPaym.TaxPmDate,1,2);
         field_value = field_value + substr(CPaym.TaxPmDate,4,2);
         field_value = field_value + substr(CPaym.TaxPmDate,9,2);
         ExecMacro( MacroFill,  "ND", field_value, Parm );
       else
         ExecMacro( MacroFill,  "NE", CPaym.TaxPmDate, Parm );
       end;
     end;

     if ( CPaym.TaxPmType!="" )
       ExecMacro( MacroFill, "NT", CPaym.TaxPmType, Parm );
     end;
  end; 

  if ( ((TypeDoc==type_001) OR (TypeDoc==type_005)) AND (SubTypeDoc==4) )
     if ( (CPaym.DocKind==PS_PAYORDER) AND (CPaym.PartPaymNumber>0))
        if((CPaym.PartPaymNumber != 1) OR 
           ((CPaym.PartPaymNumber == 1) AND (CPaym.PartPaymRestAmountMain != 0.0)))
           ExecMacro( MacroFill, SB_Tag_OperContent, "Частичная оплата", Parm );
        end;
        ExecMacro( MacroFill, "QA", CB_StrSubst0( String(double(CPaym.PartPaymRestAmountMain)*100:17:0)), Parm );

        ExecMacro( MacroFill, "QC", substr(CPaym.PartPaymShifrMain,1,2), Parm );
        ExecMacro( MacroFill, "QD", ДатаДДММГГ(CPaym.PartPaymDateMain), Parm );
           

        ExecMacro( MacroFill, "QO", string(CPaym.PartPaymNumber), Parm );
        ExecMacro( MacroFill, "QN", CB_StrSubst0(String(substr(CPaym.PartPaymNumMain,1,6):6:r)), Parm  );
     /*else
       /* Это не частичная оплата */
       RunError( "|Платежным ордером оформлена не частичная оплата" );*/
     end;
  end;

  if(IsPaydem)
     ExecMacro( MacroFill, "UO", IfThenElse(pspaydem.AcceptTerm == 0, "1", "2"), Parm );
  else 
    /* Заполнение данных по аккредитиву, только для исходящих (т.е. для type_001) */
    if( (TypeDoc==type_001) AND (SubTypeDoc==5) )
      /* Аккредитивы в электронном виде можно направить только внутри Сбербанка */
      if( IsBankType( CPaym.ReceiverBankID, PT_KIND_SAVINGS_BANK ) )
        
          /* Проверяем по шифру операции */
          if( CPaym.ShifrOper == "08" )    
          
          /* Номер счета поставщика */
             if( CPaym.AkkrAccRealReceiver != "" )
               ExecMacro( MacroFill, "AA", strLeft(CPaym.AkkrAccRealReceiver,35), Parm );
          else
            RunError( "|Для аккредитива не заполнено поле '№ счета получателя'" );
          end;
          
          /* Дата аккредитива */
             if( CPaym.AkkrDate!=date(0,0,0) )
               ExecMacro( MacroFill, "AD", ДатаДДММГГ(CPaym.AkkrDate), Parm );
          else
            RunError( "|Для аккредитива не заполнено поле 'Cрок действия аккредитива'" );      
          end;
        
             /* Приложение к аккредитиву */
             if(CPaym.AkkrAddDocs != "" )
               ExecMacro( MacroFill, "AE", SB_КодироватьШестнадцатирично(strLeft( CPaym.AkkrAddDocs, 4000/2)), Parm );
             /*Возможно этот else необходимо убрать ЗАП.№108363*/
             else
               RunError( "|Для аккредитива не заполнено поле 'Приложение к аккредитиву'" );
             end;
  
             /* Дополнительные условия аккредитива */
             if( CPaym.AkkrAddCondition != "" )
               ExecMacro( MacroFill, "DU", strLeft(StrSubst( CPaym.AkkrAddCondition, "\n", " " ),160), Parm );
             else
               RunError( "|Для аккредитива не заполнено поле 'Дополнительные условия'" );
             end;
  
             /* Требуемые документы */
             if( CPaym.AkkrRepresentation != "" )
               ExecMacro( MacroFill, "TD", strLeft(StrSubst( CPaym.AkkrRepresentation, "\n", " " ),160), Parm );
             else
               RunError( "|Для аккредитива не заполнено поле 'Платеж по представлению'" );
             end;
  
          /* Условие оплаты: без акцепта/с акцептом */
          ClearRecord(cb_namealg);
          cb_namealg.iTypeAlg = 2987;
             cb_namealg.iNumberAlg = CPaym.AkkrPayCondition;
          if( getEQ(cb_namealg) )
            ExecMacro( MacroFill, "UO", strLeft(cb_namealg.szNameAlg,140), Parm );
             else
               RunError( "|Для аккредитива не заполнено поле 'Условие оплаты аккредитива'" );
          end;
        
             /*--------------------------------------------------------------------------------*/
             /* Ищем БИК по ЦБ с учетом вышестоящих организаций */
             Code_Receiver = ПолучитьКодСубъекта( CPaym.ReceiverBankID, PTCK_BIC, error, 1 );
             if( error != 0 )
               /* Не найден такой код */
               RunError( "|Для банка-получателя не найден код с видом кода: " + PTCK_BIC );
             end;
             /* Ищем субъекта */
             PartyID = ПолучитьКодСубъекта( Code_Receiver, PTCK_BIC, Error );
             if( Error != 0 )
               RunError( "|Не найден субъект с кодом: " + Code_Receiver );
             end;
             /* Если банк-получатель и вышестоящая организация это один и тот же субъект */
             if( PartyID == CPaym.ReceiverBankID )
                Bank_Receiver  = CPaym.ReceiverBankName;
          else
               if( ПолучитьСубъекта(PartyID, cb_sbparty) != 0 )
                 RunError( "|Не найден субъект с идентификатором: " + PartyID );
               end;
               Bank_Receiver = cb_sbparty.rec.Name;
          end;
  
             ExecMacro( MacroFill, "AB", strLeft(Code_Receiver,35), Parm );
             ExecMacro( MacroFill, "AN", strLeft(StrSubst( Bank_Receiver, "\n", " " ),140), Parm );
             ВидКодаСтр = ПолучитьВидКодаSBRF3( PTCK_BIC );
             if( ВидКодаСтр != "" )
               ExecMacro( MacroFill, "AR", ВидКодаСтр, Parm );
             else /* Не найден такой вид кода */      
               RunError( "|Невозможно определить платежную систему банка-получателя|для вида кода " + string(PTCK_BIC) );
          end;
             /*--------------------------------------------------------------------------------*/
        else
            RunError( "|Данный ПД не является аккредитивом" );
        end;
      else
        RunError( "|Аккредитив в электронном виде можно направлять только в учреждения Сбербанка" );
      end;
    end;
  end;

  SetParm( 3, Parm );

  return TRUE; /* Успешное завершение */

end;
