//--------------------------------------------------------------------------------------------------
// @fname  : d_r_imp.mac 
// @desc   : процедура списания доходов-расходов. Процедуры
// @changes: 28.12.2011 vihrov C-8028
//           17.02.2012 vihrov I-00154847-2
//           14.01.2012 GSP R-142533-3 добавил условие для банка "Пойдем" по формированию счета зачисления.
//           16.01.2012 zip_z. починил обработчик панели
//           29.12.2014 Teleshova Адаптация под 31ю сборку ЭВ
//--------------------------------------------------------------------------------------------------

import BankInter, OprInter, globals, CTInter, InsCarryDoc, PTInter, FIInter; //distr1
import rsd, oralib, likepy, lib_lang, fg_life_parm;   //GSP добавил fg_life_parm

record dr(dr_docs ,"userop.def");
file acc("account" ) key 0;
file bal("balance") key 0;
file p(person);
file dep(dp_dep);
record dpan(RD_DOC ,"userop.lbr") dialog;

const 
  КатегорияДляОтчетности   = 1,
  ВидПДДоходыРасходы       = 5010,
  ВидОперацииДоходыРасходы = 5500;

var AccSymbolsForm102;

const
  Входящий = "Входящий"
 ,Исходящий = "Исходящий";

const
  ТипВходящий = 1
 ,ТипИсходящий = 2;

var eto1 = fg_life_subject(); //vihrov для разделения филиалов ЭВ


/*
 * Получить субъекта для филиала
 */
macro GetDprtParty(Department)
  dep.Code = Department;
  if(GetEQ(dep))
    return dep.PartyID;
  end;
  return {OurBank};
end;

/*
 * Получить сотрудника с правом второй подписи
 */
macro GetDprtSecondPerson(Department)
  var select:string;
  var params:TArray;
  var rs:object;
  var OperID = 0;

  select = "SELECT prsn.t_oper " +
           "  FROM dperson_dbt prsn " +
           " WHERE prsn.t_codedepart = :codedepart " +
           "   AND prsn.t_partyid IN ( " +
           "          SELECT offcr.t_personid " +
           "            FROM dofficer_dbt offcr " +
           "           WHERE offcr.t_partyid IN (SELECT dp.t_partyid " +
           "                                       FROM ddp_dep_dbt dp " +
           "                                      WHERE dp.t_code = prsn.t_codedepart) " +
           "             AND offcr.t_issecondperson = CHR (88)) ";

  params = makeArray( SQLParam(":codedepart", Department ));
        
  rs = execSQLselect( select, params, false );
        
  if(rs.moveNext())
    OperID = rs.value(0);
  end;

  return OperID;
end;

/*
 * Проверка существования балансового счета
 */
macro CheckBalanceQuiet(Balance, Required, Quiet)
  var stat = false;
  
  if((strlen(Balance) == 3) or (strlen(Balance) == 5))
    bal.Chapter  = ternary(substr(Balance,1,1)=="9",4,1);
    bal.iNumPlan = 0;
    bal.Balance = Balance;
    if( GetEQ(bal) )
      stat = true;
    else
      stat = false;
    end;

  elif((strlen(Balance) == 0) and (not Required))
    stat = true;
  
  elif((strlen(Balance) == 0) and (Required))
    stat = false;
  end;

  if((not stat) and (not Quiet)) msgbox("Введен неправильный номер балансового счета"); end;
  
  return stat;
end;

macro CheckBalance(Balance, Required)
  return CheckBalanceQuiet(Balance, Required, false);
end;

/* 
 * Получить значение категории для счета 
 */
macro CheckAccountCategory( accRec, GroupID : integer, Code : string, Num : string, DateCategory : date ) : bool
  var NumExists : bool;
  var stat : bool;
  stat = CheckObjAttrPresence(NumExists, OBJTYPE_ACCOUNT, UniID(accRec, OBJTYPE_ACCOUNT), GroupID, null, Code, Num, null);
  if(stat)
    return NumExists;
  end;
  return stat;
end;

/* 
 * Установить значение категории для счета на шаге операции
 */
macro SetAccountCategory( accRec, Autokey, GroupID, Num )
  var AccountID; 
  
  if(Autokey)
    AccountID = Autokey;
  else
    AccountID = UniID(accRec, OBJTYPE_ACCOUNT);
  end;
  
  return ConnectCategory(OBJTYPE_ACCOUNT, GroupID, AccountID, true, null, "", Num);
end;

/*
 * Определить и установить число знаков символа в счете
 */
macro SetAccSymbolsForm102()
  GetRegistryValue( "REPTREG\\REP_GROUPS\\ФОРМА 102\\ЧИСЛО ЗНАКОВ СИМВОЛА В СЧЕТЕ", V_INTEGER, AccSymbolsForm102, null );
end;

/* Определить счета зачисления для филиалов ЭВ vihrov 25.01.2012 */
macro GetAccountTo_EXV_filial(accRec, CurrencyTo : @INTEGER)
   var account,BIC_Code,AccBalanceF,AccBalance2;
   VAR AccKey      = "k";
   var get_ovp:string	= "select t_account from daccount$_dbt where t_chapter=1 and lower(t_account) like 'овп%' and t_code_currency="+accrec.code_currency + " and t_open_close<>'╟'";
   var ds;
   var ds16,ss16;
   eto1.ConsrtuctByBic( {MFO_Bank} );  // vihrov 25.01.2012 яюыєўшь срэъ

   if (substr(accrec.account,6,3) != "810")
      CurrencyTo = accRec.Code_Currency;
      ds = trsbdataset(get_ovp);
      if (ds.movenext())
         return (ds.value("t_account"));
      else 
         msgbox("Не найден счет ОВП для зачисления. Код валюты "+string(CurrencyTo));
      end;
   else
      if(strlen(dr.BalanceFrom) == 3)
         AccBalanceF = dr.BalanceFrom;
         AccBalanceF = AccBalanceF + substr(accRec.Account, 4, 2);
      elif(strlen(dr.BalanceFrom) == 5)
         AccBalanceF = dr.BalanceFrom;
      end;
      
      if(strlen(dr.BalanceTo) == 3)
         AccBalance2 = dr.BalanceTo;
         AccBalance2 = AccBalance2 + substr(accRec.Account, 4, 2);
      elif(strlen(dr.BalanceTo) == 5)
         AccBalance2 = dr.BalanceTo;
      end;

      if ((AccBalanceF=="70601") or 
          (AccBalanceF=="70602") or 
          (AccBalanceF=="70603") or 
          (AccBalanceF=="70604") or 
          (AccBalanceF=="70605"))
         if (eto1.is_EXV_Stavropol)
            account="30301810500000000001";
         elif (eto1.is_EXV_Ulyanovsk)
            account="30301810550000000009"; 
         elif (eto1.is_EXV_Voronezh)
            account="30301810800000000005";
         elif (eto1.is_EXV_Volgograd)
            account="30301810201000000010";
         elif (eto1.is_EXV)
            //msgbox(" р эх эрфю чряєёърЄ№ ¤Єє яЁюЎхфєЁє фы  ╤рЁрЄютр..."); exit(1);
            account = AccBalance2 + "810" + "k" + substr(accRec.Account, 10, 11);


            ss16="select t_account from daccount_dbt where t_account like '" + 
               AccBalance2 + "810_____"+substr(accRec.Account,14,4)+"%'";
            ds16=trsbdataset(ss16);
            if (ds16.movenext())
               account=ds16.value("t_account");
               return (Account)
            else
               msgbox("Не нашел счет для сивола ОПУ "+substr(accRec.Account,14,4));
               getstring(ss16);
            end;
         else
            msgbox("Какой-то неизвестный БИК ...");
            exit(1);
         end;
      elif ((AccBalanceF=="70606") or 
            (AccBalanceF=="70607") or 
            (AccBalanceF=="70608") or 
            (AccBalanceF=="70609") or 
            (AccBalanceF=="706010") or 
            (AccBalanceF=="70611"))
         if (eto1.is_EXV_Stavropol)
            account="30302810800000000001";
         elif (eto1.is_EXV_Ulyanovsk)
            account="30302810850000000009";
         elif (eto1.is_EXV_Voronezh)
            account="30302810100000000005";
         elif (eto1.is_EXV_Volgograd)
            account="30302810701000000166";
         elif (eto1.is_EXV)
            ss16="select t_account from daccount_dbt where t_account like '" +
                   AccBalance2 + "810_____"+substr(accRec.Account,14,4)+"%'";
            ds16=trsbdataset(ss16);
            if (ds16.movenext())
               account=ds16.value("t_account");
               return (Account)
            else
               msgbox("Не нашел счет для сивола ОПУ "+substr(accRec.Account,14,4));
               getstring(ss16);
            end;
         else
            msgbox(" Какой-то неизвестный БИК..."); exit(1);
         end;
      else
         msgbox("Списани предусмотрено со счетов 70601-70611.");
      end;
      if(not CB_GetDprtPartyCode(accRec.Department, PTCK_BIC, BIC_Code))
         Account = GetKey(Account, BIC_Code);
      else
         Account = GetKey(Account);
      end;
      return (Account);
   end;
end;



/*
 * Определить счет зачисления
 */
macro GetAccountTo(accRec, CurrencyTo : @INTEGER)
   var Account, AccBalance, AccCurrency, AccKey, AccDepartment, AccLastSymb, CatValueNum, BIC_Code;
   record fi( "fininstr.dbt", "bank.def" );
   AccCurrency = "810";
   AccKey      = "0";

   if(strlen(dr.AccountTo) > 0)
      if(dr.CurrencyTo != ALLFININSTR)
         CurrencyTo = dr.CurrencyTo;
      else
         CurrencyTo = accRec.Code_Currency;
      end;
      return dr.AccountTo;
   else
      if(strlen(dr.BalanceTo) == 3)
         AccBalance = dr.BalanceTo;
         AccBalance = AccBalance + substr(accRec.Account, 4, 2);
      elif(strlen(dr.BalanceTo) == 5)
         AccBalance = dr.BalanceTo;
      end;
   end;

   CurrencyTo = accRec.Code_Currency;
   // CurrencyTo = 0;
   if( ПолучитьФинИн( CurrencyTo, fi ) == 0 )
      AccCurrency = fi.CodeInAccount;
   end;

   if(dr.IsDepSave == "X")
      AccDepartment = substr(accRec.Account, 10, 4);
   else
      AccDepartment = "0000";
   end;

   if(dr.LastChar == 1)
      AccLastSymb = substr(accRec.Account, 14, 7);
   else
      AccLastSymb = substr(accRec.Account, 14, AccSymbolsForm102);
      /*Дополнить недостающие символы нулями*/
      while(strlen(AccLastSymb) < 6)
         AccLastSymb = AccLastSymb + "0";
      end;
      /*Последний символ зависит от категории на счете списания*/
      if(accRec.Code_Currency == NATCUR)
         if(CheckAccountCategory( accRec, КатегорияДляОтчетности, "", "V", dr.Date_Carry ))
            AccLastSymb = AccLastSymb + "1";
         else
            AccLastSymb = AccLastSymb + "0";
         end;
      else 
         AccLastSymb = AccLastSymb + "0";
      end;
   end;

   Account = AccBalance + AccCurrency + AccKey + AccDepartment + AccLastSymb;
   if(not CB_GetDprtPartyCode(accRec.Department, PTCK_BIC, BIC_Code))
      Account = GetKey(Account, BIC_Code);
   else
      Account = GetKey(Account);
   end;
   return Account;
end;

/* 
 * Получить буфер счета 
 */
macro GetAccountRecord( accRec, Chapter, Account, Code_Currency )
  acc.Chapter       = Chapter;
  acc.Account       = Account;
  acc.Code_Currency = Code_Currency;
  if( getEQ( acc ) )
    Copy( accRec, acc );
    return true;
  end;
  return false;
end;

/* 
 * Получить вид счета А/П
 */
macro GetKindAccount( Balance )
  bal.Chapter  = 1;
  bal.iNumPlan = 0;
  bal.Balance  = Balance;
  if( getEQ( bal ) )
    return bal.Kind_Account;
  end;
  return "";
end;

macro CheckOpenAccount(Account, Currency)
  acc.Account = Account;
  acc.Chapter = 1;
  acc.Code_Currency = Currency;
  if(( GetEQ(acc) ) and 
    (( acc.Open_Close == "" ) or ( acc.Open_Close == " " )))
    return true;
  else
    return false;
  end;
end;

macro CheckAccount(Account, Balance, Currency)
  var stat = true;

  var select:string;
  var params:TArray;
  var rs:object;

  if(strlen(Account))
    if(strlen(Balance))
      select = "SELECT 1 " +
               "  FROM daccblnc_dbt accblnc " +
               " WHERE accblnc.t_Account = :Account " +
               "   AND accblnc.t_Code_Currency = :Currency " +
               "   AND accblnc.t_Chapter = :Chapter " +
               "   AND accblnc.t_Balance0 = :Balance ";
      
      params = makeArray( SQLParam(":Account", Account)
                         ,SQLParam(":Currency", Currency)
                         ,SQLParam(":Chapter", 1)
                         ,SQLParam(":Balance", Balance));
            
      rs = execSQLselect( select, params, false );
            
      if(not rs.moveNext())
        stat = false;
      end;
    end;
    
    if(stat)
      select = "SELECT acc.t_Open_Close " +
               "  FROM daccount_dbt acc " +
               " WHERE acc.t_Account = :Account " +
               "   AND acc.t_Code_Currency = :Currency " +
               "   AND acc.t_Chapter = :Chapter ";
      
      params = makeArray( SQLParam(":Account", Account)
                         ,SQLParam(":Currency", Currency)
                         ,SQLParam(":Chapter", 1));
            
      rs = execSQLselect( select, params, false );
            
      if(rs.moveNext() and (( rs.value(0) == "" ) or ( rs.value(0) == " " )))
        stat = true;
      else
        stat = false;
      end;
    end;
  end;

  if(not stat) msgbox("Введен неправильный номер лицевого счета"); end;
  
  return stat;
end;

macro CheckNameOper(Oper)
  p.Oper = Oper;
  if(not GetEQ(p))
    return "<Операционист не найден>";
  else
    return p.Name;
  end;
end;

macro CheckNameDepartment(Department)
  if(Department == 0)
    return "Bсе филиалы";
  else
    dep.Code = Department;
    if(not GetEQ(dep))
      return "<Филиал не найден>";
    else
      return dep.Name;
    end;
  end;
end;
