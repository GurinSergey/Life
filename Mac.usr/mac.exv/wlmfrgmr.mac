/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank 6.0                      */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/*  Генерация сообщения в филиал и Экономбанк из Экспресс-Волги г. Саратов  */
/*  Имя файла: wlmfrgmr.mac                                                 */
/*  Создан:  17.01.11                                             Зубко С.  */
/****************************************************************************/

import OprInter, "wlgenmes.mac", "wlmctool.mac", "wlmfr_lib.mac";


const НашКодСоставителя = substr({mfo_bank},3) + "000";
const Числовое_Поле = true;

var СтрокаДокумента = "";

macro Форматный_вывод_в_строку( СтрокаВывода, Число, Длина, i)
  var Строка, Число_ведущих_пробелов, Строка_нулей;
  if( valtype( Число ) == V_STRING )
/*      Первым параметром передана строка - отбросить ведущие пробелы   */
    Строка = Число;
    while( index( Строка, " " ) == 1 )
      Строка = substr( Строка, 2 );
    end;
  else
    Строка = string( Число:0:0 );
  end;
  Число_ведущих_пробелов = Длина - strlen( Строка );
  if( Число_ведущих_пробелов < 0 )
/*      Если строка длинней поля вывода - обрезать конец строки         */
    Число_ведущих_пробелов = 0;
    Строка = substr( Строка, 1, Длина );
  end;
                                        /* А не числовое ли это поле? */
  if ( (valtype(Число) == V_STRING) and ( not ( (valtype(i) == V_BOOL) and i) ) )
     Строка_нулей = mkstr( " ", Число_ведущих_пробелов );
     return СтрокаВывода+Строка+Строка_нулей;
  else
     Строка_нулей = mkstr( "0", Число_ведущих_пробелов );
     return СтрокаВывода+Строка_нулей+Строка ;
  end;
end;

/* Выравнивает вправо и дополняет нулями */
macro Форматный_вывод_в_строкуExt( str, dataStr,  len, type )

    return Форматный_вывод_в_строку( str, dataStr, len, type );
end;

/* Выравнивает влево и дополняет пробелами */
macro вывод_в_строкуExt( str, dataStr,  len )
    var v="";
    v = string(dataStr);
    v = substr(v,1,len);
    if ( strlen(v)<len )
       v = v + mkstr( " ", len-strlen(v) );
    end;
    return str = str + v;
end;

macro вывод_в_строку( str, dataStr,  len )
   return вывод_в_строкуExt( str, dataStr,  len );
end;


/********/
/* MAIN */
/********/
macro GenMes( addrMes, addrPm )
  var DKFlag = -1;
  var FlagTrans, НомерДокументаСтр, День, Месяц, Год,
      ShifrOperation, Вид_обработки, MFO_9, СсылкаНаБанк, Сумма, Основание,
      Плательщик, Получатель, Error,
      PayerINN, ReceiverINN, PayerKPP, ReceiverKPP, StringError = "",
      PlaceDate, PaymentIDStr, PayerChargeOffDate, displacement;
  var v_ShifrOper;

  /* Разрешенные шифры пл. документа для вида операции 16*/
  var PartPmShifrMain = "01 02 03 04 05 06 07 08 09 10 11 12 13 14 15 16";
  var RsPaym;

  var LoroNostro = "";

  SetBuff( wlmes,  addrMes );
  SetBuff( wlpmpaym, addrPm );

  PrintLog( 2, "Генерация сообщения для МФР " );

  RsPaym = RsbPayment( wlpmpaym.PaymentID );

  PayerINN    = RemoveKPP(RsPaym.PayerINN);
  ReceiverINN = RemoveKPP(RsPaym.ReceiverINN);
  PayerKPP    = RemoveINN(RsPaym.PayerINN);
  ReceiverKPP = RemoveINN(RsPaym.ReceiverINN);

  if ( IsCreditPaym(RsPaym) )
     DKFlag = WL_CREDIT;
  elif ( IsDebetPaym(RsPaym) )
     DKFlag = WL_DEBET;
  else
     DKFlag = -1;
  end;            

  FlagTrans = IsTransitPayment(RsPaym);  

  if( DKFlag == -1 ) /* документ не внешний */
    RunError( "|Платеж с ID: " + string( RsPaym.PaymentID ) + "не внешний" );
  end ;

  if( DKFlag == WL_DEBET )
    RunError( " Документ " + RsPaym.Number + "дебетовый! Для МФР и Экономбанка могу выгржаться только кредитовые документы" );
  end;

  /* Порядковый номер электронного документа */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, wlmes.TRN, 6 );   /* 1-6 */

  datesplit( {curdate}, День, Месяц, Год );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );   /* 7-8 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );   /* 9-10 */  /* 7-14 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );   /* 11-14 */

  /* Уникальный номер составителя */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, НашКодСоставителя, 10 ); /* 15-24 */

  /*БИК банка плательщика*/
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Свой_МФО, 9, Числовое_Поле );  /* 25-33 */

  /* Лицевой счет плательщика */
  if( RsPaym.PayerAccount == "" )
    RunError( "Неверен номер счета плательщика" ); 
  end;

  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, RsPaym.PayerAccount, 20, Числовое_Поле );  /* 34-53 */

  /*Номер корсчета банка плательщика*/
  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, Свой_Корсчет, 20, Числовое_Поле );  /* 54-73 */

  /*Номер документа*/
  НомерДокументаСтр = Последние_Символы( RsPaym.Number, 3 ) ;

  if( ПроверитьНомерДокумента(НомерДокументаСтр)  ==  false )
    /* Номер документа или уже нулевой или не числовой */
    НомерДокументаСтр = "0";
  end;

  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, НомерДокументаСтр, 3 ) ;   /* 74-76 */

  /* Дата документа, должна быть датой и не больше текущего опердня */
  if( (RsPaym.Date > {curdate} ) )
    RunError( "|Неверная дата документа" );
  end;
  datesplit( RsPaym.Date, День, Месяц, Год );
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );   /* 77-78 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );   /* 79-80 */  /* 77-84 */
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );   /* 81-84 */

  /*Вид операции*/
  v_ShifrOper = RsPaym.ShifrOper;
  if(trim(v_ShifrOper) == "16")
    v_ShifrOper = "01";
  end;
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, v_ShifrOper, 2, Числовое_Поле );   /* 85-86 */

  /*БИК банка получателя*/
  СсылкаНаБанк = ПолучитьКодСубъекта( RsPaym.ReceiverBankCode, RsPaym.ReceiverBankCodeKind, Error ) ;
  if( Error == 0 )
    MFO_9 = ПолучитьКодСубъекта( СсылкаНаБанк, PTCK_BIC, Error ) ;
    if( Error == 0 )
      СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, MFO_9, 9, Числовое_Поле );  /* 87-95 */
    else
      RunError( "|Не найден БИК получателя" );
    end;
  else
    RunError( "|Не найдена ссылка на банк получателя" );
  end;

  /* Счет получателя */
  if( ( RsPaym.ReceiverAccount == "" ) AND ( Вид_обработки != 1 ) )
    RunError( "|Неверен номер счета получателя" );
  end;
  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, RsPaym.ReceiverAccount, 20, Числовое_Поле );   /* 96-115 */

  /*Номер корсчета банка получателя*/
  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, RsPaym.ReceiverCorrAccNostro, 20, Числовое_Поле );  /* 116-135 */

  /* Сумма документа */
  if( RsPaym.ReceiverAmount == 0 )
    RunError( "|Нулевая сумма документа" );
  end;
  Сумма = double( RsPaym.ReceiverAmount )*100;
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Сумма, 18 );                  /* 136-153 */

  /* Код очередности платежа - допустимы лишь 01, 02, 03, 04, 05, 06 */
  if( ( RsPaym.Priority < 1) OR ( RsPaym.Priority > 6 ) )
    RunError( "|Неверная очередность платежа" );
  end;

  /*Очередность*/
  СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, RsPaym.Priority, 1 ); /* 154 */
  
  /* ИНН КО Плательщика, разрешено 000000000000 */
  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, PayerINN, 12 );     /* 155-166 */

  if ( (RsPaym.TaxAuthorState!="") AND (PayerKPP=="") )
     RunError( "|Отсутсвует КПП плательщика" );
  end;

  /*КПП плательщика*/ 
  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, PayerKPP, 9 ); /* 167-175 */

  /*Наименование плательщика*/
  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.PayerName, 160 ) ;  /* 176-335 */

  /*ИНН получателя*/
  СтрокаДокумента = вывод_в_строку( СтрокаДокумента, ReceiverINN, 12 );     /* 336-347 */

  if ( (RsPaym.TaxAuthorState!="") AND (ReceiverKPP=="") )
     RunError( "|Отсутсвует КПП получателя" );
  end;

  /*КПП получателя*/
  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, ReceiverKPP, 9 ); /* 348-356 */

  /*Наименование получателя*/
  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.ReceiverName, 160 ) ;  /* 357-516 */

  /*Назначение платежа*/
  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.Ground, 210 ); /* 517-726 */

  /* Дата поступления в банк плательщика */
  if ( (ShifrOperation == 16) OR (RsPaym.PayerBankEnterDate==date(0,0,0)) OR (IsOwnerAccOwnBank(substr(СтрокаДокумента,26,9), 0, substr(СтрокаДокумента,35,20))) )
     СтрокаДокумента = СтрокаДокумента + mkstr( " ", 8 ); /* 727 -734 */
  else
     datesplit( RsPaym.PayerBankEnterDate, День, Месяц, Год );
     СтрокаДокумента = Форматный_вывод_в_строкуExt( СтрокаДокумента, День,  2 );   /* 727-728 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( СтрокаДокумента, Месяц, 2 );   /* 729-730 */  /* 727-734 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( СтрокаДокумента, Год,   4 );   /* 731-734 */
  end;

  /* Дата помещения в картотеку */
  if ( (ShifrOperation != 16) AND (RsPaym.I2PlaceDate!=date(0,0,0)) AND (not IsOwnerAccOwnBank(substr(СтрокаДокумента,26,9), 0, substr(СтрокаДокумента,35,20))) )  
     datesplit( RsPaym.I2PlaceDate, День, Месяц, Год );
     СтрокаДокумента = Форматный_вывод_в_строкуExt( СтрокаДокумента, День,  2 );   /* 735-736 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( СтрокаДокумента, Месяц, 2 );   /* 737-738 */  /* 735-742 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( СтрокаДокумента, Год,   4 );   /* 739-742 */
  else
     СтрокаДокумента = СтрокаДокумента + mkstr( " ", 8 ); /* 735-742 */
  end;

  /*Дата списания со счета плательщика */
  if ( (ShifrOperation == 16) OR (IsOwnerAccOwnBank(substr(СтрокаДокумента,26,9), 0, substr(СтрокаДокумента,35,20))) )
     СтрокаДокумента = СтрокаДокумента + mkstr( " ", 8 ); /* 743 -750 */
  else
     PayerChargeOffDate = PM_GetPayerChargeOffDate(RsPaym.PaymentID);
     if( PayerChargeOffDate == date( 0, 0, 0 ) )
       datesplit( {curdate}, День, Месяц, Год );
     else
       datesplit( PayerChargeOffDate, День, Месяц, Год );
     end ;
     СтрокаДокумента = Форматный_вывод_в_строкуExt( СтрокаДокумента, День,  2 );   /* 743-744 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( СтрокаДокумента, Месяц, 2 );   /* 745-746 */  /* 743-750 */
     СтрокаДокумента = Форматный_вывод_в_строкуExt( СтрокаДокумента, Год,   4 );   /* 747-750 */
  end;

  /*Дата отметки банка получателя*/
  if( ( ShifrOperation == 6 ) AND ( RsPaym.ReceiverBankMarkDate !=date(0,0,0)) )
    datesplit( RsPaym.ReceiverBankMarkDate, День, Месяц, Год );
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );   /* 751-752 */
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );   /* 753-754 */  /* 751-758 */
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );   /* 755-758 */
  else
    СтрокаДокумента = СтрокаДокумента + mkstr( " ", 8 ); /* 751-758 */
  end;

  /*Статус составителя расчетного документа*/
  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.TaxAuthorState, 2 );  /* 759 -760 */

  if ( (RsPaym.TaxAuthorState!="") 
     AND (   (RsPaym.BttTICode=="")   OR (RsPaym.OKATOCode  =="")   OR (RsPaym.TaxPmGround=="")
          OR (RsPaym.TaxPmPeriod=="") OR (RsPaym.TaxPmNumber=="")   OR (RsPaym.TaxPmDate=="")
          OR (RsPaym.TaxPmType=="")
         )
     )
     RunError( "|Не все поля налогового платежа заполнены" );
  end;

  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.BttTICode, 20 ); /* 761-780 */

  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.OKATOCode, 11 ); /* 781-791 */

  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.TaxPmGround, 2 ); /* 792-793 */

  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.TaxPmPeriod, 10 ); /* 794-803 */

  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.TaxPmNumber, 15 ); /* 804-818 */

  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.TaxPmDate, 10 );  /* 819-828 */

  СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.TaxPmType, 2 );  /* 829-830 */

  /*Номер частичного платежа*/
  if( ShifrOperation == 16 )
    СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.PartPaymNumber, 3 );  /* 831-833 */

    /*Шифр пл. документа.*/ /* 834-835 */
    if ( ( RsPaym.PartPaymShifrMain == "") OR 
         (not index(PartPmShifrMain, RsPaym.PartPaymShifrMain)) )
      RunError( "|Шифр платежного документа "+ string(RsPaym.PartPaymShifrMain) + " не соответствует виду операции 16" );
    else
      СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.PartPaymShifrMain, 2 );  /* 834-835 */
    end;

    /* Номер платежного документа */
    if( RsPaym.PartPaymNumMain == "" )
      RunError( "|Отсутствует номер платежного документа" );
    end;
    СтрокаДокумента = вывод_в_строкуExt( СтрокаДокумента, RsPaym.PartPaymNumMain, 3 );  /* 836-838 */

    /* Дата платежного документа */
    if( RsPaym.PartPaymDateMain !=date(0,0,0))
      datesplit( RsPaym.PartPaymDateMain, День, Месяц, Год );
      СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, День,  2 );   /* 839-840 */
      СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Месяц, 2 );   /* 841-842 */  /* 839-846 */
      СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Год,   4 );   /* 843-846 */
    else
      СтрокаДокумента = mkstr( " ", 8 ); /* 839-846 */
    end;

    /* Сумма остатка платежа */
    Сумма = double( RsPaym.PartPaymRestAmountMain );
    СтрокаДокумента = Форматный_вывод_в_строку( СтрокаДокумента, Сумма, 18 );        /* 847-864 */

  else
    СтрокаДокумента = СтрокаДокумента + mkstr( " ", 34 ); /* 831-864 */
  end;

  /* Резервное поле, только пробелы */
  СтрокаДокумента = СтрокаДокумента + mkstr( " ", 16 ); /* 865-880 */


  LoroNostro = ""; /*значение этой переменной мы поместим в позиции 881-889 отправляемого в Экономбанк файла*/
                   /* для Экономбанка добавляем значение лоро/ностро, или ничего не добавляем, если не для Экономбанка*/

  if (MFO_9 == "046311722")       // Экономбанк    передаём параметром Mess_Vygr в sar_lib1.mac признак Лоро/Ностро и дату перечисления платежа
    if (RsPaym.FutureReceiverAccount == "30109810800000002152")
      LoroNostro = StrSubst(("L" + string({curdate})), ".", "");
    elif (RsPaym.FutureReceiverAccount == "30110810200000002152")
      LoroNostro = StrSubst(("N" + string({curdate})), ".", "");
    else
      RunError( "Для информации: платеж на Экономбанк,  а счет лоро/ностро ", RsPaym.FutureReceiverAccount, " в Экономбанк не уйдёт!" );
    end;
  else
    LoroNostro = mkstr( " ", 8 ); /* 881-889 */
  end;    

  /*Признак лоро/ностро*/
  СтрокаДокумента = СтрокаДокумента + LoroNostro; /* 881-889 */

  /* Резервное поле, только пробелы */
  /*добиваем резервное поле пробелами, чтобы длина его была 24*/
  СтрокаДокумента = СтрокаДокумента + mkstr( " ", 24-StrLen(LoroNostro));  /* 890-904 */

  ЗаписатьПолеЛог( FormFldDoc, СтрокаДокумента, TRUE );

  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    ExeptionMessage(er);
    return FALSE;
end;
