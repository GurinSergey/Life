/*************************************************************************/
/*     Дополнение к автоматизированной банковской системе RS-Bank        */
/*                                                                       */
/*    Имя файла        : usr_me_time_panel.mac                           */
/*                                                                       */
/*    Описание         : Креглосуточное проведение документов            */
/*                       Работа с пользовательской таблицей              */
/*                       usr_me_time_dbt                                 */
/*                                                                       */
/*    Программист      : Лавренов А.А.                                   */
/*                                                                       */
/*    Создан           : 11.08.2011                                      */
/*                                                                       */
/*    Изменения: 06.03.2013 LAO Добавил поле с почтой, спец для нового   */
/*                                                 круглосуточного робота*/
/*************************************************************************/

import rsbdataset;
import ldmklbr, "KeyCodes.mac";

 var v_tb_include, v_timebegin, v_te_include, v_timeend, v_action, v_is_holiday, v_id,v_email,v_email_a,v_email_b,v_is_final,v_final_id;
 var choice;
 var exitscroll;
 var RecordSet,CHOO;

 var isupdate, ischanged=false;

 array m_action, name_action;

 m_action(0)= "m_vnutren";
 m_action(1)= "m_vneshn";
 m_action(2)= "m_all";
 m_action(3)= "m_nothing";

 name_action(0)= "Внутренние";
 name_action(1)= "Внешние";
 name_action(2)= "Все";
 name_action(3)= "Ни чего";
 


macro EventProc (rs, Command, id, key )  
      if   ((Command == DLG_KEY) and (key == KEY_ENTER))
        return CM_SELECT;
      elif ((Command == DLG_KEY) and (key == KEY_ESC))
       return CM_CANCEL;
       
      else
        CM_IGNORE;
      end;
end;
macro AddColS (ar,ind, fld, head, width, rdonly, DecPoint, Point)
    ar.value (ind * 6)     = fld;
    ar.value (ind * 6 + 1) = head;
    ar.value (ind * 6 + 2) = width;
    ar.value (ind * 6 + 3 ) = DecPoint;   // fldType
    ar.value (ind * 6 + 4 ) = Point;  // decPoint
    ar.value (ind * 6 + 5 ) = 0;   // reserv
  end;
  
private MACRO GetTime();  
  var str,rs;
 
 var col = TArray;
 
 str =   "select T_ID ,T_TIMEBEGIN,"+
                " T_TB_INCLUDE,"+
                " T_TIMEEND,"+
                " T_TE_INCLUDE,"+
                "       decode(t_action,'m_vnutren','Внутренние', "+ 
                "                         'm_vneshn',    'Внешние', "+ 
                "                         'm_all',    'Все', "+ 
                "                         'm_nothing','Ни чего', "+ 
                "                                     t_action) t_actions,  " +
                " T_ACTION,"+
                " T_IS_HOLIDAY,"+
                " DECODE (EMAIL, CHR (0), CHR (1), ',' || EMAIL) EMAIL,"+
                " DECODE (EMAIL_B, CHR (0), CHR (1), ',' || EMAIL_B) EMAIL_B,"+
                " DECODE (EMAIL_A, CHR (0), CHR (1), ',' || EMAIL_A) EMAIL_A "+
                " from usr_me_time_dbt "+ 
                "  ORDER BY T_IS_HOLIDAY,TO_DATE (SUBSTR (t_timebegin, 1, INSTR (t_timebegin, '.') - 1), 'hh24:mi:ss') ";
 
  rs = RSDRecordset(str , RSDVAL_CLIENT, RSDVAL_Static );
 
  AddColS (col, 0, "T_ID", "T_ID" , 3 ,0, false,0);
  AddColS (col, 1, "T_TIMEBEGIN", "Старт" , 15, false);
  AddColS (col, 2, "T_TB_INCLUDE", "Включен" , 5 , false);
  AddColS (col, 3, "T_TIMEEND", "Окончание" , 15 , false);
  AddColS (col, 4, "T_TE_INCLUDE", "Включен" , 5 , false); 
  AddColS (col, 5, "t_actions", "Событие" , 8, false);
  AddColS (col, 6, "T_IS_HOLIDAY", "Признак выходного дня" , 5 , false);
  AddColS (col, 7, "T_ACTION", "Процедура" , 8, false);
  if (RunScroll(rs,
                 8,
               col,
              null,
           @EventProc, "Диапазоны",false,null,NULL,10,80));
   return    rs.Value("T_ID")+"||"+ rs.Value("T_ACTION"); 
 end;
end;



//Является ли строка числом
macro Is_Digit( str )
  var isdigit, digit="1234567890";
  var i, n, cur_symb;

  isdigit = true;
  i = 1;
  n = strlen(str);
  while( (isdigit) and (i <= n) )
     cur_symb = SubStr(str, i, 1);
     if(index(digit,cur_symb) == 0)
       isdigit = false;
     end;
     i = i+1;
  end;
  return isdigit;
end;

//Проверка правильности заполнения поля
private macro check_time(tm)
  var hh,mm,ss,ms;
  var pos;

  if ((substr(tm,3,1)!= ":") or (substr(tm,6,1)!= ":") or (substr(tm,9,1)!= "."))
    msgbox("Не правильно задано время!|| Формат: ЧЧ:ММ:СС.МС");
    return false;
  end;

  hh = int(substr(tm,1,2));
  mm = int(substr(tm,4,2));
  ss = int(substr(tm,7,2));
  ms = int(substr(tm,10));

  if(not is_digit(substr(tm,1,2)))
    msgbox("В часах есть недопустимые символы!");
    return false;
  end;

  if((hh < 0) or (hh > 23))
    msgbox("Часы должны быть в диапазоне 0-23!");
    return false;
  end;

  if(not is_digit(substr(tm,4,2)))
    msgbox("В минутах есть недопустимые символы!");
    return false;
  end;

  if((mm < 0) or (mm > 59))
    msgbox("Минуты должны быть в диапазоне 0-59!");
    return false;
  end;

  if(not is_digit(substr(tm,7,2)))
    msgbox("В секундах есть недопустимые символы!");
    return false;
  end;

  if((ss < 0) or (ss > 59))
    msgbox("Секунды должны быть в диапазоне 0-59!");
    return false;
  end;

  if(not is_digit(substr(tm,10)))
    msgbox("В милисекундах есть недопустимые символы!");
    return false;
  end;

  if((ms < 0) or (ms > 99))
    msgbox("Милисекунды должны быть в диапазоне 0-99!");
    return false;
  end;


  return true;
End;

//Выбор типа документов
MACRO Get_mass_index(act, mas)

 var cnt, i;
 cnt = asize(mas);
 i=0;
 while (i<cnt)
    if(act == mas(i))
       return i;
    end;
    i = i+1;
 end;
 return -1;
end; 


//Обновление записи в таблице
  MACRO Update_usr_me_time(id, tb_include, timebegin, te_include, timeend, action, is_holiday,email,email_a,email_b,IS_FINAL,FINAL_ID)
     var rs, str;


     str = "UPDATE usr_me_time_dbt " +
           "   set t_timebegin = '"+timebegin+"' ," +
           "       t_timeend = '"+timeend+"' ," +
           "       t_action = '"+m_action(get_mass_index(action,name_action))+"' , " ;

    if(tb_include == ">=") 
     str = str + "       t_tb_include = 'X' ," ;
    else
     str = str + "       t_tb_include = chr(0) ," ;
    end;

    if(te_include == "<=") 
     str = str + "       t_te_include = 'X' ," ;
    else
     str = str + "       t_te_include = chr(0) ," ;
    end;

    if(is_holiday == "X") 
     str = str + "       t_is_holiday = '"+is_holiday+"' ," ;
    else
     str = str + "       t_is_holiday = chr(0) ," ;
    end;
    
    if (email == "") 
      str = str + "email   =  chr(0), " ;
    else 
      str = str + " email   =  '" +email +"', " ;
    end;
    if(email_a == "") 
     str = str +  " email_a =  chr(0), " ;
     else 
      str = str + " email_a = '" +email_a +"', " ;
    end;
    if(email_b == "") 
      str = str + "email_b = chr(0), " ;
    else 
      str = str + " email_b = '" +email_b +"' ," ;
    end;
    if(IS_FINAL == "") 
      str = str + "T_IS_FINAL =chr(0), T_FINAL_ID=null" ;
    else 
      str = str + "T_IS_FINAL= '" +IS_FINAL +"', " ;
      str =  str + "T_FINAL_ID= " +FINAL_ID +"" ;
    end;

    str = str + " WHERE t_id = "+ int(id);

     rs = rsdcommand(str);
     if(rs)
        rs.execute;
        return true;
     else 
        return false;
     end;
  END;


//Следующее значение идентификатора в таблице
  MACRO GetNextVal()
     var rs, str;
     str = "SELECT NVL (MAX (t_id), 0) id " +
           "  FROM usr_me_time_dbt " ;
     rs = trsbdataset(str);
     if (rs and rs.movenext)
        return int(rs.id+1);
     end;
     return 1;
  END;

//Вставка записи в таблицу
  MACRO insert_usr_me_time(tb_include, timebegin, te_include, timeend, action, is_holiday,email,email_a,email_b,IS_FINAL,FINAL_ID)
     var rs, str;
     str = "INSERT INTO usr_me_time_dbt " +
           "       (t_timebegin, t_timeend, t_action, t_tb_include, t_te_include, t_is_holiday,email,email_b,email_a,T_IS_FINAL,T_FINAL_ID, t_id) " +
           "VALUES ('"+timebegin+"', '"+timeend+"','"+m_action(get_mass_index(action,name_action))+"', " ;
    

    if(tb_include == ">=") 
     str = str + " 'X' ," ;
    else
     str = str + " chr(0) ," ;
    end;

    if(te_include == "<=") 
     str = str + " 'X' ," ;
    else
     str = str + " chr(0) ," ;
    end;

    if(is_holiday == "X") 
     str = str + " 'X', " ;
    else
     str = str + " chr(0), " ;
    end;
    
    if (email == "") 
      str = str + " chr(0), " ;
    else 
      str = str + "'" +email +"', " ;
    end;
    if(email_a == "") 
     str = str + " chr(0), " ;
     else 
      str = str + "'" +email_a +"', " ;
    end;
    if(email_b == "") 
      str = str + " chr(0), " ;
    else 
      str = str + "'" +email_b +"', " ;
    end;
     if(IS_FINAL == "") 
      str = str + " chr(0), " ;
    else 
      str = str + "'" +IS_FINAL +"', " ;
      str =  str + "'" +FINAL_ID +"', " ;
    end;

     str = str + GetNextVal()+" ) ";

     rs = rsdcommand(str);

     if(rs)
        rs.execute;
        return true;
     else 
        return false;
     end;            
  END;



//Удаление записи из таблицы
  MACRO delete_usr_me_time(id)
     var rs, str;
     str = " delete from usr_me_time_dbt " +
           "  where t_id = "+ id;

     rs = rsdcommand(str);

     if(rs)
        rs.execute;
        return true;
     else 
        return false;
     end;            
  END;



/*********************************************************************************************************/

Class (TRecHandler) TParm(LbrName)

    InitTRecHandler ("metime", (LbrName), true);

    this.("timeBegin")  = v_timebegin;
    this.("timeEnd")    = v_timeend;
    this.("tb_include") = v_tb_include;
    this.("te_include") = v_te_include;
    this.("Action")     = v_action;
    this.("is_holiday") = v_is_holiday;
    this.("email")      = v_email;
    this.("email_a")    = v_email_a;
    this.("email_b")    = v_email_b;
    this.("is_final")   = v_is_final;
    this.("T_FINAL_ID") = v_FINAL_ID;
//Обработчик панели
    Macro Handler ( dlg, cmd, id, key )

      if ( cmd == DLG_INIT )
        dlg.("timeBegin")  = v_timebegin;
        dlg.("timeEnd")    = v_timeend;
        dlg.("tb_include") = v_tb_include;
        dlg.("te_include") = v_te_include;
        dlg.("Action")     = v_action;
        dlg.("is_holiday") = v_is_holiday;
        dlg.("email")      = v_email;
        dlg.("email_a")    = v_email_a;
        dlg.("email_b")    = v_email_b;
        dlg.("is_final")   = v_is_final;
        if ( valType( v_FINAL_ID ) != 26 ) // SpecVal 0
          dlg.("T_FINAL_ID") = v_FINAL_ID;
        else
          dlg.("T_FINAL_ID") = "";
          dlg.("final_name") = "";  
        end;
       
        
        UpdateFields(dlg);

      elif ((cmd == DLG_REMFOCUS))
        if( (id == FldIndex ("timeBegin")))
          if(not check_time(dlg.("timeBegin"))) 
            return CM_CANCEL;
          end;
          return CM_DEFAULT;
        elif( (id == FldIndex ("timeEnd")))
          if(not check_time(dlg.("timeEnd"))) 
            return CM_CANCEL;
          end;
          return CM_DEFAULT;
        end;
      elif (cmd == DLG_KEY) 
        if ((key == Key_F2) or (key == Key_F9))
           v_timeBegin  = dlg.("timeBegin");
           v_timeend    = dlg.("timeEnd");
           v_tb_include = dlg.("tb_include");
           v_te_include = dlg.("te_include");
           v_is_holiday = dlg.("is_holiday");
           v_action     = dlg.("action");
           v_email      = dlg.("email");
           v_email_a    = dlg.("email_a");
           v_email_b    = dlg.("email_b");
           v_is_final    = dlg.("is_final");
           v_FINAL_ID    = dlg.("T_FINAL_ID");
           if(not check_time(dlg.("timeEnd"))) 
               return CM_IGNORE;
           end;
           if(not check_time(dlg.("timeBegin"))) 
               return CM_IGNORE;
           end;
           if(strlen(dlg.("action"))<=2 )
               msgbox("Не выбран вид документов");
               return CM_IGNORE;
           end; 
           if(isupdate)//Обновляем запись
              if (not Update_usr_me_time(v_id, v_tb_include, v_timebegin, v_te_include, v_timeend, v_action, v_is_holiday,v_email,v_email_a,v_email_b,v_is_final,v_FINAL_ID))
                 msgbox("Не обновилось :(!!!");
              end;
              ischanged = true;
           else//Вставляем запись 
              if (not Insert_usr_me_time( v_tb_include, v_timebegin, v_te_include, v_timeend, v_action, v_is_holiday,v_email,v_email_a,v_email_b,v_is_final,v_FINAL_ID))
                 msgbox("Не обновилось :(!!!");
              end;
              ischanged = true;
           end;
           return CM_SAVE;
        elif (key == Key_F3)
         if (id == FldIndex ("action"))
            choice = menu(name_action,"Документы","Документы");
            if(choice >= 0)
                dlg.("action")= name_action(choice);
            end;
         end;
         if (id == FldIndex ("T_FINAL_ID"))
          //  choice = menu(name_action,"Документы","Документы");
            
              CHOO = GetTime();
             If ((ValType(CHOO)!=V_UNDEF) OR (CHOO!=""))
                CHOO=split(CHOO,"||");
                dlg.("T_FINAL_ID")= CHOO(0);
                //msgbox(CHOO(1));
                dlg.("final_name")= CHOO(1);
               
                UpdateFields(dlg);

                
             end;
         end;
        elif (key == Key_ESC)
          return CM_SAVE;
        elif (key == KEY_SPACE)
         if (id == FldIndex ("te_include"))
           if (dlg.("te_include") == "<=") 
             dlg.("te_include") = "<"; 
           else 
             dlg.("te_include") = "<="; 
           end;
         elif (id == FldIndex ("tb_include"))
           if (dlg.("tb_include")== ">=") 
             dlg.("tb_include") = ">"; 
           else 
             dlg.("tb_include") = ">="; 
           end;
         elif (id == FldIndex ("is_holiday"))
           if (dlg.("is_holiday") == "X") 
             dlg.("is_holiday") = strfor(0); 
           else 
             dlg.("is_holiday") = "X"; 
           end;
          elif (id == FldIndex ("is_final"))
           if (dlg.("is_final") == "X") 
             dlg.("is_final") = strfor(0); 
             dlg.("T_FINAL_ID")=strfor(0); 
             dlg.("final_name")=strfor(0); 
             UpdateFields(dlg);
           else 
             dlg.("is_final") = "X"; 
           end;
         

         end;

        end;
      end;
       return CM_DEFAULT;
    End;

    Macro Run
        return RunDialog (this, R2M (this, "Handler"));
    End;
End;


//Формируем панельку
Macro MkDatePanel( lbr )

Var	rc = lbr.AddPanel( "metime", 35, 10, 70, 9, lbr.EColour.SHLP, lbr.EFRame.UNARY ),
	elem;

       rc.StatusLine = "Пробел - Поменять,  F3 - Справочник,  F9 - Сохранить,  ESC - Выход";
       rc.title = "Ввод/Редактирование интервала";

    elem = rc.AddLabel( 3, 2, "Начало:");
    elem = rc.AddField( 15, 2, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "timebegin",12 );
    elem = rc.AddLabel( 3, 3, "Конец:");
    elem = rc.AddField( 15, 3, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "TimeEnd",12 );	
    elem = rc.AddLabel( 33, 2, "Условие включения");
    elem = rc.AddField( 30, 2, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "tb_include",2 );
    elem = rc.AddLabel( 33, 3, "Условие включения");
    elem = rc.AddField( 30, 3, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "te_include",2 );
    elem = rc.AddLabel( 3, 4, "Отбираемые документы:");
    elem = rc.AddField( 19, 4, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "action",27 );
    elem = rc.AddLabel( 3, 5, "Признак выходного дня");
    elem = rc.AddField( 20, 5, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "is_holiday",1 );
    elem = rc.AddLabel(25, 5, "Признак финального опроса");
    elem = rc.AddField( 45, 5, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "is_final",1 );
    elem = rc.AddLabel(47, 5, ":");
    elem = rc.AddField( 50, 5, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "T_FINAL_ID",1 );
    elem = rc.AddField( 52, 5, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "final_name",10);
    elem = rc.AddLabel( 3, 6, "Почта основная:");
    elem = rc.AddField( 20, 6, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "email",60);
    elem = rc.AddLabel( 3, 7, "Почта дополнительная 1:");
    elem = rc.AddField( 20, 7, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "email_a",60);
    elem = rc.AddLabel( 3, 8, "Почта дополнительная 2:");
    elem = rc.AddField( 20, 8, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "email_b",60);
end;

Macro MkLbrFile()

Var  LbrName = "..\\txtfile\\panel"+usernumber()+".lbr",
     lbr = TLbrFile( LbrName, "C" );
     MkDatePanel( lbr );
     lbr.Save();
     lbr = NULL;
     return LbrName;
end;

/*********************************************************************************************************/

 Var LbrName = MkLbrFile();
 Var Parm = TParm(LbrName);


/*********************************************************************************************************/
//Скроллинг
MACRO me_time_scroll ()
  var CmdText,Command, deleting=false;
  var col = TArray;


  //Обработчик для скроллинга
  macro EvProc (RecordSet, Command, id, key )
    if ((Command == DLG_KEY) and (key == 13)) //Enter

       isupdate = true;           
       //Панель изменения записи
       v_timebegin  = RecordSet.value(1);
       v_timeend    = RecordSet.value(3);
       v_tb_include = RecordSet.value(0);
       v_te_include = RecordSet.value(2);
       v_action     = RecordSet.value(4);
       v_is_holiday = RecordSet.value(5);
       v_id         = RecordSet.value(6);
       v_email      = RecordSet.value(7);
       v_email_a    = RecordSet.value(8);
       v_email_b    = RecordSet.value(9);
       v_is_final    = RecordSet.value(10);
       v_final_id    = RecordSet.value(11);
      // v_is_final,v_final_id
  //     v_is_holiday = RecordSet.value(10);
       Parm.Run(); 

       if(ischanged)
          return CM_Select;
       else
          return CM_IGNORE;
       end;                       

    elif ((Command == DLG_KEY) and (key == 323))//F9


       v_timebegin  = "00:00:00.00";
       v_timeend    = "00:00:00.00";
       v_tb_include = ">";
       v_te_include = "<";
       v_action     = "";
       v_is_holiday = strfor(0);
       v_is_final = strfor(0);
       v_email  ="";
       v_email_a="";
       v_email_b="";
       v_is_final="";
       v_final_id="";
       isupdate = false;
       //Панель ввода новой записи
       Parm.Run(); 
    
       if(ischanged)
          return CM_Select;
       else
          return CM_IGNORE;
       end;                         
    elif ((Command == DLG_KEY) and (key == KEY_F8))

       gettrue(deleting,"Удалить запись?");
       if(deleting)
          delete_usr_me_time(RecordSet.value(6)); 
       end;  
       return CM_Select;
    elif ((Command == DLG_KEY) and (key == KEY_CTRL_R))
       return CM_Select;
    elif ((Command == DLG_KEY) and (key == 27))
       exitscroll = true;
    end;

 end;

  //Добавляем колонки в скроллинг
  macro AddCol (ar,ind, fld, head, width, rdonly, DecPoint, Point)
    ar.value (ind * 6)     = fld;
    ar.value (ind * 6 + 1) = head;
    ar.value (ind * 6 + 2) = width;
    ar.value (ind * 6 + 3 ) = DecPoint;   // fldType
    ar.value (ind * 6 + 4 ) = Point;  // decPoint
    ar.value (ind * 6 + 5 ) = 0;   // reserv
  end;

  AddCol (col, 0, "t_tb_include", "Условие", 8, true);
  AddCol (col, 1, "t_timebegin", "Начало", 15, true);
  AddCol (col, 2, "t_te_include", "Условие", 8, true);
  AddCol (col, 3, "t_timeend", "Конец", 15, true);
  AddCol (col, 4, "t_is_holiday", "Выходной", 8, true);
  AddCol (col, 5, "t_action", "Документы", 8, true);
  AddCol (col, 6, "email", "Почта основная", 15, true);
  AddCol (col, 7, "T_IS_FINAL", "Финальный опрос", 8, true);
  AddCol (col, 8, "t_id", "Ид диапазона", 4,0, true,0);
  AddCol (col, 9, "T_FINAL_ID", "Ид связанного диапазона", 4,0, true,0);

  //Выбираем данные из таблицы
  CmdText = "SELECT DECODE (t.t_tb_include, 'X', '>=', '>') t_tb_include, t.t_timebegin, " +
            "       DECODE (t.t_te_include, 'X', '<=', '<') t_te_include, t.t_timeend, "+
            "       decode(t.t_action,'m_vnutren','Внутренние', "+ 
            "                         'm_vneshn',    'Внешние', "+ 
            "                         'm_all',    'Все', "+ 
            "                         'm_nothing','Ни чего', "+ 
            "                                     t.t_action) t_action,  " +
            "       t.t_is_holiday, t.t_id,email , email_a , email_b,T_IS_FINAL,T_FINAL_ID " +
            "  FROM usr_me_time_dbt t " +
            " ORDER BY t_is_holiday, TO_DATE (SUBSTR (t_timebegin, 1, INSTR (t_timebegin, '.') - 1),'hh24:mi:ss'),T_FINAL_ID " ;




  Command = RSDCommand(CmdText);
  Command.Execute();
  RecordSet = RSDRecordSet(Command, RSDVAL_CLIENT, RSDVAL_STATIC);


  //Запускаем скроллинг
  runScroll(RecordSet, 10, col, null, @EvProc, "Временные интервалы", "Enter - Редактировать,  F9 - Ввод,  ESC - Выход", false);


END;


/******************************************************/
/*                   Точка входа                      */
/******************************************************/

//Пока не нажмем Esc не выйдем
MACRO Run_panel() 
  while(not exitscroll)
     me_time_scroll();
  end;
  exit(1);
end;




