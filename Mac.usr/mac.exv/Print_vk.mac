/*Печать уведомления/извещения для валюты/нерезидента                   */
/*                                                                      */
/*                                                                      */
/* Тихомиров А.Н. Стручков А. 10.07.2009                    Версия 1.0  */
/* Seleznev 16.07.2009                                                  */
/* Teleshova 27.10.2014 Адаптация 2031*/
/* RR  02.12.2015 Доработки согласно R-629798-2                         */

import globals,rsd, Календарь, BankInter, PaymInter;
import cb_sql;
//LVV 19.11.14 R-496887 заприватил переменную , чтобы не вызывала ошибку компиляции
private CONST KEY_STRBREAK           :string = "/STRBREAK";
CONST REF_NUM = 1000009;
import "print_vk_text.mac";

private var out, fulloutput;
GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR",2,out);
Fulloutput = out + "\\print_vk.txt";                    


macro ISPayerNerezPtID(partyid)
   private var cmd, rs;
   cmd = RSDCommand("select 1 from dparty_dbt where t_partyid =  ? and t_notresident = 'X'");
   cmd.addParam("id",RSDBP_IN,   partyid );
   rs = RSDRecordset(cmd);

   return (rs.MoveNext);
end;

macro ISTransitAcc(acc, fiid)
   private var cmd, rs;
   //Gurin S. 29.01.2014 R-322082-1 Адаптация 2031
   cmd = RSDCommand("select 1 from daccount_dbt where t_account =  ? and t_chapter = 1 and t_code_currency = ? and t_type_account like '%Y%'");
   cmd.addParam("acc", RSDBP_IN, acc  );
   cmd.addParam("fiid",RSDBP_IN, fiid );
   rs = RSDRecordset(cmd);

   return (rs.MoveNext);
end;

macro IsCarry(paymentid)
   private var cmd, rs;
   cmd = RSDCommand("select 1 from dpmdocs_dbt where t_paymentid =  ?");
   cmd.addParam("id",RSDBP_IN, paymentid);
   rs=RSDRecordset(cmd);

   return (rs.MoveNext);
end;

Macro ТранзитныйСчетЗачисленияИзПроводки(paymentid, acnt)
   var SQL, cmd, rs;
   // EVG 28/1/2014 Адаптация под 2031
   cmd = RsdCommand(
                  "select trn.t_account_receiver" +
                  "  from dacctrn_dbt trn, dpmdocs_dbt pm, daccount_dbt acnt"+
                  " where pm.t_paymentid = ?"+ 
                  "   and trn.t_acctrnid = pm.t_acctrnid"+
                  "   and trn.t_account_receiver = acnt.t_account"+
                  "   and acnt.t_chapter = 1"+
                  "   and acnt.t_code_currency > 0"+
                  "   and instr(upper(acnt.t_type_account),'Y') != 0"
                  );
   cmd.AddParam("pm", RSDBP_IN, paymentid);
   rs = RsdRecordSet(cmd);
   if(rs.MoveNext())
      return rs.Value(0);
   else
      return acnt;
   end;
end;

//TAM 19.10.2012 I-00270572-2
macro GetClientNameByAcc(acnt)
   var SQL, cmd, rs;
   //Gurin S. 30.01.2014 Адаптация 2031
   cmd = RsdCommand(  "SELECT   PT.T_NAME " +
                      " FROM   daccount_dbt ac, dparty_dbt pt " +
                      " WHERE   AC.T_ACCOUNT = :acc AND AC.T_CLIENT = PT.T_PARTYID "
                  );
   cmd.AddParam("acc", RSDBP_IN, acnt);
   rs = RsdRecordSet(cmd);
   if(rs.MoveNext())
      return rs.Value("t_name");
   else
      return "";
   end;
end;

/* EVG 5/03/2011 Получить номер уведомления по референсу */
Macro GetRefNumber( pmId )
   var stat, val;
   var rs, query;
   var isCur = false;
   // Определим валюту
   query = " Select t_BaseFiid From DPmPaym_dbt " +
           "  Where t_PaymentId = " + pmId;
   rs = rsdRecordSet( query );
   if ( rs and rs.MoveNext() )
      if ( rs.Value(0, null, V_INTEGER) > 0 )
         isCur = true;
      end;
   end;

   // Рубли
   if ( not isCur )
      stat = GenerateReference( 1000023, val );
      val = val;
   // Валюта
   else
      stat = GenerateReference( 1000024, val );
   end;

   if ( stat != 0 )
      msgbox ("Ошибка формирования номера уведомления (референс № 1000023 или 1000024)");
      val = "xxx";
   end;

   return val;
end;

/* EVG 16/03/2011 Вставка уведомления вынесена в отдельную процедуру */
Macro InsTrnsfNotify( paymentId, isCur )
   var cmd, rs;
   /* EVG 23/03/2011 Возникла проблема: номера валютных и рублёвых уведомлений
      потенциально могут пересекаться (пересекутся сразу, если нумерация будет с 1).
      Поэтому для рублёвых уведомлений я создал отдельную таблицу usr_trnsf_notify_rur,
      полностью идентичную usr_trnsf_notify. Она будет использоваться только для сохране-
      ния номера уведомления, в других процессах она не задействована. */
   var tblName = "usr_trnsf_notify_rur";
   if ( isCur )
      tblName = "usr_trnsf_notify";
   end;

   cmd = RSDCommand("select 1 from " + tblName + " where payment_id = ?");
   cmd.addparam("pmid", RSDBP_IN, paymentid);
   rs = RSDRecordset(cmd);
   if (not(rs.movenext))
      cmd = RSDCommand("insert into " + tblName + " (notify_num,notify_date,payment_id) values (?,?,?)");
      cmd.addparam("notify_num", RSDBP_IN, GetRefNumber( paymentid ) );
      cmd.addparam("notify_date", RSDBP_IN, {curdate});
      cmd.addparam("payment_id", RSDBP_IN, paymentid);
      cmd.execute();
   else
      /* Для извещений о поступлении средств в рублях обновление даты не выполняется */
      if ( not isCur )
         return;
      end;
      // EVG 28/1/2014 Адаптация под 2031
      cmd = RSDCommand("update usr_trnsf_notify t set notify_date =  " +
                       "(select  trn.t_date_carry     " +
                       "  from   dpmpaym_dbt pm, " +
                       "         dpmdocs_dbt pmd,  " +
                       "         dacctrn_dbt trn,  " +
                       "         daccount_dbt ac     " +
                       " where       pm.t_dockind in (320, 322, 27, 202)  " +
                       "         and pm.t_FIID_FutureRecAcc <> 0          " +
                       "         and pm.t_paymentid = payment_id " +
                       "         and ac.t_code_currency = pm.t_FIID_FutureRecAcc  " +
                       "         and ac.t_chapter = 1  " +
                       "         and ac.t_type_account like '%Y%' " +
                       "         and pmd.t_paymentid = pm.t_paymentid  " +
                       "         and trn.t_acctrnid = pmd.t_acctrnid  " +
                       "         and trn.t_account_receiver = ac.t_account  " +
                       "         and trn.t_chapter = 1  " +
                       "         and trn.t_fiid_receiver = pm.t_FIID_FutureRecAcc ) " +
                       //"         and trn.t_sum_Payer = pm.t_payamount) " +  zmp 17.04.2014 R-364918
                       "         where payment_id = ? "
                       );
      cmd.addparam("payment_id", RSDBP_IN, paymentid);
      cmd.execute();
   end;
end;

Macro ПечатьУведомления(Doc_date,Account_num,Client_Name,Value,Currency_str,Value_str,Reply_date, BankName, paymentid, ismassprint, NeedPrintLog)
   debugbreak;
   if ((NeedPrintLog) or (valtype(NeedPrintLog) == V_UNDEF))
     setoutput(fulloutput,ismassprint);
   end;

   var rs, cmd;
   var PmObj = RsbPayment(paymentid);
   // EVG 28/1/2014 Адаптация под 2031
   cmd = RSDCommand(
                    "select   trn.t_date_carry      " +
                    "  from   dpmpaym_dbt pm, " +
                    "         dpmdocs_dbt pmd,  " +
                    "         dacctrn_dbt trn,  " +
                    "         daccount_dbt ac     " +
                    " where       pm.t_dockind in (320, 322, 27)  " +
                    "         and pm.t_FIID_FutureRecAcc <> 0 " +
                    "         and ac.t_code_currency = pm.t_FIID_FutureRecAcc  " +
                    "         and ac.t_chapter = 1  " +
                    "         and ac.t_type_account like '%Y%' " +
                    "         and pmd.t_paymentid = pm.t_paymentid  " +
                    "         and trn.t_acctrnid = pmd.t_acctrnid  " +
                    "         and trn.t_account_receiver = ac.t_account  " +
                    "         and trn.t_chapter = 1  " +
                    "         and trn.t_fiid_receiver = pm.t_FIID_FutureRecAcc  " +
                    "         and trn.t_sum_Payer = pm.t_payamount " +
                    "         and  pm.t_paymentid = ? "
                   );
   cmd.addparam("pmid",RSDBP_IN, paymentid);
   rs = RSDRecordset(cmd);
   rs.MoveNext();

   if (strlen(string(sql_ConvTypeDate(rs.value(0)))) == 0) //TAM 26.09.2012 I-00227548-2
      // EVG 28/1/2014 Адаптация под 2031
      cmd = RSDCommand(
                       "select   trn.t_date_carry  " +
                       "  from   dpmpaym_dbt pm,  " +
                       "         dpmdocs_dbt pmd, " +
                       "         dacctrn_dbt trn  " +
                       " where       pm.t_dockind in (320, 322, 27, 202)  " +
                       "         and pm.t_FIID_FutureRecAcc <> 0 " +
                       "         and pmd.t_paymentid = pm.t_paymentid  " +
                       "         and trn.t_acctrnid = pmd.t_acctrnid  " +
                       "         and trn.t_chapter = 1  " +
                       "         and trn.t_fiid_receiver = pm.t_fiid  " +
                       "         and trn.t_sum_Payer = pm.t_amount " +
                       "         and  pm.t_paymentid = ? "
                     );
      cmd.addparam("pmid",RSDBP_IN, paymentid);
      rs = RSDRecordset(cmd);
      rs.MoveNext();
   end;

   if (strlen(rs.value(0)) > 0)
      Reply_date = DateAfterWorkDays(date(Doc_date), 15);
   end;

   if((PmObj.dockind == 320) and (PmObj.BaseFIID != 0))
      Account_Num = ТранзитныйСчетЗачисленияИзПроводки(paymentid, Account_Num);
   end;

   InsTrnsfNotify( paymentid, true );
   /* EVG 11/03/2011 Пришло сверху */
   
   if ((NeedPrintLog) or (valtype(NeedPrintLog) == V_UNDEF))
     println(ТекстУведомления(Client_name, paymentid, Currency_str, Value, Value_str, Account_Num, sql_ConvTypeDate(rs.value(0)), Reply_date ));
     if (ismassprint)
        println("");
     end;

     SetOutput(null,true);
     if (not(ismassprint))
        viewfile(fulloutput);
     end;
   end;
end;

macro ПечатьИзвещения(Account_num,Client_Name,Value,Currency_str,Value_str, paymentid, ismassprint, pdate, send_client, NeedPrintLog)

   if ((NeedPrintLog) or (valtype(NeedPrintLog) == V_UNDEF))
     setoutput(fulloutput,ismassprint);
   end;
   /* EVG 16/03/2011 По поступлению средств на р/с от нерезидентов теперь 
      также создаётся уведомление (необходимо для того, чтобы сохранить в 
      нём номер, необходимый при печати). */
   InsTrnsfNotify( paymentid );

   if ((NeedPrintLog) or (valtype(NeedPrintLog) == V_UNDEF))
     println (ТекстИзвещения(client_name, paymentid, value, currency_str, value_str, account_num, pdate,send_client));
     if (ismassprint)
        println("");
     end;

     SetOutput(null,true);



     if (not(ismassprint))
       viewfile(fulloutput);
     end;

   end;
end;

macro ПечатьВсехУведомлений(for_send, NotifyDate, NeedAskDepartment, NeedPrintLog)
   debugbreak;
   var pdate = {curdate};
   var rub, kop,CURtext, case="";
   var depi = "";
   var rs1, cm1, dep = "0";

   if (valType(NotifyDate) == V_UNDEF)
     if (not(getDate(pdate,"Укажите дату зачисления на счет")))
       exit(1);
     end;
   else
     pdate = NotifyDate;
   end;

   if ((NeedAskDepartment) or (valType(NeedAskDepartment) == V_UNDEF))
     if (not(getstring(dep,"Укажите подразделение (0 - Весь банк)", 3)))
       exit(1);
     else
       if (dep > 0)
        cm1 = rsdcommand("select t_code from ddp_dep_dbt where t_name = ?");
        cm1.addparam("name", RSDBP_IN, dep);
        rs1 = rsdrecordset(cm1);
        if (rs1.movenext())
          depi = "and ac.t_branch = "+rs1.value(0);
        end;
       end;
     end;

   end;

   private var rs, cmd;
   // EVG 28/1/2014 Адаптация под 2031
   cmd = RSDCommand("select   pm.t_paymentid, trn.t_date_carry t_valuedate, " +
                    "         trn.t_account_receiver t_receiveraccount, " +
                    "         pt.t_name as t_receivername, " +       // Тихомиров А.Н. Имя для получателя не из платежа а из party_dbt
                    "         pm.t_payamount, " +
                    "         fi.t_iso_number, " +
                    "         pmr.t_receiverbankname " +
                    "  from   dpmpaym_dbt pm, " +
                    "         dpmrmprop_dbt pmr, " +
                    "         dpmdocs_dbt pmd, " +
                    "         dacctrn_dbt trn, " +
                    "         daccount_dbt ac, " +
                    "         dparty_dbt pt, " +
                    "         dfininstr_dbt fi " +
                    " where       pm.t_dockind in (320, 322, 27, 202) " +
                    "         and pm.t_FIID_FutureRecAcc <> 0 " +
                    "         and pm.t_paymentid = pmr.t_paymentid " +
                    "         and trn.t_date_carry = ? " +
                    "         and trn.t_fiid_receiver = pm.t_FIID_FutureRecAcc " +
                    "         and trn.t_chapter = 1 " +
                    "         and ac.t_type_account like '%Y%' " + case +
                    "         and fi.t_fiid = pm.t_FIID_FutureRecAcc " + depi +
                    "         and pt.t_partyid = ac.t_client " +
                    "         and pt.t_notresident = chr (0) " +
                    "         and pmd.t_paymentid = pm.t_paymentid " +
                    "         and trn.t_acctrnid = pmd.t_acctrnid " +
                    "         and trn.t_account_receiver = ac.t_account " +
                    "         and trn.t_chapter = 1 " +
                    "         and trn.t_fiid_receiver = pm.t_FIID_FutureRecAcc "
                    );
   cmd.addparam("pdate", RSDBP_IN, pdate);
   rs = RSDRecordset(cmd);

   if ((NeedPrintLog) or (valType(NeedPrintLog) == V_UNDEF))
     setoutput(fulloutput,false);
     setoutput(null,true);
   end;

   while (rs.moveNext())
      /* EVG 2.12.2011 Надо переводить 4-й параметр к числу*/
      CURtext    = CurToStrAlt(money(rs.value("t_payamount")), rub, kop, int(rs.value("t_iso_number")), false); 
      ПечатьУведомления(date(rs.value("t_valuedate")),rs.value("t_receiveraccount"),rs.value("t_receivername"),money(rs.value("t_payamount")),
                    SubStr(CURtext, StrLen(rub)+1, Index(CURtext,kop)-Strlen(rub)-1), CURtext,
                    DateAfterWorkDays(date(rs.value("t_valuedate")), 15), rs.value("t_receiverBankname"), rs.value("t_paymentid"), true, NeedPrintLog);
   end;
   
   if ((NeedPrintLog) or (valType(NeedPrintLog) == V_UNDEF))
     viewfile(fulloutput);
   end;
   
end;


macro ПечатьВсехИзвещений(for_send, NotifyDate, NeedAskDepartment, NeedPrintLog)
   var pdate = {curdate};
   var rub, kop,CURtext, case = "";
   var depi = "";
   var rs1, cm1, dep = "0";

   if (valType(NotifyDate) == V_UNDEF)
     if (not(getDate(pdate,"Укажите дату зачисления на счет")))
       exit(1);
     end;
   else
     pdate = NotifyDate;
   end;

   if ((NeedAskDepartment) or (valType(NeedAskDepartment) == V_UNDEF))
     if (not(getstring(dep,"Укажите подразделение (0 - Весь банк)", 3)))
       exit(1);
     else
        if (dep > 0)
        cm1 = rsdcommand("select t_code from ddp_dep_dbt where t_name = ?");
        cm1.addparam("name", RSDBP_IN, dep);
        rs1 = rsdrecordset(cm1);
        if (rs1.movenext())
          depi = "and ac.t_branch = "+rs1.value(0);
        end;
       end;
     end;

   end;

   private var rs, cmd;
   //Gurin S. 31.10.2014 I-00528228-2
   cmd = RSDCommand("select   pm.t_paymentid, trn.t_date_carry t_valuedate,  " +
                    "         nvl(pm.t_payeraccount,' ') t_payeraccount,  " + //TAM R-184098-2 24.04.2013
                    "         trn.t_account_receiver t_receiveraccount,  " +
                    "         pmr.t_receivername,  " +
                    "         pmr.t_payername,  " +
                    "         pm.t_amount,          " +
                    "         pmr.t_receiverbankname,  " +
                    "         pm.t_receiver,  " +
                    // EVG 16/08/2012 Для анализа основания
                    "         pmr.t_ground " +
                    "  from   dpmpaym_dbt pm,  " +
                    "         daccount_dbt ac, " +
                    "         dacctrn_dbt trn, " +
                    "         dpmrmprop_dbt pmr,  " +
                    "         dpmdocs_dbt pmd       " +
                    " where   pm.t_dockind in (320, 322, 16, 201)  " +
                    "         and pm.t_FIID_FutureRecAcc = 0  " +
                    /*SDA 08.07.2011 - только на счета получателей в нашем банке */
                    "         and pm.T_RECEIVERBANKID = " + {OurBank} +
                    /* EVG 5/04/2011 В отчёт выводятся только документы по счетам ЮЛ */
/*                    "         and subStr( pm.t_PayerAccount, 1, 2 ) = '40' " +*/
                    /*SDA  ошибочка - имеются ввиду счета получателей ЮЛ  */
                    "         and subStr( pm.t_ReceiverAccount, 1, 2 ) = '40' " +

                    "         and trn.t_account_receiver = ac.t_account " +
                    "         and pm.t_paymentid = pmr.t_paymentid  " + case +
                    "         and ac.t_chapter = 1 " + depi +
                    "         and pmd.t_paymentid = pm.t_paymentid " +
                    "         and trn.t_acctrnid         = pmd.t_acctrnid " +
                    "         and trn.t_date_carry = ?  " +
                    "         and pmd.t_paymentid = pm.t_paymentid  "
                    );
   cmd.addparam("pdate", RSDBP_IN, pdate);
   rs = RSDRecordset(cmd);

   if ((NeedPrintLog) or (valType(NeedPrintLog) == V_UNDEF))
     setoutput(fulloutput,false);
     setoutput(null,true);
   end;
   
   var  ReciverMask = "401*-407*, 40802*"; /*21-01-2013 joy I-00306892-3 добавлены маски, согл. с Севостьяновой О.Б.*/
   while (rs.moveNext())
      if ((CompareStrWithMasks (ReciverMask,  rs.value("t_receiveraccount")  ) == 0) )
         if ( ISPayerNerezAcc(rs.value("t_payeraccount")))
            CURtext    = CurToStrAlt(money(rs.value("t_amount")), rub, kop, 810); 
            ПечатьИзвещения(rs.value("t_receiveraccount"),rs.value("t_receivername"),money(rs.value("t_amount")), SubStr(CURtext,       StrLen(rub)+1, Index(CURtext,kop)-Strlen(rub)-1), CURtext, rs.value("t_paymentid"), true, pdate, rs.value("t_payername"), NeedPrintLog);
         end;
      end;
   end;
   
   if ((NeedPrintLog) or (valType(NeedPrintLog) == V_UNDEF))
     viewfile(fulloutput);
   end;
   
end;