/*
  Протокол контроля записей книги продаж                
                                                               
                                                               
  А.Киселев 22.03.2011      
+  LVV      06.11.2014 Адаптация под 31 версию.

*/

import rcw, rslx, Календарь, oralib, likepy, bankinter, globals, "lib_str.mac";

array Rep_Mode;
      Rep_Mode(0) = "Банк";
      Rep_Mode(1) = "Филиал";
      Rep_Mode(2) = "Подразделение";

var 
 ex, ob, obbook, obsheet, flag, DateBegin, DateEnd, DprtCode, CntDoc, SumDoc, RepModeFlag;

private const  KEY_F1      =315;
private const  KEY_F2      =316;
private const  KEY_F3      =317;
private const KEY_ESC      = 27;
private const KEY_SPACE    = 32;
private const KEY_ENTER    = 13;


private Class TBranchParam ( dprt_code )
var  Code = "";
var  Name = "";
var  Id;



 macro GetDepartName(dprt_code);
 Var
  Query :string = "",
  Params :TArray,
  DataSet :RsdRecordset;

  Id = dprt_code;
  Query = "SELECT A.T_NAME, B.T_NAME FROM dparty_dbt A, ddp_dep_dbt B WHERE B.T_CODE = :dprt_code AND A.T_PARTYID = B.T_PARTYID";
  Params = makeArray( SQLParam( "dprt_code" , dprt_code ));
  DataSet = execSQLselect( Query, Params , true  );
  if (DataSet.MoveNext())
   Code = DataSet.value(1);
   Name = DataSet.value(0);
   return 0;
  else
   Code = "";
   Name = "";
   return 1;
  end;
 end;

 macro GetDepartName_(dprt_code);
 Var
  Query :string = "",
  Params :TArray,
  DataSet :RsdRecordset;

  Query = "SELECT A.T_NAME, B.T_NAME, B.T_CODE FROM dparty_dbt A, ddp_dep_dbt B " +
          " WHERE DECODE(LTRIM(B.T_NAME,'0'),'','0',LTRIM(B.T_NAME,'0')) = DECODE(LTRIM(:dprt_code_1,'0'),'','0',LTRIM(:dprt_code_2,'0'))  " +
          " AND A.T_PARTYID = B.T_PARTYID ";
  Params = makeArray( SQLParam( "dprt_code_1" , dprt_code ), SQLParam( "dprt_code_2" , dprt_code ) );
  DataSet = execSQLselect( Query, Params , true  );
  if (DataSet.MoveNext())
   Code = DataSet.value(1);
   Name = DataSet.value(0);
   Id = DataSet.value(2);
   return 0;
  else
   Code = "";
   Name = "";
   Id = 0;
   return 1;
  end;
 end;


end;

macro GetOfficer(Oper_Id) :string
Var
 Query :string = "",
 Params :TArray,
 DataSet :RsdRecordset;

 Query = "SELECT T_NAME FROM dperson_dbt WHERE T_OPER = :Oper_Id ";
 Params = makeArray( SQLParam( "Oper_Id" , Oper_Id ));
 DataSet = execSQLselect( Query, Params , true  );
 if (DataSet.MoveNext())
  return DataSet.value(0);
 else
  return "";
 end;
end;


macro HeaderTxt()
var
 BranchParam :TBranchParam;

 BranchParam.GetDepartName(DprtCode);

 [                                                                  Протокол ошибок
                                                        выявленных при формировании книги продаж
                                                         за период с  ########## по  ##########



  Исп. ____________ (#####################)
                                                                                                                                          Подразделение: ###########
  Дата выпуска отчета ###########                                                                                                         Режим: ###################
 ┌─────────────────┬───────────────────────────┬───────────────────────────────────────────────────────────────────────────┬───────────────────────┬──────────────────┐
 │      Код        │           Опер.           │                                Проводка НДС                             │          Сумма        │  Статус ошибки   │
 │    клиента      ├────────┬──────────────────┼───────────────┬───────────┬───────────────────────┬───────────────────────┤                       │                  │
 │                 │   №    │      Ф.И.О.      │   №           │    Дата   │        Дебет          │        Кредит         │                       │                  │
 ├─────────────────┼────────┼──────────────────┼───────────────┼───────────┼───────────────────────┼───────────────────────┼───────────────────────┼──────────────────┤]
 (DateBegin, DateEnd, GetOfficer( {oper}), BranchParam.Code:r, Date, Rep_Mode(RepModeFlag):r);


end;


macro FooterTxt()
 [└─────────────────┴────────┴──────────────────┴───────────────┴───────────┴───────────────────────┴───────────────────────┴───────────────────────┴──────────────────┘
 Итого документов ########  на сумму #############]
 (CntDoc,SumDoc);
end;

record Department ("dp_dep.dbt");
var Fulloutput_lbr, out_lbr, output_lbr="blif.lbr";                    
//var Fulloutputt = GetTxtFileName("blif");
var out_templ, output_templ = "templ_err_bilf.xls", Fulloutput_templ;

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,out_lbr);
Fulloutput_lbr = FindPath(output_lbr, out_lbr);
var dlgRep = TRecHandler("blif", Fulloutput_lbr, TRUE); 




private macro MakeRep()
var in;
Var
 Query :string = "",
 StrSelBranch,
 Params :TArray,
 DataSet_1 :RsdRecordset,
 DataSet_2 :RsdRecordset,
 BranchParam :TBranchParam;

 BranchParam.GetDepartName(DprtCode);


 CntDoc = 0; SumDoc = 0.00;
 
 if(Flag)
   if (IsStandAlone()) // Двухзвенка
     if ((ValType(ex)!=V_UNDEF) and (ex.Visible==False))
       ex = ActiveX("Excel.Application",NULL,false); 
     else
       ex = ActiveX("Excel.Application",NULL,true);
     end;
   else // Трехзвенка
     ob = CreateObject ("rsax","TRsAxServer","RsAxServer",IsStandAlone());
     ex = ob.CreateComObject ("Excel.Application",true);
   end; 
   GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\USERTEMPLSDIR",2,out_templ);
   Fulloutput_templ = FindPath(output_templ, out_templ);                    
   obBook = ex.Workbooks.open(Fulloutput_templ); 
   obSheet = obBook.ActiveSheet(); 
   obSheet.Range("A"+3).Value=string("Протокол ошибок выявленных при формировании книги продаж за период с  ",DateBegin," по ",DateEnd);
   obSheet.Range("A"+4).Value=string("Исп. ____________ ( " , GetOfficer( {oper}) , " )");
   obSheet.Range("A"+5).Value=string("Дата выпуска отчета ", Date);
   obSheet.Range("I"+4).Value=string("Подразделение: ", BranchParam.Code);
   obSheet.Range("I"+5).Value=string("Режим: ", Rep_Mode(RepModeFlag));
   obSheet.Range("A"+6).Value="Код клиента";
   obSheet.Range("B"+6).Value="Опер.";
   obSheet.Range("B"+7).Value="№";
   obSheet.Range("C"+7).Value="Ф.И.О.";
   obSheet.Range("D"+6).Value="Проводка НДС";
   obSheet.Range("D"+7).Value="№";
   obSheet.Range("E"+7).Value="Дата";
   obSheet.Range("F"+7).Value="Дебет";
   obSheet.Range("G"+7).Value="Кредит";
   obSheet.Range("H"+7).Value="Сумма";
   obSheet.Range("I"+6).Value="Статус ошибки";
 else
  HeaderTxt();
 end;

 InitProgress(-1,"Ждите, обрабатка данных...","Обработка проводок НДС, поиск связей с СФ");

 UseProgress(-1);

 debugbreak;
 if ( RepModeFlag == 1) //Филиал
  StrSelBranch = " AND EXISTS( SELECT 1 FROM dperson_dbt M, ddp_dep_dbt N " +
                 "             WHERE M.T_OPER = Z.T_OPER " +
                 "              AND M.T_CODEDEPART =  " + string(DprtCode) + " OR N.T_CODE = M.T_CODEDEPART AND N.T_PARENTCODE =  " + string(DprtCode) + " ) ";

 elif (RepModeFlag == 2) //Подразделение
  StrSelBranch = " AND EXISTS( SELECT 1 FROM dperson_dbt M " +
                 " WHERE M.T_OPER = Z.T_OPER " +
                 " AND M.T_CODEDEPART = " + string(DprtCode) + " ) ";
 else
  StrSelBranch = ""; //Банк
 end;

 Query = " SELECT Z.T_ACCTRNID, W.T_CODE,Z.T_OPER, Y.T_NAME, Z.T_NUMB_DOCUMENT, Z.T_DATE_CARRY,Z.T_ACCOUNT_PAYER, Z.T_ACCOUNT_RECEIVER,Z.T_SUM_NATCUR " +
         " FROM dacctrn_dbt Z, daccount_dbt X, dobjcode_dbt W, dperson_dbt Y " +
         " WHERE Z.T_CHAPTER = 1 " +
         "  AND Z.T_ACCOUNT_RECEIVER LIKE '60309%'  " +
         "  AND Z.T_DATE_CARRY BETWEEN :DateBegin AND :DateEnd " +
         "  AND Z.T_SUM_NATCUR > 0 " +
         "  AND X.T_CHAPTER = 1 AND X.T_ACCOUNT = Z.T_ACCOUNT_PAYER " +
         "  AND W.T_OBJECTTYPE = 3 AND W.T_CODEKIND = 1 AND W.T_OBJECTID = X.T_CLIENT AND W.T_STATE = 0 " +
         "  AND Y.T_OPER = Z.T_OPER " +
          StrSelBranch +
         " ORDER BY T_DATE_CARRY, T_SUM_NATCUR ";
debugbreak;
 Params = makeArray( SQLParam( "DateBegin" , DateBegin ), SQLParam( "DateEnd" , DateEnd ) );
 DataSet_1 = execSQLselect( Query, Params );
 while ( DataSet_1.MoveNext() ) //СФ не сформирован
  UseProgress(CntDoc);

//-----------------отловили проводки по которым нет по операции платежей с происхождением - комиссия соответственно нет СФ-------------------------------
  Query = " SELECT 1 FROM doprdocs_dbt B,doproper_dbt C, dpmpaym_dbt D " +
          " WHERE B.T_ACCTRNID = :DocId " +
          "  AND C.T_ID_OPERATION = B.T_ID_OPERATION " +
          "  AND D.T_PAYMENTID = TO_NUMBER(C.T_DOCUMENTID) "; //AND D.T_ORIGIN IN (1,3,1102,1700) ";
  Params = makeArray( SQLParam( "DocId" , DataSet_1.value("t_acctrnid")) );
  DataSet_2 = execSQLselect( Query, Params );
  if ( not DataSet_2.MoveNext() )
   CntDoc = CntDoc+1;
   SumDoc = SumDoc+DataSet_1.value("t_sum_natcur");
   if(Flag)
    obSheet.Range("A"+(7+CntDoc)).Value = DataSet_1.value("T_CODE");
    obSheet.Range("B"+(7+CntDoc)).Value = DataSet_1.value("T_OPER");
    obSheet.Range("C"+(7+CntDoc)).Value = DataSet_1.value("T_NAME");
    obSheet.Range("D"+(7+CntDoc)).Value = DataSet_1.value("T_NUMB_DOCUMENT");
    obSheet.Range("E"+(7+CntDoc)).Value = DataSet_1.value("T_DATE_CARRY");
    obSheet.Range("F"+(7+CntDoc)).Value = DataSet_1.value("T_ACCOUNT_PAYER");
    obSheet.Range("G"+(7+CntDoc)).Value = DataSet_1.value("T_ACCOUNT_RECEIVER");
    obSheet.Range("H"+(7+CntDoc)).Value = DataSet_1.value("t_sum_natcur");
    obSheet.Range("I"+(7+CntDoc)).Value = "Не найдены документ комиссии и СФ";
   else
    [│#################│########│##################│#############│###########│#######################│#######################│#######################│##################│]
     (DataSet_1.value("T_CODE"):c, DataSet_1.value("T_OPER"):r, DataSet_1.value("T_NAME"):l:w, DataSet_1.value("T_NUMB_DOCUMENT"):r,Date(DataSet_1.value("T_DATE_CARRY")):c,DataSet_1.value("T_ACCOUNT_PAYER"):c,
      DataSet_1.value("T_ACCOUNT_RECEIVER"):c,DataSet_1.value("t_sum_natcur"):r, "Не найдены документ комиссии и СФ":w);
   end;
  else
//-----------------отловили проводки по которым нет по операции платежей с происхождением - комиссия соответственно нет СФ-------------------------------

//-----------------отловили проводки по которым нет по операции СФ-------------------------------
//????????????запрос выполняется медленно после связки с dbilbookentry_dbt разбил на два
   Query = " SELECT D.T_BOOKENTRYID FROM doprdocs_dbt B,doproper_dbt C, dbilreconcile_dbt D " +
           " WHERE B.T_ACCTRNID = :DocId " + 
           "  AND C.T_ID_OPERATION = B.T_ID_OPERATION " +
           "  AND D.T_DOCKIND IN(70, 201, 286)  " +
           "  AND D.T_DOCID = NVL(( SELECT MAX(G.T_PURPOSEPAYMENT) FROM dpmlink_dbt G " +
           "                        WHERE G.T_INITIALPAYMENT = TO_NUMBER(C.T_DOCUMENTID) " +
           "                      ),TO_NUMBER(C.T_DOCUMENTID) " +
           "                     )  ";

   Params = makeArray( SQLParam( "DocId" , DataSet_1.value("t_acctrnid") ) );
   DataSet_2 = execSQLselect( Query, Params );
   if ( not DataSet_2.MoveNext() )
/*    Query =        " SELECT 1 D.T_BOOKENTRYID FROM dbilbookentry_dbt E, dbilfactura_dbt F " + если нужны параметры СФ можно раскоментить
           " WHERE E.T_BOOKENTRYID = D.T_BOOKENTRYID " +
           "  AND F.T_FACTURAID = E.T_FACTURAID AND F.T_STATUS <> 3 ";
    Params = makeArray( SQLParam( "AppKnd" , DataSet_2.value(0) ) );
    DataSet_3 = execSQLselect( Query, Params );
    if ( not DataSet_3.MoveNext() )

      //Вывод данных из DataSet_1
      MsgBox("BEBEBE_1");
    end;*/
//    Println("BEBEBE_1  |" + DataSet_1.value(1));

    CntDoc=CntDoc+1;
    SumDoc = SumDoc+DataSet_1.value("t_sum_natcur");
    if(Flag)
     obSheet.Range("A"+(7+CntDoc)).Value = DataSet_1.value("T_CODE");
     obSheet.Range("B"+(7+CntDoc)).Value = DataSet_1.value("T_OPER");
     obSheet.Range("C"+(7+CntDoc)).Value = DataSet_1.value("T_NAME");
     obSheet.Range("D"+(7+CntDoc)).Value = DataSet_1.value("T_NUMB_DOCUMENT");
     obSheet.Range("E"+(7+CntDoc)).Value = DataSet_1.value("T_DATE_CARRY");
     obSheet.Range("F"+(7+CntDoc)).Value = DataSet_1.value("T_ACCOUNT_PAYER");
     obSheet.Range("G"+(7+CntDoc)).Value = DataSet_1.value("T_ACCOUNT_RECEIVER");
     obSheet.Range("H"+(7+CntDoc)).Value = DataSet_1.value("t_sum_natcur");
     obSheet.Range("I"+(7+CntDoc)).Value = "Не найден СФ";
    else
     [│#################│########│##################│###############│###########│#######################│#######################│#######################│##################│]
      (DataSet_1.value("T_CODE"):c, DataSet_1.value("T_OPER"):r, DataSet_1.value("T_NAME"):l:w, DataSet_1.value("T_NUMB_DOCUMENT"):r,Date(DataSet_1.value("T_DATE_CARRY")):c,DataSet_1.value("T_ACCOUNT_PAYER"):c,
       DataSet_1.value("T_ACCOUNT_RECEIVER"):c,DataSet_1.value("t_sum_natcur"):r, "СФ не сформирован":w);
    end;
   end;
  end;
//-----------------отловили проводки по которым нет по операции СФ-------------------------------

 end;



 Query = " SELECT X.T_CODE,Z.T_OPER, Y.T_NAME, Z.T_FACTURANUMBER, Z.T_REGDATE, CHR(0), CHR(0), Z.T_TOTALWITHNDS " + //было BANKDATE
         " FROM dbilfactura_dbt Z, dobjcode_dbt X, dperson_dbt Y " +
         " WHERE Z.T_REGDATE BETWEEN :DateBegin AND :DateEnd " + //было BANKDATE
         "  AND Z.T_DIRECTION = 1 " +
         "  AND NOT EXISTS (SELECT 1 FROM dbilbookentry_dbt W WHERE W.T_FACTURAID = Z.T_FACTURAID) " +
         "  AND X.T_OBJECTTYPE = 3 AND X.T_CODEKIND = 1 AND X.T_OBJECTID = Z.T_RECEIVERID AND X.T_STATE = 0 " +
         "  AND Y.T_OPER = Z.T_OPER  " +
          StrSelBranch +
         " ORDER BY T_REGDATE, T_TOTALWITHNDS "; //было BANKDATE
 Params = makeArray( SQLParam( "DateBegin" , DateBegin ), SQLParam( "DateEnd" , DateEnd ) );
 DataSet_1 = execSQLselect( Query, Params );
 while ( DataSet_1.MoveNext() ) //СФ сформирован но не внесен в книгу продаж
  UseProgress(CntDoc);

  CntDoc=CntDoc+1;
  SumDoc = SumDoc+DataSet_1.value(9-2);
  if(Flag)
   obSheet.Range("A"+(7+CntDoc)).Value = DataSet_1.value(2-2);
   obSheet.Range("B"+(7+CntDoc)).Value = DataSet_1.value(3-2);
   obSheet.Range("C"+(7+CntDoc)).Value = DataSet_1.value(4-2);
   obSheet.Range("D"+(7+CntDoc)).Value = DataSet_1.value(5-2);
   obSheet.Range("E"+(7+CntDoc)).Value = DataSet_1.value(6-2);
   obSheet.Range("F"+(7+CntDoc)).Value = DataSet_1.value(7-2);
   obSheet.Range("G"+(7+CntDoc)).Value = DataSet_1.value(8-2);
   obSheet.Range("H"+(7+CntDoc)).Value = DataSet_1.value(9-2);
   obSheet.Range("I"+(7+CntDoc)).Value = "СФ сформирован но не внесен в книгу продаж";
  else
   [│#################│########│##################│###############│###########│#######################│#######################│#######################│##################│]
    (DataSet_1.value(2-2):c, DataSet_1.value(3-2):r, DataSet_1.value(4-2):l:w, DataSet_1.value(5-2):r,Date(DataSet_1.value(6-2)):c,DataSet_1.value(7-2):c,
     DataSet_1.value(8-2):c,DataSet_1.value(9-2):r, "СФ сформирован но не внесен в книгу продаж":w);
  end;

 end;


 Query = " SELECT X.T_CODE,Z.T_OPER, Y.T_NAME, Z.T_FACTURANUMBER, Z.T_REGDATE, CHR(0), CHR(0), Z.T_TOTALWITHNDS " + //было BANKDATE
         " FROM dbilfactura_dbt Z, dobjcode_dbt X, dperson_dbt Y " +
         " WHERE Z.T_REGDATE BETWEEN :DateBegin AND :DateEnd " + //было BANKDATE
         "  AND Z.T_DIRECTION = 1 " +
         "  AND EXISTS (SELECT 1 FROM dbilbookentry_dbt W WHERE W.T_FACTURAID = Z.T_FACTURAID) " +
         "  AND NOT EXISTS (SELECT 1 FROM dbilbookentry_dbt V, dbilreconcile_dbt U WHERE V.T_FACTURAID = Z.T_FACTURAID AND U.T_BOOKENTRYID = V.T_BOOKENTRYID) " +
         "  AND X.T_OBJECTTYPE = 3 AND X.T_CODEKIND = 1 AND X.T_OBJECTID = Z.T_RECEIVERID AND X.T_STATE = 0 " +
         "  AND Y.T_OPER = Z.T_OPER " +
          StrSelBranch +
         " ORDER BY T_REGDATE, T_TOTALWITHNDS "; //было BANKDATE
 Params = makeArray( SQLParam( "DateBegin" , DateBegin ), SQLParam( "DateEnd" , DateEnd ) );
 DataSet_1 = execSQLselect( Query, Params );
 while ( DataSet_1.MoveNext() ) //Отсутствует квитовка с документом оплаты
  UseProgress(CntDoc);

// вывод в отчет
  CntDoc=CntDoc+1;
  SumDoc = SumDoc+DataSet_1.value(9-2);
  if(Flag)
   obSheet.Range("A"+(7+CntDoc)).Value = DataSet_1.value(2-2);
   obSheet.Range("B"+(7+CntDoc)).Value = DataSet_1.value(3-2);
   obSheet.Range("C"+(7+CntDoc)).Value = DataSet_1.value(4-2);
   obSheet.Range("D"+(7+CntDoc)).Value = DataSet_1.value(5-2);
   obSheet.Range("E"+(7+CntDoc)).Value = DataSet_1.value(6-2);
   obSheet.Range("F"+(7+CntDoc)).Value = DataSet_1.value(7-2);
   obSheet.Range("G"+(7+CntDoc)).Value = DataSet_1.value(8-2);
   obSheet.Range("H"+(7+CntDoc)).Value = DataSet_1.value(9-2);
   obSheet.Range("I"+(7+CntDoc)).Value = "Отсутствует квитовка с документом оплаты";
  else
   [│#################│########│##################│###############│###########│#######################│#######################│#######################│##################│]
    (DataSet_1.value(2-2):c, DataSet_1.value(3-2):r, DataSet_1.value(4-2):l:w, DataSet_1.value(5-2):r,Date(DataSet_1.value(6-2)):c,DataSet_1.value(7-2):c,
     DataSet_1.value(8-2):c,DataSet_1.value(9-2):r, "Отсутствует квитовка с документом оплаты":w);
  end;
 end;




 RemProgress(CntDoc); 

 if(Flag)
  obSheet.Range("C"+(8+CntDoc)).Value = "Итого документов ";
  obSheet.Range("D"+(8+CntDoc)).Value = CntDoc;
  obSheet.Range("E"+(8+CntDoc)).Value = " на сумму ";
  obSheet.Range("F"+(8+CntDoc)).Value = SumDoc;

  obSheet.Range("A6:I"+(7)).Borders.Weight=2;

  obSheet.Range("A8:I"+(7+CntDoc)).Borders.Weight=2;
  obSheet.Range("A6:I"+7).interior.color=4035000;
  Ex.visible = true;       
 else
  FooterTxt();
  SetOutput (Null,True);
 end;



end;


 macro Event (dlg, cmd, id, key) 

 var mnu, const_mess = "~F2~ Продолжить ~ESC~ Выход ";
 var const_mess2 = "~F2~ Продолжить ~SPACE~ ~ESC~ Выход ";
 var BranchParam = TBranchParam;

  if(cmd == DLG_INIT)
     DprtCode = {OperDprtNode};
     dlg.rec.dep = Rep_Mode(0);
     RepModeFlag = 0;
     dlg.rec.flag  = "";
     dlg.rec.Datebegin = {curDate};
     dlg.rec.Dateend = {curDate};
     BranchParam.GetDepartName({OperDprtNode});
     dlg.rec.Branch = BranchParam.Code;
     dlg.rec.branchname = BranchParam.Name; //неактивно
     UpdateFields(dlg); 
  end;
  
  if (cmd==DLG_SETFOCUS)
    if (FldName(dlg,id)=="Flag") 
      message(const_mess2);
    elif ((FldName(dlg,id)=="DateBegin") or (FldName(dlg,id)=="DateEnd"))
      message(" ~F3~ Выбор даты из календаря "+const_mess);
    elif (FldName(dlg,id)=="Branch")
      message(" ~F3~ Выбор филиала "+const_mess);
   end;
  end;
  
  if (cmd == DLG_REMFOCUS)
    if (FldName(dlg,id) == "DateEnd")
      if ( dlg.rec.DateEnd > {curdate} )
       MsgBox("Дата конца периода не может быть больше даты последнего открытого операционного дня");
       return CM_CANCEL;
      end;
      if ( dlg.rec.DateBegin > dlg.rec.DateEnd )
       MsgBox("Дата начала периода не может быть больше даты конца периода");
       return CM_CANCEL;
      end;
    elif (FldName(dlg,id) == "DateBegin")
      if ( dlg.rec.DateBegin > dlg.rec.DateEnd )
       MsgBox("Дата начала периода не может быть больше даты конца периода");
       return CM_CANCEL;
      end;
      if ( dlg.rec.DateBegin > {curdate} )
       MsgBox("Дата начала периода не может быть больше даты последнего открытого операционного дня");
       return CM_CANCEL;
      end;
    elif (FldName(dlg,id) == "Branch")
     if (RepModeFlag <= 0)
      BranchParam.GetDepartName({OperDprtNode});
      dlg.rec.Branch = BranchParam.Code;
      dlg.rec.branchname = BranchParam.Name; //неактивно
      DprtCode = {OperDprtNode};
     elif( not BranchParam.GetDepartName_(dlg.rec.Branch) ) 
      dlg.rec.Branch = BranchParam.Code;
      dlg.rec.Branchname = BranchParam.Name;
      DprtCode = BranchParam.Id;
     else
      MsgBox("Не найдено подразделение с номером " + dlg.rec.Branch);
      return CM_CANCEL;
     end;
    end;
    UpdateFields(dlg); 
  end;

  if (cmd == DLG_KEY)
    if (KEY == KEY_ESC)
      return exit(1);//CM_CANCEL;
    elif ( KEY == KEY_F3)
     if (FldName(dlg,id) == "dep")
       mnu = Menu(Rep_Mode,"Режим выпуска отчета");
       RepModeFlag = mnu;
       if (mnu >= 0)
         dlg.rec.dep = Rep_Mode(RepModeFlag);
       end;
       if ( RepModeFlag <= 0 )
        BranchParam.GetDepartName({OperDprtNode});
        dlg.rec.Branch = BranchParam.Code;
        dlg.rec.branchname = BranchParam.Name; //неактивно
        DprtCode = {OperDprtNode};
        UpdateFields(dlg);
       end;
     end;
     if (FldName(dlg,id) == "DateBegin")
       dlg.rec.DateBegin = GetDateByCalendar ({curDate});
     end;
     if (FldName(dlg,id) == "DateEnd")
       dlg.rec.DateEnd = GetDateByCalendar ({curDate});
     end;
     if ((FldName(dlg,id) == "Branch") and (RepModeFlag > 0))
      if (ListDepartment (Department))
       DprtCode = department.code;
       BranchParam.GetDepartName(department.code);
       dlg.rec.Branch = BranchParam.Code;
       dlg.rec.Branchname = BranchParam.Name;
       message(" ~F3~ Список подразделений "+const_mess);
       UpdateFields(dlg);
      end;
     end;
    elif (KEY == KEY_SPACE)
     if (FldName(dlg,id) == "Flag") 
      if (dlg.rec.flag == "")
       dlg.rec.flag = "X";
       UpdateFields(dlg);
      else
       dlg.rec.flag="";
       UpdateFields(dlg);
      end;
     end;
    elif (( KEY == KEY_F2 ))        //Проверки при вводе
     if ( dlg.rec.DateBegin > {curdate} )
       MsgBox("Дата начала периода не может быть больше даты последнего открытого операционного дня");
       return CM_IGNORE;
     end;
     if ( dlg.rec.DateEnd > {curdate} )
       MsgBox("Дата конца периода не может быть больше даты последнего открытого операционного дня");
       return CM_IGNORE;
     end;
     if ( dlg.rec.DateBegin > dlg.rec.DateEnd )
       MsgBox("Дата начала периода не может быть больше даты конца периода");
       return CM_IGNORE;
     end;
     DateBegin  = dlg.rec.DateBegin;
     if (DateBegin == "")
      DateBegin = "01.01.0001";
     end;  
     DateEnd  = dlg.rec.DateEnd;
     Flag = dlg.rec.Flag;
     if( not BranchParam.GetDepartName_(dlg.rec.Branch) ) 
     else
      MsgBox("Не найдено подразделение с номером " + dlg.rec.Branch);
      return CM_IGNORE;
     end;

     Return CM_SAVE;
    end;     
  end;
 end;

//private macro total
 if (RunDialog(dlgRep, "Event"))                  
  MakeRep();
 end;
END;


