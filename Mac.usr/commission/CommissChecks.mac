/*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++


   joy Жаворонкова Н.

   Имя файла: ComissChecks.mac

   Создан:    26.03.2014

   Описание:  Макрос различных проверок по комиссиям: текущие статусы, смена статусов и т.д. (чтобы не загружать CommissLib)
              Создан по заявке R-338686 На ДО счетов СКС все периодические комиссии должны быть в состоянии "Не начисляется"
              
   ChangeLog:

+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/



IMPORT  CTInter, ctinter,  Cb_SQL,  fg_Life_parm, RsbDataSet, globals, RSBError;


 private var LifeBank = fg_life_subject({OurBank});
 
 
 
 MACRO GetSQLForSKS (SQLType);
    
    var sql;
    var str1 = " ";
    var str2 = " ";
    var exceptCom;
    CONST ExcComGeb = "1095, 1096, 1097"; // Номера комиссий-исключений для ГЭБа
    

    if (StrUpr (SQLType) == "UPDATE");
        str1 = "Update dsfconcom_dbt set t_status = 1 where t_id in ( select com.t_id";
        str2 = " )";
    elif (StrUpr(SQLType) == "REPORT");
        str1 = "select   contr.t_object, cs.t_code, com.t_id, com.t_commnumber";
    else
        msgbox ("Ошибка выполнения");
        return 0;
    end;
    
    if ( LifeBank.is_GEB )
        exceptCom = " and com.t_commnumber not in (" + ExcComGeb + ") "; // эти комиссии на СКС могут начисляться
    elif (LifeBank.is_VUZ)
        exceptCom = " "; 
    else
        msgbox ("Данная проверка реализована только для банокв ГЭБ и ВУЗ");
        return 1;
    end;
    
    sql =  str1 +
       "   from   dsfconcom_dbt com, dsfcontr_dbt contr, daccount_dbt ac, dsfcomiss_dbt cs                              " +
       "   where   contr.t_id = com.t_objectid                                                                          " +
       "         and contr.t_dateclose = to_date ('01.01.0001', 'dd.mm.yyyy')                                           " +
       "         and com.t_objecttype = 659                                                                             " +
       "         and com.t_feetype = 1                                                                                  " +
       "         and com.t_status = 0                                                                                   " +
       "         and com.t_dateend = to_date ('01.01.0001','dd.mm.yyyy')                                                " +
       "         and com.t_commnumber = cs.t_number                                                                     " +
       "         and cs.t_feetype = 1                                                                                   " +
       "         and ac.t_account = contr.t_object                                                                      " +
       "         and (substr (ac.t_account, 1, 3) between '405' and '407' or substr (ac.t_account, 1, 5) in ('40802', '40807'))  " +
       "         and (substr (ac.t_account, 11, 1) = '9' or instr (ac.t_usertypeaccount, 'D') > 0)                      " +
       "         and instr (ac.t_usertypeaccount, 'H') = 0                                                              " +
       "         and ac.t_open_close != 'З'                                                                             " +
       "         and instr (ac.t_type_account, 'П') = 0  " + exceptCom + str2 ;
    return sql;
 END;
 
 
 
 
 MACRO CheckSKSPeriodCom ();
        
    var sql, cmd, rs;
    Var path, hh, mm, ss, er;
    private var Counter = 0;
    GetRegistryValue ("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, path, er );
    
    
    sql =  GetSQLForSKS ("RePort");
    if (sql != 1) // равно 1 - значит в этом банке проверка не реализована. просто выходим.
        cmd = rsdcommand (sql);
        rs = rsdRecordSet( cmd );
    
        TimeSplit (time, hh, mm, ss);
        var Fname = MergeFile ( path,"CommissSKS_log_" + {oper} + "_" + hh + "_" + mm + "_" + ss);
        setOutput(FName, true);
    
        println ("По следующим ДО указанные комиссии будут переведены в статус НЕ НАЧИСЛЯЕТСЯ (Для продолжения нажмите ESC) :");
        println (" ");
 
        while (rs and rs.moveNext)
            Counter = Counter + 1;
            println (Counter, "  ", rs.value(0), "  ", rs.value(1), "  ");
        
        end;
    
        if (Counter > 0) // Если обнаружены записи, где надо сменить статус
            SetOutput (NULL, true);
            viewfile(FName);
            if (msgBoxEx ("Переключить статус комиссий?", MB_YES+MB_NO) == IND_YES);
                sql =  GetSQLForSKS ("Update");
                cmd = rsdcommand (sql);
                rs = cmd.Execute;
                msgBoxeX ("Выполнено", MB_OK);
          
            else
                msgBoxex ("Отменено пользователем");
            end;
        else
            println ("Счетов СКС с включенными периодическими комиссиями не найдено");
            SetOutput (NULL, true);
            viewfile(FName);
        end; 
    else
        return 0;
    end;
        onerror(er)
        msgBoxeX ("Ошибка:" +er.message+", код ошибки:"+er.code+". Переключение не выполнено", MB_OK);

 END;