/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   EVG Макрос пользовательского алгоритма расчёта базовых сумм комиссии 2.1
       "Ведение счетов с использованием системы Интернет-Клиент".

       Тип комиссии - периодическая.
   ChangeLog:

//RR 11.02.2013 Был произведен рефакторинг когда и переход от импорта к ExecMacroFile
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/

Import sfinter, ctinter, currinter, fiinter, cb_sql, rsd;

/*Если при расчете произошла ошибка, поместить в эту переменную ненулевое значение*/
var MacroError :integer = 0;
private var FICode_calc:string = "";

/* Тип величины enum SfBaseTypes{*/
private const   SF_BASETYPE_SUM     = 1;      /* Сумма*/
private const   SF_BASETYPE_QUONT   = 2;      /* Количество*/
private const   NATCUR = 0;
private const   UsTypeAcCLB     = "Ё"; //После перехода на ExecMacroFile константу пришлось определять в этом макросе.

record sfbassum( "sfbassum.str" );
record sfcontr( sfcontr );

/*Расчет для периодических комиссий*/
/* EVG Добавил приём структуры sfcontr для вызова из макроса sfacrpay.mac */
 Macro CalcServiceSum( sfcontr_addr/*Договор обслуживания*/, BeginDate/*Начало периода*/, EndDate/*Конец периода*/,
      sAddr /* EVG Какой-то ещё адрес */, sfcontrStruc /*Cтруктура sfcontr*/, _ComNumber /*FIV ID комиссии из sfacrpay*/ )

    var stat;
    var CommQuont = 1;
    var cmd, rsd;
    
    if ( ValType (sfcontr_addr) != V_UNDEF )
       SetBuff( sfcontr, sfcontr_addr );
    else
       Copy ( sfcontr, sfcontrStruc );
    end;
    /*Заменил проверку на наличие записи клиента в И-К на то что было в 7.2.0*/
    if (ExecMacroFile ("Commisslib.mac", "CheckUserField", sfcontr.PartyID, UsTypeAcCLB))
       CommQuont = 0;
    end;

    /*FIV Добавил проверку на то, что если CommQuont == 0, то клиент не подключен к И-К, то дальше можно не проверять*/           
    if ((CommQuont != 0) and (ExecMacroFile ("Commisslib.mac", "CheckRestricted", sfcontr) or
                              ExecMacroFile ("Commisslib.mac", "CheckArrested", EndDate, sfcontr) or
                              ExecMacroFile ("Commisslib.mac", "CheckIndex2", EndDate, sfcontr) or
                              ExecMacroFile ("Commisslib.mac", "CheckRest", BeginDate, EndDate, sfcontr, _ComNumber)
                              )
        ) 
       CommQuont = 0;
     end;
         
    /* EVG При вызове из sfacrpay.mac возвращаем true в случае положительного результата расчёта */
    if ( ValType (sfcontrStruc) != V_UNDEF )
       if (CommQuont > 0)
          return true;
       end;
       return false;
    end;

    ClearRecord(sfbassum);

    sfbassum.baseType = SF_BASETYPE_QUONT;
    sfbassum.baseQuont  = CommQuont;

    sfbassum.baseType2 = SF_BASETYPE_QUONT;
    sfbassum.baseQuont2  = CommQuont;

    stat = InsertSumList(sfbassum);

    if( stat )
      MsgBox("Ошибка при вставке базовой суммы");
    end;

END;/*CalcServiceSum*/

/*Печать заголовка подробного отчета о расчете периодической комиссии*/
macro CalcReportPrintHeader( sfrepdet )
    FICode_calc = "";
[----------------------------------------------------------------------------------------------------------------------];
[|             Сумма                    |             Ставка                   |              Итого                   |];
[|                                      |                                      |                                      |];
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintHeader*/

/*Печать строчки отчета подробного отчета о расчете периодической комиссии*/
macro CalcReportPrintLine( sfrepdet )
    var FICode_base, FICOde_tariff:string;
    file fininstr( fininstr )key 0;
    
    if(sfrepdet.baseType == SF_BASETYPE_SUM )
        fininstr.FIID = sfrepdet.FIID_baseSum;
        if( not GetEQ(fininstr))
            MsgBox("Не найдена валюта ", sfrepdet.FIID_baseSum );
            return 1;
        end;
        FICode_base = fininstr.FI_code;
    else
        FICode_base = "";
    end;

    fininstr.FIID = sfrepdet.FIID_tariff;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_tariff );
        return 1;
    end;
    FICOde_tariff = fininstr.FI_code;

    fininstr.FIID = sfrepdet.FIID_CalcSum;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_CalcSum );
        return 1;
    end;
    FICode_calc = fininstr.FI_code;


[| ################### ################ | ################### ################ | ################### ################ |]

    ( sfrepdet.BaseSum, FICode_base,
        sfrepdet.tariff, FICOde_tariff,
        sfrepdet.CalcSum, FICode_calc );

    return 0;
end;/*CalcReportPrintLine*/

/*Печать окончания отчета подробного отчета о расчете периодической комиссии*/
macro CalcReportPrintFooter( sfrepdet, TotalSum )

[----------------------------------------------------------------------------------------------------------------------];
[Итого за период                                                               | ################### ################ |]
(TotalSum, FICode_calc);
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintFooter*/

