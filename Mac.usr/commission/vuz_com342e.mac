/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   EVG Банк ВУЗ

       Макрос пользовательского алгоритма расчёта базовых сумм комиссии 3.4.2
       "Прочие платежи в пользу клиентов (юр. лиц,ИП,физ.лиц в рамках зарплатных 
        проектов, подтвержденных договором) других банков, принятые с 16-00 до 
        18-00 на бумажных носителях".

       Тип комиссии - единовременная.

Изменения:
joy 13.12.2013  Жаворонкова Н. С-25038 Исключаем платежи с происхождением "Счета на оплату"

++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/

import sfinter, ctinter, cb_sql;
import VuzComLib;

/*
    Если при расчете произошла ошибка, поместить в эту переменную ненулевое значение
*/
var MacroError :integer = 0;
private var FICode_calc:string = "";

  /* Тип величины
enum SfBaseTypes{*/
private const   SF_BASETYPE_SUM     = 1;      /* Сумма*/
private const   SF_BASETYPE_QUONT   = 2;      /* Количество*/

record sfbassum( "sfbassum.str" );

/* Допустимые номера пачек для комиссии */
var PackNumber = TArray();
    PackNumber(0) = 306;
    // EVG 25/01/2012 Добавлено
    PackNumber(1) = 4306;


/*
    Расчет для единовременных комиссий
*/
/* EVG Добавил приём структуры sfcontr для вызова из макроса sfacrpay.mac */
macro CalcCommissionSum( docKind/*Тип первички*/, Doc/*Буфер первички*/, sfcalcalusr_addr/*Алгоритм расчета*/, sfcontr_addr /*Договор обслуживания*/, beginDate, endDate  )
    record sfclusr( "sfclusr.str" );
    record sfcontr( sfcontr );
    record sfcalcal(sfcalcal);
    record payord(pspayord);

    SetBuff( sfcalcal, sfcalcalusr_addr );
    SetBuff( sfcontr, sfcontr_addr );
    SetBuff( payord, Doc );

    var stat;
    var CommQuont   = 0,
        CommSum     = $0;
    var PaymObj:object;
    var retVal, Attrid, Code, Num;


    PaymObj = RsbPayment( Payord.OrderID );

//RR 10.07.2012 Платежи внутри банков группы комиссией не облагаются
 if (  not IsPayerBankIdInLifeGroup(PaymObj.ReceiverBankID) )
    /* Только для внешних платежей */
    if ( PaymObj.isExternal )

       if ( ( Payord.Origin != CLB_DocOrigin ) and ( Payord.Origin != 3201 )    // Не Клиент-Банк и не Счет на оплату
            and
            ( isProperPack( PaymObj.NumberPack, PackNumber ) )
            and (
                   ( ( VUZ_isAccount_Jur( PaymObj.PayerAccount )        // Плательщик - юр. лицо
                       or VUZ_isAccount_IP( PaymObj.PayerAccount ) )    // Плательщик - ИП
                     and
                     ( VUZ_isAccount_Jur( PaymObj.ReceiverAccount )     // Получатель - юр. лицо
                       or VUZ_isAccount_IP( PaymObj.ReceiverAccount ) ) // Получатель - ИП
                   )
                 or
                   ( ( ( VUZ_isAccount_Jur( PaymObj.PayerAccount )                      // Плательщик - юр. лицо
                         or VUZ_isAccount_IP( PaymObj.PayerAccount ) )                  // Плательщик - ИП
                       and VUZ_typeAccount_SalaryProject( PaymObj.PayerAccount )        // Польз. тип счета плательщика содержит Z - зарплатный проект
                     )
                     and
                     ( VUZ_isAccount_Phis( PaymObj.ReceiverAccount )                    // Получатель - физ. лицо
                       or VUZ_isAccount_Incomplete( PaymObj.ReceiverAccount ) )         // Получатель - счёт незавершенных расчётов
                   )
                )
          )

          CommSum = PaymObj.BaseAmount;
       end;
       
    end;
    
END;
  
    ClearRecord(sfbassum);

    sfbassum.baseType    = SF_BASETYPE_SUM;
    sfbassum.baseSum     = CommSum;

    sfbassum.baseType2   = SF_BASETYPE_SUM;
    sfbassum.baseSum2    = CommSum;

    stat = InsertSumList(sfbassum);

    if( stat )
      MsgBox("Ошибка при вставке базовой суммы");
    end;

    
end;/*CalcServiceSum*/








/*
    Печать заголовка подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintHeader( sfrepdet )
    FICode_calc = "";
[----------------------------------------------------------------------------------------------------------------------];
[|             Сумма                    |             Ставка                   |              Итого                   |];
[|                                      |                                      |                                      |];
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintHeader*/

/*
    Печать строчки отчета подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintLine( sfrepdet )
    var FICode_base, FICOde_tariff:string;
    file fininstr( fininstr )key 0;
    
    if(sfrepdet.baseType == SF_BASETYPE_SUM )
        fininstr.FIID = sfrepdet.FIID_baseSum;
        if( not GetEQ(fininstr))
            MsgBox("Не найдена валюта ", sfrepdet.FIID_baseSum );
            return 1;
        end;
        FICode_base = fininstr.FI_code;
    else
        FICode_base = "";
    end;

    fininstr.FIID = sfrepdet.FIID_tariff;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_tariff );
        return 1;
    end;
    FICOde_tariff = fininstr.FI_code;

    fininstr.FIID = sfrepdet.FIID_CalcSum;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_CalcSum );
        return 1;
    end;
    FICode_calc = fininstr.FI_code;


[| ################### ################ | ################### ################ | ################### ################ |]

    ( sfrepdet.BaseSum, FICode_base,
        sfrepdet.tariff, FICOde_tariff,
        sfrepdet.CalcSum, FICode_calc );

    return 0;
end;/*CalcReportPrintLine*/

/*
    Печать окончания отчета подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintFooter( sfrepdet, TotalSum )

[----------------------------------------------------------------------------------------------------------------------];
[Итого за период                                                               | ################### ################ |]
(TotalSum, FICode_calc);
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintFooter*/

