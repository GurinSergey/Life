/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   EVG Специфические функции, используемых при расчёте комиссий в банке НБС.

++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/
import CommissLib;


PRIVATE CONST {Tarif 5.2.3 MIN} =       $0.00,
              {Tarif 5.2.3 MAX} =  $600000.00,
              {Tarif 5.2.4 MIN} =  $600000.01,
              {Tarif 5.2.4 MAX} = $1000000.00,
              {Tarif 5.2.5 MIN} = $1000000.01;

CONST TotalCashLimit = $5000000;




MACRO NBS_IsProperCashCommission (Contr, Calcal, Sum)
   var sql, rs;
   var TarifNumber, TarifPlan;

   SQL = "SELECT subStr(com.t_code, 1, 5) cod, pln.t_num num ";
   SQL = SQL + "  FROM dsfcomiss_dbt com, dsfplan_dbt pln, dsfcontrplan_dbt cplan ";
   SQL = SQL + " WHERE com.t_feetype     = " + Calcal.FeeType;
   SQL = SQL + "   AND com.t_number      = " + Calcal.CommNumber;
   SQL = SQL + "   AND cplan.t_sfcontrid = " + Contr.Id;
   SQL = SQL + "   AND cplan.t_begin     = (SELECT MAX (t_begin) ";
   SQL = SQL + "                              FROM dsfcontrplan_dbt ";
   SQL = SQL + "                             WHERE t_sfcontrid = cplan.t_sfcontrid) ";
   SQL = SQL + "   AND pln.t_sfplanid    = cplan.t_sfplanid ";

   rs = rsdRecordSet(SQL);
   if (rs and rs.MoveNext())
      TarifNumber = rs.Value("cod", null, V_STRING);
      TarifPlan   = rs.Value("num", null, V_INTEGER);
   end;


   if   ( (TarifNumber == "5.2.3") )

      return CheckMinMax( Sum, {Tarif 5.2.3 MIN}, {Tarif 5.2.3 MAX});
      
   elif ( (TarifNumber == "5.2.4") )

      return CheckMinMax( Sum, {Tarif 5.2.4 MIN}, {Tarif 5.2.4 MAX});

   elif ( (TarifNumber == "5.2.5") )

      return ( Sum >= {Tarif 5.2.5 MIN} );           // Для тарифа 5.2.5 только минимум.

   end;


   return false;
END;



MACRO NBS_Cash_CalcSymbSum_Period( dKind, CashDoc, CashSymbols)
  var query, rs, rs1, mm, yy;
  var PaymValueDate = date(0,0,0),
      MonthBegDate  = date(0,0,0),
      CashSymbSum   = $0,
      cdocAutokey = 0,
      cdocAppKind = 0,
      cdocAppKey  = "";

      macro AddLeftZero( val, len )

         while ( strLen(val) < len )
            val = "0" + val;
         end;
         return val;

      end;

    
    /* Найдём из платежа дату документа */
    query = " Select paym.t_ValueDate " +
            "   From DPmPaym_dbt paym " +
            "  Where paym.t_DocKind    = " + dKind +
            "    and paym.t_DocumentID = " + CashDoc.AutoKey;
    rs = rsdRecordSet(query);
    if (rs and rs.moveNext())
       PaymValueDate = rs.value(0, null, V_DATE);
    end;

    /* Определим дату начала текущего месяца */
    dateSplit(PaymValueDate, null, mm, yy);
    MonthBegDate = date(1, mm, yy);
    
    
    
    /* Посчитаем общую сумму снятия наличных по указанному символу за текущий месяц 
       (с начала месяца по дату выполнения данной операции). */
    CashSymbSum = $0;

    /* Цикл по кассовым документам со счёта клиента за указанный период */
    query = " Select doc.t_AutoKey AutoKey, doc.t_ConnectAppKind AppKind, doc.t_ConnectAppKey AppKey" +
            "   From DPsCshDoc_dbt doc, DpmPaym_dbt pm " +
            "  Where doc.t_ClientAccount   = " + GetSQLString(CashDoc.ClientAccount) +
            "    and pm.t_PaymentID = doc.t_Autokey " +
            "    and pm.t_DocKind   = doc.t_DocKind " +
            "    and pm.t_ValueDate >= " + GetSQLDate(MonthBegDate) +
            "    and pm.t_ValueDate <= " + GetSQLDate(PaymValueDate);
    rs = rsdRecordSet(query);
    //msgbox ("1   ", query);
    while (rs and rs.moveNext())

       cdocAutokey = SQL_ConvTypeInteger(rs.value("AutoKey", null, V_INTEGER));
       cdocAppKind = SQL_ConvTypeInteger(rs.value("AppKind", null, V_INTEGER));
       cdocAppKey  = SQL_ConvTypeStr(rs.value("AppKey",  null, V_STRING));

       if ( (cdocAppKind == 0) and (cdocAppKey == "") )
          query = " Select nvl(sum(symb.t_Sum), 0) " +
                  "   From DSymbCash_dbt symb " +
                  "  Where trim(symb.t_Symbol)  in " + CashSymbols +
                  "    and symb.t_ApplicationKey = " + GetSQLString( AddLeftZero(cdocAutokey, 34) );
          rs1 = rsdRecordSet(query);
          while (rs1 and rs1.moveNext())
             CashSymbSum = CashSymbSum + rs1.value(0, null, V_MONEY);
          end;
       //msgbox ("2   ", query);
       else
          query = " Select nvl(sum(symb.t_Sum), 0) " +
                  "   From DSymbCash_dbt symb " +
                  "  Where trim(symb.t_Symbol)  in " + CashSymbols +
                  "    and symb.t_ApplicationKey = " + GetSQLString( AddLeftZero(cdocAppKind, 5) +
                                                                                 cdocAppKey );
          rs1 = rsdRecordSet(query);
          while (rs1 and rs1.moveNext())
             CashSymbSum = CashSymbSum + rs1.value(0, null, V_MONEY);
          end;
       //msgbox ("3   ", query);
       end;

    end;

    return CashSymbSum;
END;