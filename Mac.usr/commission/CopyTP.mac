/****

RR Рахмедов Руслан 

26.07.2012

Макрос комирования тарифных планов для спец. счетов 40821*

Имя файла : CopyTP.mac

Особая благодарность : Чеснокову Д., Данилкиву О., Скибо Ю., Котову С.


****/

import Cb_SQL, Rsd, Globals;
import oralib, likepy;

var AccForProc = TArray();
var ComissRSet = TArray();
var i = 0;
var FirstInc    = 0;
var SecondInc   = 0;
var ThirdInc    = 0;
var IncForProgr = 0;
var c = 0;
var l = 0;
var flag; //RR ввод данной переменной делает код мароса "Индусским" сказано zip_z предложено CDS

Class ComissParam(t_objectid, t_feetype, t_commnumber, t_status, t_calcperiodtype, t_calcperiodnum, t_date, t_getsummin, t_summin,
                  t_summax, t_datebegin, t_dateend, t_objecttype, t_id, t_sfplanid, t_isfreeperiod,T_ISINDIVIDUAL)
var objectid = t_objectid;
var feetype = t_feetype;
var commnumber = t_commnumber;
var shtatus = t_status;
var calcperiodtype = t_calcperiodtype;
var calcperiodnum = t_calcperiodnum;
var date = t_date;
var getsummin = t_getsummin;
var summin = t_summin;
var summax = t_summax;
var datebegin = t_datebegin;
var dateend = t_dateend;
var objecttype = t_objecttype;
var id = t_id;
var sfplanid = t_sfplanid;
var isfreeperiod = t_isfreeperiod;
var ISINDIVIDUAL = T_ISINDIVIDUAL;
end;


 Class AccountForProcessing (t_id, t_object, t_partyid)
 var id      = t_id;
 var object  = t_object;
 var partyid = t_partyid
 End;
 
 //RR Процедура вставки комиссий ДО, аналогичных комиссиям привязанным к ДО по валютному счету признаком Х - текущий
  Macro InsertComisstoDO()

 var qwerty,scmd;
  IncForProgr = IncForProgr + 1;
  ThirdInc = 0;
  RslDefCon.BeginTrans();
  while(ThirdInc <= ((ComissRSet.Size) - 2) )

  qwerty = " insert into dsfconcom_dbt "
+"\n           (t_objectid, "
+"\n           t_feetype, "
+"\n           t_commnumber, "
+"\n           t_status, "
+"\n           t_calcperiodtype, "
+"\n           t_calcperiodnum, "
+"\n           t_date, "
+"\n           t_getsummin, "
+"\n           t_summin, "
+"\n           t_summax, "
+"\n           t_datebegin, "
+"\n           t_dateend, "
+"\n           t_objecttype, "
+"\n           t_id, "
+"\n           t_sfplanid, "
+"\n           t_isfreeperiod, "
+"\n           T_ISINDIVIDUAL) "
+"\n             values (?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )";

  scmd = rsdCommand( qwerty );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).objectid );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).feetype );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).commnumber );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).shtatus );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).calcperiodtype );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).calcperiodnum );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).date );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).getsummin );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).summin );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).summax );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).datebegin );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).dateend );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).objecttype );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).id );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).sfplanid); 
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).isfreeperiod );
  scmd.AddParam("", RSDBP_IN,ComissRSet(ThirdInc).ISINDIVIDUAL );
  scmd.execute();

    TestEvent (100);//RR ждем пол секунды(чтобы Ораклу крышку не срывало), да долго, да нудно, но зато работает и не грузит систему
    ThirdInc = ThirdInc + 1;
  end;
  RslDefCon.CommitTrans();
  UseProgress (IncForProgr);
End;
 
//RR Функция поиска спец счетов 40821* у которых нет тарифного плана(t_sfplanid = 0)
//полученные данные записываем в объект класса AccountForProcessing и загоням в массив AccForProc
 Macro SearchContractWithoutTP()
 i = 0;
  var TempSfPlanId, TempContId; 
  var qwerty,cmd, rcmd, lcmd, lrs, rs, rrs;
  var NullDate = "to_date ('01.01.0001', 'dd.mm.yyyy')";
qwerty =   "  select ctr.t_id, ctr.t_object, ctr.t_partyid "
+"\n            from dsfcontr_dbt ctr, dsfcontrplan_dbt dpl "
+"\n           where substr (ctr.t_object, 1, 5) = '40821' "
+"\n                and dpl.t_sfcontrid = ctr.t_id "
+"\n                and  dpl.t_sfplanid = 0 " 
+"\n                and  dpl.t_end = "+NullDate+" " 
+"\n                and ctr.t_dateclose = "+NullDate+" ";
  lcmd = rsdCommand( qwerty );
  lrs  = rsdRecordSet( lcmd );
   while (lrs and lrs.MoveNext())
    AccForProc(FirstInc) = AccountForProcessing(lrs.value(0), lrs.value(1), lrs.value(2));
      FirstInc = FirstInc + 1; 
   end;
   
   if (FirstInc == 0)
     msgbox("Счета для обработки не найдены");
     return 0;
   else
     FirstInc = 0;
   end;
   
   while (FirstInc < AccForProc.Size )
       qwerty =   "  select dpl.t_sfplanid, ctr.t_id  "
       +"\n            from dsfcontr_dbt ctr, dsfcontrplan_dbt dpl "
       +"\n           where substr (ctr.t_object, 1, 5) != '40821' "
       +"\n                and dpl.t_sfcontrid = ctr.t_id "
       +"\n                and dpl.t_end = "+NullDate+" " 
       +"\n                and ctr.t_dateclose = "+NullDate+" "
       +"\n                and ctr.t_partyid = "+AccForProc(FirstInc).partyid+" "
       +"\n                and dpl.t_end = "+NullDate+" order by ctr.t_datebegin  ";
       cmd = rsdCommand( qwerty );
       rs  = rsdRecordSet( cmd );
       /*if(FirstInc == 5)
        break;
        RslDefCon.CommitTrans();
       end;*/
       if (rs and rs.MoveNext())
       
           TempSfPlanId = rs.value(0);
           TempContId   = rs.value(1);

           qwerty =   " delete from  dsfcontrplan_dbt where t_sfcontrid = "+AccForProc(FirstInc).id+" ";
           cmd = rsdCommand( qwerty );
           cmd.execute();
           
           qwerty =   " insert into dsfcontrplan_dbt (T_ID,T_SFCONTRID,T_SFPLANID,T_BEGIN,T_END) values(0,"+AccForProc(FirstInc).id+","+TempSfPlanId+",'"+{curdate}+"', "+NullDate+" ) ";
           cmd = rsdCommand( qwerty );
           cmd.execute();
           SecondInc = 0;
           qwerty = "  select "+AccForProc(FirstInc).id+" id, "
           +"\n             com.t_feetype, "
           +"\n             com.t_commnumber, "
           +"\n             com.t_status, "
           +"\n             com.t_calcperiodtype, "
           +"\n             com.t_calcperiodnum, "
           +"\n             '"+{curdate}+"', "
           +"\n             com.t_getsummin, "
           +"\n             nvl(com.t_summin,0), "
           +"\n             nvl(com.t_summax,0), "
           +"\n             '"+{curdate}+"', "
           +"\n             com.t_dateend, "
           +"\n             com.t_objecttype, "
           +"\n             0, "
           +"\n             com.t_sfplanid, "
           +"\n             com.t_isfreeperiod, "
           +"\n             com.T_ISINDIVIDUAL "
           +"\n      from dsfconcom_dbt com "
           +"\n      where com.t_objectid = "+TempContId+" "
           +"\n         and com.t_sfplanid = "+TempSfPlanId+" and com.t_dateend = "+NullDate+" ";
             rcmd = rsdCommand( qwerty );
             rrs  = rsdRecordSet( rcmd );

             while (rrs and rrs.MoveNext())
              ComissRSet(SecondInc) = ComissParam (rrs.value(0), rrs.value(1), rrs.value(2), rrs.value(3), rrs.value(4), rrs.value(5), rrs.value(6), 
                                                   rrs.value(7), rrs.value(8), rrs.value(9), rrs.value(10), rrs.value(11), rrs.value(12), rrs.value(13),
                                                   rrs.value(14), rrs.value(15), rrs.value(16)); 
              SecondInc = SecondInc + 1;
             end;
             
             qwerty =   " delete from  dsfconcom_dbt where t_objectid = "+AccForProc(FirstInc).id+" ";
             cmd = rsdCommand( qwerty );
             cmd.execute();
             
             InsertComisstoDO();
             
             ComissRSet      = null;         //убиваем массим
             ComissRSet      = TArray;      //пересоздаем структуру
             ComissRSet.Size = 0;
             
             println("Для счета "+AccForProc(FirstInc).object+" успешно скопирован тарифный план");
       else
         println("Для счета "+AccForProc(FirstInc).object+" не найден донор");
       end;
       FirstInc = FirstInc + 1;
    end;
 End;


//RR Точка входа
 If (GetTrue(True, "Процедура скопирует тарифные планы для спец счетов 40821* \n Хотите продолжить?"))
    InitProgress(-1, "Ждите...", "Идет обработка счетов" );
    SearchContractWithoutTP();
    RemProgress();
 else
    return 0;
 End;
    OnError();
    RslDefCon.RollBackTrans();
