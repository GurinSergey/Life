/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
   EVG Макрос пользовательского алгоритма расчёта базовых сумм по комиссии 5.2.5
       "Выдача наличных с оформлением заявки в течение предыдущего опер. дня:
          - прочее (от 600 тыс. до 1 млн.руб.) ".
   Chesnokov D. После введения комиссии 5.2.10, проверяем на то что не установлена
        категория "Вид тарифа комиссии 5.2.10 за выдачу разового кредита,      
        предоставленного банком".
       Тип комиссии - единовременная.
    
    ChangeLog:
    
27-05-2012 Жаворонкова (joy) С-10885 Добавлена разбивка базы комиссии на граничном чеке
//RR 19.02.2013 Провел рефакторинг
//joy 30.01.2014 I-00454531 Перенос модификации от 28.11.2013  C-23416 Добавила парметры true, sfcontr в Cash_CalcSymbSum_Period, чтобы искать чеки по всем счетам клиента
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/
import sfinter, ptinter, cb_sql, OprInter, CTInter;

var MacroError :integer = 0;
private var FICode_calc:string = "";

private const   SF_BASETYPE_SUM     = 1;      /* Сумма*/
private const   SF_BASETYPE_QUONT   = 2;      /* Количество*/

private const NATCUR = 0;
record sfbassum( "sfbassum.str" );
record paym(pmpaym);

/* Символ кассы, по которому берётся данная комиссия */
CONST CommCashSymbol = "('42', '46', '47', '53', '54', '58', '59', '60', '61')";    // прочие и т.п.
/* Номер категории, которая определяет тариф для платежей (Тариф 5.2.10) */
const Tarif5210Number = "5.2.10";
/* ID Категории "Вид тарифа комиссии 5.2.10 за выдачу разового кредита, предоставленного банком" */
const GroupID = 26;

macro CalcCommissionSum( docKind/*Тип первички*/, Doc/*Буфер первички*/, sfcalcalusr_addr/*Алгоритм расчета*/, sfcontr_addr /*Договор обслуживания*/, beginDate, endDate  )
    record sfclusr( "sfclusr.str" );
    record sfcontr( sfcontr );
    record sfcalcal(sfcalcal);
    record cashDoc(pscshdoc);

    var stat:integer; 
    var rs;
    var CommSum   = $0,
        SymbSum   = $0,
        TotalCashSum,
        Num,
        retVal,
        error;

    SetBuff( sfcalcal, sfcalcalusr_addr );
    SetBuff( sfcontr, sfcontr_addr );
    SetBuff( cashDoc, Doc );

    /* Получим значение категории "Вид тарифа комиссии 5.2.10 за выдачу разового кредита, предоставленного банком" */
    Num = "";

    ClearRecord(paym);
    paym.PaymentID = cashDoc.AutoKey;                    // V.6.20: PaymentID = Autokey

    GetMainObjAttr (retVal, OBJTYPE_PAYMENT, UniID(paym, OBJTYPE_PAYMENT), GroupID, null, null, Num);

    // Комиссия по чекам
    if (( docKind == CASH_PS_OUTORDER ) and (Num != Tarif5210Number))

       rs = ExecMacroFile ("Commisslib.mac", "GetComBank52RecordSet", cashDoc.IsCurrency, cashDoc.DocKind, cashDoc.AutoKey, CommCashSymbol);
       while (rs and rs.moveNext())
          SymbSum = SymbSum + rs.value(0, null, V_MONEY);
       end;


       if (ExecMacroFile ("Commisslib.mac", "Cash_IsProperCommission", sfcontr, sfcalcal, SymbSum))
          TotalCashSum = ExecMacroFile ("Commisslib.mac", "Cash_CalcSymbSum_Period", DocKind, cashDoc, CommCashSymbol, true, sfcontr);
          
          if ( TotalCashSum + SymbSum <= $3000000 ) /*Если сумма с начала месяца, вкл. текущий чек, не превышает лимита, то база = сумме текущего чека*/
             CommSum = SymbSum;
          else 
             CommSum = ($3000000 - TotalCashSum); /* Комиссию берем с той части суммы текущего чека, которая осталась до лимита в 3 млн.*/
          end;
       end;

    end;

    ClearRecord(sfbassum);

    sfbassum.baseType  = SF_BASETYPE_SUM;
    sfbassum.baseSum   = CommSum;

    sfbassum.baseType2  = SF_BASETYPE_SUM;
    sfbassum.baseSum2   = CommSum;

    stat = InsertSumList(sfbassum);

    if( stat )
      MsgBox("Ошибка при вставке базовой суммы: ", GetErrMsg );
    end;
end;
