/***********************************************************************************************
Банк "Газэнергобанк" (ГЭБ)

Макрос пользовательского алгоритма расчета базовой суммы по комиссии "За перевод средств 
со счетов клиента Банка на счета в ОАО "Газэнергобанк либо в пользу клиентов других 
банков на бумажных носителях или по системе Интернет-Клиент, направляемых на участие в тендерах 
и аукционах, в том числе электронных"

Пункт 3.11.1 - Сумма до 500 000 руб, комиссия 4%, не менее 10 000 руб 

Тип комиссии - Периодическая

@fname: ..\mac.usr\commission\geb_com3111p.mac
@author: Жаворонкова Н. (joy) C-9117
@changes: 14-03-2012  zip_z. execSQLSelect + оптимизация SQL
          13-04-2012 joy  Добавлено получение тарифа и минимальной суммы комиссии из ДО или из ТП
***********************************************************************************************/


import sfinter, oralib, likepy, cb_sql;
import GebComLib;

/* Тип величины */ 
private const   SF_BASETYPE_SUM     = 1;      /* Сумма*/
private const   SF_BASETYPE_QUONT   = 2;      /* Количество*/


record sfbassum( "sfbassum.str" );
// record paym(pmpaym);

macro CalcServiceSum (sfcontr_addr    /* Договор обслуживания*/, 
                      BeginDate       /* Начало периода*/, 
                      EndDate         /* Конец периода*/,
                      sAddr           /* EVG Какой-то ещё адрес */, 
                      sfcontrStruc    /* Cтруктура sfcontr*/ 
                      )
   record sfcontr( sfcontr );
   var stat;
   var CommQuont   = 0,
       CommSum     = $0;
   var retVal, Attrid, Code, Num, rs, cmd, min_sum_paym;
   
   const MIN_PAYM_AMOUNT = $0;
   const MAX_PAYM_AMOUNT = $500000;
   
   
   if ( valType (sfcontr_addr) != V_UNDEF )
      setBuff( sfcontr, sfcontr_addr );
   else
      copy ( sfcontr, sfcontrStruc );
   end;
   
   var comiss_percent = 0.04;   // 4% по умолчанию из ТЗ
   var comiss_minimal = 10000; // минимальная сумма комиссии по умолчанию из ТЗ
     DebugBreak ();
     
    /* 13-04-2012 joy Получим значение тарифа и min суммы из ДО (scl.t_ObjectType = 659), а если инд. условий нет, из ТП (scl.t_ObjectType = 57) */
      var SQL = " WITH t AS " +
    " (SELECT   NVL (trf.t_TarifSum / 1000000, 0), NVL (trf.t_MinValue / 10000, 0), " +
    "           scl.t_ObjectType " +
    "      FROM DSFTARIF_DBT trf, DSFTARSCL_DBT scl, DSFCOMISS_DBT com, DSFCONTRPLAN_DBT PLAN " +
    "     WHERE com.t_FeeType = 1 " +
    "       AND com.t_Code = '3.11.1 гэб' " +
    "       AND PLAN.t_SfContrID = ? " +
    "       AND PLAN.t_Begin = (SELECT MAX (t_Begin) " +
    "                             FROM DSFCONTRPLAN_DBT " +
    "                            WHERE t_SfContrID = PLAN.t_SfContrID " +
    "                            AND t_Begin <= ?) " +
    "       AND scl.t_FeeType = com.t_FeeType " +
    "       AND scl.t_CommNumber = com.t_Number " +
    "       AND scl.t_BeginDate = " +
    "              (SELECT MAX (t_BeginDate) " +
    "                 FROM DSFTARSCL_DBT " +
    "                WHERE     t_FeeType = com.t_FeeType " +
    "                      AND t_CommNumber = com.t_Number " +
    "                      AND t_BeginDate BETWEEN ? AND ? " +
    "                   OR t_BeginDate = TO_DATE ('01.01.0001', 'dd.mm.yyyy')) " +
    "       AND (   (scl.t_ObjectType = 659 AND scl.t_ObjectID = PLAN.t_SfContrID) " +
    "            OR (scl.t_ObjectType = 57 AND scl.t_ObjectID = PLAN.t_SfPlanID) " +
    "           ) " +
    "       AND trf.t_TarSclID = scl.t_ID " +
    "       AND trf.t_BaseSum = 0 " +
    "  ORDER BY scl.t_ObjectType DESC) " +
    "   SELECT * " +
    "    FROM t " +
    "    WHERE rownum = 1 ";

    cmd = rsdcommand(SQL);
    cmd.AddParam("", RSDBP_IN,sfcontr.ID);
    cmd.AddParam("", RSDBP_IN,EndDate);
    cmd.AddParam("", RSDBP_IN,BeginDate);
    cmd.AddParam("", RSDBP_IN,EndDate);
    rs = rsdRecordSet( cmd );
  
   if (rs and rs.moveNext())
      comiss_percent = rs.value(0, null, V_DOUBLE);
      comiss_minimal = rs.value(1, null, V_DOUBLE);
   end;
   
    rs = null;
    min_sum_paym = comiss_minimal/comiss_percent; // сумма платежа, с которого возьмется минимальная сумма комиссии
/* 13-04-2012 joy end */

   DebugBreak ();
   var query = "SELECT NVL (SUM (comissamount), 0) comissamount "
               "  FROM (SELECT pm.t_paymentid, t_amount, CASE "
               "                  WHEN t_amount < :p_amount1 "  // т.е. если сумма платежа меньше min_sum_paym, то берем min_sum_paym
               "                     THEN :p_amount2 "
               "                  ELSE t_amount "
               "               END comissamount "
               "          FROM dpmpaym_dbt pm, dpmrmprop_dbt rm "
               "         WHERE pm.t_paymentid = rm.t_paymentid "
               "           AND t_valuedate = :p_date "
               "           AND t_fiid = 0 "
               "           AND t_payfiid = 0 "
               "           AND t_basefiid = 0 "
               "           AND t_payeraccount = :p_account "
               "           AND t_dockind = 201 "
               "           AND t_amount <= :p_max_amount "
               "           AND t_paymstatus = 32000 "
               "           AND (   TO_NUMBER (SUBSTR (t_payeraccount, 1, 3)) BETWEEN 405 AND 407 "
               "                OR SUBSTR (t_payeraccount, 1, 5) IN ('40807', '40802')"
               "               ) "
               "           AND (   TO_NUMBER (SUBSTR (t_receiveraccount, 1, 3)) BETWEEN 405 AND 407 "
               "                OR SUBSTR (t_receiveraccount, 1, 5) IN ('40807', '40802') "
               "               ) "
               "           AND REGEXP_LIKE (regexp_replace "
               "                  (LOWER (rm.t_ground), ' +', ' '), "
               "                   'для обеспечения участия|открыт[а-я]+.*аукцион[а-я]+|аукцион[а-я]+.*в.*электронн[а-я]+|по.*электронно[а-я]+.*аукцион+' "
               "                  ))";

    rs = execSQLSelect (query, makeArray (SQLParam ("p_amount1",         min_sum_paym), 
                                             SQLParam ("p_amount2",      min_sum_paym),
                                             SQLParam ("p_date",         endDate),
                                             SQLParam ("p_account",      sfcontr.object),
                                             SQLParam ("p_max_amount",   MAX_PAYM_AMOUNT)));
   

   if (rs.moveNext ())
      CommSum = rs.value("comissamount");
   end;

    /* При вызове из sfacrpay.mac возвращаем true в случае положительного результата расчёта */
   if ( ValType (sfcontrStruc) != V_UNDEF )
      return (CommSum > 0);
   end;
    
   ClearRecord(sfbassum);

   sfbassum.baseType    = SF_BASETYPE_SUM; 
   sfbassum.baseSum     = CommSum;

   sfbassum.baseType2   = SF_BASETYPE_SUM; 
   sfbassum.baseSum2    = CommSum;

   stat = InsertSumList(sfbassum);

   if( stat )
      MsgBox("Ошибка при вставке базовой суммы");
   end;

end;