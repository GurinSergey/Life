/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   EVG Макрос пользовательского алгоритма расчёта базовых сумм комиссии 3.14.1
       "Ежемесячная комиссия за услугу <Постоянное поручение>".

       Тип комиссии - периодическая.

История изменений:

EVG 2/08/2013 Добавлена проверка даты окончания договора на постоянные перечисления.
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/

import sfinter, cb_sql;

/*
    Если при расчете произошла ошибка, поместить в эту переменную ненулевое значение
*/
var MacroError :integer = 0;
private var FICode_calc:string = "";

  /* Тип величины
enum SfBaseTypes{*/
private const   SF_BASETYPE_SUM     = 1;      /* Сумма*/
private const   SF_BASETYPE_QUONT   = 2;      /* Количество*/

record sfbassum( "sfbassum.str" );



/*
    Расчет для периодических комиссий
*/
/* EVG Добавил приём структуры sfcontr для вызова из макроса sfacrpay.mac */
macro CalcServiceSum( sfcontr_addr/*Договор обслуживания*/, BeginDate/*Начало периода*/, EndDate/*Конец периода*/,
      sfcalcal_addr /* EVG Адрес алгоритма (sfcalcal) */, sfcontrStruc /*Cтруктура sfcontr*/, CommNumber_sfacrpay )

    record sfcontr( sfcontr );
    record calcal (sfcalcal);

    var query, rs, stat, cmd;
    var CommNumber, CommQuont = 0;


    debugbreak;
    /* EVG При вызове из sfacrpay.mac используем полученную структуру sfcontrStruc вместо адреса sfcontr_addr */
    if ( ValType (sfcontr_addr) != V_UNDEF )
       SetBuff( sfcontr, sfcontr_addr );
       SetBuff( calcal, sfcalcal_addr );

       CommNumber = calcal.CommNumber;               // CommNumber берём из алгоритма
    else
       Copy ( sfcontr, sfcontrStruc );
       CommNumber = CommNumber_sfacrpay;             // CommNumber берём тот, который пришёл из sfacrpay.mac
    end;



    query = " SELECT COUNT( t_Contr_ID )                                          " +
            "   FROM usr_regular_contr_dbt                                        " +
            "  WHERE t_account             = ?                                    " +
            "    AND t_Contr_Date         <= ?                                    " +
            // EVG 2/08/2013 Проверка на дату окончания договора
            "    AND ( t_Contr_End_Date   >= ?                                    " +
            "          OR t_Contr_End_Date = to_date('01-01-0001','dd-mm-rrrr') ) " +
            "    AND t_Contragent_Acc     <> '40702810700020037446'               ";      // Номер счёта контрагента MyApps
    cmd = RSDCommand ( query );
    cmd.AddParam     ( "account", RSDBP_IN, sfcontr.Object );
    cmd.AddParam     ( "date",    RSDBP_IN, EndDate        );
    cmd.AddParam     ( "date1",   RSDBP_IN, EndDate        );
    rs = RSDRecordset( cmd );

    if( rs and rs.moveNext() )
       CommQuont = rs.value( 0, null, V_INTEGER );
    end;



    /* EVG При вызове из sfacrpay.mac возвращаем true в случае положительного результата расчёта */
    if ( ValType (sfcontrStruc) != V_UNDEF )
       if (CommQuont > $0)
          return true;
       end;
       return false;
    end;

    
    ClearRecord(sfbassum);

    sfbassum.baseType   = SF_BASETYPE_QUONT;
    sfbassum.baseQuont  = CommQuont;

    sfbassum.baseType2  = SF_BASETYPE_QUONT;
    sfbassum.baseQuont2 = CommQuont;

    stat = InsertSumList(sfbassum);

    if( stat )
      MsgBox("Ошибка при вставке базовой суммы");
    end;

    
end;/*CalcServiceSum*/






