/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++ 

RR - Рахмедов Руслан 12.09.2011, без Чеснокова Д. не справился бы!!!!!!!!!!!!!!!!!!!!!!

Комиссия 3.13.2 - Перевод средств со счетов клиентов Банка на счета Физически лиц
(за исключением переводов в рамках зарплатных проектов)
в случае, если общая сумма таких перечислений за текущий календарный месяц составляет 
БОЛЕЕ 5 миллионов рублей в месяц(маслянное масло, но так было написано в ТЗ)
в адрес физлиц других банков(внешние платежи)

За документы Ручного ввода и FineReaderBank

Тип комиссии: Единовременная

Имя файла exv_com3132e.mac

upd RR 18.10.2012 Теперь комиссия берется за документы ABBY FC
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/

import sfinter, ctinter, cb_sql;
import exvComLib;

/*
    Если при расчете произошла ошибка, поместить в эту переменную ненулевое значение
*/
var MacroError :integer = 0;
private var FICode_calc:string = "";

  /* Тип величины
enum SfBaseTypes{*/
private const   SF_BASETYPE_SUM     = 1;      /* Сумма*/
private const   SF_BASETYPE_QUONT   = 2;      /* Количество*/
private const   LimitAmount = $5000000; //лимит перечислений

record sfbassum( "sfbassum.str" );


/*
    Расчет для единовременных комиссий
*/
/* EVG Добавил приём структуры sfcontr для вызова из макроса sfacrpay.mac */
macro CalcCommissionSum( docKind/*Тип первички*/, Doc/*Буфер первички*/, sfcalcalusr_addr/*Алгоритм расчета*/, sfcontr_addr /*Договор обслуживания*/, beginDate, endDate  )
    record sfclusr( "sfclusr.str" );
    record sfcontr( sfcontr );
    record sfcalcal(sfcalcal);
    record payord(pspayord);

    SetBuff( sfcalcal, sfcalcalusr_addr );
    SetBuff( sfcontr, sfcontr_addr );
    SetBuff( payord, Doc );

    var stat;
    var DocAmount = $0,  //сумаа документа
        MonthAmountEXT = $0; //сумма перечислений за месяц
    var CommQuont   = 0,
        CommSum     = $0;
    var PaymObj:object;

    PaymObj = RsbPayment( Payord.OrderID );

     if (PaymObj.isExternal )
           if     (( ( Payord.Origin == HM_DocOrigin ) or ( Payord.Origin == FNB_DocOrigin ) or ( Payord.Origin == ABBY_FC_DocOrigin ) ) //Ручной ввод или FineReaderBank или ABBY FC
                 and
                    (EXV_isAccount_Jur( PaymObj.PayerAccount ))  //Плательщик юр. лицо
                 and
                    ((EXV_isAccount_Phis( PaymObj.ReceiverAccount )) //Получатель физ.лицо
                     or ((EXV_isAccount_Phis1( PaymObj.ReceiverAccount )) //Или получатель 30232*, 47422* 
                          and (EXV_isGround_PhisFace(PaymObj.Ground)))) // И в назначении платеже присутствует пк,карточный счет и.т.д.
                 and
                    (not EXV_typeAccount_SalaryProject( PaymObj.PayerAccount )))  //Счет плательщика не содержит Z(зарплатный проект)
                DocAmount = PaymObj.BaseAmount;
           end;
     end;

    /******************Скомунижжено у Чеснокова*************************/
    /*Определяем глобальные параметры для их дальнейшей передачи*/
MonthAmountEXT = GetGlobalParameter("MonthAmountEXT", false);
    /******************************************************************/


//Логика мать ее
// 2012-02-28 joy  Исправлена ошибка расчета базовой суммы

/* joy begin --
    if ((DocAmount+MonthAmountEXT)<=LimitAmount)
        CommSum = 0
      elif ((DocAmount+MonthAmountEXT)>LimitAmount)
        CommSum = (DocAmount-(LimitAmount-MonthAmountEXT))
      else
        CommSum = 0
    end;
*/

    CommSum = 0;
    if ((DocAmount + MonthAmountEXT) > LimitAmount)
        if ( (LimitAmount - MonthAmountEXT) >= 0 )
            CommSum = DocAmount - (LimitAmount - MonthAmountEXT);
        else
            CommSum = DocAmount;
        end;
    end;
    
/*  ---joy end*/
    
    
    
    
    ClearRecord(sfbassum);

    sfbassum.baseType    = SF_BASETYPE_SUM;
    sfbassum.baseSum     = CommSum;

    sfbassum.baseType2   = SF_BASETYPE_SUM;
    sfbassum.baseSum2    = CommSum;

    stat = InsertSumList(sfbassum);

    if( stat )
      MsgBox("Ошибка при вставке базовой суммы");
    end;

    
end;/*CalcServiceSum*/








/*
    Печать заголовка подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintHeader( sfrepdet )
    FICode_calc = "";
[----------------------------------------------------------------------------------------------------------------------];
[|             Сумма                    |             Ставка                   |              Итого                   |];
[|                                      |                                      |                                      |];
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintHeader*/

/*
    Печать строчки отчета подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintLine( sfrepdet )
    var FICode_base, FICOde_tariff:string;
    file fininstr( fininstr )key 0;
    
    if(sfrepdet.baseType == SF_BASETYPE_SUM )
        fininstr.FIID = sfrepdet.FIID_baseSum;
        if( not GetEQ(fininstr))
            MsgBox("Не найдена валюта ", sfrepdet.FIID_baseSum );
            return 1;
        end;
        FICode_base = fininstr.FI_code;
    else
        FICode_base = "";
    end;

    fininstr.FIID = sfrepdet.FIID_tariff;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_tariff );
        return 1;
    end;
    FICOde_tariff = fininstr.FI_code;

    fininstr.FIID = sfrepdet.FIID_CalcSum;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_CalcSum );
        return 1;
    end;
    FICode_calc = fininstr.FI_code;


[| ################### ################ | ################### ################ | ################### ################ |]

    ( sfrepdet.BaseSum, FICode_base,
        sfrepdet.tariff, FICOde_tariff,
        sfrepdet.CalcSum, FICode_calc );

    return 0;
end;/*CalcReportPrintLine*/

/*
    Печать окончания отчета подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintFooter( sfrepdet, TotalSum )

[----------------------------------------------------------------------------------------------------------------------];
[Итого за период                                                               | ################### ################ |]
(TotalSum, FICode_calc);
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintFooter*/

