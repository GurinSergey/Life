// -------------------------------------------------------------------------------------------------
// @filename: com143p.mac
// @author  : 2013-07-09 Гуцу Е. C-19750
// @desc    : Макрос расчета базовой суммы для довзимания платы за ТК
// @changes : none
//RR 19.02.2014 Адаптирую под 2031(darhdoc_dbt -> dacctrn_dbt)
//joy 14.11.2014 I-00532241 в 2031 обязательно надо проверять, что t_state в проводках = 1
// -------------------------------------------------------------------------------------------------
import sfinter, календарь;
import lib_SqlTools;


macro CalcTurnover( account, beginDate, endDate )
    
   startQueryCapture ();
   [SELECT NVL (SUM (pm.t_amount), 0) m_sum
    FROM dacctrn_dbt ad,
         dpmdocs_dbt pd,
         dpmpaym_dbt pm,
         dpmrmprop_dbt rm,
         daccount_dbt ac
   WHERE ad.t_account_payer = :acc
     AND ad.t_date_carry BETWEEN :begdate AND :enddate
     AND ad.t_chapter = 1
     AND ad.t_state = 1
     AND ad.T_ACCTRNID = PD.T_ACCTRNID
     AND pm.t_payeraccount = ac.t_account
     AND ac.t_chapter = 1
     AND ac.t_code_currency = 0
     AND pm.t_paymentid = pd.t_paymentid
     AND pm.t_paymstatus = 32000
     -- AND pm.t_dockind = 201
     -- ((внешний платеж) или (внутренний и перечисление на СНР - прямое или косвенное))
     AND (   pm.t_payerbankid != pm.t_receiverbankid
          OR (    pm.t_payerbankid = pm.t_receiverbankid
              AND (   REGEXP_INSTR (pm.t_receiveraccount, '^(30213|30232)') = 1
                   OR REGEXP_INSTR (pm.t_futurereceiveraccount,
                                    '^(30213|30232)') = 1
                  )
             )
         )
     AND pm.t_paymentid = rm.t_paymentid
     AND rm.t_taxauthorstate = CHR (1) ];
  
   var sql = endQueryCapture ();
   sql = execSqlSelect (sql, makeArray (SqlParam ("acc",     account       ),
                                        SqlParam ("begdate", beginDate     ),
                                        SqlParam ("enddate", endDate       )));
   if (sql.moveNext ())
       return sql.value ("m_sum");
   end;

   return $0;
end;


macro calcServiceSum (sfcontr_addr, beginDate, endDate, sAddr, sfcontrStruc)
    debugbreak ();
    
    private const   SF_BASETYPE_SUM     = 1;      /* Сумма*/
    private const   SF_BASETYPE_QUONT   = 2;      /* Количество*/
    
    record sfcontr (sfcontr);
    record sfbassum ("sfbassum.str");

    var CommSum = $0; 
    var MonthTurnoverSum = $0;
    var mm, yy;

    
    if (valType (sfcontr_addr) != V_UNDEF)
        setBuff (sfcontr, sfcontr_addr);
    else
        copy (sfcontr, sfcontrStruc);
    end;


    /* Посчитаем месячный оборот */
    dateSplit( beginDate, null, mm, yy );
    MonthTurnoverSum = CalcTurnover( sfcontr.Object, date( 1, mm, yy ), endDate );

    /* Попробуем рассчитывать эту комиссию как процент от месячного оборота.
       Условия по ставке в зависимости от месячного оборота и собственно ставки заведены в тарифах комиссии 1.4.3. */
    CommSum = MonthTurnoverSum;


    if ( valType (sfcontrStruc) != V_UNDEF )
        return (CommSum > 0);
    end;
    
    sfbassum.baseType      = SF_BASETYPE_SUM;
    sfbassum.baseSum       = CommSum;
    sfbassum.FIID_baseSum  = 0;
    
    sfbassum.baseType2     = SF_BASETYPE_SUM;
    sfbassum.baseSum2      = CommSum;
    sfbassum.FIID_baseSum2 = 0;

    if( insertSumList(sfbassum) )
        msgBox("Ошибка при вставке базовой суммы");
    end;
end;
