/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   EVG Банк ВУЗ

       Макрос пользовательского алгоритма расчёта базовых сумм комиссии 5.2.2 
       "Выдача наличных с оформлением заявки от 100001 до 500000 рублей в день".

       Тип комиссии - единовременная.

       EVG Комиссия модифицирована 18.02.2011 в соответствии с приказом № 50
           от "01" февраля 2011 г.

       13.04.2011 Согласно заявки С-234
       CDS Переписан алгоритм комиссии, теперь комиссия считается 
           нарастающим итогом с начала месяца. Так же объявлены глобальные
           параметры сумм, что бы не считать суммы в следующих комиссиях
           5.2.3 и 5.2.4. В комиссии 5.2.4 глобальные параметры разрушаются.
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/
//21.01.2014 C-25244 VV 
import sfinter, ctinter, cb_sql;
import VuzComLib;

/*
    Если при расчете произошла ошибка, поместить в эту переменную ненулевое значение
*/
var MacroError :integer = 0;
private var FICode_calc:string = "";

  /* Тип величины
enum SfBaseTypes{*/
private const   SF_BASETYPE_SUM     = 1;      /* Сумма*/
private const   SF_BASETYPE_QUONT   = 2;      /* Количество*/

record sfbassum( "sfbassum.str" );
record paym(pmpaym);


CONST CommLimit_Min = $0,
      CommLimit_Max = $500000;

/*
    Расчет для единовременных комиссий
*/
/* EVG Добавил приём структуры sfcontr для вызова из макроса sfacrpay.mac */
macro CalcCommissionSum( docKind/*Тип первички*/, Doc/*Буфер первички*/, sfcalcalusr_addr/*Алгоритм расчета*/, sfcontr_addr /*Договор обслуживания*/, beginDate, endDate  )
    record sfclusr( "sfclusr.str" );
    record sfcontr( sfcontr );
    record sfcalcal(sfcalcal);
    record cashDoc(pscshdoc);

    SetBuff( sfcalcal, sfcalcalusr_addr );
    SetBuff( sfcontr, sfcontr_addr );
    SetBuff( cashDoc, Doc );

    var stat, DrawSum = $0, PaymentSum = $0;
    var CommQuont   = 0,
        CommSum     = $0;
    var query, rs, sql, cmd,TPNumber, NewAlg;
    const NumberTP1 = 107; // Тарифные планы для которых используется отдельный алгоритм.
    const NumberTP2 = 108; // Тарифные планы для которых используется отдельный алгоритм.
    debugbreak;
    rs    = null;
    sql   = null;
    cmd   = null;
    /*Проверим к какому ТП привязан ДО*/
    sql = " select d.t_sfplanid " +
          "   from dsfcontrplan_dbt d " +
          "  where d.t_sfcontrid = ? and t_end = to_date ( '01.01.0001', 'dd.mm.yyyy' )";
    cmd = rsdcommand(sql);
    cmd.AddParam("", RSDBP_IN,sfcontr.id); //Id ДО
    rs = rsdRecordSet(cmd);
       
    if (rs and rs.moveNext() )
       TPNumber = rs.value("t_sfplanid");
    else
        MsgBox("На нейден Тарифный план для договора обслуживания " + sfcontr.object);
    end;

    //RR Определяем по какому алгоритму будем считать.
 if ( (TPNumber == NumberTP1) or (TPNumber == NumberTP2) )
    NewAlg = true;
 end;
 
    var {AccExist>1Yr} = false,
        DrawSumPM    = $0; // Сумма по символам платежа не на зарплату

    {AccExist>1Yr} = ( ( endDate - GetAccountOpenDate( sfcontr.Object ) ) > 365 );
    
    if ( {AccExist>1Yr} )
      DrawSumPM     = Vuz_SumSymb_Payment( cashDoc.Autokey, 
                                              DRDIR_Other );

      PaymentSum  = Vuz_SumSymb_StartMonth( sfcontr.Object,
                                            endDate,
                                            cashDoc.DocKind,
                                            DRDIR_Other,
                                            NewAlg);
                                          
      /* Комиссия берётся в случае:
         - если счёт клиента открыт менее года;
           или
         - если наличные берутся не на з/п. */
      /* Если счёт клиента открыт менее года, то в дальнейшем на 
         расчёт передаётся вся сумма документа */
    else
      DrawSumPM   = Vuz_SumSymb_Payment( cashDoc.Autokey, 
                                              DRDIR_NOT );
                                               
      PaymentSum  =  Vuz_SumSymb_StartMonth( sfcontr.Object,
                                             endDate,
                                             cashDoc.DocKind,
                                             DRDIR_NOT,
                                             NewAlg);
    end;
    
    DrawSum = DrawSumPM + PaymentSum;
    /*******************************************/
    /*Определяем глобальные параметры для их дальнейшей передачи*/
    SetGlobalParameter("DrawSum", DrawSum);
    SetGlobalParameter("DrawSumPM", DrawSumPM);
    SetGlobalParameter("PaymentSum", PaymentSum);
    /*******************************************/

    if ( DrawSumPM > $0 )
       /* EVG Комиссия взимается прогрессивно: 
          - сумма базы рассчитывается как общая сумма снятия, превышающая нижнюю границу лимита
          - если сумма базы больше лимита, комиссия взимается с суммы верхней границы лимита. */
       if (PaymentSum <= CommLimit_Max)
         if(PaymentSum == $0)
           if(DrawSumPM <= CommLimit_Max)
             CommSum = DrawSumPM;
           else
             CommSum = CommLimit_Max;
           end;
         else
           if(DrawSum <= CommLimit_Max)
             CommSum = DrawSumPM;
           else
             CommSum = CommLimit_Max - PaymentSum;
           end;
         end;
       else
         CommSum = $0;
       end;
    end;
    
//VV C-25244   
    var  quer="SELECT   CAT.T_OBJECT "+
            "FROM   dobjatcor_dbt cat "+
            "WHERE   CAT.T_GROUPID = 1023 "+
            " AND CAT.T_OBJECTTYPE = 501 "+
            " and CAT.T_OBJECT=" +cashDoc.AutoKey ;
    var  qrs=TRsbDataSet(quer);

     if(qrs.movenext)
     debugbreak;
       CommSum=$0;
     end;  
//VV C-25244 

    ClearRecord(sfbassum);

    sfbassum.baseType    = SF_BASETYPE_SUM;
    sfbassum.baseSum     = CommSum;

    sfbassum.baseType2   = SF_BASETYPE_SUM;
    sfbassum.baseSum2    = CommSum;

    stat = InsertSumList(sfbassum);

    if( stat )
      MsgBox("Ошибка при вставке базовой суммы");
    end;

    
end;/*CalcServiceSum*/


/*
    Печать заголовка подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintHeader( sfrepdet )
    FICode_calc = "";
[----------------------------------------------------------------------------------------------------------------------];
[|             Сумма                    |             Ставка                   |              Итого                   |];
[|                                      |                                      |                                      |];
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintHeader*/

/*
    Печать строчки отчета подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintLine( sfrepdet )
    var FICode_base, FICOde_tariff:string;
    file fininstr( fininstr )key 0;
    
    if(sfrepdet.baseType == SF_BASETYPE_SUM )
        fininstr.FIID = sfrepdet.FIID_baseSum;
        if( not GetEQ(fininstr))
            MsgBox("Не найдена валюта ", sfrepdet.FIID_baseSum );
            return 1;
        end;
        FICode_base = fininstr.FI_code;
    else
        FICode_base = "";
    end;

    fininstr.FIID = sfrepdet.FIID_tariff;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_tariff );
        return 1;
    end;
    FICOde_tariff = fininstr.FI_code;

    fininstr.FIID = sfrepdet.FIID_CalcSum;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_CalcSum );
        return 1;
    end;
    FICode_calc = fininstr.FI_code;


[| ################### ################ | ################### ################ | ################### ################ |]

    ( sfrepdet.BaseSum, FICode_base,
        sfrepdet.tariff, FICOde_tariff,
        sfrepdet.CalcSum, FICode_calc );

    return 0;
end;/*CalcReportPrintLine*/

/*
    Печать окончания отчета подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintFooter( sfrepdet, TotalSum )

[----------------------------------------------------------------------------------------------------------------------];
[Итого за период                                                               | ################### ################ |]
(TotalSum, FICode_calc);
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintFooter*/

