/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
   CDS Макрос пользовательского алгоритма расчёта базовых сумм по комиссиям 
       5.5.6 "Прием и зачисление наличных средств на счет клиента от 
              реализации платных услуг (кассовый символ 11)  "

       Тип комиссии - единовременная.

       Комиссия 5.5.6 взимается в случае, если платёж не помечен признаком "Тариф 5.5.1"
       категории "Вид тарифа комиссии за прием и зачисление наличных (5.5)". Если 
       признак категории на платеже присутствует данная комиссия не взимается.
//RR 19.02.2013 Провел рефакторинг, перенес все проверки в ComissLib
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/
import sfinter, ptinter, OprInter, CTInter, BankInter;

var MacroError :integer = 0;
private var FICode_calc:string = "";

private const   SF_BASETYPE_SUM     = 1;      /* Сумма*/
private const   SF_BASETYPE_QUONT   = 2;      /* Количество*/

private const NATCUR = 0;
  record sfbassum( "sfbassum.str" );
  record paym(pmpaym);
  
/* ID Категории "Вид тарифа комиссии за прием и зачисление наличных (5.5)" */
const GroupID = 20;

/* Номер категории, которая определяет тариф для платежей
   (Тариф 5.5.1) */
const Tarif551Number = "5.5.1";
/* Исключаемый символ*/
const CommCashSymbol = "('11')";

macro CalcCommissionSum( docKind/*Тип первички*/, Doc/*Буфер первички*/, sfcalcalusr_addr/*Алгоритм расчета*/, sfcontr_addr /*Договор обслуживания*/, beginDate, endDate  )
    record sfclusr( "sfclusr.str" );
    record sfcontr( sfcontr );
    record sfcalcal(sfcalcal);
    record pscshdoc(pscshdoc); // Определяем буфер первички

    var stat:integer; 
    var CommQuont = 0,
        CommSum   = $0,
        num,
        rs,
        retVal,
        error;

    SetBuff( sfcalcal, sfcalcalusr_addr );
    SetBuff( sfcontr, sfcontr_addr );
    SetBuff( pscshdoc, Doc ); // Заполняем буфер первички

    // Комиссия по ОВН
    if ( docKind == CASH_PS_INCORDER )

       /* Получим значение категории "Вид тарифа комиссии за перевод средств" */
       Num = "";

       ClearRecord(paym);
       paym.PaymentID = pscshdoc.Autokey;                    // V.6.20: PaymentID = Autokey

       GetMainObjAttr (retVal, OBJTYPE_PAYMENT, UniID(paym, OBJTYPE_PAYMENT), GroupID, null, null, Num);

       /* Если категория не установлена то взымается */
       if (Num != Tarif551Number)
          rs = ExecMacroFile ("Commisslib.mac", "Get55RecordSet", docKind, pscshdoc.Autokey, false, CommCashSymbol);
          /* Только по рублям */
          if (rs.MoveNext())
             if (rs.value(1, null, V_INTEGER) == 0)
                CommSum = rs.value(0, null, V_MONEY);
             end;
          end;
        end;
    end;

    ClearRecord(sfbassum);

    sfbassum.baseType  = SF_BASETYPE_SUM;
    sfbassum.baseSum   = CommSum;

    sfbassum.baseType2 = SF_BASETYPE_SUM;
    sfbassum.baseSum2  = CommSum;

    stat = InsertSumList(sfbassum);

    if( stat )
      MsgBox("Ошибка при вставке базовой суммы: ", GetErrMsg );
    end;
end;