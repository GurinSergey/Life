/*++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   EVG Банк ЭВ

       Макрос пользовательского алгоритма расчёта базовых сумм комиссии 2.2.0
       "Единая комиссия за ведение счетов клиентов".

       Тип комиссии - периодическая.
_______________________________________________________________________________
Изменения:
        joy 25.02.2013 Жаворонкова Н. I-00330596-2 Добавлена проверка CheckCatOnAcc - 
                       на наличии категории авансовых комиссий 
        joy 22.03.2013 Жаворонкова Н.С-18196 В Воронеже теперь 2 ТП Стартовый:
                       № 105(ID129), № 17 (ID206)
        joy 10.09.2013 Жаворонкова Н. С-23141 Добавлена проверка новых авансовых 
                       комиссий 2.2.3 и 2.2.4
        joy 20.03.2015 joy C-36721 Новая проверка регистриации клиента
                        CheckClientRegistration_new
++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/

import sfinter, ctinter, currinter, fiinter, cb_sql, rsd, exvComLib, fg_Life_parm;

/* Если при расчете произошла ошибка, поместить в эту переменную ненулевое значение*/
var MacroError :integer = 0;
private var FICode_calc:string = "";

  /* Тип величины enum SfBaseTypes{*/
private const   SF_BASETYPE_SUM     = 1;      /* Сумма*/
private const   SF_BASETYPE_QUONT   = 2;      /* Количество*/

private const NATCUR = 0;
private const fgBank = fg_life_subject({OurBank});


record sfbassum( "sfbassum.str" );
record sfcontr( sfcontr );

/* EVG 27/08/2012 id тарифного плана, по которому  будет проверяться дата регистрации и дата, с которой   начинает действовать эта проверка. */
var CheckRegDate_Plan = "129";                        // ТР № 105 "Стартовый"
const CheckRegDate_Date = date(1,8,2012);               // 1.08.2012

if (fgBank.is_EXV_Voronezh())
    CheckRegDate_Plan = "129|206";
end;

/* Расчет для периодических комиссий */
/* EVG Добавил приём структуры sfcontr для вызова из макроса sfacrpay.mac */
macro CalcServiceSum( sfcontr_addr/*Договор обслуживания*/, BeginDate/*Начало периода*/, EndDate/*Конец периода*/,
      sAddr /* EVG Какой-то ещё адрес */, sfcontrStruc /*Cтруктура sfcontr*/, _ComNumber /*FIV ID комиссии из sfacrpay*/ )

    var stat, rrstat;
    var CommQuont = 1,
        flg       = true;

    /* EVG При вызове из sfacrpay.mac используем полученную структуру sfcontrStruc вместо адреса sfcontr_addr */
    if ( ValType (sfcontr_addr) != V_UNDEF )
       SetBuff( sfcontr, sfcontr_addr );
    else
       Copy ( sfcontr, sfcontrStruc );
    end;
    
    rrstat = ExecMacroFile ("Commisslib.mac", "CheckCatOnAcc", sfcontr.PARTYID);

    if ((rrstat == 1) or (rrstat == 3) or (rrstat == 4) or (rrstat == 222) or (rrstat == 223) or (rrstat == 224)) /*10.09.2013 joy C-23141*/
        flg       = false;
        CommQuont = 0;
    end;
    /*19.03.2015 joy C-36721 Новая проверка регистриации клиента*/
    if (flg)
        if ( /* CheckClientRegistration */ CheckClientRegistration_new( EndDate, sfcontr, CheckRegDate_Plan, CheckRegDate_Date ) or           // EVG 27/08/2012 добавлена проверка на дату регистрации клиента
            EXV_CheckRestricted( sfcontr ) or
            CheckArrested( EndDate, sfcontr ) or
            CheckIndex2( EndDate, sfcontr ) or
            CheckRest( BeginDate, EndDate, sfcontr, _ComNumber ) or
            EXV_CheckPartArrestClaim( sfcontr )
           )
            CommQuont = 0;
        end;
    end;
    //msgbox  (CommQuont, "|", EXV_CheckRestricted( sfcontr ), " - ", CheckArrested(EndDate, sfcontr), " - ", CheckIndex2(EndDate, sfcontr), " - ", CheckRest(BeginDate, EndDate, sfcontr), " - ", EXV_CheckPartArrestClaim( sfcontr ));
    
    /* EVG При вызове из sfacrpay.mac возвращаем true в случае положительного результата расчёта */
    if ( ValType (sfcontrStruc) != V_UNDEF )
       if (CommQuont > 0)
          return true;
       end;
       return false;
    end;

    
    ClearRecord(sfbassum);

    sfbassum.baseType = SF_BASETYPE_QUONT;
    sfbassum.baseQuont  = CommQuont;

    sfbassum.baseType2 = SF_BASETYPE_QUONT;
    sfbassum.baseQuont2  = CommQuont;

    stat = InsertSumList(sfbassum);

    if( stat )
        MsgBox("Ошибка при вставке базовой суммы");
    end;

    
end;/*CalcServiceSum*/








/*
    Печать заголовка подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintHeader( sfrepdet )
    FICode_calc = "";
[----------------------------------------------------------------------------------------------------------------------];
[|             Сумма                    |             Ставка                   |              Итого                   |];
[|                                      |                                      |                                      |];
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintHeader*/

/*
    Печать строчки отчета подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintLine( sfrepdet )
    var FICode_base, FICOde_tariff:string;
    file fininstr( fininstr )key 0;
    
    if(sfrepdet.baseType == SF_BASETYPE_SUM )
        fininstr.FIID = sfrepdet.FIID_baseSum;
        if( not GetEQ(fininstr))
            MsgBox("Не найдена валюта ", sfrepdet.FIID_baseSum );
            return 1;
        end;
        FICode_base = fininstr.FI_code;
    else
        FICode_base = "";
    end;

    fininstr.FIID = sfrepdet.FIID_tariff;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_tariff );
        return 1;
    end;
    FICOde_tariff = fininstr.FI_code;

    fininstr.FIID = sfrepdet.FIID_CalcSum;
    if( not GetEQ(fininstr))
        MsgBox("Не найдена валюта ", sfrepdet.FIID_CalcSum );
        return 1;
    end;
    FICode_calc = fininstr.FI_code;


[| ################### ################ | ################### ################ | ################### ################ |]

    ( sfrepdet.BaseSum, FICode_base,
        sfrepdet.tariff, FICOde_tariff,
        sfrepdet.CalcSum, FICode_calc );

    return 0;
end;/*CalcReportPrintLine*/

/*
    Печать окончания отчета подробного отчета о расчете периодической комиссии
*/
macro CalcReportPrintFooter( sfrepdet, TotalSum )

[----------------------------------------------------------------------------------------------------------------------];
[Итого за период                                                               | ################### ################ |]
(TotalSum, FICode_calc);
[----------------------------------------------------------------------------------------------------------------------];
    return 0;
end;/*CalcReportPrintFooter*/

