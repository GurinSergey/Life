/****************************************************************************************/
/*  RS-Bank 6.0                                          R-Style Software Lab           */
/*                                                                                      */
/*  File name   : fns_lib.mac                                                           */
/*  Created     : 22.12.2011                                                            */
/*  Programmer  : Chesnokov D. Lavrenov A.                                              */
/*  Description : Макрос вспомогательных процедур для генерации информационных          */
/*                сообщений по 365-П                                                    */
/*  Request     : C-7026                                                                */
/*  Mod.        : 23.12.2011 Переименована процедура AccountExists_RUR                  */
/*              : 29.12.2011 Исправления по заявке I-00136948                           */
/*              : 04.01.2012 Изменена функция ПолучитьВидСчДляСправки                   */
/*                  длинна возвращаемых значений < 25 символов, иначе неформат          */
/*              : 12.01.2012 На основании письма Климахиной от 11.01.2012               */
/*              : 25.01.2012 Gurin S. N. C-8300-6, C-8473-6, C-8440                     */
/*              : 30.01.2012 Chesnokov D. Исправлен формат времени                      */
/*              : 31.01.2012 Зленко М. добавил cutFIO                                   */
/*              : 05.02.2012 Gurin S. N I-00149620-1                                    */ 
/*              : 06.02.2012 Gurin S. N I-00150224-1                                    */ 
/*              : 18.04.2012 Chesnokov D. Добавлена функция                             */ 
/*                ПолучитьИмяВходФайлаПоСообщ для массовой генерации PNO в 2030         */
/*              : 02.05.2012 Chesnokov D.S. Запись в лог по C-10486 функция             */
/*                           INSERT_MES365_LOG_manual                                   */
/*              : 01.04.2012 Golovkin функция ПроверитьНаличиеДепозитныхСчетов          */
/*                           C-18653                                                    */
/*              : 13.12.2013 Chesnokov D.S. По I-00450795 исправил переопреде           */
/*                           ления для 31 патча                                         */
/*              : 10.02.2014 Chesnokov D.S. По запросу C-26943 добавил функцию          */
/*                Is_Ulianovsk_Client.                                                  */
/*              : 14.12.2015 RR Is_Ulianovsk_Client заменена на IsTransferedClient      */
/****************************************************************************************/
import bankinter, rsd, likepy, oralib, ptinter, globals, rsbdataset, FIInter, keycodes;

macro ПроверитьНаличиеДепозитныхСчетов( partyid, req, datein, dateout )

  var cmd,rs;
  var clientName, clientCode, depoAccounts = "", msgText;

  if   ( ( req != null ) and ( req != "" ) )
    req = "Запрос " + req;
  elif (   req == null )
    req = "";
  end;

  if( datein == null )
    datein = {curdate};
  end;

  if( dateout == null )
    dateout = {curdate};
  end;

  cmd = RsdCommand(" SELECT   RS_CODE.T_CODE         CLIENT_CODE , "
                   "          CLIENT.T_NAME          CLIENT_NAME , "
                   "          ACCOUNT.T_ACCOUNT      ACCOUNT     , "
                   "          ACCOUNT.T_NAMEACCOUNT  NAME_ACCOUNT, "
                   "          ACCOUNT.T_OPEN_DATE    OPEN_DATE   , "
                   "          ACCOUNT.T_CLOSE_DATE   CLOSE_DATE    "
                   "   FROM   DACCOUNTS_VIEW  ACCOUNT, "
                   "          DPARTY_DBT      CLIENT , "
                   "          DOBJCODE_DBT    RS_CODE  "
                   "  WHERE       ACCOUNT.T_CLIENT = :PARTYID "
                   "          AND RSB_MASK.COMPARESTRINGWITHMASK ('410-426', ACCOUNT.T_ACCOUNT) = 1 "
                   "          AND INSTR (ACCOUNT.T_TYPE_ACCOUNT, 'П') = 0  "
                   "          AND     ACCOUNT.T_OPEN_DATE  < :END_DATE     " // дата открытия счета должна быть меньше даты окончания периода
                   "          AND (   ACCOUNT.T_CLOSE_DATE > :BEGIN_DATE   " // дата закрытия должна быть больше даты начала периода
                   "               OR ACCOUNT.T_CLOSE_DATE = '01.01.0001') "
                   "          AND ACCOUNT.T_CLIENT = CLIENT.T_PARTYID   "
                   "          AND ACCOUNT.T_CLIENT = RS_CODE.T_OBJECTID "
                   "          AND RS_CODE.T_OBJECTTYPE = 3 "
                   "          AND RS_CODE.T_CODEKIND   = 1 "
                   "          AND RS_CODE.T_STATE      = 0 ");

  cmd.addparam( "partyid"   , RSDBP_IN, partyid );
  cmd.addparam( "end_date"  , RSDBP_IN, dateout ); 
  cmd.addparam( "begin_date", RSDBP_IN, datein  );

  rs = RsdRecordSet( cmd, RSDVAL_CLIENT, RSDVAL_STATIC );

  if( rs.movenext )

    clientName = rs.value( "CLIENT_NAME" );
    clientCode = rs.value( "CLIENT_CODE" );

    rs.moveprev;

    while( rs.movenext )
      depoAccounts = depoAccounts + rs.value( "ACCOUNT" ) + "|";
    end;

    msgText = req                                   + "|" +
              " Клиент "                            + 
              clientCode                            + "|" + 
              clientName                            + "|" +
              " имеет следующие депозитные счета: " + "|" +
              depoAccounts;

    msgbox( msgText );

    return 1;

  end;

  return 0;

  onerror
  return 0;
end;

macro ПолучитьРелизID(TPShemID, NameRLS)
                      
  var select:string = " SELECT mesrls.t_rlsformid " + 
                      "   FROM dwltpshem_dbt shem, dwlmesfrm_dbt mesfrm, dwlmesrls_dbt mesrls " + 
                      "  WHERE shem.t_tpshemid = :TPShemID " + 
                      "    AND shem.t_tpid = mesfrm.t_tpid " + 
                      "    AND mesfrm.t_name = :NameRLS "
                      "    AND mesfrm.t_formid = mesrls.t_formid ";
                      
  var params:TArray = makeArray( SQLParam(":TPShemID", TPShemID ),
                                 SQLParam(":NameRLS",  NameRLS  ));
  var rset:RsdRecordset = execSQLselect( select, params, TRUE );
  
  if( rset and rset.moveNext() )
    return rset.value(0);
  end;
  return -1; 
end;


macro ПолучитьFormID(TPShemID, NameFrm)
                      
  var select:string = " SELECT mesfrm.T_FORMID " + 
                      "   FROM dwltpshem_dbt shem, dwlmesfrm_dbt mesfrm " + 
                      "  WHERE shem.t_tpshemid = :TPShemID " + 
                      "    AND shem.t_tpid = mesfrm.t_tpid " + 
                      "    AND mesfrm.t_name = :NameRLS ";
                      
  var params:TArray = makeArray( SQLParam(":TPShemID", TPShemID ),
                                 SQLParam(":NameFRM",  NameFRM  ));
  var rset:RsdRecordset = execSQLselect( select, params, TRUE );
  
  if( rset and rset.moveNext() )
    return rset.value(0);
  end;
  return -1; 
end;


macro ПолучитьTPSchemID(ReqID)

  var select:string = " SELECT mes.t_tpschemid " + 
                      "   FROM dwlmeslnk_dbt lnk, dwlmes_dbt mes " + 
                      "  WHERE lnk.t_objid = :ReqID " + 
                      "    AND lnk.t_objkind = 505 " + 
                      "    AND lnk.t_direct = 'X' " + 
                      "    AND lnk.t_mesid = mes.t_mesid ";
  var params:TArray = makeArray( SQLParam(":ReqID", ReqID ));
  var rset:RsdRecordset = execSQLselect( select, params, TRUE );
  
  if( rset and rset.moveNext() )
    return rset.value(0);
  end;
  return -1; 
end;

macro ПолучитьИмяТранспорта(tpId)
 var rs,str;
  str = " SELECT t.t_name " +
        "   FROM dwltransp_dbt t " +
        "  WHERE t.t_tpid = " + tpId;
  rs =  trsbdataset(str);
  if(rs and rs.movenext)
     return rs.t_name;
  end;
  return "";
end;

macro ПолучитьИмяФормы(ReqID)

  var select:string = " SELECT mesfrm.t_name " +
                      "   FROM dwlmeslnk_dbt lnk, " +
                      "        dwlmes_dbt mes, " +
                      "        dwlmesrls_dbt mesrls, " +
                      "        dwlmesfrm_dbt mesfrm, " +
                      "        dwltpshem_dbt shem " +
                      "  WHERE lnk.t_objid = :ReqID " +
                      "    AND lnk.t_objkind = 505 " +
                      "    AND lnk.t_mesid = mes.t_mesid " +
                      "    AND mes.t_rlsformid = mesrls.t_rlsformid " +
                      "    AND mesrls.t_formid = mesfrm.t_formid " +
                      "    AND mes.t_tpschemid = shem.t_tpshemid " +
                      "    AND mesfrm.t_tpid = shem.t_tpid";
                      
  var params:TArray = makeArray( SQLParam(":ReqID", ReqID ));
  var rset:RsdRecordset = execSQLselect( select, params, TRUE );
  
  if( rset and rset.moveNext() )
    return rset.value(0);
  end;
  return -1; 
end;


//Получаем ID клиента по его ИНН
macro  GetPartyidByINN(inn,kpp)

  var ptid = -1,error=0;
  var sql, cmd, rs, col = TArray;
  var sqlcount, cmdcount, rscount, count = 0;
  var inn_kpp:string = "";

  /*Обработчик RunScrolla*/
  macro EvProc (rs, cmd, id, key )

    if (cmd == DLG_KEY) 
      if ((key == KEY_ENTER) or (key == KEY_F2))
        return CM_SELECT;
      elif (key == KEY_ESC)
        msgbox("Не выбран клиент. |Выберите клиента. ");
        return CM_IGNORE;
      end;
    end;
  end;

  macro AddCol (ar,ind, fld, head, width, rdonly, DecPoint)
    ar.value (ind * 6)     = fld;
    ar.value (ind * 6 + 1) = head;
    ar.value (ind * 6 + 2) = width;
    ar.value (ind * 6 + 3 ) = 0;   // fldType
    ar.value (ind * 6 + 4 ) = decPoint;  // decPoint
    ar.value (ind * 6 + 5 ) = 0;   // reserv
  end;

  if(strlen(trim(inn)) == 10)
     ptid = ПолучитьКодСубъекта(inn+"/"+kpp, PTCK_INN/*16*/, Error );
     inn_kpp = inn+"/"+kpp;
  else
     ptid = ПолучитьКодСубъекта(inn, PTCK_INN/*16*/, Error );
     inn_kpp = inn;
  end;
   
  if((strlen(trim(inn)) == 10) and (error != 0))
    error = 0;
    ptid = ПолучитьКодСубъекта(inn, PTCK_INN/*16*/, Error );
    inn_kpp = inn;
  end;
  sqlcount  =  " SELECT Count(party.t_partyid) AS count " +
               "   FROM dobjcode_dbt obj, dparty_dbt party " +
               "  WHERE obj.t_code LIKE '" + inn_kpp + "%' " +
               "    AND obj.t_codekind = 16 " +
               "    AND obj.t_state = 0 " +
               "    AND obj.t_objecttype = 3 " +
               "    AND obj.t_objectid = party.t_partyid ";
    
  cmdcount = RSDCommand(sqlcount);
  rscount = RSDRecordSet(cmdcount);

  if (rscount and rscount.Movenext)
    count = rscount.value(0);
  end;

 /*zmp 02.10.2012 I-00261345-2 - Если есть больше одного субъекта экономики
  с одинаковыми кодами выводим их в скроллинг
 */
  if ((error != 0) or (count > 1))     
        
    sql = " SELECT obj.t_code AS code, party.t_name AS name, party.t_partyid AS id " +
          "   FROM dobjcode_dbt obj, dparty_dbt party " +
          "  WHERE obj.t_code LIKE '" + inn_kpp + "%' " +
          "    AND obj.t_codekind = 16 " +
          "    AND obj.t_state = 0 " +
          "    AND obj.t_objecttype = 3 " +
          "    AND obj.t_objectid = party.t_partyid ";
          
    cmd = RSDCommand(sql);
    rs = RSDRecordset( cmd, RSDVAL_CLIENT, RSDVAL_STATIC );

    AddCol (col, 0, "code",    "Код",          22, true);
    AddCol (col, 1, "name",    "Наименование", 40, true);
    AddCol (col, 2, "id",      "ИД субъекта",  10, true);

    if (count > 1)
       if (RunScroll (rs, 3, col, "ListParty", "EvProc", "Выберите клиента", "~Enter~ выбор"))
         ptid = rs.value(2);
       else
         ptid = -1;
       end;
    elif (count == 1)
      if (rs and rs.movenext)
        ptid = rs.value(2);
      else
        ptid = -1;
      end;
    else
      ptid = -1;
    end;
  end;

  return ptid;
end;

macro array_to_str(arr,delim)
   var i = 0, text = "";
   while (i < arr.size)
      text = text + arr[i] + delim;
      i = i+1;
   end;

   text = substr(text, 1, strlen(text)-strlen(delim));
   return text;
end;

macro get_fiid(acc);
   var cmd = RSDCommand("begin ? := usr_common.get_fiid(?); end;");
   cmd.addparam("fiid", RSDBP_RETVAL,V_INTEGER);
   cmd.addparam("acc", RSDBP_IN, acc);
   cmd.execute();

   return cmd.param("fiid").value;
end;

// Клиент, по которому создается сообщение
class ZNSParty( ID:integer )
  var PartyID   :integer;
  var LegalForm :integer;
  var FullName  :string;
  var INN   :string;
  var KPP   :string;

  macro ZNSParty( ID:integer )
    private var PartyObj = RsbParty( ID );
    private var FullInn;
    PartyID    = ID;
    LegalForm  = PartyObj.LegalForm;
    if( LegalForm == PTLEGF_INST )
      FullName =  PartyObj.FullName;
    else
      FullName = string( PartyObj.LastName, ",", PartyObj.FirstName, ",", PartyObj.Patronymic );
    end;
    FullINN    = PartyObj.Code(PTCK_INN);
    splitfullinn(FullInn,INN,KPP);
  end;
  ZNSParty(ID);
end;

macro ToDateStr()
  var strYYYYMMDD = date();
  var YYYY = trim(substr(string(strYYYYMMDD), 7, 4));
  var MM = trim(substr(string(strYYYYMMDD), 4, 2));
  var DD = trim(substr(string(strYYYYMMDD), 1, 2));
  if(StrLen(MM) < 2)
    MM = "0" + MM;
  end;
  if (StrLen(DD) < 2)
    DD = "0" + DD;
  end;
  return string(YYYY +"-"+ MM +"-"+ DD);
end;


// Получить родительский запрос
macro get_parrent_wlreq(id, ret_req)
  var f_req:tbfile = tbfile ( "wlreq", "bank.def" ), 
  
  r_req:TRecHandler = TRecHandler( "wlreq", "bank.def" );
  f_req.rec.reqid = id;
  
  if(f_req.GetEQ())
    copy (r_req, f_req);
    setparm (1, r_req);
    return 0;
  end;
  return 1;
end;

MACRO AccountExists_RUR(acc,chapt)
  var rs, str;
  str = "SELECT t_account " +
        "  FROM daccount_dbt " +
        " WHERE t_account = '"+acc+"' " + 
        " and t_chapter = "+ chapt;

  rs = trsbdataset(str);
  if(rs and rs.movenext)
     return true;
  else
     return false;
  end;
END;

MACRO CutCode(clientid,name,_inn, objid)   //Gurin S. N. 23.01.2012 C-8300-6
  var rs, str;
  var cl_acc_kinds, isfirst, i, errCode ;
  
  GetRegistryValue("PS\\REQOPENACC\\OPERATION\\365-П\\ТИПЫ СЧЕТОВ", V_STRING, cl_acc_kinds, errCode);
  if ( errCode > 0 )
    msgbox("Проверьте наличие настройки: \"PS\\REQOPENACC\\OPERATION\\365-П\\ТИПЫ СЧЕТОВ\"");
    return false;
  end;
  
  str = "SELECT   acc.t_nameaccount, acc.t_inn, inn_t.t_objectid "+
        "FROM   daccount_5_dbt acc,"+
        "      (SELECT   code.t_objectid,"+
        "         CASE"+
        "           WHEN INSTR (code.t_code, '/') = 0"+
        "           THEN"+
        "              code.t_code"+
        "           ELSE"+
        "              SUBSTR (code.t_code,"+
        "                      1,"+
        "                      INSTR (code.t_code, '/') - 1)"+
        "         END"+
        "             AS INN"+
        "       FROM   dobjcode_dbt code"+
        "       WHERE   code.t_objecttype = 3 AND code.t_codekind = 16) inn_t"+
        " WHERE   acc.t_inn IS NOT NULL and acc.t_inn = inn_t.inn"+
        "       AND inn_t.t_objectid ="+clientid +
        "    AND INSTR (acc.t_type_account, 'П') = 0 " +
        "    AND INSTR (acc.t_type_account, 'Н') = 0 " +
        "    AND INSTR (acc.t_type_account, 'М') = 0 " +
        "    AND INSTR (acc.t_type_account, 'U') = 0 ";
        i= 0;
        isfirst = true;
        while (i < strlen(cl_acc_kinds))
            if(isfirst)
                str = str + " AND (instr(acc.t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 "; 
                isfirst = false;
            else 
                if (i+1 != strlen(cl_acc_kinds))
                    str = str + " or instr(acc.t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 " ;
                else
                    str = str + " or instr(acc.t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 )" ;
                end;
            end;
        i = i+1;
        end;  

  rs = trsbdataset(str);

  if(rs and rs.movenext)
    SetParm(1,rs.value("t_nameaccount"));
    SetParm(2,rs.value("t_inn"));
    SetParm(3,rs.value("t_objectid"));
  else
    return 0; 
  end;
END;

Macro GetName(obj_id)   //Gurin S. N. 23.01.2012 C-8300-6
  
  var str1, rs1;
  str1= " SELECT accs.T_CLIENT, accs.t_nameaccount "
        "   FROM daccounts_dbt accs "
        "  WHERE accs.T_CLIENT IN (SELECT PRT.T_PARTYID "
        "                            FROM dparty_dbt prt "
        "                           WHERE PRT.T_PARTYID =" + obj_id +")";
  rs1 = trsbdataset(str1);
  if(rs1 and rs1.movenext)
    return(rs1.value("t_nameaccount"));
  else
    return 0; 
  end;
end;

Macro GenOpenDateAcc(_acc) // Gurin S. N. 02.02.2012
var str2, rs2;
str2=" SELECT t_open_date"
     "   FROM daccounts_dbt"
     "  WHERE t_account = '" + _acc +"'";
  rs2 = trsbdataset(str2);
  debugbreak;
  if (rs2 and rs2.movenext)
     return(rs2.value("t_open_date"))
  else
     return 0;
  end;
end;

MACRO AccountExist(acc,chapt)
  var rs, str;
  str = "SELECT t_account " +
        "  FROM daccounts_dbt " +
        " WHERE t_account = '"+acc+"' " + 
        " and t_chapter = "+ chapt;

  rs = trsbdataset(str);
  if(rs and rs.movenext)
     return true;
  else
     return false;
  end;
END;

/*19.12.2013 Chesnokov D.S. Адаптация для 2031. Счета теперь в 1 таблице, покрытия нет*/
macro GetClientAccounts(clientid, mns_zno)
var select, end_date, cl_acc_kinds, errcode, i, isfirst;
var params;
var name, _inn, objid;
var quest,Error,ErrorMes;

  GetRegistryValue("PS\\REQOPENACC\\OPERATION\\365-П\\ТИПЫ СЧЕТОВ", V_STRING, cl_acc_kinds, errCode);
  if ( errCode > 0 )
    msgbox("Проверьте наличие настройки: \"PS\\REQOPENACC\\OPERATION\\365-П\\ТИПЫ СЧЕТОВ\"");
    return false;
  end;

  if(mns_zno.ВидЗапр == "3")
     end_date = {curdate};
  else
     end_date = date(mns_zno.ДатаЗапр);
  end;
  //Lavrenov: для нового механизма проверки на даты не нужны
  select =  " SELECT t_account, t_code_currency t_fiid, t_chapter, 0 t_state" +
            "   FROM daccount_dbt " +
            "  WHERE t_client = :ClientID1" +
           // zmp 20.05.2014 убираем проверку так как валютные счета не попадают под условие "    AND t_account LIKE '_____810%' " +
            "    AND t_chapter = 1 " +
            "    AND instr(t_type_account,'Н') = 0 " +
            "    AND instr(t_type_account,'М') = 0 " +
            "    AND instr(t_type_account,'U') = 0 " ;
        i= 0;
        isfirst = true;
        while (i < strlen(cl_acc_kinds))
           if(isfirst)
              select = select + " AND (instr(t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 "; 
              isfirst = false;
           else 
              if (i+1 != strlen(cl_acc_kinds))
                 select = select + " or instr(t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 " ;
              else
                 select = select + " or instr(t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 )" ;
              end;
           end;
           i = i+1;
        end;

  //Gurin S. N. 23.01.2012 C-8300-6
  if (mns_zno.ВидЗапр != "3")
     if (CutCode(Clientid,name,_inn,objid) != 0)
        debugbreak;
        if (( name != GetName(objid)) and (name != strupr(GetName(objid))))
           gettrue(quest, string("Наименование в v5:" + name + "|| наименование в v6:" +GetName(objid) +"|| Добавить счета из 5.0 в запрос?" ));
           if(quest == true)  
              select = select +"  UNION ALL"
                 " SELECT t1.t_account,"+
                 " t1.t_code_currency,"+
                 " t1.t_chapter,"+
                 " 0 t_state"+
                 " FROM   daccount_5_dbt t1,"+
                 "      (SELECT   code.t_objectid,"+
                 "         CASE"+
                 "           WHEN INSTR (code.t_code, '/') = 0"+
                 "           THEN"+
                 "              code.t_code"+
                 "           ELSE"+
                 "              SUBSTR (code.t_code,"+
                 "                      1,"+
                 "                      INSTR (code.t_code, '/') - 1)"+
                 "         END"+
                 "             AS INN"+
                 "       FROM   dobjcode_dbt code"+
                 "       WHERE   code.t_objecttype = 3 AND code.t_codekind = 16) inn_t"+
                 " WHERE   t1.t_inn IS NOT NULL and t1.t_inn = inn_t.inn"+
                 "    AND inn_t.t_objectid =" + Clientid+
                 "    AND INSTR (t1.t_type_account, 'П') = 0 " +
                 "    AND INSTR (t1.t_type_account, 'Н') = 0 " +
                 "    AND INSTR (t1.t_type_account, 'М') = 0 " +
                 "    AND INSTR (t1.t_type_account, 'U') = 0 ";
             
              i= 0;
              isfirst = true;
              while (i < strlen(cl_acc_kinds))
                 if(isfirst)
                    select = select + " AND (instr(t1.t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 "; 
                    isfirst = false;
                 else 
                    if (i+1 != strlen(cl_acc_kinds))
                       select = select + " or instr(t1.t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 " ;
                    else
                       select = select + " or instr(t1.t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 )" ;
                    end;
                 end;
                 i = i+1;
              end;
           end;
        else
           select = select +"  UNION ALL"
                 " SELECT t1.t_account,"+
                 " t1.t_code_currency,"+
                 " t1.t_chapter,"+
                 " 0 t_state"+
               " FROM   daccount_5_dbt t1,"+
               "      (SELECT   code.t_objectid,"+
               "         CASE"+
               "           WHEN INSTR (code.t_code, '/') = 0"+
               "           THEN"+
               "              code.t_code"+
               "           ELSE"+
               "              SUBSTR (code.t_code,"+
               "                      1,"+
               "                      INSTR (code.t_code, '/') - 1)"+
               "         END"+
               "             AS INN"+
               "       FROM   dobjcode_dbt code"+
               "       WHERE   code.t_objecttype = 3 AND code.t_codekind = 16) inn_t"+
               " WHERE   t1.t_inn IS NOT NULL and t1.t_inn = inn_t.inn"+
               "    AND inn_t.t_objectid =" + Clientid+
               "    AND INSTR (t1.t_type_account, 'П') = 0 " +
               "    AND INSTR (t1.t_type_account, 'Н') = 0 " +
               "    AND INSTR (t1.t_type_account, 'М') = 0 " +
               "    AND INSTR (t1.t_type_account, 'U') = 0 ";
           
           i= 0;
           isfirst = true;
           while (i < strlen(cl_acc_kinds))
              if(isfirst)
                 select = select + " AND (instr(t1.t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 "; 
                 isfirst = false;
              else 
                 if (i+1 != strlen(cl_acc_kinds))
                    select = select + " or instr(t1.t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 " ;
                 else
                    select = select + " or instr(t1.t_type_account,'"+substr(cl_acc_kinds,i+1,1)+"') != 0 )" ;
                 end;
              end;
              i = i+1;
           end;
        end;
     end;
  end;
    
   params = makeArray( SQLParam("ClientID1", clientid),
                       SQLParam("ClientID2", clientid) );
        

  var rs = execSQLselect( select, params );
  return rs; 
end;

macro ПолучитьКодФинИн_usr(fiid)
 var code;
 code = ПолучитьКодФинИн(fiid);
 
 if(code == "810")
    code = "643";
 end;
 
 return code;
end;

/*   */

macro ПолучитьТекущийЗапрос( sessid )
  var rs, str;
  str = " SELECT req.* " +
        "   FROM dwlmes_dbt mes, dwlmeslnk_dbt lnk, dwlreq_dbt req  " +
        "  WHERE mes.t_sessionid = " + sessid + 
        "    AND lnk.t_mesid = mes.t_mesid " +
        "    AND lnk.t_objkind = 505 " +
        "    AND req.t_reqid = lnk.t_objid ";

  rs = trsbdataset(str);
  if(rs and rs.movenext)
     return rs;
  else
     return null;
  end;
end;

macro ОтрезатьПутьОтИмениФайла(flname)
  var dir, ext, name;
  dir = SplitFile(flName, name, ext);
  return name+ext;
end;

macro ПолучитьИмяВходФайла( reqid )
  var rs, str;
  str = " SELECT ses.t_filename " +
        "   FROM dwlmes_dbt mes, dwlmeslnk_dbt lnk, dwlsess_dbt ses " +
        " WHERE lnk.t_objid = " + reqid +
        "   AND lnk.t_objkind = 505 " +
        "   AND mes.t_mesid = lnk.t_mesid " +
        "   AND ses.t_sessionid = mes.t_sessionid " +
        "   AND ses.t_direct = 'X' " ;

  rs = trsbdataset(str);
  if(rs and rs.movenext)
     return rs.t_filename;
  else
     return "";
  end;

end;

macro ПолучитьИмяВходФайлаПоСообщ( mes_id )
  var rs, str;
  str = " SELECT ses.t_filename " +
        "   FROM dwlmes_dbt mes, dwlsess_dbt ses " +
        " WHERE mes.t_mesid = " + mes_id +
        "   AND ses.t_sessionid = mes.t_sessionid " +
        "   AND ses.t_direct = 'X' " ;

  rs = trsbdataset(str);
  if(rs and rs.movenext)
     return rs.t_filename;
  else
     return "";
  end;

end;

//Lavrenov I-00141917 Жестко, но пока другого пути нет
macro ОткатитьОбработкуЗапроса(reqid, formname)
  var str, cmd, rs;

  str = " select count(*) t_cnt FROM dwlreq_dbt " +
        "  WHERE t_kind = 6 AND t_direct = CHR (0) " +
        "    AND t_fiid = -1 AND t_corschem = -1 " +
        "    AND t_state > 10  and t_relatedref = TO_CHAR ("+ReqID+") "; 
  rs = trsbdataset(str);
  if(rs and rs.movenext)  
     if(rs.t_cnt > 0)
        msgbox("Невозможно откатить обработку запроса, порожденные ответы уже обработаны!!!");
        return false;
     end;
  end;
  
  str = " delete FROM dwlmesval_dbt " +
        "  WHERE t_mesid IN  " +
        "  (SELECT lnk.t_mesid FROM dwlmeslnk_dbt lnk, dwlreq_dbt req " +
        "    WHERE lnk.t_objkind = 505 " +
        "      AND lnk.t_objid = req.t_reqid AND req.t_kind = 6 " +
        "      AND req.t_direct = CHR(0) AND req.t_fiid = -1 " +
        "      AND req.t_corschem = -1 AND req.t_relatedref = TO_CHAR("+ReqID+")) ";
  cmd = RSDCommand(str);
  cmd.execute;

  str = " delete FROM dwlmes_dbt " +
        "  WHERE t_mesid IN " +
        "  (SELECT lnk.t_mesid " +
        "     FROM dwlmeslnk_dbt lnk, dwlreq_dbt req " +
        "    WHERE lnk.t_objkind = 505 " +
        "      AND lnk.t_objid = req.t_reqid AND req.t_kind = 6  " +
        "      AND req.t_direct = CHR (0) AND req.t_fiid = -1 " +
        "      AND req.t_corschem = -1 AND req.t_relatedref = TO_CHAR("+ReqID+")) ";
  cmd = RSDCommand(str);
  cmd.execute;

  str = " delete FROM dwlmeslnk_dbt " +
        "  WHERE t_objkind = 505 AND t_objid IN " +
        "  (SELECT t_reqid FROM dwlreq_dbt " +
        "    WHERE t_kind = 6 AND t_direct = CHR (0) " +
        "      AND t_fiid = -1 AND t_corschem = -1 " +
        "      AND t_relatedref = TO_CHAR ("+ReqID+")) "; 
  cmd = RSDCommand(str);
  cmd.execute;

  str = " delete FROM dwlhistor_dbt t " +
        "  WHERE t_objkind = 505 AND t_objid IN " +
        "  (SELECT t_reqid FROM dwlreq_dbt " +
        "    WHERE t_kind = 6 AND t_direct = CHR (0) " +
        "      AND t_fiid = -1 AND t_corschem = -1 " +
        "      AND t_relatedref = TO_CHAR ("+ReqID+")) "; 
  cmd = RSDCommand(str);
  cmd.execute;

  str = " delete FROM dwlreq_dbt " +
        "  WHERE t_kind = 6 AND t_direct = CHR (0) " +
        "    AND t_fiid = -1 AND t_corschem = -1 " +
        "    AND t_relatedref = TO_CHAR ("+ReqID+") "; 
  cmd = RSDCommand(str);
  cmd.execute;

  str = " delete from dwlhistor_dbt where t_OBJKIND = 505 and t_STATE = 70 and T_OBJID = "+ReqID;
  cmd = RSDCommand(str);
  cmd.execute;

  if((formname == "ZNO") or (formname == "ERR"))
   str = " update dwlreq_dbt SET T_STATE = 60 where T_REQID = "+ReqID;
  else
     str = " update dwlreq_dbt SET T_STATE = 60, t_queries = '0' where T_REQID = "+ReqID;
  end;
  cmd = RSDCommand(str);
  cmd.execute;
  
  return true;
end;

//Lavrenov: Проверка на причастность операциониста к группе
//Lavrenov: убрал, теперь тянется ВходитВГруппу из diver.mac
//Lavrenov: diver.mac не выход :((((((
Macro OperInGroup_Блин(Oper,IdGroup)
 var str, rs;

 str = "select count(*) cnt from DACSGROUPOPER_DBT where T_GROUPID = "+ IdGroup +" and T_OPER = " + Oper;
 rs = trsbdataset(str);
 if(rs and rs.movenext)
   if(rs.cnt > 0)
     return true;
   else
     return false;
   end;    
 else
   return false;
 end;
End;

//Lavrenov: Скроллинг входящих запросов
MACRO get_reqs (begindate, enddate)
  private var Group_195 = False, Group_197 = False;
  private array m;
  private var change;
  var recordset;
  var CmdText,Command, reqids;
  var col = TArray;
  var flag = false;

  //Обработчик для скроллинга
  macro EvProc (RecordSet, Command, id, key )

   if (Command == DLG_INIT)
      if (not AddMultiAction (RecordSet, 316)) 
        msgbox ("!!!");
      end;
      reqids = "";
   end;

   if ((Command == DLG_MSEL) and (key == 316)) // F2
      reqids = reqids + RecordSet.value("T_reqid")+", ";
      return CM_MSEL_CONT_CLEAR;
   end;

   if (Command == DLG_MSELEND)
     flag = true;
     return CM_cancel;
   end;
   
   if ((Command == DLG_KEY) and Flag)
       reqids = substr(reqids, 1, strlen(reqids)-2);
       return CM_Cancel;
   elif ((Command == DLG_KEY) and (key == 27))
       reqids = "0";
       return CM_cancel;
   elif ((Command == DLG_KEY) and ((key == 322) or (key == 323)))
       return cm_ignore
   end;

 end;

  //Добавляем колонки в скроллинг
  macro AddCol (ar,ind, fld, head, width, rdonly)
    ar.value (ind * 6)     = fld;
    ar.value (ind * 6 + 1) = head;
    ar.value (ind * 6 + 2) = width;
    ar.value (ind * 6 + 3 ) = 2;   // fldType
    ar.value (ind * 6 + 4 ) = -1;  // decPoint
    ar.value (ind * 6 + 5 ) = 0;   // reserv
  end;

  AddCol (col, 0, "t_formname", "Форма", 5, true);
  AddCol (col, 1, "T_TRN", "Ном. док", 10, true);
  AddCol (col, 2, "t_pmdatevalue", "Дата док",10, true);
  AddCol (col, 3, "T_RELATEDREF", "ИД файл", 25, true);
  AddCol (col, 4, "t_bankdate",  "Дата", 10, true);
  AddCol (col, 5, "t_userid",  "Операционист", 5, true);
  AddCol (col, 6, "t_queries",  "Сообщение", 50, true);
  AddCol (col, 7, "t_originatorcode",  "Отправитель", 10, true);
  AddCol (col, 8, "t_originatorname",  "Наименование отправителя", 50, true);
  AddCol (col, 9, "t_reqid",  "ID объекта", 10, true);

  //Выбираем данные из таблицы
  CmdText = "SELECT wlmesfrm.t_name t_formname, t.t_trn, t.t_pmdatevalue, t.t_relatedref, " +
            "      t.t_bankdate, t.t_userid, t.t_queries, t.t_originatorcode, " +
            "      t.t_originatorname, t.t_reqid " +
            "  FROM dwlreq_dbt t, dwlmes_dbt wlmes, dwlmesfrm_dbt wlmesfrm, " +
            "      dwlmeslnk_dbt wlmeslnk, dwlmesrls_dbt wlmesrls " +
            " WHERE    wlmeslnk.t_objid(+) = t.t_reqid " +
            "       AND wlmeslnk.t_objkind(+) = 505 " +
            "       AND wlmeslnk.t_direct(+) = 'X' " +
            "       AND wlmes.t_mesid(+) = wlmeslnk.t_mesid " +
            "       AND wlmeslnk.t_mesid = " +
            "              (SELECT NVL (MIN (sublnk.t_mesid), 0) " +
            "                 FROM dwlmeslnk_dbt sublnk " +
            "                WHERE sublnk.t_objid = t.t_reqid AND sublnk.t_objkind = 505) " +
            "       AND wlmes.t_department = 1 " +
            "       AND wlmesrls.t_rlsformid(+) = wlmes.t_rlsformid " +
            "       AND wlmesfrm.t_formid(+) = wlmesrls.t_formid " +
            "       AND t.t_state = 60 " +
            "       AND t.t_kind = 5 " +
            "       AND t.t_direct = 'X' " +
            "       AND t.t_BankDate BETWEEN to_date('"+begindate+"','dd.mm.yyyy') AND to_date('"+enddate+"','dd.mm.yyyy') "+
            "       and rownum < 1000 ";//Lavrenov: Иначе вылетает ошибка если больше 1000 элементов сразу обрабатываем 
            
  if (OperInGroup_Блин({oper},195))
    Group_195 = True;
  end;
  if (OperInGroup_Блин({oper},197))
    Group_197 = True;
  end;

  if ((Group_195) and (Group_197))
    m(0) = "Обработать информационные сообщения";
    m(1) = "Обработать приостановления по счетам";
  elif(Group_195)
    m(0) = "Обработать информационные сообщения";
  elif(Group_197)
    m(0) = "Обработать приостановления по счетам";
  end;
  
  change = menu(m, "~Enter~ Выбор пункта меню", "Выберите тип обрабатываемых файлов");
  
  if ((Group_195) and (Group_197) and (change == 0))
    CmdText = CmdText + "  and wlmesfrm.t_name = 'ZNO' ";
  elif ((Group_195) and (Group_197) and (change == 1))
    CmdText = CmdText + "  and wlmesfrm.t_name in ('ROO','RPO') ";
  elif ((Group_195) and (change == 0))
    CmdText = CmdText + "  and wlmesfrm.t_name = 'ZNO' ";
  elif ((Group_197) and (change == 0))
    CmdText = CmdText + "  and wlmesfrm.t_name in ('ROO','RPO') ";
  else
    msgbox("Вы отказались от обработки сообщений");
    reqids = 0;
    return reqids
  end;

  CmdText = CmdText + "  order by  t.t_BankDate, t.t_reqid ";
   //msgbox(CmdText);
  Command = RSDCommand(CmdText);
  Command.Execute();
  RecordSet = RSDRecordSet(Command, RSDVAL_CLIENT, RSDVAL_STATIC);

  //Запускаем скроллинг
  runScroll(RecordSet, 10, col, null, @EvProc, string("Необработанные запросы "), "~F2~   Выбор", false);

  return reqids;
END;

//Lavrenov: дебааааааааааааааааажим
macro debugstr(path, str);
  setoutput(path,true);
  println (str);
  setoutput(null,true);
end;

macro strYYYYMMDD(str)
  str = string(str);
  
  var dd = trim(string(substr(str, 1, 2)));
  var mm = string(substr(str, 4, 2));
  var yyyy = string(substr(str, 7, 4));
  
  if (StrLen(DD) < 2)
    dd = "0" + dd;
  end;
  
  return string(yyyy + mm + dd);
end;

//Lavrenov: Чистим состояние счетов по запросу перед обработкой
macro Clear_wlacclnk(reqid)
  var cmd, str;
  str = " update dwlacclnk_dbt " + 
        "    set T_LNKOBJECTTYPE = 0, T_LNKOBJECTID = 0, t_state = 0 " +
        "  where t_objecttype = 505 and t_objectid = "+reqid;
  cmd = rsdcommand(str);
  cmd.execute();  
end;


/*Очистка временной таблицы*/
macro TruncateTempTable()

  var sql, cmd;
  
  sql = " truncate table USR_VIPISKA_TMP"; 
  cmd = RSDCommand(sql);
  cmd.execute;
  return true;
  
  onError(x);
    msgbox("Не найдена таблица USR_VIPISKA_TMP;");
    return false;
  
end;

/*Вставка строки во временную таблицу*/
macro InsertStrTmp(Num, Str)
  
  var sql, cmd;
  
  sql = "Insert into USR_VIPISKA_TMP values (0, ?, ?)";
  cmd = RSDCommand(sql);
  cmd.AddParam("Num", RSDBP_IN, Num);
  cmd.AddParam("Str", RSDBP_IN, Str);
  cmd.execute;

  onError(x);
    msgbox("Не удалось вставить запись в таблицу USR_VIPISKA_TMP");
    exit(0);
end;

/*30.01.2012 Chesnokov D. Преобразование времени в */
/*           строку по формату hh:mm:ss            */
macro TimeToStr(время:time):string
  
  var строка_время;

  строка_время = trim(string(время));
  if (index(строка_время, ":") == 2)
    строка_время = "0" + строка_время;
  end;

  return строка_время;
end;

/*31.01.2011 Зленко М. избавляемся от ИП */ 
macro cutFIO(_FIO)
   var rep: Tarray = Tarray();
   var   tmprep:Tarray = Tarray(); 
   var i:integer = 0, k:integer = 0;
 
   rep(rep.size) = "ИП";  
   rep(rep.size) = "(ИП)"; 
   rep(rep.size) = "ИНДИВИДУАЛЬНЫЙ";
   rep(rep.size) = "ПРЕДПРИНИМАТЕЛЬ";
   rep(rep.size) = "НОТАРИУС";
   
   var f = split(trim(_FIO)," "); 
   while (i < f.size)
     tmprep(tmprep.size) = strUpr(f(i));
     k = 0;
     while (k < rep.size)
       if (tmprep(i) == rep(k))
         _FIO = strsubst(_FIO,f(i),""); 
         _FIO = strsubst(_FIO,"  "," ");   
       end;
     k = k + 1;
     end;
   i = i + 1; 
   end;
return trim(_FIO);
end;

macro getTpIDbyName(_name)
 var rs,str;
  str = "  SELECT t.t_tpid " +
        "  FROM dwltransp_dbt t " +
        "  WHERE t.t_name = '" + _name +"'";
  rs =  trsbdataset(str);
  if(rs and rs.movenext)
     return rs.t_tpid;
  end;
  return "";
end;

macro INSERT_MES365_LOG_manual(file_name, ext, Soun_code)

  var rs, cmd, str, FullName, SounID;
  
  FullName = file_name + ext;
  
  str = " SELECT t_objectid " +
        "   FROM dobjcode_dbt " +
        "  WHERE t_code = ? " +
        "     AND t_objecttype = 3 " +
        "     AND t_codekind = 28 " +
        "     AND t_state = 0 ";
  cmd = RsdCommand(str);
  cmd.AddParam("Code",   RSDBP_IN, Soun_code);
  rs = RsdRecordSet(cmd);
  if (rs and rs.MoveNext)
    SounID = rs.value(0);
  else
    SounId = 0;
  end;
  
  str = " MERGE INTO dmes365_log_dbt d " +
        "      USING (SELECT 1 FROM DUAL) o " +
        "         ON (Substr(d.t_outsidemes, 1, length(d.t_outsidemes) - 4) = ? " +
        "             AND d.t_reqid = 0 )"
        " WHEN MATCHED " +
        " THEN " +
        "    UPDATE SET " +
        "       d.t_date_in = ?, " +
        "       d.t_date_out = ?, " +
        "       d.t_time = TO_DATE ('01.01.0001 ' || TO_CHAR (SYSDATE, 'hh24:mi:ss'), 'dd.mm.yyyy hh24:mi:ss'), " +
        "       d.t_oper = rsbsessiondata.oper " +
        " WHEN NOT MATCHED " +
        " THEN " +
        "    INSERT (d.t_reqid,       d.t_insidemes, " +
        "            d.t_date_in,     d.t_outsidemes, " +
        "            d.t_date_out,    d.t_time, " +
        "            d.t_recipientid, d.t_oper, " +
        "            d.t_kwt_date,    d.t_check, " +
        "            d.t_kwt_mes,     d.t_kwt_oper) " +
        "    VALUES (0, " +
        "            chr(0), " +
        "            ?, " +
        "            UPPER(?), " +
        "            ?, " +
        "            TO_DATE ('01.01.0001 ' || TO_CHAR (SYSDATE, 'hh24:mi:ss'), 'dd.mm.yyyy hh24:mi:ss' ), " +
        "            ?, " +
        "            rsbsessiondata.oper, " +
        "            TO_DATE('01.01.0001','dd.mm.yyyy'), "+
        "            CHR(0), " +
        "            CHR(0), " +
        "            null) ";

  cmd = RsdCommand(str);
  cmd.AddParam("fileName",   RSDBP_IN, file_name);
  cmd.AddParam("date_in",    RSDBP_IN, {Curdate});
  cmd.AddParam("date_out",   RSDBP_IN, {Curdate});
  cmd.AddParam("date_in_1",  RSDBP_IN, {Curdate});
  cmd.AddParam("fname3",     RSDBP_IN, FullName);
  cmd.AddParam("date_out_1", RSDBP_IN, {Curdate});
  cmd.AddParam("SounID",     RSDBP_IN, SounID);
  cmd.execute;
  
end;

/*10.02.2014 Chesnokov D.S. По запросу C-26943*/
macro IsTransferedClient(PartyID:integer):double
  
  var rs, cmd, str;
  
  str = " SELECT t_attrid attrid" +
        "   FROM dobjatcor_dbt " +
        "  WHERE t_objecttype = 3 " +
        "    AND t_groupid = 220 " + //Категория в которой отмечаем Ульяновских клиентов
        "    AND t_object = lpad(?, 10, '0') " +
        "    AND t_validfromdate <= ? " + //Проверим что на текущую дату категория действует
        "    AND t_validtodate >= ?";     //Проверим что на текущую дату категория действует
  
  cmd = RsdCommand(str);
  cmd.AddParam("PartyID",  RSDBP_IN, PartyID);
  cmd.AddParam("date1",    RSDBP_IN, {curdate});
  cmd.AddParam("date2",    RSDBP_IN, {curdate});
  rs = RsdRecordSet(cmd);
  if (rs and rs.MoveNext)
    return rs.value("attrid");
  else
    return -1;
  end;
end;

/*02.06.2014 SDA определение принадлежности клиента к реорганизованным филиалам Солидарности*/
macro Is_5NT_Client_account(НомСчета,Филиал,открыт,закрыт);
  var rs, cmd, str;
  
  str = " SELECT F.T_FILIALNAME, F.T_OPENDATE, F.T_CLOSEDATE "+
        "       FROM dfilial_dbt f "+
        "   WHERE trim(F.T_ACCOUNT) = ?";
 
  cmd = RsdCommand(str);
  cmd.AddParam("account",  RSDBP_IN, НомСчета);

  rs = RsdRecordSet(cmd);
  if (rs and rs.MoveNext)
    setparm(1, rs.value("T_FILIALNAME"));
    setparm(2,sqlDate2date(rs.value("T_OPENDATE")));
    setparm(3,sqlDate2date(rs.value("T_CLOSEDATE")));
    return true;
  else
    return false;
  end;

end;


// zmp 12.09.2014 I-00514395-2
macro Is_5NT_Client(inn,accarr);
  
  var rs, cmd, str;
  accarr.size = 0;
  str = "  SELECT F.T_ACCOUNT, "+
        "         F.T_FILIALNAME, "+
        "         F.T_OPENDATE, "+
        "         F.T_CLOSEDATE "+
        "    FROM dfilial_dbt f "+
        "   WHERE trim(F.T_INN) = ?   AND REGEXP_LIKE (trim(F.T_ACCOUNT), '^40[1-8]|^438|^421|^419|^30109')";

  cmd = RsdCommand(str);
  cmd.AddParam("PartyID",  RSDBP_IN, inn);
  rs = RsdRecordSet(cmd);

  while (rs and rs.MoveNext)
   accarr[accarr.size] = rs.value("T_ACCOUNT")+"  "+rs.value("T_FILIALNAME")+" открыт: "+ sqlDate2date(rs.value("T_OPENDATE"))+" закрыт: "+sqlDate2date(rs.value("T_CLOSEDATE"));
  end;

end;
