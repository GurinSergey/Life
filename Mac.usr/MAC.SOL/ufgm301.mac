/****************************************************************************/
/*                   R-Style SoftLab, RS-Bank 6.0                           */
/****************************************************************************/
/*                  Подсистема "Межбанковские расчеты"                      */
/* Генерация сообщения ED301 транспорта УФЭБС                               */
/*                                                                          */
/*  Имя файла: ufgm301.mac                                                  */
/*  Создан:    28.09.12  Chukina T.                                         */
/****************************************************************************/

import "ufgenmes.mac", "wluftool.mac";

private macro ParseQueries( Queries: string, // /N/БИК/Сумма
                            BIC: @string, 
                            Sum: @string, 
                            LiquidityTransKind: @string
                          )

  var ind1 = 0, ind2 = 0, ind3 = 0;
 msgbox(Queries);
  ind1 = index(Queries, "/");
  ind2 = index(Queries, "/", ind1 + 1);
  ind3 = index(Queries, "/", ind2 + 1);

  if( not ind1 or not ind2 or not ind3 )
    RunError("Не определен БИК ПУР или сумма операции или код операции");
  elif( ind2 - ind1 == 1 )
    RunError("Не определен код операции по управлению позицией ликвидности");
  elif( ind3 - ind2 == 1 )
    RunError("Не определен БИК ПУР");
  elif( ind3 == strlen(Queries) )
    RunError("Не определена сумма операции");
  end;

  BIC = substr(Queries, ind2+1, ind3-ind2-1);
  Sum = substr(Queries, ind3+1);
  LiquidityTransKind = substr(Queries, ind1+1, ind2-ind1-1);
end;

macro GenMes( addrMes, addrReq )

  SetBuff( wlmes, addrMes );
  SetBuff( wlReq, addrReq );

  var xml:object = ActiveX("Microsoft.XMLDOM");  
  var mes:object;
  
  mes = xml.createElement("ED301");
  mes.setAttribute("xmlns", "urn:cbr-ru:ed:v2.0");

  FillEDNoDateAuthorByRef_XMLField(mes, wlmes.TRN);
  
  var BIC = "", Sum = "", LiquidityTransKind = "";
  ParseQueries(wlReq.Queries, @BIC, @Sum, @LiquidityTransKind);

  mes.setAttribute("EDReceiver", string(substr({MFO_RCC}, 3, 7),"001"));  
  mes.setAttribute("BIC",  BIC);
  mes.setAttribute("Sum",  Sum);
  mes.setAttribute("LiquidityTransKind",  LiquidityTransKind);

  /*if(wlmes.RelatedRef)
    var elem = xml.createElement("EDRefID");
    FillEDNoDateAuthorByRef_XMLField(elem, wlmes.RelatedRef);
    elem.appendChild(xml.createTextNode(""));
    mes.appendChild(elem);
  end;*/

  ЗаписатьПолеЛог( "xml", mes.xml, TRUE );
  
  return TRUE; /* Успешное завершение */

  OnError( er ) /* обработка ошибок */
    std.msg(String(er.Message, "|Модуль: ", er.Module, " Строка: ", er.Line));
    return FALSE; 
end;