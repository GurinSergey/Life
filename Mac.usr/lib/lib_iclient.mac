// -------------------------------------------------------------------------------------------------
// @filename: lib_iclient.mac
// @author  : 2012-10-17 zip_z. 
// @desc    : библиотека процедур для работы с интернет-клиентом
// @changes : none
// -------------------------------------------------------------------------------------------------

import rsd, globals;
import lib_registry;

// @desc    : получает строку параметров соединения с ИК
macro IC_GetConnstring ()
    var m_DS   = RSL_GetRegistryValue ("PRBB/REPORT/PRINT_VK/DSN"     ,true);
    var m_User = RSL_GetRegistryValue ("PRBB/REPORT/PRINT_VK/USERID"  ,true);
    var m_Pass = RSL_GetRegistryValue ("PRBB/REPORT/PRINT_VK/PASSWORD",true);
    return string ("DSN=", m_DS, ";USER ID=", m_User, ";PASSWORD=", m_Pass);
end;

// @desc    : подключён ли ИК для клиента p_PartyId нашего банка (true = подключён)
macro IC_FindClientByPartyID (p_PartyId:string)
    var sql = "select 1 from ib6_ext_sys_id sys_id "
              " where sys_id.extid = :clientid "
              " and sys_id.external = (select external from ib6_dct_sys_branch sb "
              "                                        join ib6_dct_sysentity se  on se.stored_pk = sb.id and se.kind = 1225 "
              "                                        join ib6_dct_tail_branch b on b.id = se.id "
              "                                        join ib6_dct_tail_bank bn on b.bank = bn.id "
              "                        where bn.bic = :mfo_bank) "
              " and sys_id.entitytype = 'EntityKind:clientBranch'";
    
    var cmd = RsdCommand(IC_GetConnstring (), sql);
    cmd.AddParam ("clientid", RSDBP_IN, p_PartyId  );
    cmd.AddParam ("mfo_bank", RSDBP_IN, {mfo_bank} );
    cmd.Execute();

    var rs = RsdRecordset (cmd);
    return rs.MoveNext ();
end;



/* -------------------------------------------------------------------------------------------------
 @desc    : отправка уведомления с темой m_subject и содержанием m_text для клиента p_PartyId в ИК
            Параметры функции message () из интернет-клиента:
                out_error                   OUT INTEGER,                    -- ошибка
                client_absid             IN     INTEGER,                    -- абс id клиента
                document_subject         IN     VARCHAR,                    -- тема письма
                document_text            IN     VARCHAR,                    -- текст письма
                document_attachedfiles   IN     FILEINF_T,                  -- список файлов
                p_client_abs             IN     VARCHAR DEFAULT '044525986' -- бик филиала
------------------------------------------------------------------------------------------------- */
macro IC_SendMessage (p_PartyId, m_subject, m_text)
    debugbreak;
    var errcode = 0;
    var sql = " DECLARE "                                            + "\n" + 
              "    e     PLS_INTEGER := 0; "                              + "\n" + 
              " BEGIN "                                              + "\n" + 
              "    MESSAGE (out_error                => e,         " + "\n" + 
              "             client_absid             => :clientid, " + "\n" + 
              "             document_subject         => :subject,  " + "\n" + 
              "             document_text            => :text,     " + "\n" + 
              "             p_client_abs             => :mfo_bank, " + "\n" + 
              "             document_attachedfiles   => NULL);     " + "\n" + 
              //"   :errcode := e;                                   " + "\n" +  //TAM 06.02.13 I-00321562-3
              " END;";
    
    var cmd = RsdCommand (IC_GetConnstring, sql);
    cmd.AddParam ("clientid",  RSDBP_IN,  p_PartyId  );
    cmd.AddParam ("subject",   RSDBP_IN,  m_subject  );
    cmd.AddParam ("text",      RSDBP_IN,  m_text     );
    cmd.AddParam ("mfo_bank",  RSDBP_IN,  {mfo_bank} );
    //cmd.AddParam ("errcode",   RSDBP_OUT, errcode, V_INTEGER  ); //TAM 06.02.13 I-00321562-3
    cmd.Execute ();
    
    return errcode;
end;