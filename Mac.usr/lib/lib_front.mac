// -------------------------------------------------------------------------------------------------
// @filename: lib_front.mac v.1
// @author  : 2013-02-18 zip_z. 
// @desc    : Общие функции для работы с Фронтом
// @changes : Gurin S. 23.12.2014 Добавил GetMailFromFront() и GetBankIdFromFront()
// -------------------------------------------------------------------------------------------------
import lib_registry, frontCommon, Globals, lib_fg;

/*** состояния ADO State ***/
// @rtfm: http://www.w3schools.com/ado/prop_comm_state.asp
const RSL_ADO_STATE_CLOSED     = 0; // The object is closed
const RSL_ADO_STATE_OPEN       = 1; // The object is open
const RSL_ADO_STATE_CONNECTING = 2; // The object is connecting
const RSL_ADO_STATE_EXECUTING  = 4; // The object is executing a command
const RSL_ADO_STATE_FETCHING   = 8; // The rows of the object are being retrieved

/** пути настроек реестра для определения ConnectionString **/
const FR_LEGAL      = "PRBB/FRONT/CONNECTION/ФРОНТ-ЮЛ";
const FR_INDIVIDUAL = "PRBB/FRONT/CONNECTION/ФРОНТ-ФЛ";
const FR_CASH       = "PRBB/FRONT/CONNECTION/ФРОНТ-КАССА";
const FR_DEALING    = "PRBB/FRONT/CONNECTION/ФРОНТ-ДИЛИНГ";

// @desc: получает ConnectionString для внешней системы (из настроек реестра)
macro getFrontConnectionString (_systemId:string)
    var ret = RSL_GetRegistryValue (_systemId, true);
    return ret;
end;

// @desc: вспомогательный класс для установки ОДНОГО коннекта с Фронтом
class FR_GlobalConnection (_systemId:string, _info:string)
    var systemId    :string  = _systemId;
    var programInfo :string  = _info;
    var conn        :object  = ActiveX ("ADODB.Connection");
    
    // начало опроса канала
    macro StartPooling ()
        Conn2Front (@conn, getFrontConnectionString(this.systemId), this.programInfo);
        return conn;
    end;
    
    // завершение опроса канала, закрытие соединения
    macro EndPooling () 
        if (conn.state != RSL_ADO_STATE_CLOSED)
            conn.close (); 
        end;
        conn = null;
    onError
    end;
end;

// @desc: получение id нашего банка во Фронте
macro GetBankIdFromFront()
    if   (_bank.is_PRBB)            return 252;
    elif (_bank.is_GEB)             return 1963;
    elif (_bank.is_VUZ)             return 2519;
    elif (_bank.is_SLD)             return 346;
    elif (_bank.is_EXV_Saratov)     return 133;
    elif (_bank.is_EXV_Stavropol)   return 8484;
    elif (_bank.is_EXV_Volgograd)   return 127;
    elif (_bank.is_EXV_Voronezh)    return 9000;
    elif (_bank.is_EXV_Ulyanovsk)   return 7743;
    end;
end;

// @desc: получение адреса эл. почты пользователя с Фронта
macro GetMailFromFront(_oper, _mail)
    array aInput;
    var oConnection_lib = ActiveX("ADODB.Connection");

    var c    = FR_GlobalConnection (FR_LEGAL, "RS-Bank::Получение электронного адреса пользователя");
    var conn = c.StartPooling (); 

    aInput [0] = GetBankIdFromFront();

    aInput [1] = _oper;

    //Функция во Фронте: get_f_user_ln_name
    //Аргументы:  @bank_id    - id  банка           (тип: int)
    //            @oper       - номер операциониста (тип: int)
    //return:     mail

    var rset = RSADORecordset (ExecuteFrontProcEx("get_f_user_ln_name", aInput, conn));
    if (rset.movenext)
       SetParm(1, rset.m_value[0]);
    end;
end;

