// -------------------------------------------------------------------------------------------------
// @filename: lib_RsbSession.mac
// @author  : 2013-01-01 zip_z. 
// @desc    : работа с параметрами сессии RS-Bank
// @changes : none
// -------------------------------------------------------------------------------------------------
import oralib, likepy;

// @desc    : параметры сессии со стороны RS-Bank
class RSL_RsbSession ()
    var oper;               // номер операциониста, работающего в системе
    var cTypePerson;        // права доступа операциониста, работающего в системе
    var cRealTypePerson;    // права доступа операциониста, работающего в системе, заданные в карточке пользователя
    var curdate;            // дата текущего операционного дня
    var HeadDprt;           // ИД головного отделения 
    var OperDprt;           // ИД вышестоящего узла ТС с типом "ФИЛИАЛ" для узла ТС операциониста, работающего в системе
    var OperDprtNode;       // ИД узла ТС операциониста, работающего в системе
    var OurBank;            // ИД (из справочника субъектов) банка операциониста, работающего в системе
    var OurBankSubDivision; // ИД (из справочника субъектов) подразделения банка операциониста, работающего в системе
    var CNum;               // Connection number
    
    macro RSL_RsbSession_Init ()
        var sql = "select RsbSessionData.oper, RsbSessionData.cTypePerson, RsbSessionData.cRealTypePerson," +
                  "       RsbSessionData.curdate, RsbSessionData.HeadDprt, RsbSessionData.OperDprt," +
                  "       RsbSessionData.OperDprtNode, RsbSessionData.OurBank, RsbSessionData.OurBankSubDivision," +
                  "       RsbSessionData.CNum from dual";
        sql = ExecSqlSelect (sql);
        if ( sql.MoveNext () )
            this.oper               = sql.value ("oper"              ,null ,V_INTEGER);
            this.cTypePerson        = sql.value ("cTypePerson"       ,null ,V_STRING );
            this.cRealTypePerson    = sql.value ("cRealTypePerson"   ,null ,V_STRING );
            this.curdate            = sql.value ("curdate"           ,null ,V_DATE   );
            this.HeadDprt           = sql.value ("HeadDprt"          ,null ,V_INTEGER);
            this.OperDprt           = sql.value ("OperDprt"          ,null ,V_INTEGER);
            this.OperDprtNode       = sql.value ("OperDprtNode"      ,null ,V_INTEGER);
            this.OurBank            = sql.value ("OurBank"           ,null ,V_INTEGER);
            this.OurBankSubDivision = sql.value ("OurBankSubDivision",null ,V_INTEGER);
            this.CNum               = sql.value ("CNum"              ,null ,V_INTEGER);
        end;
    end;
    
    RSL_RsbSession_Init ();
end;

// @desc    : параметры сессии со стороны Oracle
class RSL_OraSession ()
    var sid;
    var serial;
    var audsid;
    var logon_time;
    var osuser;
    var machine;
    var terminal;
    
    macro RSL_OraSession_Init ()
        var sql = "select sid, serial# serial, audsid, logon_time, machine, terminal, osuser from v$session where audsid = sys_context('USERENV','SESSIONID')";
        sql = ExecSqlSelect (sql);
        if ( sql.MoveNext () )
            this.sid        = sql.value ("sid"       ,null ,V_INTEGER);
            this.serial     = sql.value ("serial"    ,null ,V_INTEGER);
            this.audsid     = sql.value ("audsid"    ,null ,V_INTEGER);
            this.logon_time = sql.value ("logon_time",null ,V_DTTM   );
            this.osuser     = sql.value ("osuser"    ,null ,V_STRING );
            this.machine    = sql.value ("machine"   ,null ,V_STRING );
            this.terminal   = sql.value ("terminal"   ,null ,V_STRING );
        end;
    end;
    
    RSL_OraSession_Init ();
end;