// -------------------------------------------------------------------------------------------------
// @filename: lib_oper.mac
// @author  : 2013-01-03 zip_z. 
// @desc    : библиотека процедур для работы с операционистами (пользователями)
// @changes : none
// -------------------------------------------------------------------------------------------------

import oralib, likepy;

// @desc    : dperson_dbt
 class RSL_Person (OperNumber:integer)
    var oper;             // номер пользователя
    var name;             // имя пользователя
    var partyID;          // ИД пользователя
    var cTypePerson;      // уровень доступа
    var lastConnectDate;  // дата последнего входа в систему
    var userBlocked;      // признак блокировки
    var userClosed;       // признак закрытия
    var ArmNumber;        // номер АРМ (dperson_lnk_dbt.t_role)
    var ArmName;          // название АРМ
    
    /* bool - для удобства использования в if/case conditions */
    var m_isRobot   = false;
    var m_isRssl    = false;
    var m_isMigrate = false;
    var m_isBlocked = false;
    var m_isClosed  = false;
    
    /* конструктор */
    macro RSL_Person_Init (OperNumber:integer)
        var sql = "select t_Oper, t_Name, t_PartyID, t_cTypePerson, t_lastConnectDate, t_UserBlocked, t_UserClosed from dperson_dbt where t_oper = :o";
        var parm = makeArray (SqlParam ("o", OperNumber));
        sql = execSqlSelect (sql, parm, false);
        if (sql.MoveNext ())
            this.oper            = sql.value ("t_Oper"           ,null ,V_INTEGER);
            this.name            = sql.value ("t_Name"           ,null ,V_STRING );
            this.partyID         = sql.value ("t_PartyID"        ,null ,V_INTEGER);
            this.cTypePerson     = sql.value ("t_cTypePerson"    ,null ,V_STRING );
            this.lastConnectDate = sql.value ("t_lastConnectDate",null ,V_DATE   );
            this.userBlocked     = sql.value ("t_UserBlocked"    ,null ,V_STRING );
            this.userClosed      = sql.value ("t_UserClosed"     ,null ,V_STRING );
        end;
        this.m_isRobot   = (index (strupr (name), "РОБОТ ") == 1); // пробел обязателен, иначе под раздачу попадут Д[робот]ова Зоя и Роботенко Иван
        this.m_isRssl    = (index (name, "RSSL") > 0);
        this.m_isMigrate = (index (name, "MIGRATE") > 0);
        this.m_isBlocked = (userBlocked == "X");
        this.m_isClosed  = (userClosed == "X");
        
        sql = "SELECT lnk.t_role, NVL (mn.t_name, 'Образец меню не найден') t_arm_name " +
              "  FROM dperson_lnk_dbt lnk, dmenutpl_dbt mn " +
              " WHERE lnk.t_role = mn.t_menuid(+) AND lnk.t_user = :o ";
        
        sql = execSqlSelect (sql, parm, false);
        if (sql.MoveNext ())
            this.ArmNumber = sql.value ("t_Role" ,null ,V_INTEGER);
            this.ArmName   = sql.value ("t_arm_name" ,null ,V_STRING);
        end;
    end;
    
    /* интерфейсы */
    macro isRobot   () return m_isRobot;   end;
    macro isRssl    () return m_isRssl;    end;
    macro isMigrate () return m_isMigrate; end;
    
    /* проверка пользователя на принадлежность АРМу */
    macro check_ARM ( ) 
        var i:integer = 1, parm:integer;
        while( getParm(i, parm) ) 
            if (parm == this.ArmNumber) return true; end;
            i = i + 1;
        end;
        return false; 
    end;
    
    RSL_Person_Init (OperNumber);
end;