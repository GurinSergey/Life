// -------------------------------------------------------------------------------------------------
// @filename: startbnk_handler.mac v.1
// @author  : 2013-04-04 zip_z.
// @desc    : Оповещение о входе на тестовую и боевую среду
// @changes : none
// -------------------------------------------------------------------------------------------------

import BankInter, Globals;
import lib_oper, lib_rsbsession, lib_const;

// @desc  : получить дату создания схемы oracle (используется для тестовой среды )
// @return: V_DATE
private macro getDateRenewal ():string
    var sql = execSqlSelect ("select created from dba_users where username = user");
    sql.moveNext ();
    return (string (sql.value ("created", null, V_DATE)));
end;

// @desc  : среда тестовая?
// @return: V_BOOL
private macro isTestingEnvironment ():bool
    return (rsldefcon.conString == "SOFTLAB");
end;

macro getTestWarningMessage ()
    /*** коды ответа ***/
    const RET_CONTINUE    = 0; // Продолжить работу
    const RET_INFORMATION = 1; // Информация о сессии
    const RET_EXIT        = 1; // Выйти
    
    var m_oper    = RSL_Person ({OPER});
    var m_session = RSL_RsbSession ();
    
    var msg;
    if ((m_oper.isRssl) and (not m_oper.isRobot))
        if (isTestingEnvironment ())
            msg = ConfWin (makeArray ("Вы работаете на тестовой среде RS-Bank.|Дата обновления " + getDateRenewal ()), makeArray ("Продолжить работу", "Информация о сессии"), RET_CONTINUE);
            if (msg == RET_INFORMATION)
                setOutput (GetTxtFileName ("SessionData"));
                printprops (RslDefCon);
                printprops (m_oper);
                printprops (m_session);
                setOutput (null, true);
                viewFile (GetTxtFileName ("SessionData"));
            end;
        else
            msg = ConfWin (makeArray ("Боевая среда " + strupr (RslDefCon.user)  + "|Use responsibly."), makeArray ("Продолжить работу", "Выйти"), RET_EXIT);
            if (msg == RET_EXIT) return RSL_EXIT_FAILURE; end;
        end;
    end;
    
    return RSL_EXIT_SUCCESS;
end;
