//--------------------------------------------------------------------------------------------------
// @filename : openCurdate.mac
// @desc     : Аварийное открытие опердня при несрабатывании стандартной процедуры
// @author   : 12.10.2010 Тихомиров А.Н.  
// @changes  : 17.01.2013 Gurin S. R-144247-2 Ограничение доступа к пункту меню
//             19.01.2013 zip_z.   отрефакторил и переписал, ибо ОНО принципиально отказывалось работать
//                                 по пятницам (пыталось открыть субботу вместо понедельника)
//--------------------------------------------------------------------------------------------------

import globals, lib_oper, rsd, Календарь;

/*13.03.2013 Chesnokov D.S. логгирование запуска для поиска неиспользуемых макросов*/
ExecmacroFile("lib_log.mac", "LogProcedure", "OpenCurdate.mac");

// проверяем, есть ли такой день в списке открытых
private Macro CheckDay (CDate)
    var sql = RsdCommand ("select 1 from dcurdate_dbt where t_curdate = ? and t_branch = 1");
    sql.addparam("Dt", RSDBP_IN, CDate);
    var rsd = rsdrecordset (sql);
    return  (rsd.movenext());
end;

/*** ***/
macro main ()
    if (RSL_Person ({oper}).Check_Arm (1001, 1016))
        // NextDate - следующий рабочий день после текущего
        var NextDate = getDateAfterWorkDays ({CURDATE}, 1);
        const MSG = "Ждите, открывается новый операционный день";
        
        // проверяем, есть ли такой день в списке открытых
        if (CheckDay(NextDate))
            msgbox (String("Операционный день ",Nextdate, " уже открыт"));
            return;
        end;

        // спрашиваем пользователя. 
        if (Gettrue(True, String("Вы уверены, что необходимо открыть операционный день ", Nextdate, " ?")))
            initprogress (-1, MSG, MSG );
            var cmd = RsdCommand ("begin "+
                              " INSERT INTO dcurdate_dbt VALUES (?,0,1,1,1,CHR(0),CHR(0)); " +
                              " UPDATE daccount_dbt  SET t_d0 = 0, t_k0 = 0; " + // а ещё здесь работает туча триггеров
                              " UPDATE daccount$_dbt SET t_d0 = 0, t_k0 = 0; " + // и здесь тоже :(
                              " end;");
            cmd.AddParam("Dt", RSDBP_IN, NextDate);
            cmd.execute();
            writeFiscLog (OLstrproc, string("Аварийное открытие операционного дня ", Nextdate));
            println("Операционный день открыт");
            remprogress();
        end;
    else
        msgbox ("Процедуру может выполнять только пользователь "
                "| с АРМ \"1001 - Администратор\" или \"1016 - Сотрудник HD -открытие дня\" ");
    end;
end;

/*** ***/
main ();
exit (1);