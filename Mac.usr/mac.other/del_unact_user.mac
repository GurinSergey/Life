/*************************************************************************/
/*     Дополнение к автоматизированной банковской системе RS-Bank        */
/*                                                                       */
/*    Имя файла        : del_unact_user.mac                              */
/*                                                                       */
/*    Описание         : I-00046198 Тема: Нужен макрос для периодической */
/*                       проверки таблицы привязки к типовым АРМ         */
/*                                                                       */
/*    Программист      : Котов С.С.                                      */
/*                                                                       */
/*    Создан           : 12.05.2011                                      */
/*                                                                       */
/*************************************************************************/

/*
Подробное описание: Нужен макрос проверки таблицы person_lnk.
 Макрос должен делать удаление из таблицы пользователей, 
номера которых   заблокированы или  отсутствуют  в списке
работающих пользователей в п/с "Сервис ГКБО".
подобные несоответствия косвенно выявляются в arm_user.mac.
*/

import oralib;
var select,rs,CmdText,Command;

if ( gettrue(true,"Удалить всех неактивных||пользователей из person_lnk?"))
  select = "select lnk.t_user, \n" +
           "       nvl((select t_userblocked from dperson_dbt p where p.t_oper=lnk.t_user),' ') b, \n" +
           "       nvl((select t_userclosed  from dperson_dbt p where p.t_oper=lnk.t_user),' ')  c, \n" +
           "       (select case count(*) when 0 then 'X' else ' ' end from dperson_dbt p where p.t_oper=lnk.t_user) n \n" +
           "  from dperson_lnk_dbt lnk \n" +
           " where lnk.t_user not in \n" +
           "       (select t_oper \n" +
           "          from dperson_dbt person \n" +
           "         where nvl(person.t_userblocked,chr(0)) != chr(88) \n" +
           "           and nvl(person.t_userclosed,chr(0)) != chr(88))" +
           " order by t_user";

  rs = execSQLselect( select, null, false );
      [];
      [Протокол процедуры удаления неактивных операционистов];
      [];
      [┌────────────┬───────────────────────────────────┐];
      [│            │   Причина удаления операциониста  │];
      [│Операционист├──────────┬──────────┬─────────────┤];
      [│            │Блокировка│ Закрытие │Не существует│];
      [├────────────┼──────────┼──────────┼─────────────┤];

  while( rs and rs.moveNext())
    if (valtype(rs.value(0)) != 26)
      [│  ########  │    #     │    #     │      #      │]
       (rs.value(0),rs.value(1),rs.value(2),rs.value(3));
      CmdText = "delete from dperson_lnk_dbt where t_user="+rs.value(0);
              
      Command = RSDCommand(CmdText);
      Command.Execute();

    end;
  end;

      [└────────────┴──────────┴──────────┴─────────────┘];

end;