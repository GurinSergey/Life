/************************************************************************************************************************
  Название макроса: copy_makets.mac
  Описание        : Копирование индивидуальных макетов одного пользователя системы другому 
  Номер заявки    : R-221645-22 от 23.07.2013
  Автор           : DPN
  Изменения       : - 
************************************************************************************************************************/


import RsbFormsInter, PaymInter, MoCommon, prnfrm, BankInter, RsbDataSet, RSD, globals, OprInter, KeyCodes, RCW, rsexts;
import  LikePy , oralib;
var idPDoc: Integer = 0;
class Copier()
    macro copyMakets(oldOper: Integer, newOper: Integer, idPDoc):Bool
        var insMakDoc, selOld, selNew, insDataFld, makIdOld, makIdNew, queryOld, queryNew, insMakFld, oldMakTabFld, newMakTabFld, fieldOld, fieldNew, insFldVal;
        var parameters, selCount, condition, queryCount, count, i = 0, selUnic;
        if (idPDoc == 0)
            condition = "";
        else
            condition = " AND DOC.T_DOCKIND =" + idPDoc;
        end;
//----------------Берём все макеты старого пользователя, заодно проверяем есть ли они у него вообще-----------------------       
        selOld = " SELECT doc.T_AUTOKEY " +
                 " FROM dmaketdoc_dbt doc" +
                 " WHERE doc.T_OPER =:oldOp" +
                   condition +
                 " Order by doc.T_AUTOKEY DESC";
        parameters = makeArray(SQLParam ("oldOp", int(oldOper)));
        queryOld = execSQLSelect(selOld,parameters);
        
        selCount = " select count(1) c from (" + selOld + ")";
        parameters = makeArray(SQLParam ("oldOp", Int(oldOper)));
        queryCount = execSQLSelect(selCount,parameters);
        
        queryCount.movenext;
        count = Int(queryCount.value("c"));   
        
        if (count == 0)           
            msgbox("У этого пользователя нет индивидуальных макетов по этому первичному документу");
            return FALSE;
        else
//-------------------Проверяем на наличие одинаковых макетов друг у друга------------------------
            selUnic = " SELECT DOC.T_AUTOKEY" +
                      " FROM dmakettabfld_dbt fld, dmaketdoc_dbt doc,dmakettabfldval_dbt valu," +
                      "      ((SELECT   mfld.T_FMT_TABLE, MFLD.T_FMT_FIELD, val.t_value" +
                      "        FROM   dmaketdoc_dbt mdc, dmakettabfld_dbt mfld, DMAKETTABFLDVAL_DBT val" +
                      "        WHERE   mdc.t_oper = :oldOp" +
                      "        AND mdc.T_AUTOKEY = mfld.T_MAKETID" +
                      "        AND MFLD.T_FIELDID = VAL.T_FIELDID)" +
                      "        MINUS" +
                      "       (SELECT MFLD.T_FMT_TABLE, MFLD.T_FMT_FIELD, val.t_value" +
                      "        FROM   dmaketdoc_dbt mdc, dmakettabfld_dbt mfld, DMAKETTABFLDVAL_DBT val" +
                      "        WHERE   mdc.t_oper = :newOp" +
                      "        AND mdc.T_AUTOKEY = MFLD.T_MAKETID" +
                      "        AND mfld.T_FIELDID = VAL.T_FIELDID)) result" +
                      " WHERE fld.T_FMT_TABLE = result.T_FMT_TABLE" +
                      " AND fld.T_FMT_FIELD = result.T_FMT_FIELD" +
                      " AND doc.T_OPER = :oldOp" +
                      " AND DOC.T_AUTOKEY = FLD.T_MAKETID" +
                      " AND valu.t_value = result.t_value" +
                      " and VALU.T_FIELDID=FLD.T_FIELDID" +
                       condition +
                      " group by DOC.T_AUTOKEY" +
                      " order by DOC.T_AUTOKEY ";
            parameters = makeArray(SQLParam ("oldOp", Int(oldOper)),
                                   SQLParam ("newOp", int(newOper)));
            var cmdUnic = execSQLSelect(selUnic, parameters);
            
            selCount = " select count(1) c from (" + selUnic + ")";
            parameters = makeArray(SQLParam ("oldOp", Int(oldOper)),
                                   SQLParam ("newOp", int(newOper)));
            queryCount = execSQLSelect(selCount,parameters,TRUE);
            queryCount.movenext;
            count = Int(queryCount.value("c"));
 
           
            if (count == 0)
                 msgbox("У выбраных пользователей все макеты одинаковые");
                 return FALSE;
            else
                InitProgress( count, " Ctrl+Brk - Прервать", "Копированте макетов" ); 
                while (cmdUnic.movenext)
                    var makId = cmdUnic.value("T_AUTOKEY");
                    insMakDoc = " INSERT INTO dmaketdoc_dbt( T_DOCKIND, T_CHAPTER, T_OPER, T_NAME, T_NAMEPANEL, T_SORT, T_DEPARTMENT)" +
                                " SELECT   mdoc.T_DOCKIND, mdoc.T_CHAPTER,:newOp, mdoc.T_NAME, MDOC.T_NAMEPANEL, MDOC.T_SORT, MDOC.T_DEPARTMENT" +
                                " FROM dmaketdoc_dbt mdoc" +
                                " WHERE MDOC.T_OPER =:oldOp"
                                " AND mdoc.T_AUTOKEY = " + makid +
                                " ORDER BY mdoc.T_DOCKIND";    
                    parameters = makeArray(SQLParam ("newOp", Int(newOper)),
                                           SQLParam ("oldOp", int(oldOper)));
                    var cmdOld = execSQL(insMakDoc,parameters);  
                end;
       
                selNew =  " SELECT  DMak.T_AUTOKEY" +
                          " FROM dmaketdoc_dbt dmak" +
                          " WHERE DMak.T_OPER = :newOP" +
                          " and DMak.T_AUTOKEY not in (Select DM.T_AUTOKEY" +
                          "                           from dmaketdoc_dbt dm, dmakettabfld_dbt field" +
                          "                           where DM.T_OPER = :newOP" +
                          "                           and DM.T_AUTOKEY = FIELD.T_MAKETID)" +
                          " ORDER BY dmak.T_AUTOKEY DESC";
       
                parameters = makeArray(SQLParam ("newOp", Int(newOper)));
                queryNew = execSQLSelect(selNew,parameters);
                     
                while(queryOld.movenext and  queryNew.movenext)
                    makIdOld = queryOld.value("T_AUTOKEY");
                    makIdNew = queryNew.value("T_AUTOKEY");
                    useprogress(i);
                    i = i + 1;

                    insDataFld = " INSERT INTO dmakettabfld_dbt (T_MAKETID, T_FMT_TABLE, T_FMT_FIELD, T_TYPE, T_SIZE)" 
                                 " SELECT :makIdN, T_FMT_TABLE, T_FMT_FIELD, T_TYPE, T_SIZE"
                                 " FROM dmakettabfld_dbt mf" +
                                 " WHERE mf.T_MAKETID =: makIdO";
                    parameters = makeArray(SQLParam ("makIdN", Int(makIdNew)),
                                           SQLParam ("makIdO", int(makIdOld)));
                     
                    var cmdDataFld = execSQL(insDataFld,parameters);
            
                    insMakFld = " INSERT INTO DMAKETFLD_DBT (T_MAKETID,T_NAMEFIELD, T_LOCKFIELD)" + 
                                " SELECT :makIdN , T_NAMEFIELD, T_LOCKFIELD" +
                                " FROM DMAKETFLD_DBT" +
                                " WHERE T_MAKETID =: makIdO";
                    parameters = makeArray(SQLParam ("makIdN", Int(makIdNew)),
                                           SQLParam ("makIdO", int(makIdOld)));            
                    var cmdMAkFld = execSQL(insMakFld, parameters);
   
                    oldMakTabFld = " SELECT T_FIELDID" +
                                   " FROM dmakettabfld_dbt" +
                                   " WHERE T_MAKETID =:makIdO";
                    parameters = makeArray(SQLParam ("makIdO", int(makIdOld)));
                    var queryOldMakTabFld = execSQLSelect(oldMakTabFld, parameters);
          
                    newMakTabFld = " SELECT T_FIELDID" +
                                   " FROM dmakettabfld_dbt" +
                                   " WHERE T_MAKETID =: makIdN";
                    parameters = makeArray(SQLParam ("makIdN", Int(makIdNew)));
                    var queryNewMakTabFld = execSQLSelect(newMakTabFld, parameters);
        
                    while (queryOldMakTabFld.movenext)
                        queryNewMakTabFld.movenext;
                        fieldOld = queryOldMakTabFld.value("T_FIELDID");
                        fieldNew = queryNewMakTabFld.value("T_FIELDID");
            
                        insFldVal = " INSERT INTO DMAKETTABFLDVAL_DBT (T_FIELDID, T_ROWKEY, T_VALUE)" +
                                    " SELECT :fieldN , T_ROWKEY, T_VALUE" +
                                    " FROM DMAKETTABFLDVAL_DBT" +
                                    " WHERE T_FIELDID =: fieldO";
                        parameters = makeArray(SQLParam ("fieldN", Int(fieldNew)),
                                               SQLParam ("fieldO", int(fieldOld)));
                        var cmdFldVal = execSQL(insFldVal, parameters);
                    end;
                end;
                remProgress(i);
                println("Копирование макетов выполнено успешно");
            end;            
            println("Было скопировано " + count + " макетов");
            return TRUE;
        end;
    end;
end;

class dialogWindow()
    private const FT_INTEGER = 0;
    private const FT_STRING  = 7;
    private const  allVal: String = "Все макеты";
    
    var mainPanel:TRsbPanel;
    
    var labelOldOper: TRsbLabel = TRsbLabel(5, 3, "Введите номер старого пользователя:");
    var labelNewOper: TRsbLabel = TRsbLabel(6, 5, "Введите номер нового пользователя:");
    var labelPrimaryDoc: TRsbLabel = TRsbLabel(8, 1, "Выберете первичный документ:");
    
    var fieldOldOper: TRsbEditField = TRsbEditField(FT_INTEGER);
    var fieldNewOper: TRsbEditField = TRsbEditField(FT_INTEGER);
    var m_Combo:TRsbComboBox = TRsbComboBox();
    
    
    var selDoc, querySelDoc, cmd, startValue = 0, startDoc = "";
    
    mainPanel.setSize(35,7);
    mainPanel.setPosition(43,15);
    
    mainPanel.setCaption("Передача индивидуальных макетов пользователей");
    mainPanel.setStatus("F2 - Выполнить, F3 - Выбор первичного документа, Esc - Выход");
    
    fieldOldOper.bindValue(this, "startValue", 5);
    fieldOldOper.setPosition(20,4);   
    fieldOldOper.setSize(5,1);
    fieldOldOper.onKeyPressed(R2M(this, "doIt")); 
    
    fieldNewOper.bindValue(this, "startValue", 5);
    fieldNewOper.setPosition(20,6);   
    fieldNewOper.setSize(5,1);
    fieldNewOper.onKeyPressed(R2M(this, "doIt")); 

    m_Combo.setPosition(7,2); 
    m_Combo.setSize(22,1); 

    selDoc = " select DOC.T_DOCKIND id, PD.T_NAME Name" +
             " from dmaketdoc_dbt doc, doprkdoc_dbt pd" +
             " where DOC.T_DOCKIND = PD.T_DOCKIND" +
             " AND DOC.T_DOCKIND != 261" +
             " AND DOC.T_DOCKIND != 262" +
             " GROUP BY DOC.T_DOCKIND, PD.T_NAME";
    cmd = RSDCommand(selDoc);
    querySelDoc =  RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_Static );
    
    
    
    while (querySelDoc.movenext)
        var id = querySelDoc.value("id");
        var name = querySelDoc.value("Name");
        m_Combo.addChoice(id, name); 
    end;  
    m_Combo.addChoice(0, allVal);
    m_Combo.value = allVal;
    m_Combo.OnItemSelected(R2M(this, "onItemSelect")); 

     
    
    mainPanel.addlabel(labelOldOper);
    mainPanel.addlabel(labelNewOper);
    mainPanel.addlabel(labelPrimaryDoc);
    
    mainPanel.addControl(fieldOldOper);
    mainPanel.addControl(fieldNewOper);
    mainPanel.addControl(m_Combo);
    
    macro checkOper(operId:Integer)
        var param, select, queryOper, out;
        select = " select PRS.T_NAME" +
                 " from dperson_dbt prs" +
                 " where prs.T_OPER = :oper";
        param = makeArray(SQLParam ("oper", Int(operId)));
        queryOper = execSQLSelect(select, param);
        
        if (queryOper.movenext)
            out = TRUE;
        else 
            out = FALSE;
        end;
        return out;
    end;
    macro onItemSelect(RsbEvent:object) 
        m_Combo.currentChoice;
        idPDoc = m_Combo.currentChoice;
    end; 

    macro doIt(key:TRsbKeyPressedEvent)
    var copyMak:Copier,newVal, oldVal;
        if(key.KeyCode == KEY_F2)
            oldVal = fieldOldOper.value;
            newVal = fieldNewOper.value;
            if((checkOper(oldVal) == TRUE) and (checkOper(newVal) == TRUE))
                if (copyMak.copyMakets(oldVal, newVal, idPDoc) == TRUE);
                    mainPanel.close(1);
                end;
            else
                msgbox("Пользователя с таким номером не существует!");
            end;
        end;
        if(key.keyCode == KEY_ESC)
            mainPanel.close(1);
        end;
    end;        
    
    
    
    macro start()
        mainPanel.run();
    end;
end;
    

var newPanel: dialogWindow;
newPanel.start;
