// -------------------------------------------------------------------------------------------------
// @filename: NoticeClaimant.mac
// @author  : Zlenko M.P.
// @desc    : "Письмо взыскателю"
// @request : C-8696
// @modify  : 
// -------------------------------------------------------------------------------------------------
Import ldmklbr, globals, BankInter, RSD, KeyCodes, PaymInter,SprAcc;
import "lib_registry.mac", "lib_oper.mac";

private macro CreateWordApplication()
var startAX, WordApplication;        
  if (isStandAlone())
     return ActiveX("Word.Application");
  else
     startAX = CreateObject("rsax", "TRsAxServer", "LoansAxServer", isStandalone());
     WordApplication = startAX.CreateComObject("Word.Application"); 
     /* EVG Не нужно сразу показывать Word.
     WordApplication.Visible = true;*/
     return WordApplication;
  end;    
end;

private macro ShowReportOnTerminal( WordApp, WordDoc )  
  var DocFileName : string, TermFileName : string;    
  DocFileName = SplitFile( GetTxtFileName("") );      
  DocFileName = MergeFile( DocFileName, "nc_" + string(UserNumber), ".doc" );
  DocFileName = MergeFile( GetCurDir(), DocFileName );     
  WordApp.visible = true; 
end;  

macro getShortName(Oper)
var cmd, rs; 
cmd = RSDCommand("select p.t_ShortName from dparty_dbt p, dperson_dbt o where o.t_partyid = p.t_partyid and o.t_oper = ?");
cmd.AddParam("1", RSDBP_IN, Oper);
rs = RsdRecordset (cmd);
    if (rs and rs.moveNext ())
        return rs.value(0);
    else
        return "";
    end; 
end;


macro setFields(t :TRecHandler, PaymentID :Integer)
var cmd; 
var sqlText = " MERGE INTO NoticeClaimant_usr nc        " +
              "      USING (SELECT 1 FROM DUAL) o       " +
              "         ON (nc.t_paymentid =  ?)        " +
              " WHEN MATCHED                            " +
              " THEN                                    " +
              "    UPDATE SET                           " +
              "       nc.T_Claimant          = ?,       " +
              "       nc.t_AddressClaimant   = ?,       " +
              "       nc.t_ExecuteDoc        = ?        " +
              " WHEN NOT MATCHED                        " +
              " THEN                                    " +
              "    INSERT (nc.T_paymentid,              " +
              "            nc.T_Claimant,               " +
              "            nc.t_AddressClaimant,        " +
              "            nc.t_ExecuteDoc)             " +
              "    VALUES (?,?,?,?)                     " ;  

  cmd = RsdCommand(sqlText);
  cmd.AddParam("1", RSDBP_IN, PaymentID);
  cmd.AddParam("2", RSDBP_IN, trim(t.("Claimant")));
  cmd.AddParam("3", RSDBP_IN, trim(t.("AddressClaimant")));
  cmd.AddParam("4", RSDBP_IN, trim(t.("ExecuteDoc")));
  cmd.AddParam("5", RSDBP_IN, PaymentID);
  cmd.AddParam("6", RSDBP_IN, trim(t.("Claimant")));
  cmd.AddParam("7", RSDBP_IN, trim(t.("AddressClaimant")));
  cmd.AddParam("8", RSDBP_IN, trim(t.("ExecuteDoc")));

  cmd.execute;
end;

macro Rep(t :TRecHandler, PaymentID :integer)
var Payment :RsbPayment = RsbPayment(PaymentID);
var templPath;
var WordApp = CreateWordApplication();
var WordDoc;
if(not (templPath = findpath("NoticeClaimant.dot", RSL_GetRegistryValue("BANK_INI/ОБЩИЕ ПАРАМЕТРЫ/ДИРЕКТОРИИ/USERTEMPLSDIR"))))
   msgBox("Не удалось найти шаблон");
   exit();
end;
   if (WordApp)
      WordDoc = WordApp.Documents.Add(templPath);
      WordDoc.Bookmarks("Claimant").Range.Text        =  trim(t.("Claimant"));
      WordDoc.Bookmarks("AddressClaimant").Range.Text =  trim(t.("AddressClaimant"));
      WordDoc.Bookmarks("ExecuteDoc").Range.Text      =  trim(t.("ExecuteDoc"));
      WordDoc.Bookmarks("PayerAccount").Range.Text    =  Payment.PayerAccount;
      WordDoc.Bookmarks("PayerName").Range.Text       =  Payment.PayerName;
      WordDoc.Bookmarks("PayerINN").Range.Text        =  Payment.PayerINN;
      WordDoc.Bookmarks("Date").Range.Text            =  Payment.Date; 
      WordDoc.Bookmarks("Amount").Range.Text          =  Payment.PayerAmount;
      WordDoc.Bookmarks("Oper").Range.Text            =  getShortName({OPER});
    
      if( IsStandAlone() )
         WordApp.Visible = TRUE;
      else
         ShowReportOnTerminal( WordApp, WordDoc );
      end; 

   end;
end;

Class (TRecHandler) TParm(LbrName, PaymentID :Integer)
   InitTRecHandler ("NC", (LbrName), true);
   var Payment:RsbPayment = RsbPayment(PaymentID);
   var ParentPaymentID :integer =PaymentID; 
   debugbreak; 
macro TParm_init()
   var cmd, rs; 
   cmd = RSDCommand("Select T_INITIALPAYMENT from dpmlink_dbt where T_PURPOSEPAYMENT = :ID");
   cmd.AddParam("1", RSDBP_IN, Payment.PaymentID );
   rs = RsdRecordset (cmd);
   if (rs and rs.moveNext()) ParentPaymentID = rs.value(0)  end;
   cmd = RSDCommand("SELECT  decode(T_Claimant,null,chr(1),T_Claimant) Claimant, decode(t_AddressClaimant,null,chr(1),t_AddressClaimant) Address, decode(t_ExecuteDoc,null,chr(1),t_ExecuteDoc) ExecuteDoc  FROM  NoticeClaimant_usr ldp  WHERE  ldp.t_paymentid = ? ");
   cmd.AddParam("1", RSDBP_IN,ParentPaymentID );
   rs = RsdRecordset (cmd);
       if (rs and rs.moveNext ())
          this.("Claimant") = rs.value("Claimant");
          this.("AddressClaimant") = rs.value("Address");
          this.("ExecuteDoc") = rs.value("ExecuteDoc");
       else
          this.("Claimant") = Payment.ReceiverName;
          this.("AddressClaimant") = "";
          this.("ExecuteDoc") = "";
       end;
end;

TParm_INIT();    

MACRO Event (dlg, cmd, id, key)  
   if (cmd == DLG_KEY)
      if ( KEY == KEY_F9 ) 
         setFields(this, ParentPaymentID);
         return CM_IGNORE;
      elif ( KEY == KEY_F2 )
         setFields(this, ParentPaymentID);   
         Rep(this, Payment.PaymentID);
         return CM_CANCEL;    
      elif ( KEY == KEY_ESC )
         return CM_CANCEL;
      else
  //       return CM_IGNORE;
      end;  
   end; 
end;

   Macro Run
      return RunDialog (this, R2M (this, "Event"));
   End;

End;

/*Точка входа*/
macro NoticeClaimant(PaymentID :Integer)
   /*29.01.2012 zmp I-00317863 сделал вызов статической LBR*/
   var LbrPath;
   if(not (LbrPath = findpath("NoticeClaimant.lbr", RSL_GetRegistryValue("BANK_INI/ОБЩИЕ ПАРАМЕТРЫ/ДИРЕКТОРИИ/LBRDIR"))))
      msgBox("Не удалось найти LBR");
      exit();
   end;
   Var Parm = TParm(LbrPath, PaymentID);
   if ( not Parm.Run () )
      exit ();
   end;
end;
//NoticeClaimant(32323);
