// Gurin S. N. I-00249053-2 06.09.2012
// KS 22.11.2013 Предварительная адаптация под 31ю сборку
import BankInter, PaymInter, rsd, globals;

private macro УзелТС(PaymentObj, kind)  

var SQL, cmd, rs;
var acc = "", chpt = 0;

  /* Определяем, какой счет брать */
  if  (kind == 1)
    acc = PaymentObj.PayerAccount;
  elif(kind == 2)
    acc = PaymentObj.ReceiverAccount;
  end;
  chpt = PaymentObj.Chapter;

  /* Ищем обслуживающее подразделение ТС по выбранному лицевому счету */
  SQL = "SELECT t_branch as node FROM daccount_dbt WHERE t_chapter = ? AND t_account = ?";

  cmd = RsdCommand(SQL);
  cmd.AddParam("chapter", RSDBP_IN, chpt);
  cmd.AddParam("account", RSDBP_IN, acc);
  cmd.execute();

  rs = RsdRecordSet(cmd);

  if (rs.MoveNext)
      return int(rs.Value("node"));
  else
      return ""; //Gurin S. N. I-00249053-2 06.09.2012 320 документ, невозможно определить branch счета, они не наши.. ничего умнее не придумал как возвратить пусто.
  end;

end;


macro GetPmTSNode(PaymentObj)

var SQL, cmd, rs;
var node = 1; /* По умолчанию */ 

  if ((PaymentObj.IsExternal) and (not PaymentObj.IsTransit)) /* Только для внешних нетранзитных платежей */
     if   (PaymentObj.InCorschem != -1)  /* Входящий платеж */
       node = УзелТС(PaymentObj, 2);     /* Конечный узел ТС получателя   */
     elif (PaymentObj.OutCorschem != -1) /* Исходящий платеж */
       node = УзелТС(PaymentObj, 1);     /* Начальный узел ТС плательщика */
     end;
  end;

return node;

end;