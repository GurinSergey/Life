/************************************************************************************************************************
  Название макроса: menu_ARM_report.mac
  Описание        : Отчёт отбирает образцы меню, где ИСПОЛЬЗУЕТСЯ заданный модуль. В интефейсе вводится название подсистемы,
                    в которой этот модуль был СОЗДАН.  Отчёт работает одинаково и для пользовательских и для системных модулей. 
  Номер заявки    : R-230613-2 от 12.08.2013
  Автор           : DPN
  Изменения       : 15.08.2014 R-369640-2 DPN Расширил поле ввода номера модуля
************************************************************************************************************************/



import RsbFormsInter, PaymInter, MoCommon, prnfrm, BankInter, RsbDataSet, RSD, globals, OprInter, KeyCodes;
import  LikePy , oralib;
private const ON = 1, OFF = 0;
var symbApp: String = "", module, application, symb, carry = OFF, argument = -1;
class MenuARM()
    macro reportMenuARM(idModul: Integer, symbApp: String):Bool
        var condition, selectARM, parameters, queryARM, nameMenu, nameApp, counter = 0, selectCount, queryCount;
        if (symbApp == "")
            msgbox("Не выбрана подсистема!");           
        else
            BegAction(1, "Ждите, идет формирование отчета..", FALSE);
            if (carry == 0)
                selectARM = " SELECT DISTINCT mitem.t_objectid, menu.t_name, tac.t_name_type" +
                            " FROM dmenuitem_dbt mitem, ditemuser_dbt itus, dmenutpl_dbt menu, dtypeac_dbt tac" +
                            " WHERE     mitem.t_icaseitem = :modid" +
                            " AND mitem.t_iprogitem = ASCII (:symbol)" +
                            " AND CHR (mitem.t_iprogitem) = itus.t_cidentprogram" +
                            " AND itus.t_icaseitem = mitem.t_icaseitem" +
                            " AND menu.t_menuid = mitem.t_objectid" +
                            " AND tac.t_inumtype = 22" +
                            " AND tac.t_type_account = CHR (mitem.t_iidentprogram)" +
                            " AND menu.t_identprogram LIKE '%' || CHR (mitem.t_iidentprogram) || '%'" +
                            " AND mitem.T_ISTEMPLATE = 'X'" +
                            " ORDER BY mitem.t_objectid";
            else
                selectARM = " SELECT DISTINCT mitem.t_objectid, menu.t_name, tac.t_name_type" +
                            " FROM dmenuitem_dbt mitem, ditemsyst_dbt itus, dmenutpl_dbt menu, dtypeac_dbt tac" +
                            " WHERE     mitem.t_icaseitem = :modid" +
                            " AND mitem.t_iprogitem = ASCII (:symbol)" +
                            " AND CHR (mitem.t_iprogitem) = itus.t_cidentprogram" +
                            " AND itus.t_icaseitem = mitem.t_icaseitem" +
                            " AND menu.t_menuid = mitem.t_objectid" +
                            " AND tac.t_inumtype = 22" +
                            " AND tac.t_type_account = CHR (mitem.t_iidentprogram)" +
                            " AND menu.t_identprogram LIKE '%' || CHR (mitem.t_iidentprogram) || '%'" +
                            " AND mitem.T_ISTEMPLATE = 'X'" +
                            " ORDER BY mitem.t_objectid";
            end;

            parameters = makeArray(SQLParam ("modID", idModul),
                                   SQLParam ("symbol", symbApp));
            queryARM = execSQLSelect(selectARM, parameters);
            EndAction(2);
            
            nameMenu = "\"" + module + "\"";
            nameApp = "\"" + application + "\"";
               [
                   ########## ########

                   Список образцов меню, в который входит модуль ######
                   ############################################################
                   созданный в подсистеме ###########################################################
   
                   ┌──────────────┬────────────────────────────────────────────────┬─────────────────────────────────────────┐
                   │Номер образца │              Наименование образца              │       Наименование подсистемы,          │
                   │    меню      │                      меню                      │    в которой используется модуль        │
                   ├──────────────┼────────────────────────────────────────────────┼─────────────────────────────────────────┤] (Date, Time, idModul:l, nameMenu, nameApp); 

            while (queryARM.moveNext)
                if ((counter > 0) and (argument != queryARM.value(0)))
               [   ├──────────────┼────────────────────────────────────────────────┼─────────────────────────────────────────┤];
                end;
               [   │ ############ │################################################│######################################## │] (queryARM.value(0):c, queryARM.value(1):c, queryARM.value(2):c);
                counter = counter +1;
                
                  argument = queryARM.value(0);
            end;
               [   └──────────────┴────────────────────────────────────────────────┴─────────────────────────────────────────┘
                   Всего записей: #####] (counter:l);                
            return TRUE;
        end;
    end;
end;

class dialogWindow()
    private const FT_INTEGER = 0;
    private const FT_STRING  = 7;
    
    var mainPanel:TRsbPanel;
    
    var labelSyst: TRsbLabel = TRsbLabel(2, 1, "Сист./Польз.");
    var labelApplication: TRsbLabel = TRsbLabel(16, 1, "Выберите подсистему:");
    var labelModule: TRsbLabel = TRsbLabel(4, 3, "Введите номер модуля:");
    
    var fieldModule: TRsbEditField = TRsbEditField(FT_INTEGER);
    var m_Combo:TRsbComboBox = TRsbComboBox();
    var chBoxSyst:TRsbCheckBox = TRsbCheckBox;
    
    chBoxSyst.setPosition(5,2);
    chBoxSyst.onChecked(R2M(this, "onCheckedBox"));
    
    var selApp, querySelApp, cmd, startValue = 0, startApp = "", i:Integer = 1, symbArray = tarray(true);;
    
    mainPanel.setSize(34,6);
    mainPanel.setPosition(46,18);
    
    mainPanel.setCaption("Отчёт по образцам меню");
    mainPanel.setStatus("F2 - Выполнить, F3 - Вызов справочника подсистем, SPACE - Перключение пользовательских и системных модулей, Esc - Выход");

    fieldModule.bindValue(this, "startValue", 5);
    fieldModule.setPosition(4,4);   
    fieldModule.setSize(5,1); //15.08.2014 R-369640-2 DPN 
    fieldModule.onKeyPressed(R2M(this, "doIt")); 

    m_Combo.setPosition(9,2); 
    m_Combo.setSize(22,1); 

    selApp = " SELECT TAC.T_TYPE_ACCOUNT, TAC.T_NAME_TYPE" +
             " FROM dtypeac_dbt tac" +
             " WHERE TAC.T_INUMTYPE = 22" +
             " AND TAC.T_TYPE_ACCOUNT in ('Н', 'У', 'Г', 'W', 'Ы', 'I', 'Ч', 'С', 'Е')" +
             " ORDER BY TAC.T_TYPE_ACCOUNT";
    cmd = RSDCommand(selApp);
    querySelApp =  RSDRecordset(cmd, RSDVAL_CLIENT, RSDVAL_Static );
  
    while (querySelApp.movenext)
        symbArray(i) = querySelApp.value("T_TYPE_ACCOUNT");
        var name = querySelApp.value("T_NAME_TYPE");
        m_Combo.addChoice(i, name); 
        i = i + 1;
    end; 
    
    m_Combo.value = "";
    m_Combo.OnItemSelected(R2M(this, "onItemSelect")); 

    mainPanel.addlabel(labelApplication);
    mainPanel.addlabel(labelModule);
    mainPanel.addlabel(labelSyst);
    
    mainPanel.addControl(chBoxSyst);
    mainPanel.addControl(m_Combo);
    
   
    
    mainPanel.addControl(fieldModule);
    
    macro onCheckedBox(RsbEvent:Object)
        if (RsbEvent.Id == RSB_EV_CONTROL_CHECKED)
            if (chBoxSyst.checked == TRUE)
                carry = ON;
            else
                carry = OFF;
            end;
        end;
        return true;
    end;
    
     macro checkModul(moduleId:Integer)
        var param, selectModule, queryModule, out;
        if (carry == 0)   
            selectModule = " SELECT ITUS.T_ICASEITEM, ITUS.T_SZNAMEITEM" +
                           " FROM ditemuser_dbt itus" +
                           " WHERE ITUS.T_ICASEITEM =:modID"
                           " AND T_CIDENTPROGRAM ='" + symb + "'";
        else                
            selectModule = " SELECT itus.t_icaseitem, itus.t_sznameitem" +
                           " FROM ditemsyst_dbt itus" +
                           " WHERE itus.t_icaseitem = :modid " +
                           " AND t_cidentprogram ='" + symb + "'";
        end;
        param = makeArray(SQLParam ("modID", Int(moduleID)));
        queryModule = execSQLSelect(selectModule, param);
        
        if (queryModule.movenext)
            module = queryModule.value("T_SZNAMEITEM");
            out = TRUE;
        else 
            out = FALSE;
        end;
        return out;
    end;
    
    macro onItemSelect(RsbEvent:object) 
        var selectNameApp, queryNameApp;
        m_Combo.currentChoice;
        i = m_Combo.currentChoice;
        symbApp = symbArray(i);
        selectNameApp = " SELECT TAC.T_NAME_TYPE, tac.T_TYPE_ACCOUNT" +
                        " FROM dtypeac_dbt tac" +
                        " WHERE TAC.T_INUMTYPE = 22" +
                        " AND TAC.T_TYPE_ACCOUNT ='" + symbApp + "'" +
                        " ORDER BY TAC.T_TYPE_ACCOUNT";
        queryNameApp = trsbDataSet(selectNameApp);
        queryNameApp.movenext;
        application = queryNameApp.value(0);
        symb = queryNameApp.value(1);
    end; 

    macro doIt(key:TRsbKeyPressedEvent)
        var idModul, reporter: MenuARM;
        if(key.KeyCode == KEY_F2)
            idModul = fieldModule.value;
            if(checkModul(idModul) == TRUE)
                if (reporter.reportMenuARM(idModul, symbApp) == TRUE);
                    mainPanel.close(1);
                end;
            elif(carry == 0)
                msgbox("В данной подсистеме нет пользовательского модуля с таким номером!");
            elif(carry == 1)
                msgbox("В данной подсистеме нет системного модуля с таким номером!");
            end;
        end;
        if(key.keyCode == KEY_ESC)
            mainPanel.close(1);
        end;
    end;        
    
    
    
    macro start()
        mainPanel.run();
    end;
end;

var newPanel: dialogWindow;
newPanel.start;
