/************************************************************************/
/*          Автоматизированная банковская система RS-Bank v6.0          */
/*               Copyright (c) R-Style    Vologda                       */
/*  Имя файла        :                                                  */
/*  Описание         : макрос отчета "переоценкиa ин.валютных счетов раздела 5  */
/*  Программист      : Вихров Д.В.                                      */
/*  Создан           : 10.12.2009                                       */
/*  исправлен          18.12.2009                                       */
/*  25.06.2012  AAN  I-00212845-2    поправил работу с Екселем          */
/*  чтобы можно было открывать сколько угодно много екселей одновременно*/
/*  07.12.2012  AAN I-00286365-3 продолжил переделку выгрузки в Ексель  */
/*  24.12.2012  AAN I-00304397-3 проблемы с форматированием и преобразованием форматов, были. */
/*VV C-23773 02.10.13                                                   */
/************************************************************************/
import globals, RsbDataSet, fiinter, rsexts;

var KEY_F2=316;
var KEY_F3=317;
var KEY_F9=323;
var KEY_ESC=27;
var KEY_ENTER=13;
var ob,ex,obBook,obSheet;
var out, output = "pereozenkainval.xls", fulloutput;                            
var path = "", pathfile = "",    filen = "pereozenka.lbr";
var exelstroka=3;
var i=0;
GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,path);
pathfile = FindPath(filen,path);
if (not pathfile)
   msgbox("Не найдена LBR");
   exit();
end;

var tempFileName:string = "", tempFileNameBezPuti:string ="";
const TEXTDIR_REESTR		= "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";
private var dd:date, tt:time, ii:integer, ss:string;  // это переменные для использования FileInfo

//msgbox(pathfile);
record FI( fininstr );
var dlg = TRecHandler("panel",pathfile,True);
var const_mess="~F2~ выполнить,  ~F3~ Выбор валюты  ~ESC~ выход";

macro chekReestrRead(m_name:string, m_status:integer, m_err:integer)
   if ((m_status == V_UNDEF) or (m_err != 0))
      msgbox("Ошибка получения значения настройки реестра " + m_name + " \n Процедура GetRegistryValue вернула код ошибки "+m_err);
      exit(0);
   end;
end;

macro dayString(m_reestrName:string):string
   private var m_errCode:integer = NULL;
   private var m_statusGetRegistry:integer = NULL;
   private var m_zna4enie:string  = NULL;
   if (m_reestrName == "")
      msgbox("При чтении реестра не задана строка реестра");
      exit(0)
   end;
   m_statusGetRegistry=GetRegistryValue(m_reestrName, V_STRING, m_zna4enie, m_errCode);
   chekReestrRead(m_reestrName, m_statusGetRegistry, m_errCode);
   return(m_zna4enie);
end;

macro createUniqueFile()      // генерируем уникальное имя для файла, используюя номер операциониста, дату и время
   private var ff:string = "pereozenkainval__"+{oper}+"_"+date+"_"+time;
   private var file_ext:string = ".txt";
   tempFileName = dayString(TEXTDIR_REESTR);
   tempFileName = tempFileName + "\\" + ff;
   tempFileNameBezPuti = ff;
   tempFileName = StrSubst ( tempFileName, ".", "_" );
   tempFileName = StrSubst ( tempFileName, ":", "_" );
   tempFileName = StrSubst ( tempFileName, " ", "_" );
   tempFileName = tempFileName + file_ext;
   tempFileNameBezPuti = StrSubst ( tempFileNameBezPuti, ".", "_" );
   tempFileNameBezPuti = StrSubst ( tempFileNameBezPuti, ":", "_" );
   tempFileNameBezPuti = StrSubst ( tempFileNameBezPuti, " ", "_" );
   tempFileNameBezPuti = tempFileNameBezPuti + file_ext;
   if (substr(tempFileName,1,2) == "__")
      tempFileName=".."+substr(tempFileName,3)
   end;
END;

macro copyToMe()  // копирование временного файла с данными с СП на лок машину юзера
   
   private var m_path: string = "";
   m_path = "$" + tempFileNameBezPuti;
   if (not CopyFile(tempFileNameBezPuti, m_path, TRUE))
      println ("НЕ СМОГ скопировать файл с сервера приложений на терминал. исходный файл: "+tempFileNameBezPuti+" файл назначения: " + m_path);
   else 
      println ("УСПЕШНО скопирован файл с сервера приложений на терминал. исходный файл: "+tempFileNameBezPuti+" файл назначения: " + m_path);
      if (not removeFile(tempFileName))
         println("Не смог удалить файл "+tempFileName+" с сервера приложений. ничего сташного, продолжаем работу.");
      else
         println("удален файл "+tempFileName+" с сервера приложений.");
      end;
   end;
END;

MACRO day_sql_for_mask(ms,payer,razdel)
   if ((payer==1) and (strlen(ms)==0) and (razdel==5))
      return " t_account_payer like '5%' " 
   end;
   if ((payer==0) and (strlen(ms)==0) and (razdel==5)) 
      return " t_account_receiver like '5%' "
   end;
   if ((payer==1) and (razdel==5)) 
      return ConvertMaskToSQLFormat(ms, " t_Account_payer "); 
   end;
   if ((payer==0) and (razdel==5)) 
      return ConvertMaskToSQLFormat(ms, " t_Account_receiver "); 
   end;
   if ((payer==0) and (razdel==7) and (strlen(ms)==0)) 
      return " t_account_receiver like '70603%' or t_account_receiver like '70608%' " 
   end;
   if ((payer==1) and (razdel==7) and (strlen(ms)==0)) 
      return " t_account_payer like '70603%' or t_account_payer like '70608%' " 
   end;
   if ((payer==0) and (razdel==7)) 
      return ConvertMaskToSQLFormat(ms, " t_Account_receiver "); 
   end;
   if ((payer==1) and (razdel==7)) 
      return ConvertMaskToSQLFormat(ms, " t_Account_payer "); 
   end;
END;

MACRO outputinexcel(mask, mask7);      // главна функция отчёта
   var rs;
    rs = RsdRecordSet(string("select t_name from dperson_dbt where t_oper = " + {oper}));
    rs.movenext;

   var mask_valuta;
   if (dlg.rec.val_int==V_UNDEF) 
      mask_valuta="___"; 
   else 
      mask_valuta=dlg.rec.val_int; 
   end;
   /*Gurin S. 25.04.2014 I-00483135-2 --2031 
   var sql1 = "select * from darhdoc_dbt where ( ("+day_sql_for_mask(mask,1,5)+") and t_account_payer like '_____"+mask_valuta+"%' "+
              "and ( "+day_sql_for_mask(mask7,0,7)+" ) "+
              "or ("+day_sql_for_mask(mask,0,5)+") and t_account_receiver like '_____"+mask_valuta+"%' and ( "+day_sql_for_mask(mask7,1,7)+") ) "+
            /* EVG 16/04/2012 Замена t_date_value -> t_date_carry
            "and t_date_value >= to_date ( '"+dlg.rec.date_begin+"', 'dd.mm.rrrr' ) "+
            "and t_date_value <= to_date ( '"+dlg.rec.date_end+"', 'dd.mm.rrrr' ) "+
            "and t_result_carry <> 23 order by t_date_value";*/
            "and t_date_carry >= to_date ( '"+dlg.rec.date_begin+"', 'dd.mm.rrrr' ) "+
            "and t_date_carry <= to_date ( '"+dlg.rec.date_end+"', 'dd.mm.rrrr' ) "+
            "and t_result_carry <> 23 order by t_date_carry"; */

   var sql1 = "select * from dacctrn_dbt where ( ("+day_sql_for_mask(mask,1,5)+") and t_account_payer like '_____"+mask_valuta+"%' "+
              "and ( "+day_sql_for_mask(mask7,0,7)+" ) "+
              "or ("+day_sql_for_mask(mask,0,5)+") and t_account_receiver like '_____"+mask_valuta+"%' and ( "+day_sql_for_mask(mask7,1,7)+") ) "+
            /* EVG 16/04/2012 Замена t_date_value -> t_date_carry
            "and t_date_value >= to_date ( '"+dlg.rec.date_begin+"', 'dd.mm.rrrr' ) "+
            "and t_date_value <= to_date ( '"+dlg.rec.date_end+"', 'dd.mm.rrrr' ) "+
            "and t_result_carry <> 23 order by t_date_value";*/
            "and t_date_carry >= to_date ( '"+dlg.rec.date_begin+"', 'dd.mm.rrrr' ) "+
            "and t_date_carry <= to_date ( '"+dlg.rec.date_end+"', 'dd.mm.rrrr' ) "+
            "and t_result_carry <> 23 order by t_date_carry";
   
   message (" производится выборка проводок, ждите...");
   var ds=trsbdataset(sql1);
   message (" выборка проводок закончена, формируется отчет, ждите...");
   
   createUniqueFile();
   FILE aa () txt write;
   open(aa, tempFileName);
   aa.str = " "; 
   insert(aa);
   
   
   while (ds.movenext())      // выгружаем данные полученные запросом во временный файл, построчно, вкручивая разделители столбцов ";"
      aa.str = substr(string(ds.value("t_date_carry")), 1, 10)
               + ";" + //"_" +
               strsubst(StrSubst(strsubst(string(ds.value("t_account_payer")),";"," "),strfor(10),""),strfor(13),"")
               + ";" + //"_" + 
               strsubst(StrSubst(strsubst(string(ds.value("t_account_receiver")),";"," "),strfor(10),""),strfor(13),"")
               + ";" + 
               //Gurin S. 25.04.2014 I-00483135-2 --2031 
               //round(money(ds.value("t_sum")),2);
               round(money(ds.value("t_sum_natcur")),2);
      aa.str = toansi(aa.str, true);
      insert(aa);
      i=i+1;
   end;
   
   close(aa);
   copyToMe();
   
   
   ob = CreateObject ("rsax","TRsAxServer", "RsAxServer", IsStandAlone());    //AAN
   ex = ob.CreateComObject ("Excel.Application", True);                                                    //AAN
   
   if (not(GetFileInfo("$"+tempFileNameBezPuti,@dd,@tt,@ii,@ss))); //ss- то что надо. полный путь абсолютный локальный
      msgbox("не cмог найти на локальном диске файл "+tempFileNameBezPuti+"          \n останавливаюсь");
      exit(0);
   end;
   ss = substr(ss,2);
   var aw=tarray;                                       // AAN 20.11.2012 массивы для последующего корректного открытия Екселя
   var w1=tarray;
   var w2=tarray;
   var w3=tarray;
   var w4=tarray;
   
   w1(0)=1; w1(1)=1; //формат столбца - текст
   w2(0)=2; w2(1)=2; //формат столбца - General
   w3(0)=3; w3(1)=2;//формат столбца - General
   w4(0)=4; w4(1)=2;//формат столбца - General     24.12.2012  AAN I-00304397-3
   
   aw(0)=w1;
   aw(1)=w2;
   aw(2)=w3;
   aw(3)=w4;
   
   aw.MarshalByVal =true; // работает только так. передача по значению через @aw вызывает ошибку
   w1.MarshalByVal =true;
   w2.MarshalByVal =true;
   w3.MarshalByVal =true;
   w4.MarshalByVal =true;
   
   obBook = ex.Workbooks.opentext(ss,1251,1,1,2,false,false,false,false,false,true,";",aw,1,","," ",true,true);
   
   ex.Sheets(1).Rows("1:1").Insert(-4121); 
   ex.Sheets(1).Rows("1:1").Insert(-4121); 
   ex.Sheets(1).Rows("1:1").Insert(-4121); 
   ex.Sheets(1).Rows("1:1").Insert(-4121); 
   ex.Sheets(1).Rows("1:1").Insert(-4121); 
   ex.Sheets(1).Rows("1:1").Insert(-4121);
   obSheet=ex.Sheets(1);
  // VV C-23773 02.10.13 
   obSheet.Cells(1,1).Value="Наименование банка";
   obSheet.Cells(1,2).Value=string({Name_Bank});
   obSheet.Cells(2,1).Value="Наименование регистра";
   obSheet.Cells(2,2).Value="Отчет о доходах/расходах, полученных от переоценки средств в иностранной валюте на счетах учета ценных бумаг (раздел 5)";
   obSheet.Cells(3,1).Value="Период составления";
   obSheet.Cells(3,2).Value="Период с " + dlg.rec.date_begin + " по " + dlg.rec.date_end;
   obSheet.Cells(4,1).Value="Денежные измерители операции в Рублях РФ";
   
   
    i=i+10;  
 
    obSheet.Cells(i,1).Value="Лицо, ответственное за составление регистра";
    i=i+1;
    obSheet.Cells(i,1).Value="__________________________/"+ rs.value("t_name")+"/";
    i=i+1;
    obSheet.Cells(i,1).Value="    /Подпись/";
   
   if (dlg.rec.val_int!=V_UNDEF) 
      obSheet.Cells(4,2).Value="Валюта счета: "+dlg.rec.val_str+"  ("+dlg.rec.val_int+")";
   else 
      obSheet.Cells(4,2).Value="Валюта счета: ВСЕ";
   end;
   /*
   if (strlen(dlg.rec.mask_5)==0) 
      obSheet.Cells(4,2).Value="Тип БС: 5*";
   else
      obSheet.Cells(4,2).Value="Тип БС: "+dlg.rec.mask_5;
   end;
   if (strlen(mask7)!=0)
      obSheet.Cells(5,2).Value="Корреспонденция со счетами: " + mask7; 
   end;
   if (strlen(mask7)==0) 
      obSheet.Cells(5,2).Value="Корреспонденция со счетами: 70603*, 70608*";
   end;*/
   obSheet.Cells(5,1).Value="Наименование операций";
   obSheet.Cells(5,2).Value="Ежедневные курсовые разницы от переоценки инвалютных счетов раздела 5 (операции с ценными бумагами)";
   
   obSheet.Cells(7,1).Value=" ДАТА ";
   obSheet.Cells(7,2).Value=" ДЕБЕТ ";
   obSheet.Cells(7,3).Value=" КРЕДИТ ";
   obSheet.Cells(7,4).Value=" СУММА ";
   
   obSheet.Range("A7:D7").Borders.Weight=2;
   obSheet.Range("A7:D7").interior.color=4035000;
   
   obSheet.Range("A:A").ColumnWidth=45;
   obSheet.Range("B:B").ColumnWidth=25;
   obSheet.Range("C:C").ColumnWidth=25;
   obSheet.Range("D:D").ColumnWidth=15;

   var format_str:string = "# ##0" + ex.International(3) + "00"; // вот это важная штука. ex.DecimalSeparator - фигня, может не использоваться, т.к. стоит галка "использовать региональные настройки". А эта штука походу вернет верное значение, даже если эта галка не стоит.
   obSheet.Columns("D:D").NumberFormat = format_str;
   
   format_str = "###0" ;
   obSheet.Columns("B:C").NumberFormat = format_str;
   
  ex.visible=true;
END;


 MACRO EvMacro (dlg, cmd, id, key)
   if (cmd==DLG_INIT)
      message(const_mess);
      dlg.rec.date_begin={curdate};
      dlg.rec.date_end={curdate};
      dlg.rec.mask_7=string("70603*, 70608*");
      UpdateFields(dlg);
   end;
   if (cmd==DLG_KEY)
      if (KEY==KEY_F9)
         return CM_ignore;
      end; //if F9
      if (KEY==KEY_ENTER)
         return CM_ignore;
      end; //if F9
      if (KEY==KEY_F3)
         //dlg.rec.date_name_field=GetDatebyCalendar({Curdate});    
         if (FldName(dlg,id) == "val_str") 
            if (listfi(FIKIND_CURRENCY,0,fi) )
               dlg.rec.val_int = fi.fi_code;
               dlg.rec.val_str = fi.name;
               //message(""+const_mess);
               //val=fi.fiid;
               UpdateFields(dlg);
            end;
         end;
         return CM_Ignore;
      end; //if F3
      if (KEY==KEY_F2)                                       
         if ((dlg.rec.date_begin >{curdate}) or (dlg.rec.date_end >{curdate}))
            msgbox("Дата больше даты текущего операционного дня");   
            return cm_ignore; 
         end;
         outputinexcel(dlg.rec.mask_5,dlg.rec.mask_7);
         return CM_save;
      end; //if F2
      if (KEY==KEY_ESC)
         exit(1);
         return CM_CANCEL;
      end; //if esc
   end; //if DLG_KEY
 END; //evmacro




//        точка входа
   
   if(rundialog(dlg,"evmacro")) 
      println();
      println("                                                              ОТЧЕТ СФОРМИРОВАН В EXCEL");
      println();
      println("                                                          для продолжения работы нажмите ESC");
   end;
