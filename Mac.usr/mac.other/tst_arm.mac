import RSD, BankInter;

var path, errCode, pathfile, resname = "RoleArm.lbr", dlg_p;
var str, rs, col = TArray, col_arm = TArray, ParentObj;
var Menu_Num = 0, RoleID = 0, Type_Access = "", flag = false;

class FillObj(m_rset:object)
  
  var Menu_Name    = "",
      Access_Name  = "",
      Role_Name    = "",
      Menu_ID      = 0,
      Access_Level = "",
      Role_ID      = 0;
      
  macro init(m_rset)
    Menu_Name    = m_rset.value("menu");
    Access_Name  = m_rset.value("access_level");
    Role_Name    = m_rset.value("role");
    Menu_ID      = m_rset.value("t_menuid");
    Access_Level = m_rset.value("t_access_level");
    Role_ID      = m_rset.value("t_roleid");
  end;
  
  if (ValType(m_rset) != V_Undef)
    init(m_rset);
  end;
  
end;

private macro ПрочитатьНастройки

  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",V_STRING, path, errCode);
  pathfile = FindPath(resname, path);
  if (not pathfile)
    msgbox("Не найдена LBR с именем " + resname);
    return false;
  end;
  if ( errCode > 0 )
    msgbox("Проверьте наличие настройки: BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR");
    return false;
  end;
    
  dlg_p = TRecHandler("p_RolArm", pathfile, True);
  
  return true;

end;

private macro AddCol (ar,ind, fld, head, width, rdonly, fldType, decPoint)

  if (fldType == null)
      fldType = 2;
  end;
    
  if (DecPoint == null)
      DecPoint = -1;
  end;
    
  ar.value (ind * 6)     = fld;
  ar.value (ind * 6 + 1) = head;
  ar.value (ind * 6 + 2) = width;
  ar.value (ind * 6 + 3 ) = fldType;
  ar.value (ind * 6 + 4 ) = decPoint;
  ar.value (ind * 6 + 5 ) = 0;   // reserv
end;

macro EvProc_Enter (rs, cmd, id, key )

  if (cmd == DLG_KEY) 
    if ((key == 13) or (key == 316))
      return CM_SELECT;
    end;
  end;
end;

macro Event_View(dlg_view, cmd, id, key)
  
  var rs_view, str;
  
  if (cmd == DLG_INIT)
    
    dlg_view.rec.Menu        = rs.value(0);
    dlg_view.rec.AccessLevel = rs.value(2);
    dlg_view.rec.Role        = rs.value(1);
    Menu_Num                 = rs.value(3);
    Type_Access              = rs.value(4);
    RoleID                   = rs.value(5);
        
  elif(cmd == DLG_SETFOCUS)
    if(FldName(dlg_view, id) == "Menu")
      message("~ESC~ Выход ~F9~ Сохранить");
    elif(FldName(dlg_view, id) == "AccessLevel")
      message("~ESC~ Выход ~F9~ Сохранить ~F3~ Выбрать уровень доступа");
    elif(FldName(dlg_view, id) == "Role")
      message("~ESC~ Выход ~F9~ Сохранить");
    end;
  elif (cmd == DLG_KEY)
    if (key == 317) //KEY_F3
      if (FldName(dlg_view, id) == "AccessLevel")
        
        str = " SELECT t_name_type, t_type_account " +
              "   FROM dtypeac_dbt " +
              "  WHERE t_inumtype = 13 " +
              "    AND NOT EXISTS " +
              "        (SELECT 1 " +
              "           FROM dpers_role_lnk lnk " +
              "          WHERE lnk.t_menuid = " + Menu_Num + 
              "            AND lnk.t_access_level = t_type_account)";

        rs_view = RsdRecordSet(str, RSDVAL_CLIENT, RSDVAL_STATIC);

        col.Size = 0;
        AddCol(col, 0, "t_name_type", "Уровень доступа", 30);
        if(RunScroll(rs_view, 1, col, "access_level", "EvProc_Enter", "Выберите уровень доступа", "~Enter~ Выбор", true))
          dlg_view.rec.AccessLevel = rs_view.value("t_name_type");
          Type_Access = rs_view.value("t_type_account");
//        else
//          Type_Access = "";
        end;
        
      end;
    elif(key == 323) //KEY_F9
      rs_view = RsdRecordSet("select distinct(t_access_level) t_access_level from dpers_role_lnk where t_menuid = " + Menu_Num);
      if (rs_view and rs_view.Movenext)
      
         if (Type_Access != rs_view.value("t_access_level"))
           if(GetTrue(False, "Изменен уровень доступа. Сохранять изменения?"))
             return CM_SAVE;
           else
             return CM_IGNORE;
           end
         end;
        
      else
        return CM_SAVE;
      end;
    end;
  end;
  
  UpdateFields(dlg_view);
  
end;

macro Event_Ins(dlg_ins, cmd, id, key)
  
  var const_message = "~ESC~ Выход ~F9~ Сохранить";
  var rs, rs_access, str;
   
  if (cmd == DLG_INIT)
    dlg_ins.rec.Menu        = "Выберите меню";
    dlg_ins.rec.AccessLevel = "Выберите уровень доступа";
    dlg_ins.rec.Role        = "Выберите роль";
  elif (cmd == DLG_SETFOCUS)
    if(FldName(dlg_ins, id) == "Menu")
      message(const_message + " ~F3~ Выбрать меню");
    elif(FldName(dlg_ins, id) == "AccessLevel")
      message(const_message + " ~F3~ Выбрать уровень доступа");
    elif(FldName(dlg_ins, id) == "Role")
      message(const_message + " ~F3~ Выбрать роль");
    end;
  elif (cmd == DLG_KEY)
    if (key == 317) //KEY_F3
      if (FldName(dlg_ins, id) == "Menu")
        rs = RsdRecordSet("SELECT t_menuid||'-'||t_name as menu, t_menuid FROM dmenutpl_dbt WHERE (t_isdistributed = chr(0) or t_isdistributed is null) ORDER BY t_menuid", RSDVAL_CLIENT, RSDVAL_STATIC);
        col.Size = 0;
        AddCol(col, 0, "menu", "Меню", 40);
        if(RunScroll(rs, 1, col, "menu", "EvProc_Enter", "Выберите меню", "~Enter~ Выбор", true))
          Menu_Num = rs.value("t_menuid");
          dlg_ins.rec.Menu = rs.value("menu");
          str = " SELECT distinct(acl.t_name_type), acl.t_type_account " +
                "   FROM dpers_role_lnk t, (SELECT ac.t_name_type, ac.t_type_account " +
                "                             FROM dtypeac_dbt ac " +
                "                            WHERE ac.t_inumtype = 13) acl " +
                "  WHERE t.t_access_level = acl.t_type_account AND t.t_menuid = " + Menu_Num;
          rs_access = RsdRecordSet(str);
          if (rs_access and rs_access.MoveNext())
            dlg_ins.rec.AccessLevel = rs_access.value("t_name_type");
            Type_Access = rs_access.value("t_type_account")
          else
            dlg_ins.rec.AccessLevel = "Выберите уровень доступа";
          end;
        else
          dlg_ins.rec.Menu = "Выберите меню";
          dlg_ins.rec.AccessLevel = "Выберите уровень доступа";
          Menu_Num = 0;
        end;
      elif (FldName(dlg_ins, id) == "AccessLevel")
        if (dlg_ins.rec.Menu == "Выберите меню")
          msgbox("Не выбрано меню!");
          return CM_IGNORE;
        end;

        str = " SELECT t_name_type, t_type_account " +
              "   FROM dtypeac_dbt " +
              "  WHERE t_inumtype = 13 " +
              "    AND NOT EXISTS " +
              "        (SELECT 1 " +
              "           FROM dpers_role_lnk lnk " +
              "          WHERE lnk.t_menuid = " + Menu_Num + 
              "            AND lnk.t_access_level = t_type_account)";

        rs = RsdRecordSet(str, RSDVAL_CLIENT, RSDVAL_STATIC);

        col.Size = 0;
        AddCol(col, 0, "t_name_type", "Уровень доступа", 30);
        if(RunScroll(rs, 1, col, "access_level", "EvProc_Enter", "Выберите уровень доступа", "~Enter~ Выбор", true))
          dlg_ins.rec.AccessLevel = rs.value("t_name_type");
          Type_Access = rs.value("t_type_account");
        else
          //dlg_ins.rec.AccessLevel = ParentObj.Access_Name;
          Type_Access = "";
        end;
      elif (FldName(dlg_ins, id) == "Role")
        if (dlg_ins.rec.Menu == "Выберите меню")
          msgbox("Не выбрано меню!");
          return CM_IGNORE;
        elif (dlg_ins.rec.AccessLevel == "Выберите уровень доступа")
          msgbox("Не выбран уровень доступа!");
          return CM_IGNORE;
        end;
        
        str = " SELECT n.t_name, n.t_roleid " +
              "   FROM dacsroletree_dbt n " +
              "  WHERE n.t_roleid > 0 " +
              "    AND NOT EXISTS " +
              "            (SELECT 1 " +
              "               FROM dpers_role_lnk lnk " +
              "              WHERE lnk.t_menuid = " + Menu_Num + 
              "                AND lnk.t_roleid = n.t_roleid) " +
              "  ORDER BY n.t_roleid ";
        rs = RsdRecordSet(str, RSDVAL_CLIENT, RSDVAL_STATIC);
        
        col.Size = 0;
        AddCol(col, 0, "t_name", "Роль", 40);
        if(RunScroll(rs, 1, col, "role", "EvProc_Enter", "Выберите роль", "~Enter~ Выбор", true))
          dlg_ins.rec.Role = rs.value("t_name");
          RoleID = rs.value("t_roleid");
        else
          dlg_ins.rec.Role = "Выберите роль";
          RoleID = 0;
        end;
      end;
    elif (key == 323) //KEY_F9
      if (dlg_ins.rec.Menu == "Выберите меню")
        msgbox("Не выбрано меню!");
        return CM_IGNORE;
      elif (dlg_ins.rec.AccessLevel == "Выберите уровень доступа")
        msgbox("Не выбран уровень доступа!");
        return CM_IGNORE;
      elif (dlg_ins.rec.Role == "Выберите роль")
        msgbox("Не выбрана Роль");
        return CM_IGNORE;
      end;
      debugbreak;
      rs = RsdRecordSet("select distinct(t_access_level) t_access_level from dpers_role_lnk where t_menuid = " + Menu_Num);
      if (rs and rs.Movenext)
      
         if (Type_Access != rs.value("t_access_level"))
           if(GetTrue(False, "Изменен уровень доступа. Сохранять изменения?"))
             return CM_SAVE;
           else
             return CM_IGNORE;
           end
         end;
        
      else
        return CM_SAVE;
      end;
    end;
  end;
  
  UpdateFields(dlg_ins);
  
end;

macro DeleteVal(rs_del)
  
  var str, rs;
  
  str = "delete dpers_role_lnk where t_menuid = ? and t_roleid = ? and t_access_level = ?";
  rs = RsdCommand(str);
  rs.AddParam("menuid",      RSDBP_IN, rs_del.value("t_menuid"));
  rs.AddParam("roleid",      RSDBP_IN, rs_del.value("t_roleid"));
  rs.AddParam("acess_level", RSDBP_IN, rs_del.value("t_access_level"));
  rs.Execute();
  flag = true;
end;

macro InsertVal(m_menuid:integer, m_roleid:integer, m_access:string)
  
  var str, rs;
  
  str = "update dpers_role_lnk set t_access_level = ? where t_menuid = ?";
  rs = RsdCommand(str);
  rs.AddParam("acess_level", RSDBP_IN, m_access);
  rs.AddParam("menuid",      RSDBP_IN, m_menuid);
  rs.Execute();
  
  str = "insert into dpers_role_lnk values(?, ?, ?)";
  rs = RsdCommand(str);
  rs.AddParam("menuid",      RSDBP_IN, m_menuid);
  rs.AddParam("roleid",      RSDBP_IN, m_roleid);
  rs.AddParam("acess_level", RSDBP_IN, m_access);
  rs.Execute();
  flag = true;
end;

macro UpdateVal(m_menuid:integer, m_access:string)

  var str, rs;
  
  str = "update dpers_role_lnk set t_access_level = ? where t_menuid = ?";
  rs = RsdCommand(str);
  rs.AddParam("acess_level", RSDBP_IN, m_access);
  rs.AddParam("menuid",      RSDBP_IN, m_menuid);
  rs.Execute();
end;

macro GetAccess(NameAccess:string)
  
  var rs, str;
  
  str = "select t_type_account from dtypeac_dbt where t_inumtype = 13 and t_name_type = '" + NameAccess + "'";
  rs = RsdRecordSet(str);
  if (rs and rs.movenext())
    return rs.value("t_type_account");
  else
    return "Не найден уровень доступа!";
  end;
  
end;

macro ScrollHandler(rs, cmd, id, key)
  
  var CM_FLAG = CM_SELECT;
  
  if (cmd == DLG_INIT)
    while (rs.movenext and flag)
      if ((rs.value("t_menuid") == Menu_Num) and (rs.value("t_roleid") == RoleID))
        flag = false;
        GoTOScroll();
      end;
    end;
  elif (cmd == DLG_KEY)
    if (key == 13)  //key_enter
      ПрочитатьНастройки();
      if (RunDialog(dlg_p, "Event_View"))
        UpdateVal(Menu_Num, Type_Access);
        return CM_SELECT;
      end;
    elif (key == 322) //key_F8
      DeleteVal(rs);
      return CM_SELECT;
    elif (key == 323) //key_F9
      ПрочитатьНастройки();
      if(RunDialog(dlg_p, "Event_Ins"))
        InsertVal(Menu_Num, RoleID, Type_Access);
        return CM_SELECT;
      end;
    end;
  end;
end;

  str = " SELECT NVL((SELECT m.t_menuid ||'-'||m.t_name " +
        "                FROM dmenutpl_dbt m " +
        "               WHERE m.t_menuid = t.t_menuid), 'Не найден образец меню - '||t.t_menuid) AS menu, " +
        "        (SELECT n.t_name " +
        "           FROM dacsroletree_dbt n " +
        "          WHERE n.t_roleid = t.t_roleid) AS role, " +
        "        (SELECT ac.t_name_type " +
        "           FROM dtypeac_dbt ac " +
        "          WHERE ac.t_inumtype = 13 " +
        "            AND ac.t_type_account = t.t_access_level) AS access_level, " +
        "        t.t_menuid, " +
        "        t.t_access_level, " +
        "        t.t_roleid" +
        "    FROM dpers_role_lnk t order by t.t_menuid";

  rs = RsdRecordSet(str ,RSDVAL_CLIENT, RSDVAL_STATIC );

  AddCol(col_arm, 0, "menu", "Меню", 20);
  AddCol(col_arm, 1, "role", "Роль", 40);
  AddCol(col_arm, 2, "access_level", "Уровень доступа", 20);

  while(RunScroll(rs, 3, col_arm, "role_arm", "ScrollHandler", "Таблица соответсвий", "~ESC~ Выход F8 Удалить F9 Добавить" , true))
    rs = RsdRecordSet(str ,RSDVAL_CLIENT, RSDVAL_STATIC );
  end;
  rs.Close();
  exit(1);