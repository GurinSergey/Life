/*-----------------------------------------------------------------------------
// Файл: WordIntf.mac
// Назначение: Некоторые функции для работы с закладками и таблицами
//             Word'a  из RSL.
// Версия файла: 2.0 (2-и 3 -  звенка)
// Версия RS-Bank: 5.00.034
// Автор: Лупан А.Д.
// (C) R-Style Ukraine, 2000
// Изменен: Gurin S. 05.02.2013 Добавил ф-ции GetWorkDirName() и Check_templ()
// Изменен: 28.02.2013 zmp добавлены ф-ции RSL_Reverse () и RSL_FullPath() 
// KS 22.11.2013 Предварительная адаптация под 31ю сборку
//----------------------------------------------------------------------------*/
Import rslx, /*lgxs,*/ RSD;
Import BankInter;

Import rsexts, likepy;

/*--------------------------------------------------------------
//Ключ реестра, в котором хранится путь к Word'oвым шаблонам
//-------------------------------------------------------------*/
const WRDTEMPLATEDIR_REG_KEY="RS-RETAIL\\ПЕЧАТИ\\ВКЛАДЫ\\ДОГОВОРА";

var tmswobj_WordObject=null,
    tmswobj_WordDocument=null;

macro RSL_Reverse (str :string) :string
    var cmd = RsdCommand("select reverse (?) from dual");
        cmd.AddParam("0", RSDBP_IN, str);
    var rs = RsdRecordset(cmd);
    if (rs and rs.MoveNext()) return rs.value(0); end;
    return "";
onError()
    return "";
end;

macro RSL_FullPath(relPath :string, isServer :bool) :string
    var path;
    var pos = 0;
    if(index(relPath,".."))        
        path = GetCurDir(isServer != True);
        pos = index(RSL_Reverse(GetCurDir(isServer != True)),"\\");
        return String(substr(path,1,strlen(path) - pos +1), substr(relPath, index(relPath, "..") + 3 ));
    end;
    return  relPath;     
end;


macro GetTemplateDir(TplRegKey)
var ErrCode,RegValue="";
  if  (ValType(TplRegKey)==V_UNDEF)
    TplRegKey = WRDTEMPLATEDIR_REG_KEY;
  end;
  GetRegistryValue(TplRegKey,V_STRING,RegValue,ErrCode);
  if (ErrCode==0) 
    RegValue = trim(RegValue);
    if (RegValue=="")
      return "";
    end;
    if (SubStr(RegValue,strlen(RegValue),1)!="\\")
      RegValue==RegValue+"\\";
    end;
    return  RegValue;
  end;
  return "";
end;

macro GetWordObject()
  if (tmswobj_WordObject==null)
    MsgBox("Объект \x22Microsoft Word\x22 не инициализирован.");
    Exit(1);
  end;
  return tmswobj_WordObject;
end;

macro GetDocObject()
  GetWordObject();
  if (tmswobj_WordDocument==null)
    MsgBox("Объект \x22Документ Microsoft Word\x22 не инициализирован.");
    Exit(1);
  end;

  return tmswobj_WordDocument;
end;

macro CreateDocFromTemplate(TemplateName)
  tmswobj_WordObject=NULL;
  if (IsStandAlone)
     tmswobj_WordObject = ActiveX("Word.Application",null,true);
//  else
//     tmswobj_WordObject = TlgActiveX("Word.Application",true);
  end;
  tmswobj_WordDocument = GetWordObject().Documents.Add(TemplateName);
OnError(RslErr)
  if (ValType(tmswobj_WordObject)==V_UNDEF)
    MsgBox("Ошибка создания объекта \x22Microsoft Word\x22:|",
            RslErr.Message,"|Line:",RslErr.Line,"|AX Err:",RslErr.AXMes);
  else
    MsgBox("Ошибка открытия шаблона ~Microsoft Word~:| Шаблон"+TemplateName+"|",
           RslErr.Message,"|Line:",RslErr.Line,"|AX Err:",RslErr.AXMes);
    tmswobj_WordObject.Visible = true;
  end;
  Exit(1);
end;

macro SetWordVisible(On)
    GetWordObject().Visible=On;
end;

macro SetUpdateFields()
var obj = GetDocObject();
   obj.Fields.Update();
end;

macro SetBookmarkText(BMName,NewText)
var BM = GetDocObject().Bookmarks.Item(BMName);
    BM.Range.Text = (NewText);
OnError(RslErr)
  MsgBox(" Ошибка при обращении к объекту Bookmarks(",BMName,")|",
         RslErr.Message,"|Line:",RslErr.Line,"|AX Err:",RslErr.AXMes);
end;

macro SetTableLine(BMName,RowN /*..дополнительно - любая куча параметров */)
var Tbl = GetDocObject().Bookmarks.Item(BMName).Range.Tables.Item(1);
var i=2,CurrParm;
  while (Tbl.Rows.Count<RowN)
    Tbl.Rows.Add();
  end;
  while (GetParm(i,CurrParm))
    if (ValType(CurrParm)!=V_UNDEF)
        Tbl.Cell(RowN,i-1).Range.Text = String(CurrParm);
    end;
    i = i+1;
  end;
  return;
OnError(RslErr)
  MsgBox("Ошибка при обращении к объекту Tables (Имя==",BMName,"):|",
         RslErr.Message,"|Line:",RslErr.Line,"|AX Err:",RslErr.AXMes);
end;

macro GetTableDimensions(BMName,RowCount,ColCount)
var Tbl = GetDocObject().Bookmarks.Item(BMName).Range.Tables.Item(1);
  SetParm(1,Tbl.Rows.Count);
  SetParm(2,Tbl.Columns.Count);
OnError(RslErr)
  MsgBox("Ошибка при обращении к объекту Tables (Имя==",BMName,"):|",
         RslErr.Message,"|Line:",RslErr.Line);
end;

private macro GetWorkDirName() : string
   var DirName:string = "", /* Строка с именем пути                         */
       StrErr :string = ""; /* Строка с ошибкой определения временной папки */

   GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, DirName, StrErr );
   if( DirName ) /* Если в настройке указан каталог для временных файлов, работаем в этом направлении */
       //msgbox(string("В реестре найден каталог текстовых файлов: ", DirName));
       /* Если первый символ в DirName - это точка, то значит путь относительный, */
       /* тогда приклеем к нему CurDir терминала, иначе путь абсолютный */
       if( SubStr(DirName, 1, 1) == "." )
           DirName = "$" + GetCurDir(TRUE) + replace (DirName, "..","");
       end;
       //msgbox(string("А это его модификация для терминала: ", DirName));
       if (existFile(DirName))
          //msgbox(string("Он существует на терминале, поэтому его и возвращаем: ", DirName));
          return DirName;
       end;
   end;
   DirName = "$" + GetEnv("TEMP");
   if( NOT DirName )
       DirName = "$" + GetEnv("TMP");
   end;
   if (existFile(DirName))
      //msgbox(string("Он существует на терминале, поэтому его и возвращаем: ", DirName));
      return DirName;
   end;
   msgbox("Ошибка поиска папки для временных файлов на терминале");

   return "";
end;

Macro Check_templ(Name_file_templ)
   var Templs_Dir_Term = GetWorkDirName();
   var TermFileName = toAnsi(Name_file_templ);
   var SPFileName;

   if (Templs_Dir_Term == "")
      return "";
   end;
   GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\USERTEMPLSDIR",2,SPFileName);
   SPFileName = findpath (Name_file_templ,SPFileName);
   if (not existFile(SPFileName))
      Msgbox(string("Шаблон отсутствует в папке СП: |",SPFileName));
      return "";
   end;
   DelFile(SUBSTR(Templs_Dir_Term+"\\"+TermFileName,2));
   //run (GetEnv("COMSPEC"),"/c del "+ Templs_Dir_Term + "\\"+TermFileName, ">");

   CopyFile(SPFileName, Templs_Dir_Term+"\\"+TermFileName);

   //Gurin S. 12.01.2015 I-00543992-1 
   //return replace(Templs_Dir_Term+"\\"+TermFileName,"$",""));
   if (substr(Templs_Dir_Term,1,1) == "$")
      return(substr(Templs_Dir_Term,2)+"\\"+TermFileName);
   else
      return(Templs_Dir_Term+"\\"+TermFileName);
   end;
End;




