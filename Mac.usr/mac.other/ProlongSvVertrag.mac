/*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   Гуцу Е.В.

   Имя файла: -

   Создан:    7.04.2015

   Описание:  Процедура пролонгации договоров обслуживания

   ChangeLog:
-----------------------------------------------------------------------------------------------------------------------------------------------------
   8.4.2015 Скорректирована фильтрация ДО, добавлена обработка отказа пользователя вводить дату,
            исправлена нумерация строчек в протоколе.
            Убрал уже пролонгированные договора (+1 год, NewProlDate) по просьбе Елены Шевцовой.
            Изменён расчёт новой даты пролонгации, теперь она индивидуальная для каждого пролонгируемого ДО.
            Скорректирована фильтрация ДО для вывода в скроллинг.
            Скорректирован заголовок протокола выполнения процедуры.
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/
import rsd, rslx, Календарь;
import "KeyCodes.mac", "globals.mac", "cb_sql.mac";

var RestartScroll = true;
var str, cmd, rs;
var cols = TArray();
var ind = 0, promt, vRepDate, SelectionCount, i;
var SavedPosition = 0;
var BerichtEnum = 0;


 // Атрибуты полей 
 macro SpalteHinzufugen( col, fld, head, width )
    Col.value( ind * 6 + 0 ) = fld;  // fieldName
    Col.value( ind * 6 + 1 ) = head; // header 
    Col.value( ind * 6 + 2 ) = width; // width
    Col.value( ind * 6 + 3 ) = 2;    // fldType (2 = FBT)
    Col.value( ind * 6 + 4 ) = null; // decPoint
    Col.value( ind * 6 + 5 ) = 0;    // reserv
    ind = ind + 1;
 end;

 // Печать отчёта

 // Заголовок
 macro DruckenBerichtKopf( pDate )
    [
        Протокол выполнения процедуры пролонгации по договорам обслуживания с датой пролонгации меньше #
                            
      ┌───────┬────────────┬───────────────────────────┬─────────────────────────────┬─────────────┬─────────────┐
      │ Номер │  Номер     │  Номер счета              │  Клиент                     │ Старая дата │ Новая  дата │
      │ п/п   │  договора  │                           │                             │ пролонгации │ пролонгации │
      ├───────┼────────────┼───────────────────────────┼─────────────────────────────┼─────────────┼─────────────┤
    ]( pDate );
 end;

 // Строка отчёта
 macro DruckenBericht( rs, pOldDate, pNewDate )
    BerichtEnum = BerichtEnum + 1;

    [ │ ##### │ ########## │ ######################### │ ########################### │ ########### │ ########### │  ]
    ( BerichtEnum,
      rs.value("t_number"),
      rs.value("t_object"),
      rs.value("t_party"),
      pOldDate, pNewDate );
 end;

 // Печать ошибки
 macro DruckenFehler( pText )
   [ │ #                                                                                                        │  ]
   ( pText );
 end;

 // Подвал
 macro DruckenBerichtEnde()
    [ └───────┴────────────┴───────────────────────────┴─────────────────────────────┴─────────────┴─────────────┘
      Обработано договоров: # ]( BerichtEnum:l );
 end;


               
 // Обработать запись
 macro ProzessZeichnet( rs )
    var vCmd, vSaveDateProl, vDateBegin, NewProlDate;
    var dd, mm, yy, repYY;

    //msgbox("Processing: " + rs.value("t_object") );
    vSaveDateProl = SQL_ConvTypeDate( rs.value("t_dateprolong") );
    vDateBegin    = SQL_ConvTypeDate( rs.value("t_datebegin") );

    /* EVG 8/4/2015 Расчёт новой даты пролонгации перенесён сюда. Если дата пролонгации из договора не нулевая, 
       то новая дата рассчитывается как текущий год+1 год от даты пролонгации. Иначе, +1 год от даты начала договора. */
    DateSplit( {CurDate}, null, null, repYY );
    if( vSaveDateProl > date(1,1,1000) )
       DateSplit( vSaveDateProl, dd, mm, yy );
       //NewProlDate = DateAfterCalenMonths( vSaveDateProl, 12 );
    else
       DateSplit( vDateBegin, dd, mm, yy );
       //NewProlDate = DateAfterCalenMonths( vDateBegin, 12 );
    end;
    NewProlDate = date( dd, mm, repYY+1 );

    if( NewProlDate < vSaveDateProl )
       DruckenFehler( "Рассчитанная дата пролонгации " + NewProlDate + " меньше текущей даты пролонгации по договору " + rs.value("t_number") );
       return;

    elif( NewProlDate > vSaveDateProl )
       vCmd = rsdCommand( "update dsfcontr_dbt set t_dateprolong = ? where t_id = ?" );
       vCmd.addParam( "", RSDBP_IN, NewProlDate      );
       vCmd.addParam( "", RSDBP_IN, rs.value("t_id") );
       vCmd.execute();
    end;

    DruckenBericht( rs, vSaveDateProl, NewProlDate );
 end;


 // Обработчик диалога
 macro DialogsVerarbeitung( rs, cmd, id, key )

    if ( cmd == DLG_INIT )
       if( not AddMultiAction( rs, KEY_F2 ) )
          msgbox( "Ошибка при задании клавиши обработки выделенных сообщений" );
       end;
       //msgbox(NewProlDate);
       GoToScroll( rs );
       return CM_DEFAULT;
    end;


    if ( cmd == DLG_MSELSTART )
       i = 0;
       SelectionCount = GetMultiCount( rs );
       //msgbox("Selected: " + SelectionCount);
       initProgress( SelectionCount, null, "Обработка выделенных договоров" );
       return CM_DEFAULT;
    end;


    if ( cmd == DLG_MSEL )
       //msgbox("Processing: " + rs.value("t_object") );
       i = i + 1;
       useProgress( i );
       ProzessZeichnet( rs );
       //UpdateScroll( rs, 5 );
       return CM_MSEL_CONT_CLEAR;
    end;

    if ( cmd == DLG_MSELEND )
       remProgress();
       //UpdateScroll( rs, 5 );
       SavedPosition = rs.value("t_id");
       return CM_DEFAULT;
    end;



    /* После обработки выделенной записи возвращается CM_CANCEL для того, чтобы
       система перерисовала скроллинг с обновлёнными значениями полей. */
    if( (cmd == DLG_KEY) and (key == KEY_F2) )
       ProzessZeichnet( rs );
       SavedPosition = rs.value("t_id");

       return CM_CANCEL;

    /* RestartScroll = false для того, чтобы окончательно выйти из скроллинга */
    elif( (cmd == DLG_KEY) and (key == KEY_ESC) )
       RestartScroll = false;

       return CM_CANCEL;

    /* Почему-то после команды DLG_MSELEND (окончания обработки выделенных записей)
       приходит команда DLG_KEY с key = 0. Больше не удалось найти случаев, когда бы
       приходил такой код клавиши. Поэтому можно воспользоваться этой особенностью для того,
       чтобы пересоздать скроллинг с новыми значениями, возвратив CM_CANCEL. */
    elif( (cmd == DLG_KEY) and (key == 0) )
       return CM_CANCEL;
    end;


    return CM_DEFAULT;
 end;



 vRepDate = {CurDate};
 if( not GetDate( vRepDate, "Пожалуйста, введите дату выполнения процедуры" ) )
    exit(1);
 end;


 DruckenBerichtKopf( vRepDate );


 promt = "Esc Выход Space Выделить F2 Пролонгировать выделенные";

 while( RestartScroll )

    str = "select t_id, t_number, t_object, t_name, t_dateconc, t_datebegin, t_dateprolong, t_dateclose,    " +
          "       ( select pt.t_shortname from dparty_dbt pt where pt.t_partyid = t.t_partyid ) t_party     " +
          "  from dsfcontr_dbt t                                                                            " +
          " where t_objecttype                         = 1                                                  " +    // Объект = счёт
          "   and t_fiid                               = 0                                                  " +    // Валюта = рубли
          "   and t_object                             like '40%'                                           " +    // Маска 40*
          "   and substr( to_number(t_object), 1,  5 ) not in ( 40821 , 40817, 40911 )                      " +    // Кроме счетов вкладов
          "   and substr( to_number(t_object), 11, 1 ) <> 9                                                 " +    // Кроме счетов СКС
          "   and t_dateclose                          = to_date('1.01.0001', 'dd.mm.rrrr')                 " +    // Незакрытые
          /* EVG 8/4/2015 Убрал уже пролонгированные договора (+1 год, NewProlDate) по просьбе Елены Шевцовой.
          /* Дата пролонгации - либо меньше даты отчёта, либо равна дате следующей пролонгации
             (для того, чтобы записи уже пролонгированных ДО не пропадали из скроллинга) */
          "   and (   t_dateprolong   <= ?                                                                  " +
          "        or t_dateprolong    = ? )                                                                " ; */
          "   and (  ( t_dateprolong <> to_date('1.01.0001', 'dd.mm.rrrr') and t_dateprolong <= ? )         " +
          "       or ( t_dateprolong =  to_date('1.01.0001', 'dd.mm.rrrr') and t_datebegin   <= ? ) )       " +
          " order by t_id                                                                                   " ;
    
    cmd = RSDCommand  (str);
    cmd.AddParam( "", RSDBP_IN, vRepDate );
    cmd.AddParam( "", RSDBP_IN, vRepDate );
    //cmd.AddParam( "", RSDBP_IN, NewProlDate );

    //SpalteHinzufugen(cols, "t_select",       "П",                            1); 
    SpalteHinzufugen(cols, "t_number",       "Номер",                        10);
    SpalteHinzufugen(cols, "t_object",       "Лицевой счёт",                 20);
    SpalteHinzufugen(cols, "t_name",         "Наименование договора",        20);
    SpalteHinzufugen(cols, "t_dateconc",     "Дата закл.",                   10);
    SpalteHinzufugen(cols, "t_datebegin",    "Дата нач.",                    10);
    SpalteHinzufugen(cols, "t_dateprolong",  "Дата прол.",                   10);
    SpalteHinzufugen(cols, "t_dateclose",    "Дата закр.",                   10);
    SpalteHinzufugen(cols, "t_party",        "Плательщик",                   30);

    rs = RSDRecordset( cmd, null, RSDVAL_STATIC );

    /* Если скроллинг запускается повторно, у него будет запомнена последняя позиция (SavedPosition). Нужно спозиционировать на её рекордсет,
       чтобы в DLG_INIT поставить на неё автоматом окно прокрутки с помощью функции GoToScroll() */
    if( SavedPosition )
       while( rs.moveNext() and ( rs.value("t_id") < SavedPosition ) ) ; end;
    end;

    RunScroll( rs, ind, cols, "VertragScroll", @DialogsVerarbeitung, "Договора обслуживания", promt, true, /*X*/null, /*Y*/null, null, null );

 end;   

 DruckenBerichtEnde();

 /*
 onerror(x)
 msgbox(x);*/
