/****************************************************************************/
/*                     R-Style SoftLab, RS-Bank V6                          */
/****************************************************************************/
/*                                                                          */
/*  Создан:   23.10.09                                      Лавренов А.А.   */
/****************************************************************************/

 import rsbdataset,bankinter;
 
 var RecordSet;
 var exitscroll = false;
 file claim (acclaim);
 record rclaim (acclaim);

/*13.03.2013 Chesnokov D.S. логгирование запуска для поиска неиспользуемых макросов*/
ExecmacroFile("lib_log.mac", "LogProcedure", "acclaim_scrl.mac");

//Удаление
  MACRO delete_claim(clid)
     var rs, str;
     str = "delete from dacclaimstate_dbt where t_claimid = " + clid ;

     rs = rsdcommand(str);
     if(rs)
        rs.execute;
        rs = rsdcommand("commit");
        rs.execute;
     else 
        return false;
     end;
     str = "delete from dacclaim_dbt where t_claimid = " + clid ;

     rs = rsdcommand(str);
     if(rs)
        rs.execute;
        rs = rsdcommand("commit");
        rs.execute;
     else 
        return false;
     end;
     return true;
  END;

//Скроллинг
MACRO bsgvbranch_scroll (acc)
  var CmdText,Command;
  var col = TArray;

  //Обработчик для скроллинга
  macro EvProc (RecordSet, Command, id, key )
    if ((Command == DLG_KEY) and ((key == 322) or (key == 339))) //F8 DEL
       if(msgboxex("Удалить претензию?",MB_YES+MB_NO, IND_NO,"Удаление") == IND_YES)
          claim.claimid = recordset.value(0);
          if(geteq(claim))
             copy(rclaim,claim)
          end;
          delete_claim(recordset.value(0));
          writefisclog(oldelete,claim,rclaim);
          return CM_Select;
       end;
       return cm_ignore;
    elif ((Command == DLG_KEY) and (key == 27))
       exitscroll = true;
    end;
 end;

  //Добавляем колонки в скроллинг
  macro AddCol (ar,ind, fld, head, width, rdonly)
    ar.value (ind * 6)     = fld;
    ar.value (ind * 6 + 1) = head;
    ar.value (ind * 6 + 2) = width;
    ar.value (ind * 6 + 3 ) = 2;   // fldType
    ar.value (ind * 6 + 4 ) = -1;  // decPoint
    ar.value (ind * 6 + 5 ) = 0;   // reserv
  end;

  AddCol (col, 0, "t_account", "Счет", 20, true);
  AddCol (col, 1, "t_startamount", "Сумма", 12, true);
  AddCol (col, 2, "t_claimkind", "Вид", 15, true);
  AddCol (col, 3, "t_restkind", "Тип", 15, true);
  AddCol (col, 4, "t_initiator", "Инициатор", 20, true);
  AddCol (col, 5, "t_docnumber", "№ документа", 10, true);
  AddCol (col, 6, "t_docdate", "Дата документа", 10, true);
  AddCol (col, 7, "t_startdate", "Начало", 10, true);
  AddCol (col, 8, "t_finishdate", "Конец", 10, true);
  AddCol (col, 9, "t_priority", "Очередность", 3, true);
  AddCol (col, 10, "t_comment", "Примечание", 30, true);
  AddCol (col, 11, "t_oper", "Операционист", 4, true);
  AddCol (col, 12, "t_claim_cancel", "Отменена", 3, true);

  //Выбираем данные из таблицы
  CmdText = "SELECT t.t_claimid, t.t_account, " +
            "       (SELECT t_name " +
            "          FROM dllvalues_dbt " +
            "         WHERE t_list = 2520 AND t_element = t.t_claimkind) t_claimkind, " +
            "       (SELECT t_name " +
            "          FROM dllvalues_dbt " +
            "         WHERE t_list = 2522 AND t_element = t.t_restkind) t_restkind, " +
            "       (SELECT t_name " +
            "          FROM dllvalues_dbt " +
            "         WHERE t_list = 2523 AND t_element = t.t_initiator) t_initiator, " +
            "       t.t_docnumber, t.t_docdate, t.t_startdate, t.t_finishdate, " +
            "       t.t_startamount, t.t_priority, t.t_comment, t.t_oper, t.t_claim_cancel " +
            "  FROM dacclaim_dbt t " +
            " WHERE t_account = '"+acc+"'  " +
            "   AND t_chapter = 1 " ;

  Command = RSDCommand(CmdText);
  Command.Execute();
  RecordSet = RSDRecordSet(Command, RSDVAL_CLIENT, RSDVAL_STATIC);
  //Запускаем скроллинг
  runScroll(RecordSet, 13, col, null, @EvProc, "Претензии счета", "~F8(Del) - Удалить", false);

END;
//"40502810108180027198"
 MACRO GO(acc)
   //Пока не нажмем Esc не выйдем
   while(not exitscroll)
      bsgvbranch_scroll(acc);
   end;
   exit(1);
 END;

//go("40502810108180027198");
  
