/* 
   EVG             Макрос выполнения шага создания сообщения в ИМНС об открытии лицевого счёта.
   09.07.2008
*/
//Gurin S. 26.09.2013 Проверка на существование счета (для операции 13030)

import PSInter, bcall, InsCarryDoc, OprInter, PaymInter, PTInter, pmprops;
import lib_account, lib_compare;

RECORD req  ( reqopena );

var ReqOpenAccObj:RsbReqOpenAcc;
var TB_ReqOpenAcc = TBFile("reqopena.dbt",  "R", 0, "reqopena.dbt",  "bank.def");
var TB_Oproper    = TBFile("oproper.dbt",   "R", 1, "oproper.dbt",   "bank.def");

MACRO ExecuteStep( doc, pmDoc )

  var stat:integer = 0;

   TB_ReqOpenAcc.rec.RequestID = ReqOpenAccObj.RequestID;

   if( TB_ReqOpenAcc.GetEQ() )
     Copy( req, TB_ReqOpenAcc);
     req.Account = ReqOpenAccObj.Account; /*Номер счета еще может быть не сохранен в базе*/
   end;

   var acc = RSL_Account(req.Account,req.Code_Currency);
   TB_Oproper.rec.DocumentID = RSL_LPAD(req.Requestid,34,"0");
   TB_Oproper.rec.DocKind = PS_REQOPENA;
   TB_Oproper.GetEQ();
   
   if (((req.kind_operation == 13030) or (TB_Oproper.rec.kind_operation == 13030)) and (acc.rec.account == ""))
     msgbox("Счет "+req.Account+" не найден в БД");
     return 1;       
   end;

   stat = InsertOpenAccountMessage( req );

   if( stat == ОтказОтГенерацииСообщения )
     return 1;
   end;

   if( stat != 0 )
     DisplayError();
     MsgBox( "Ошибка при создании сообщения об открытии счета " + req.Account );
     return  1;
   end;

   return 0;

end;

//TAM 10.10.2012 I-00214498-4
macro CheckStepAction ( mes )
  debugbreak;
  if (mes == OP_BACKOUT_STEP)
    if(not CheckDelivery(ReqOpenAccObj.RequestID, 1))
      return 1;
    end;
  end;
  
  return 0;
End;

MACRO PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                ID_Step,     /* внутренний идентификатор шага операции          */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep )   /* вид шага операции                               */

  var stat = 0;

  Array Text, Buttons;
  Text(0) = "Печатать сообщения?";
  Buttons( 0 ) = "Да";
  Buttons( 1 ) = "Нет";
  var CopyCount = 0;

  if( ( errTrn == 0 ) and ( message == 1 ) )// на выполнении шага
    if( (ConfWin( Text, Buttons, 1 ) == 0) and 
        CheckTypeAccPSRegVal(ReqOpenAccObj.Type_Account, "ТИПЫ_СЧЕТОВ") 
      )
      SetKindRegOrgan( REG_ORGAN_KIND_FNS );
      stat = ReqAccPrintMessage( ReqOpenAccObj.RequestID,
                                 OBJTYPE_REQOPENA );
      if( stat == 0 )
        SetKindRegOrgan( REG_ORGAN_KIND_PFR );
        stat = ReqAccPrintMessage( ReqOpenAccObj.RequestID,
                                   OBJTYPE_REQOPENA );
      end;
      if( stat == 0 )
        SetKindRegOrgan( REG_ORGAN_KIND_FSS );
        stat = ReqAccPrintMessage( ReqOpenAccObj.RequestID,
                                   OBJTYPE_REQOPENA );
      end;
      SetKindRegOrgan( REG_ORGAN_KIND_NON );
    end;
  end;
  return stat;
END;