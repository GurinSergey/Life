/** 26.03.97  Меденцев К.В.  LBS_LIB.MAC *******************************/
/* Библиотека процедур работы с лицевыми счетами                       */
//27.11.2014 R-501036-2 DPN Адаптация под 31-й патч                    */
/***********************************************************************/
Import Calend ;
import globals;

/* Наименование cчета */
MACRO AccountName(sAcc,code_c,facc)
var ret="",Chapter,bal; 

file rAcc(account) key 0;
file robAcc(obacnt) key 0;

   bal = int(substr(sAcc,1,5));

   if(bal<80000) Chapter=1;
   elif((bal>=80000)and(bal<90000)) Chapter=2;
   elif((bal>=90000)and(bal<93000)) Chapter=3;
   elif((bal>=93000)and(bal<98000)) Chapter=4;
   elif((bal>=98000)and(bal<99990)) Chapter=5;
   elif((bal>=99990)and(bal<100000))Chapter=3;
   else Chapter = 1;
   end; /*if*/

   if (valtype(facc)==0)      /* vea 13.02.02 */
       if(Chapter==1)
         facc=rAcc;
       else
         facc=robAcc;
       end;
   end;

   rewind(facc);
   if(Chapter > 1)
     facc.Chapter = Chapter;
   end;

   facc.Account = sAcc;
   facc.Code_Currency = code_c;
   if (getEQ(facc))
      ret=facc.nameaccount;
   else
      msgbox(sAcc);
   end; /* IF */
   close(rAcc);
   close(robAcc);
   return (ret);
END; /* AccountName */

/* Наименование клиента по номеру счета */
MACRO ClientName(sAcc,code_c, chapter)

  var query, rs;

  if (valType(chapter) == V_UNDEF)
     chapter = 1
  end;

  query = " Select t_Name from DAccount_dbt acc, DParty_dbt cln " +
          "  Where acc.t_Account       = " + GetSQLString(sAcc) +
          "    and acc.t_Code_Currency = " + code_c +
          "    and acc.t_Chapter       = " + GetSQLString(chapter) +
          "    and cln.t_PartyID       = acc.t_Client ";
  rs = rsdRecordSet(query);
  if (rs and rs.moveNext())
     return rs.Value(0, null, V_STRING);
  end;

  return $0;
END; /* ClientName */

/* Номер клиента по номеру счета */
/* ДОБАВЛЕНА ВАЛЮТА - АЛЕШИН В.В. 25.07.97*/
MACRO ClientNumber(sAcc)
file rvAcc(account) key 0;
  rewind(rvAcc);
  rvAcc.Account = sAcc;
  if (getEQ(rvAcc))
    return (rvAcc.Client);
  end; /* IF */
  close(rvAcc);
  return (-1);
END;

/* Краткое наименование клиента по номеру счета */
MACRO ClientShortName(sAcc)
file rvAcc(account) key 0;
file rvCln(client) key 0;
  rewind(rvAcc);
  rvAcc.Account = sAcc;
  IF (getEQ(rvAcc))
    rewind(rvCln);
    rvCln.Client = rvAcc.Client;
    IF (getEQ(rvCln))
       RETURN rvCln.szShortName;
    END;
  END; /* IF */
  close(rvAcc);
  RETURN "?";
END;

/* Наименование клиента по номеру клиента */
MACRO NumClientName(Client2)   /* las 05.04.99 */
file rvCln(client) key 0;
   next(rvCln);
  rvCln.Client = Client2;
  IF (getEQ(rvCln))
     RETURN rvCln.Name_Client;
  END;
  RETURN "?";
END;

/* Краткое наименование клиента по номеру клиента */
MACRO NumClientShortName(Client)
file rvCln(client) key 0;
  rvCln.Client = Client;
  IF (getEQ(rvCln))
     RETURN rvCln.szShortName;
  END;
  RETURN "?";
END;

MACRO AccPerfRest(dt,sAcc,code_currency,rRst, chapter) /* GSP 01.04.2014 - адаптация 2031 */
  var query, rs, RestTable;

  RestTable = "DRestDate_dbt";
  query = " Select t_Rest from DRestDate_dbt rest, daccount_dbt acc "  +
          "  Where acc.t_Account       = " + GetSQLString(sAcc) +
          "    and acc.T_ACCOUNTID = rest.T_ACCOUNTID "+
          "    and rest.T_RESTCURRENCY  = " + code_currency +
          "    and acc.t_Chapter       = " + chapter +
          "    and rest.T_RESTDATE = ( Select max (T_RESTDATE) from " + RestTable +
          "                               Where t_Accountid       = acc.t_accountid " + //GetSQLString(sAcc) +
          "                                 and T_RESTCURRENCY  = " + code_currency +
//          "                                 and t_Chapter       = " + GetSQLString(chapter) +
          "                                 and T_RESTDATE   <= " + GetSQLDate(dt) + " )";
  rs = rsdRecordSet(query);
  if (rs and rs.moveNext())
     return rs.Value(0, null, V_MONEY);
  end;

  return $0;
END; /* AccPerfRest */

MACRO AccTodayRest(sAcc,code_currency,rAcc, chapter)
open(rAcc);   /*Алешин В.В.01.12.1999*/
  rewind(rAcc);
  rAcc.Chapter = chapter;
  rAcc.Account = sAcc;
/*   msgbox (valtype(chapter)); */
  if (valtype(chapter) != 0)
    rAcc.Chapter = Chapter;
  end;
  rAcc.Code_Currency = code_currency;
  IF (NOT getEQ(rAcc))
     RETURN $0;
  ELSE
     //RETURN rAcc.R0; 27.11.2014 R-501036-2 DPN В 31-й версии нет поля R0 в таблице
     return restA(sAcc);
  END; /* IF */
END; /* AccTodayRest */

/* Остаток по счету с кодом валюты на заданную дату */
macro AccDayRest(dt,sAcc,code_currency,frestdate,faccount,chapter)
var sum;
file frrdt(restdate) key 0;
file fracc(account) key 0;
  /* EVG */
  if (valType(chapter) == V_UNDEF)
     chapter = 1;
  end;
  if (valtype(frestdate)==0)
     frestdate=frrdt;
     faccount=fracc;
  end;
  if (dt < ТекущийОперДень())
     sum=AccPerfRest(dt,sAcc,code_currency,frestdate, chapter);
  else
     sum=AccTodayRest(sAcc,code_currency,faccount, chapter);
  end;
  close(frrdt);
  close(fracc);
  return (sum);
end; /* AccDayRest */

macro accdebetday(acc,code_currency,chapter)
var deb=$0;
file frad(document) key 3;
      rewind(frad);
      clearrecord(frad);
      frad.real_payer=acc;
      frad.code_currency=code_currency;
      frad.sum=-$1000000000000.00;
      if (getGE(frad))
         prev(frad);
         while ((next(frad)) and (frad.real_payer==acc))
            if ((frad.result_carry!=23) and (index(frad.typedocument,"З")==0))
               deb=deb+frad.sum;
            end;
         end;
      end;
   return (deb);
end;

macro acckreditday(acc,code_currency,chapter)
var cred=$0;
file frad(document) key 4;
      rewind(frad);
      clearrecord(frad);
      frad.real_receiver=acc;
      frad.code_currency=code_currency;
      frad.sum=-$1000000000000.00;
      if (getGE(frad))
         prev(frad);
         while ((next(frad)) and (frad.real_receiver==acc))
            if ((frad.result_carry!=23) and (index(frad.typedocument,"З")==0))
               cred=cred+frad.sum;
            end;
         end;
      end;
   return (cred);
end;


macro accdebet(acc,code_currency,begdate,enddate,chapter)
var deb=$0;
file frad(arhdoc) key 1;
      rewind(frad);
      clearrecord(frad);
      frad.real_payer=acc;
      frad.date_carry=begdate;
      frad.code_currency=code_currency;
      frad.sum=-$1000000000000.00;
      if (getGE(frad))
         prev(frad);
         while ((next(frad)) and (frad.real_payer==acc)
                and (frad.date_carry<=enddate))
            if ((frad.result_carry!=23) and (index(frad.typedocument,"З")==0))
               deb=deb+frad.sum;
            end;
         end;
      end;
   if (enddate == ТекущийОперДень())
     deb = deb + accdebetday(acc,code_currency);
   end;

   return (deb);
end;

macro acckredit(acc,code_currency,begdate,enddate,chapter)
var cred=$0;
file frad(arhdoc) key 2;
      rewind(frad);
      clearrecord(frad);
      frad.real_receiver=acc;
      frad.date_carry=begdate;
      frad.code_currency=code_currency;
      frad.sum=-$1000000000000.00;
      if (getGE(frad))
         prev(frad);
         while ((next(frad)) and (frad.real_receiver==acc)
                and (frad.date_carry<=enddate))
            if ((frad.result_carry!=23) and (index(frad.typedocument,"З")==0))
               cred=cred+frad.sum;
            end;
         end;
      end;
   if (enddate == ТекущийОперДень())
     cred = cred + acckreditday(acc,code_currency);
   end;

   return (cred);
end;

macro accbs(account,code_currency,facc)
var retbs;
file faccradd(account) key 0;
   if (valtype(facc)==0)
         facc=faccradd;
   end;
   rewind(facc);
   clearrecord(facc);
   facc.account=account;
   facc.code_currency=code_currency;
   if (getEQ(facc))
      retbs=facc.balance;
   else
      retbs="???";
   end;
   return (retbs);
end;

/* vea */
macro AccSymbType(account,code_currency,symb)
file faccr(account) key 0;
//file faccv("account$") key 0;
   macro find(account,code_currency)
     clearrecord(faccr);
     faccr.account=account;
     faccr.code_currency=code_currency;
     if (getEQ(faccr))
       return faccr.Connect_Account;
     end;       
     return 0;
   end;

   clearrecord(faccr);
   faccr.Account = find(account,code_currency);
   if(getEQ(faccr))
     if(index(faccr.Type_Account,symb)>0)
       return true;
     end;
   end;            
   return false;
end;


/* UserType клиента по номеру счета */
MACRO ClientUserType(sAcc)
file rvAcc(account) key 0;
file rvCln(client) key 0;
  rewind(rvAcc);
  rvAcc.Account = sAcc;
  IF (getEQ(rvAcc))
    rewind(rvCln);
    rvCln.Client = rvAcc.Client;
    IF (getEQ(rvCln))
       RETURN rvCln.szUserType;
    END;
  END; /* IF */
  close(rvAcc);
  RETURN "?";
END;

