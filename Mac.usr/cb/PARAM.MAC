/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 4.3                                          R-Style Software Lab

  File Name   : param.mac                                December 18,1997
  Description : Дополнительные параметры банка

└───────────────────────────────────────────────────────────────────────────*/
IMPORT globals, CTInter, "adress.mac";
import rsbDataSet;
import lib_lang;
import rsd, cb_sql;

CONST  
       Наименование_НалОргана = "",       /* Наименование налогового органа */
       Район_НалИнспекции = "г.Москва",   /* Район, город, в которых расположена 
                                             Государственная налоговая инспекция */
       Номер_НалИнспекции = 1,            /* Номер Государственной налоговой инспекции */
       Руководитель_СлужбыКонтроля = "",  /* Руководитель службы внутреннего контроля */
       Дата_начала_работы = date(1,1,2002); /* Для нумерации дневников с начала года */

/* Макросы для получения ИНН, E-Mail, номера телефона, факса, телекса
   и почтового индекса см. ниже */

/* 24.09.2007 Malakhova 110424*/
private const PTCODELEN = 36; /*Длина строкового значения кода субъекта*/

/*--------------------------------------------------------------------*/
FILE _param_party  ( party    );
FILE _param_offcr  ( officer  );
FILE _param_persn  ( person   );
FILE _param_fnstr  ( fininstr );
//FILE _param_partcod( partcode );
FILE _param_objcod( objcode );

/* 17.09.2007 Malakhova 110424*/
/*Историчность кодов*/
private macro findObjcodeDate()
    macro getReportDate()
        return execExp("ДатаОтчета");
        onError();
        return null;
    end;

    var objcodeDate = getReportDate();

    if (objcodeDate == date(0, 0, 0)) /*Дата отчета нулевая, то есть не задана*/
        objcodeDate = NULL;
    end;
    
    return objcodeDate;

end;

private var objcodeDate;

macro getObjcodeDate()
    objcodeDate = findObjcodeDate();

    return objcodeDate;
end;

/* 27.09.2007 Malakhova 110424*/
/*Формирование строки фильтра для даты кода субъекта
  alias - алиас таблицы objcode_dbt, на которую будет накладываться фильтр

  requiredDate - заданная дата, на которую нужно найти код, можно задавать в виде строки с именем поля
  Необязательный параметр, если не задан, то генерится фильтр поиска с использованием только активных кодов (object.t_state = 0)

  isCode - признак поиска ID субъекта по коду или наоборот: 0 - ищем код по заданному ID; >0 - ищем ID по заданному значению кода
  Необязательный параметр, по умолчанию 0

  1. setFilterForCodeDate(alias:string, [requiredDate:date,         [isCode:integer]])
  2. setFilterForCodeDate(alias:string, [requiredDateString:string, [isCode:integer]])

  Если неправильно заданы параметры функции, то она возвращает пустую строку
*/
macro setFilterForCodeDate(alias)
    var queryText;
    var rsbDataSet;

    var filterString;
    var dateString;
    var isCodeString;

    var requiredDate;
    var isCode;

    getParm(1, requiredDate);
    getParm(2, isCode);

    alias = getSqlString(alias);

    if   (valType(requiredDate) == V_UNDEF)
       if   (valType(isCode) == V_UNDEF)
           dateString   = "";
           isCodeString = "";
       elif (valType(isCode) == V_INTEGER)
           dateString   = "";
           isCodeString = ", NULL, " + isCode;
       else
           filterString = "";
       end;           
    elif (valType(requiredDate) == V_DATE)
       if   (valType(isCode) == V_UNDEF)
           dateString   = ", " + getSqlString("TO_DATE(''" + string(requiredDate:f) + "'', ''DD.MM.YYYY'')");
           isCodeString = "";
       elif (valType(isCode) == V_INTEGER)
           dateString   = ", " + getSqlString("TO_DATE(''" + string(requiredDate:f) + "'', ''DD.MM.YYYY'')");
           isCodeString = ", " + isCode;
       else
           filterString = "";
       end;           
    elif (valType(requiredDate) == V_STRING)
       if   (valType(isCode) == V_UNDEF)
           dateString   = ", " + getSqlString(requiredDate);
           isCodeString = "";
       elif (valType(isCode) == V_INTEGER)
           dateString   = ", " + getSqlString(requiredDate);
           isCodeString = ", " + isCode;
       else
           filterString = "";
       end;           
    else
       filterString = "";
    end;

    if (filterString != "")

        queryText = "SELECT rsb_rep_pt.get_dateConditionsUsingString(" + alias + dateString + isCodeString + ") filterString FROM DUAL";

        rsbDataSet = TRsbDataSet(queryText);

        rsbDataSet.moveNext();

        filterString = rsbDataSet.filterString;

    end;

    return filterString;
end;

/*Функция для заданной даты возвращает ближайшую (в сторону <= )дату в хронологии, за которую нужно брать код,
чтобы он был действителен на заданную дату. 
(Предполагается использовать для задания сегмента ключа t_bankDate при поиске кода по ID, если работаем через эмулятор)

getRealDateInCodeHistory(objectType:integer, codeKind:integer, requiredDate:date, objectId:integer)
Если дату найти не удалось, то возвращает curdate
*/
macro getRealDateInCodeHistory(objectType, codeKind, requiredDate, objectId)
    var queryText;
    var data;
    var resultDate;
    var dateString = "";
    if (requiredDate == date(0, 0, 0))
        dateString = "01.01.0001";
    else
        dateString = string(requiredDate:f);
    end;

    queryText =          "SELECT MAX(obj.t_bankDate) resultDate"
                + "\n" + "  FROM dobjcode_dbt obj     "
                + "\n" + " WHERE obj.t_objectType = " + objectType
                + "\n" + "   AND obj.t_codeKind   = " + codeKind
                + "\n" + "   AND obj.t_objectId   = " + objectId
                + "\n" + "   AND obj.t_bankDate  <= " + getSqlDate(requiredDate);

    data = TRsbDataSet(queryText);
    data.SetFieldType("resultDate", V_DATE);

    if (data.moveNext())
       if (data.resultDate == "")
          resultDate = date(0, 0, 0);
       elif (data.resultDate != NULL)
          resultDate = data.resultDate;
       else
          resultDate = date(31, 12, 9999);
       end;
    else
       resultDate    = date(31, 12, 9999);
    end;

    return resultDate;

end;

/*Аналог ПолучитьКодСубъекта, только без параметра ActiveCode*/
/*1. repGetPartyCode(code:String, codeKind:Integer, [requiredDate:Date], [error:integer])
     Если ID не найден, то возвращает NULL
  2. repGetPartyCode(partyId:Integer, codeKind:Integer, [requiredDate:Date], [error:integer], [recursive:integer])
     Если код не найден, то возвращает пустую строку*/
macro repGetPartyCode()
   var queryText;
   var rsbDataSet;

   var isIdByCode;  /*Признак поиска ID по коду*/
   var dateString = "";

   var resultCode = "";
   var resultID:INTEGER = NULL;

   var partyID;
   var partyCode;
   var codeKind;
   var requiredDate;

   var zeroParameter;
   var firstParameter;
   var secondParameter;
   var fourthParameter;

   var errorCode;

   var recursive;
   var stat;
   getParm(0,zeroParameter);
   /*Если первый параметр имеет странный тип*/
   if ((valType(zeroParameter) != V_INTEGER) and (valType(zeroParameter) != V_STRING))
      isIdByCode = 1;
      stat = 1;
      setParm(3,stat);
      resultCode = "";
   end;
   
   /*Если первый параметр имеет тип числа, то будем искать значение кода в виде строки*/
   if (valType(zeroParameter) == V_INTEGER )
      isIdByCode = 0;
      PartyID   = zeroParameter;
      PartyCode = "";
   else
      isIdByCode = 1; 
   /*Будем искать ID субъекта по строковому значению кода*/
      if (strLen(zeroParameter)  >  PTCODELEN - 1)     
        stat = 1;
        setParm(3,stat);
        resultCode = "";
      end;
      partyCode = zeroParameter;
   end;

   /*Получим вид кода*/
   
   getParm(1,firstParameter);

   if( (valType(firstParameter) != V_INTEGER ) and (valType(firstParameter) != 0 ))
      stat = 2;
      setParm(3,stat);

      if (isIdByCode == 0)
        resultID = NULL;
      else
        resultCode = "";
      end;

   elif (valType(firstParameter) == 0 )
     firstParameter = 6;
   end;

   CodeKind = firstParameter;

   /*Получим дату отчета*/

   getParm(2, secondParameter);

   /*Если дата задана, то ищем на эту дату*/
   if(valType(secondParameter) == V_DATE)
      requiredDate = secondParameter;
   /*Если дата не задана, то берем objcodeDate*/
   elif ((valType(secondParameter) == V_UNDEF) or (valType(secondParameter) == V_INTEGER))
      requiredDate = getObjcodeDate();
   else
      stat = 3;
      setParm(3,stat);

      if (isIdByCode == 0)
        resultID = NULL;
      else
        resultCode = "";
      end;

   end;

   /*Получаем флаг рекурсивного поиска*/ 
   getParm(4, fourthParameter);

   if ((valType(fourthParameter) != V_INTEGER) and (valType(fourthParameter) != V_UNDEF))
     stat = 5;
     setParm(3, stat);

     if (isIdByCode == 0)
       resultID = NULL;
     else
       resultCode = "";
     end;
   end;

   if (valType(fourthParameter) == V_UNDEF)
     recursive = 0;
   else
     recursive = fourthParameter;
   end;


   if (isIdByCode == 0)
      /*  Найти код по PartyID*/

      /*if (requiredDate == date(0, 0, 0))
          dateString = ", TO_DATE('01.01.0001', 'DD.MM.YYYY'), ";
      el*/if (requiredDate == NULL)
          dateString = ", NULL, ";
      else
          dateString = ", TO_DATE('" + string(requiredDate:f) +  "', 'DD.MM.YYYY'), ";
      end;

      queryText = "SELECT rsb_rep_pt.get_partyCode(" + CodeKind + ", " 
                                                     + PartyID 
                                                     + dateString 
                                                     + recursive 
                                                + ") resultCode FROM DUAL";

      rsbDataSet = TRsbDataSet(queryText);

      if (rsbDataSet.next())
          resultCode = rsbDataSet.resultCode;   
      end;

         /*  Вернуть код*/
      setParm(3, stat);
   else
      /*  Найти PartyID по коду*/

      /*if (requiredDate == date(0, 0, 0))
          dateString = ", TO_DATE('01.01.0001', 'DD.MM.YYYY') ";
      el*/if (requiredDate == NULL)
          dateString = ", NULL ";
      else
          dateString = ", TO_DATE('" + string(requiredDate:f) +  "', 'DD.MM.YYYY') ";
      end;

      queryText = "SELECT rsb_rep_pt.get_partyIdByCode(" + CodeKind + ", " 
                                                         + getSqlString(partyCode) 
                                                         + dateString 
                                                    + ") resultPartyId FROM DUAL";

      rsbDataSet = TRsbDataSet(queryText);

      if (rsbDataSet.next())
          resultID = rsbDataSet.resultPartyId;   
      else 
          resultID = NULL;
      end;
         /*  Вернуть код*/
      setParm(3, stat);
   end;

   if (isIdByCode == 0)
       return resultCode;
   else
       return resultID;
   end;
end;

/*Аналог GetPartyInn, только с датой
  repGetPartyInn(ID:Integer [, requiredDate, [, Flag:Integer ]]):String
  Если не задана дата, то по умолчанию ищем на отчетную дату. Если же в свою очередь не задана отчетная дата, 
    то будем искать среди активных кодов
  Если не задан флаг, то по умолчанию он = 0, то есть возвращаем короткий ИНН

  Если ИНН не нашли, то возвращаем ""
*/
macro repGetPartyInn(partyId)
    var requiredDate;
    var requiredDateString;

    var flag;
    var flagString;

    var queryText;
    var data;

    var resultINN;

    getParm(1, requiredDate);
    getParm(2, flag);

    if (valType(requiredDate) == V_UNDEF) /*не задана дата*/
        if (valType(flag) == V_UNDEF)     /*не задан флаг*/
            if (getObjcodeDate() != NULL) /*есть отчетная дата*/
                requiredDateString = ", " + getSqlDate(getObjcodeDate()); /*берем за отчетную дату*/    
            else
                requiredDateString = ""; /*ищем только среди активных кодов*/
            end;
            flagString         = "";    
        else  /*флаг задан*/
            if (getObjcodeDate() != NULL) /*есть отчетная дата*/
                requiredDateString = ", " + getSqlDate(getObjcodeDate()); /*берем за отчетную дату*/    
            else
                requiredDateString = ", NULL"; /*ищем только среди активных кодов*/
            end;
            flagString         = ", " + string(flag);    
        end;
    else  /*дата задана*/
        if (valType(flag) == V_UNDEF)  /*флаг не задан*/
            if ((requiredDate != NULL) and (requiredDate != date(0,0,0))) /*заданная дата не нулевая*/
                requiredDateString = ", " + getSqlDate(requiredDate);     /*берем за нее*/
            else  /*пробуем искать на отчетную дату вместо заданной*/
                if (getObjcodeDate() != NULL) /*есть отчетная дата*/
                    requiredDateString = ", " + getSqlDate(getObjcodeDate()); /*берем за отчетную дату*/    
                else
                    requiredDateString = ""; /*ищем только среди активных кодов*/
                end;
            end;
            flagString         = "";    
        else   /*флаг задан*/
            if ((requiredDate != NULL) and (requiredDate != date(0,0,0))) /*заданная дата не нулевая*/
                requiredDateString = ", " + getSqlDate(requiredDate);     /*берем за нее*/
            else  /*пробуем искать на отчетную дату вместо заданной*/
                if (getObjcodeDate() != NULL) /*есть отчетная дата*/
                    requiredDateString = ", " + getSqlDate(getObjcodeDate()); /*берем за отчетную дату*/    
                else
                    requiredDateString = ""; /*ищем только среди активных кодов*/
                end;
            end;
            flagString         = ", " + string(flag);    
        end;
    end;
    
    
    queryText = "SELECT rsb_rep_pt.get_PartyInn(" + partyId + ", 1" + requiredDateString + flagString + ") resultInn FROM DUAL "; 

    data = TRsbDataSet(queryText);

    if (data.next())
        resultInn = data.resultInn;
    else
        resultInn = "";       
    end;

    return resultInn;

end;

/* Код национальной валюты */
MACRO  NATIONALCURCODE
 _param_fnstr.FIID = 0;
 getEQ( _param_fnstr );
 return int(_param_fnstr.ISO_Number);
END;

MACRO New_cur( cod )
  if ( cod == 0 ) return NATIONALCURCODE;
  else            return cod;
  end;
END;

/* Спозиционировать по необходимости указатель в файле party на запись
   по нашему банку */
MACRO gotoPos
 if( _param_party.PartyID == {OurBank} )
  return TRUE;
 end;

 _param_party.PartyID = {OurBank};
 return ( getEQ( _param_party ) );
END;

/*--------------------------------------------------------------------*/

/* Получить ИНН */
MACRO ИНН
 return repGetPartyInn({OurBank}, NULL, 1); /* длинный код */
END;

/* Получить E-Mail */
MACRO E_Mail
 record RecordAdress ( adress  );
 if( gotoPos )
   if(НайтиЮридическийАдресСубъекта(_param_party.PartyID,RecordAdress))
     return RecordAdress.E_Mail;
   end;
 end;
 return "";
END;

/* Номер телефона */
MACRO Номер_телефона
 record RecordAdress ( adress  );
 if( gotoPos )
   if(НайтиЮридическийАдресСубъекта(_param_party.PartyID,RecordAdress))
     return RecordAdress.PhoneNumber;
   end;
 end;
 return "";
END;

/* Номер факса */
MACRO Номер_факса
 record RecordAdress ( adress  );
 if( gotoPos )
   if(НайтиЮридическийАдресСубъекта(_param_party.PartyID,RecordAdress))
     return RecordAdress.Fax;
   end;
 end;
 return "";
END;

/* Телекс */
MACRO Номер_телекса
 record RecordAdress ( adress  );
 if( gotoPos )
   if(НайтиЮридическийАдресСубъекта(_param_party.PartyID,RecordAdress))
     return RecordAdress.TelexNumber;
   end;
 end;
 return "";
END;

/* Почтовый индекс */
MACRO ПочтовыйИндекс
 record RecordAdress ( adress  );
 if( gotoPos )
   if(НайтиЮридическийАдресСубъекта(_param_party.PartyID,RecordAdress))
     return RecordAdress.PostIndex;
   end;
 end;
 return "";
END;

/* Факс исполнителя */
MACRO Факс_Исполнителя
 record RecordAdr ( adress  );
 _param_persn.Oper = {Oper};
 if( not getEQ( _param_persn ) )
  return Номер_факса;
 end;

 _param_party.PartyID = _param_persn.PartyID;
 if( not getEQ( _param_party ) )
  return Номер_факса;
 end;
 ClearRecord(RecordAdr);
 НайтиЮридическийАдресСубъекта(_param_party.PartyID,RecordAdr);
 return trim( RecordAdr.Fax );
END;

/* Должность исполнителя */
MACRO Должность_Исполнителя
 _param_persn.Oper = {Oper};
 if( not getEQ( _param_persn ) )
  return "";
 end;

 _param_offcr.PartyID  = {OurBank};
 _param_offcr.PersonID = _param_persn.PartyID;
 if( not getEQ( _param_offcr ) )
  return "";
 end;
 return trim( _param_offcr.Post );
END;

/* Телефон руководителя */
MACRO Телефон_Руководителя
 record RecordAdr ( adress  );
 keynum( _param_offcr, 1 );

 _param_offcr.PartyID       = {OurBank};
 _param_offcr.IsFirstPerson = "X";
 if( not getEQ( _param_offcr ) )
  keynum( _param_offcr, 0 );/* вернуть прежний ключ */
  return Номер_телефона;
 end;

 _param_party.PartyID = _param_offcr.PersonID;
 keynum( _param_offcr, 0 );/* вернуть прежний ключ */
 if( not getEQ( _param_party ) )
  return Номер_телефона;
 end;
 ClearRecord(RecordAdr);
 НайтиЮридическийАдресСубъекта(_param_party.PartyID,RecordAdr);
 return trim( RecordAdr.PhoneNumber );
END;

/* Телефон бухгалтера */
MACRO Телефон_Бухгалтера
 record RecordAdr ( adress  );
 keynum( _param_offcr, 2 );

 _param_offcr.PartyID       = {OurBank};
 _param_offcr.IsSecondPerson = "X";
 if( not getEQ( _param_offcr ) )
  keynum( _param_offcr, 0 );/* вернуть прежний ключ */
  return Номер_телефона;
 end;

 _param_party.PartyID = _param_offcr.PersonID;
 keynum( _param_offcr, 0 );/* вернуть прежний ключ */
 if( not getEQ( _param_party ) )
  return Номер_телефона;
 end;
 ClearRecord(RecordAdr);
 НайтиЮридическийАдресСубъекта(_param_party.PartyID,RecordAdr);
 return trim( RecordAdr.PhoneNumber );
END;


MACRO Код_СОАТО
 Var NumInList = 0;

 if( gotoPos )
  if( not getMainObjAttr( null,
                         OBJTYPE_PARTY,
                         UniID( _param_party, OBJTYPE_PARTY ),
                         12,
                         null,
                         null,
                         NumInList ) )
   return "";
  else
   return NumInList;
  end;
 end;
 return "";
END;


MACRO Код_ОКПО
 if( gotoPos )
  return _param_party.OKPO;
 end;
 return "";
END;

/* 17.09.2007 Malakhova 110424*/
MACRO Рег_номер
 var code = "";
 var data;
 var queryText;

 if (getObjcodeDate() != NULL) /*Если задана дата отчета, то ищем за нее*/
     if( gotoPos )
        _param_objcod.objectType = OBJTYPE_PARTY;
        _param_objcod.CodeKind   = 13;
        _param_objcod.objectId   = _param_party.PartyID;
        _param_objcod.bankDate   = getRealDateInCodeHistory(OBJTYPE_PARTY, 13, getObjcodeDate(), _param_party.PartyID); 
        getEQ(_param_objcod);
        code = _param_objcod.Code;
     end;
 else /*Если дата отчета не задана, то ищем только среди активных кодов*/
     if (gotoPos)
         queryText =          "SELECT t_code"
                     + "\n" + "  FROM dpartcode_dbt"
                     + "\n" + " WHERE t_codeKind = " + 13
                     + "\n" + "   AND t_partyId  = " + _param_party.PartyID;
     

         data = TRsbDataSet(queryText);
         if (data.next())
            code = data.t_code;
         end;
     end;
 end;

 return code;
END;

/* Основной государственный регистрационный номер */
MACRO ОГРН
 var code = "";
 var data;
 var queryText;
 if (getObjcodeDate() != NULL)
     if( gotoPos )
         _param_objcod.ObjectType = OBJTYPE_PARTY;
         _param_objcod.CodeKind   = 27;
         _param_objcod.ObjectID   = _param_party.PartyID;
         _param_objcod.bankDate   = getRealDateInCodeHistory(OBJTYPE_PARTY, 27, getObjcodeDate(), _param_party.PartyID); 
        getEQ(_param_objcod);
        code = _param_objcod.Code;
     end;
 else
     if (gotoPos)
         queryText =          "SELECT t_code"
                     + "\n" + "  FROM dpartcode_dbt"
                     + "\n" + " WHERE t_codeKind = " + 27
                     + "\n" + "   AND t_partyId  = " + _param_party.PartyID;    

         data = TRsbDataSet(queryText);
         if (data.next())
            code = data.t_code;
         end;
     end;
 end;

 return code;
END;

MACRO ДатаНачалаРаботы
 return Дата_начала_работы;
END;

/* EoF */
