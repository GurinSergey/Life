/*---------------- Макро-файл, вызываемый из счета ---------------*/
/*                                                                */
/* Савченко Д -  Невыясненные документы по счету                  */
/*            - расчет % для Пойдём!                             */
/*                                                                */
/*----------------------------------------------------------------*/

import RsbDataSet;

private record a( account );

private macro percent( acc )

 var DateB, DateE, percnote, Year, Month, nmob, nmtlg, obbook, obsheet, i=3, RestAcc, Normalize = 0, procent;
 var k = 1, summ = 0;
 var day_count = 0;

   percnote = ReadNoteForObject(4,UniID(acc, OBJTYPE_ACCOUNT),19);

   if (not percnote)
     percnote = 0.00;
     if (not getDouble(percnote,"% ставка в примечании 19 не задана| задайте значение",5))
        return 0;
     end;
     if (not percnote)
        return 0;
     end;
   end;

	
//   println ("Дата":10, "Сумма остатка по выписке":30, "% ставка":10, "Сумма начисленных %%":20);
   nmob = CreateObject ("rsax","TRsAxServer",string("RsAxServer", UserNumber()),IsStandAlone());
   nmtlg = nmob.CreateComObject ("Excel.Application");
   obBook = nmtlg.Workbooks.add;
   obSheet = obBook.ActiveSheet(); 
/*obSheet.Range("A"+2).Value="Дата";
   obSheet.Range("B"+2).Value="Сумма остатка по выписке";
   obSheet.Range("C"+2).Value="% ставка";
   obSheet.Range("D"+2).Value="Сумма начисленных %%";
*/
  
[                                     #####################################                                                ]
({Name_bank});
  
   obSheet.Range("A1:F1").Merge;
   obSheet.Cells(k,1).Value			=	{Name_bank};
   obSheet.Cells(k,1).HorizontalAlignment	=	3;
   
   k = k + 1;

  
   obsheet.pagesetup.zoom=90;



  datesplit({curdate}, null, Month, Year);
  DateB = date(1, Month, Year);
  DateE = {curdate};

  if (getdate(DateB, "Выберите дату начала расчета"))
      if (getdate(DateE, "Выберите дату окончания расчета"))

         if (DateE < DateB)
            msgbox("Дата начала отчета больше даты окончания");
            return 0;
         end;

[##########################################################################                                                ]
("Расчёт процентов за период с " + DateB + " по " + DateE);

	obSheet.Range("A2:F2").Merge;
	obSheet.Cells(k,1).Value			=	"Расчёт процентов за период с " + DateB + " по " + DateE;
	   
	k = k + 1;

[##########################################################################                                                ]
("Счёт/договор: " + acc.account + " RUB  Номер счёта % 5679");

	obSheet.Range("A3:F3").Merge;
	obSheet.Cells(k,1).Value			=	"Счёт/договор: " + acc.account + " RUB  Номер счёта % 5679" ;
	   
	k = k + 1;



	var clisql = "	SELECT PART.T_NAME "
+"\n			FROM daccount_dbt acc, dparty_dbt part "
+"\n			WHERE acc.t_client = part.t_partyid "
+"\n			      AND acc.t_account = '" + acc.account + "'";
	var clirsl = trsbdataset(clisql);
	clirsl.movenext();

[##########################################################################                                                ]
("Клиент: " + clirsl.t_name);

	obSheet.Range("A4:F4").Merge;
	obSheet.Cells(k,1).Value			=	"Клиент: " + clirsl.t_name;
	   
	k = k + 1;

[ ┌──────────┬──────────┬─────┬───────────────┬──────────────┬─────────────────┐
  │ Начало   │ Конец    │Число│ Остаток счета │   Ставка     │    Сумма        │
  │ периода  │ периода  │дней │               │   год, %     │    процентов    │
  ├──────────┼──────────┼─────┼───────────────┼──────────────┼─────────────────┤
];
         
	obSheet.Cells(k,1).Value="Начало периода";
	obSheet.Cells(k,2).Value="Конец периода";
	obSheet.Cells(k,3).Value="Число дней";
	obSheet.Cells(k,4).Value="Остаток счёта";
	obSheet.Cells(k,5).Value="Ставка год, %";
	obSheet.Cells(k,6).Value="Сумма процентов";
	
	k = k + 1;

         datesplit(DateB, null, Month, Year);

         while (DateE >= dateB)
            day_count = day_count + 1;
            RestAcc = abs(resta(acc.account, (dateB-1), (dateB-1), acc.chapter));
            procent = ((percnote*RestAcc)/(100*(date(1,1,(Year+1))-date(1,1,Year))) + Normalize);
            Normalize =  procent - round(procent,2);
            procent = round(procent,2);
//          println (DateB:10, RestAcc:30, Double(percnote):10, procent:20);
[ │##########│##########│#####│###############│##############│#################│
](DateB, DateB, 1, RestAcc, Double(percnote), procent);

            obSheet.Cells(k, 1).Value=DateB;
            obSheet.Cells(k, 2).Value=DateB;
            obSheet.Cells(k, 3).Value="1";
            obSheet.Cells(k, 4).Value=RestAcc;
            obSheet.Cells(k, 5).Value=Double(percnote);
            obSheet.Cells(k, 6).Value=procent;
            summ = summ + procent;
            k = k + 1;
            dateB = dateB + 1;
            i = i + 1;

         end;
[ └──────────┴──────────┴─────┴───────────────┴──────────────┴─────────────────┘];

[ Итого:                 #####                                ################# ]
(day_count, summ);
	obSheet.Cells(k, 1).Value= "Итого: ";
	obSheet.Cells(k, 3).Value= day_count; //="=СУММ(C6:C"+(i-1)+")";
	obSheet.Cells(k, 6).Value= summ; //="=СУММ(F6:F"+(i-1)+")";
	k = k+1;
[ 

  Исполнитель:                           _________________    ################]
({Name_Oper});
	k = k + 2;
	obSheet.Cells(k, 1).Value= "Исполнитель: ";
	obSheet.Cells(k, 2).Value="";
	obSheet.Cells(k, 3).Value="____________";
	obSheet.Cells(k, 4).Value={Name_Oper};


   	obSheet.Columns("A:A").EntireColumn.AutoFit;
   	obSheet.Columns("B:B").EntireColumn.AutoFit;
   	obSheet.Columns("C:C").EntireColumn.AutoFit;
   	obSheet.Columns("D:D").EntireColumn.AutoFit;
	ObSheet.Columns("D:D").numberformat			 = "";
   	obSheet.Columns("E:E").EntireColumn.AutoFit;
   	obSheet.Columns("F:F").EntireColumn.AutoFit;
	ObSheet.Columns("F:F").numberformat			 = "";

       else
        return 0;
      end;
  else
    return 0;
  end;

   nmtlg.visible = true;

 return 0;

end;


private macro accountUn( account )

 Execmacrofile("listunknown","main",Account);
/*  println( "Это счет N ", a.Account );*/

 return 0;
end;


/* - Реализация расчета процентов для пойдем!*/
private macro account( aa )
 setbuff( a, aa );

array main;
var m,_staccount=string(a.account);

 main(asize(main)) = "Невыясненные документы по счету";
 main(asize(main)) = "Расчет процентов";

 m = menu(main);

  If (main(m) == "Невыясненные документы по счету")
      accountUn (_staccount);
   elif (main(m) == "Расчет процентов")
      Percent(a);
  end;

end;

