/*
 $Name:        prpm_common.mac
 $Module:      ББ
 $Description: Инициализация параметров для печати ордеров по передаче ценностей
*/

// Программист: TDV 2014

import prpm, prcs, bnk_common;

const DPRT_TYPE_FILIAL = 1;  /*филиал*/
const DPRT_TYPE_VSP    = 2;     /*ВСП*/

macro getFIINameOrCYY( FIID, isName ):string
  file fiinstr(fininstr);
  if( (pr_pmpaym.rec.PayFIID != NATCUR) OR (pr_pmpaym.rec.FIID != NATCUR) )
    ClearRecord(fiinstr);
    fiinstr.FIID = FIID;
    var res = "";
    if( GetEQ(fiinstr) )
      if( isName )
        res = fiinstr.Name;
      else
        res = fiinstr.CCY;
      end;
    end;
    return res;
  else
    return "";
  end;
end;

CLASS TInOutOrderPrintData
  var Number               : string,
      DateStr              : string,
      BankName             : string,
      DebAcc               : string,
      DFI                  : string,
      DAm                  : string,
      OwnerName            : string,
      CFI                  : string,
      CredAcc              : string,
      CredAmount           : string,
      Sh                   : string,
      Content              : string,
      ValName              : string,
      Valcount             : string,
      ValSum               : string,
      Propis               : string,
      FIO                  : string,
      RegDoc               : TArray,
      order                : RsbValueTransOrder;

  var CCy_Code             : string,
      CCy_Name             : string;

  var RegKeyFDate          : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\ФОРМАТ_ДАТЫ",          dateFormat,
      RegKeyNVal           : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\НАИМЕНОВАНИЕ_ВАЛЮТЫ",  nameVal,
      RegKeySSum           : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\РАЗДЕЛИТЕЛЬ_СУММЫ",    sepSumm,
      RegKeyToDigit        : string = "COMMON\\ПЕЧАТЬ\\ОРДЕР\\КАССА\\ДРОБНАЯ_ЧАСТЬ_ЦИФРАМИ",toDigit;

  RegDoc = TArray();

  /*Функция для оформления с = или копейками через -*/
  macro MoneyStyle(sum:money):string          
    var newsum:string;
  if (sum !=0)
    newsum = sum;
    newsum = StrSubSt(newsum, ".00", "=");
    newsum = StrSubSt(newsum, ".", "-"); 
  else
    newsum = "";
  end;
  return newsum;
  end;

  private macro getRegVal()
    GetRegistryValue( RegKeyFDate,   V_INTEGER, dateFormat, "" );
    GetRegistryValue( RegKeyNVal,    V_INTEGER, nameVal,    "" );
    GetRegistryValue( RegKeySSum,    V_STRING,  sepSumm,    "" );
    GetRegistryValue( RegKeyToDigit, V_BOOL,    toDigit,    "" );
  end;

  private macro FillDateString( Date : date )
    return  string( SubStr( string(Date:m:f), 1, strlen(string(Date:m:f) ) - 1 ), "ода" ) ;
  end;

  private macro GetRegDoc( pmvaltr:TRecHandler ):TArray
  
    var strRegDoc:string = GetPaperKindName( pmvaltr.rec.PaperKind );
    if( strRegDoc )
      if( trim( pmvaltr.rec.PaperSeries ) )
        strRegDoc = strRegDoc + " серии " + trim( pmvaltr.rec.PaperSeries );
      end;
      if( trim( pmvaltr.rec.PaperNumber ) )
        strRegDoc = strRegDoc + " номер " + trim( pmvaltr.rec.PaperNumber );
      end;
      if( trim( pmvaltr.rec.PaperIssuer ) )
        strRegDoc = strRegDoc + ", выдан " + trim( pmvaltr.rec.PaperIssuer );
      end;
      if( pmvaltr.rec.PaperIssuedDate != date(0,0,0) )
        strRegDoc = strRegDoc + string( ", ", pmvaltr.rec.PaperIssuedDate, " года." );
      end;
    end;

    if( FIO == "" )
      strRegDoc = "";
    end;

    var arrRegDoc:TArray = StrSplit2( strRegDoc, 93, 47, 3 );
    return arrRegDoc;
  end;

  /*Заполнение наименования банков*/
  private macro FillBankName(Department,OperNode):string
    var cmd, RS;
    var BankName: string = "",
        EndSymb:  string = "";
 
    cmd = RSDCommand( "SELECT prt.t_Name, prt.t_ShortName, dp.t_NodeType "
                      "  FROM dparty_dbt prt, ddp_dep_dbt dp "
                      " WHERE prt.t_PartyId = dp.t_PartyID "
                      "     AND dp.t_Code IN (?,?) " ) ; 
    cmd.addParam( "", RSDBP_IN, Department );
    cmd.addParam( "", RSDBP_IN, OperNode );
    cmd.execute();
    RS = TRsbDataSet(cmd);
    while( RS.movenext() )
      if( Rs.Name != "" )
        BankName = BankName + Rs.Name;
      else
        BankName = BankName + Rs.ShortName;
      end;
      if( (Rs.NodeType == DPRT_TYPE_VSP) AND (strlen(EndSymb) == 0) )
        BankName = BankName + " <";
        EndSymb = ">";
        continue;
      end;
      BankName = BankName + EndSymb;
      break;
    end;
    return BankName;
  end;

  private macro SplitAmount( sum:money ):string
    private var str = string( Sum:f ), stat = 0;
    if( ( toDigit == FALSE ) AND ( SubStr( str, strlen( str ) - 2 ) == "-00" ) )
      str = SubStr( str, 1, strlen( str ) - 3 ) + sepSumm + IfThenElse( sepSumm, "00", "" );
    end;
    return str;
  end;

  /* -----------------------------------
  Проинициализировать данными мемориального ордера
  ----------------------------------- */
  MACRO InitByCashOrder( pmpaym:TRecHandler, pmrmprop:TRecHandler, pmstval:TRecHandler, pmvaltr:TRecHandler )

    Number     = pmrmprop.rec.Number;
    DateStr    = IfThenElse( dateFormat==0, pmrmprop.rec.Date, FillDateString( pmrmprop.rec.Date ) );

    BankName   = FillBankName( pmpaym.rec.Department, pmpaym.rec.OperNode );
    order      = RsbValueTransOrder( pmpaym.rec.PaymentID );

    // выводить если нет разноски иначе из расноски
    DebAcc     = pmpaym.rec.PayerAccount;
    DFI        = pmpaym.rec.FIID;
    DAm        = SplitAmount( pmpaym.rec.Amount );
    Propis     = MoneyStyle( pmpaym.rec.PayAmount );
    CFI        = pmpaym.rec.PayFIID;
    CredAcc    = pmpaym.rec.ReceiverAccount;
    CredAmount = SplitAmount( pmpaym.rec.PayAmount );
    //
    OwnerName  = pmvaltr.rec.OwnerValues;
    Sh         = pmrmprop.rec.ShifrOper;
    Content    = pmrmprop.rec.Ground;
    ValName    = pmstval.rec.NameValue;
    Valcount   = pmstval.rec.CountValue;
    ValSum     = pmstval.rec.AmountValue;

    FIO        = pmvaltr.rec.ReceiverNameValues;

    RegDoc     = GetRegDoc( pmvaltr );

  END;

  getRegVal();
  
END;
