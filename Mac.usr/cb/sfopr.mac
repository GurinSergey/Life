/*                                                                          */
/*                         R-Style SoftWare Lab                             */
/*                                                                          */
/****************************************************************************/
/*                       Подсистема "ГКБО"                                  */
/*                                                                          */
/*                             ПЗО -- новая модель                          */
/*  Макрос формирования документов на шаге                                  */
/*  операции оплаты периодического взимания                                 */
/*                                                                          */
/*  Имя файла: sfopr.mac                                                    */
/*  Создан: 23.07.2001                                        Стасевич В.   */
/*                                                                          */
/****************************************************************************/

import InsCarryDoc, sfacrpay, sfpay;
/* EVG */
import "sf_prbb.mac";


private const SFDOC_DEF_PERIOD = 51;
private const RashetCatCode = "+Расчеты"; 
private const RashetNVPI_CatCode = "+Расчеты,НВПИ"; 

record sfdefcomRec( "sfdef.dbt" );

record SfContrRec("sfcontr.dbt");
record SfComissRec("sfcomiss.dbt");
record SfAccrueRec("sfaccrue.dbt");
record SfConComRec("sfconcom.dbt");
record SfSrvDocRec("sfsrvdoc.dbt");

private macro GetNameAlg( TypeAlg, NumberAlg )
  file Алгоритмы ( namealg );    /* Названия алгоритмов*/
   Алгоритмы.iTypeAlg   = TypeAlg;
   Алгоритмы.iNumberAlg = NumberAlg;
   if( getEQ(Алгоритмы) )
         return Алгоритмы.szNameAlg;
   else  return "не найден алгоритм "+NumberAlg;
   end;
end;


private macro InsertSfRepAcc( SfDefCom, BeginDate, EndDate, TransactionDate, Amount, InvoiceID, 
                              Debit, Credit, Comment, Kind, Error )

  var strQuery = "", query = "";  
  var cmd, rs;
  var count = 0;
  
  query = " SELECT count(1) as ncount FROM DSFREPACC_TMP WHERE T_BEGINDATE = ?  AND T_CONTRID = ? AND T_FEETYPE = ? AND T_COMISSNUMBER = ? AND T_KIND = ? ";

  cmd = RsdCommand( query );

  cmd.addParam( "", RSDBP_IN, BeginDate );
  cmd.addParam( "", RSDBP_IN, SfDefCom.SfContrID       );
  cmd.addParam( "", RSDBP_IN, SfDefCom.FeeType         );
  cmd.addParam( "", RSDBP_IN, SfDefCom.CommNumber      );
  cmd.addParam( "", RSDBP_IN, Kind                     );

  rs = RsdRecordset( cmd );

  if( rs.moveNext() )
    count = rs.value("ncount" );
  end;

  if(count == 0)
  strQuery = "INSERT INTO DSFREPACC_TMP "
             "(T_SFDEFCOMID, T_BEGINDATE, T_ENDDATE, T_TRANSACTIONDATE, T_AMOUNT, " +
             " T_INVOICEID,  T_DEBIT,     T_CREDIT,  T_COMMENT, T_CONTRID, T_FEETYPE, T_COMISSNUMBER, T_DEPARTMENT, " +
             " T_KIND, T_ERRORCODE ) " +
             " VALUES( ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ?, ? )";
  else
    strQuery = " UPDATE DSFREPACC_TMP SET "
               " T_BEGINDATE = ?, T_ENDDATE = ?, T_TRANSACTIONDATE = ?, T_AMOUNT = ?, " +
               " T_INVOICEID = ?, T_DEBIT = ?,   T_CREDIT = ?,  T_COMMENT = ?, T_CONTRID = ?, T_FEETYPE = ?, T_COMISSNUMBER = ?, T_DEPARTMENT = ?, " +
               " T_KIND  = ?, T_ERRORCODE = ? " +
               " WHERE T_BEGINDATE = ?  AND T_CONTRID = ? AND T_FEETYPE = ? AND T_COMISSNUMBER = ? AND T_KIND = ?";

  end;

  cmd = RsdCommand( strQuery );
  if(count == 0)
  cmd.addParam( "", RSDBP_IN, SfDefCom.ID );
  end;
  cmd.addParam( "", RSDBP_IN, BeginDate );
  cmd.addParam( "", RSDBP_IN, EndDate );
  cmd.addParam( "", RSDBP_IN, TransactionDate );
  cmd.addParam( "", RSDBP_IN, Amount );
  cmd.addParam( "", RSDBP_IN, InvoiceID );
  cmd.addParam( "", RSDBP_IN, Debit );
  cmd.addParam( "", RSDBP_IN, Credit );
  cmd.addParam( "", RSDBP_IN, Comment );
  cmd.addParam( "", RSDBP_IN, SfDefCom.SfContrID );
  cmd.addParam( "", RSDBP_IN, SfDefCom.FeeType );
  cmd.addParam( "", RSDBP_IN, SfDefCom.CommNumber );
  cmd.addParam( "", RSDBP_IN, SfDefCom.Department );
  cmd.addParam( "", RSDBP_IN, Kind );
  cmd.addParam( "", RSDBP_IN, Error );
  
  if(count != 0)
    cmd.addParam( "", RSDBP_IN, BeginDate                );
    cmd.addParam( "", RSDBP_IN, SfDefCom.SfContrID       );
    cmd.addParam( "", RSDBP_IN, SfDefCom.FeeType         );
    cmd.addParam( "", RSDBP_IN, SfDefCom.CommNumber      );
    cmd.addParam( "", RSDBP_IN, Kind                     );
  end;

  cmd.execute();

end;


macro UpdateSfRepAcc(SfDefComRec, Comment)

  var strQuery = "", cmd;  

  strQuery = "UPDATE DSFREPACC_TMP SET T_COMMENT = ?, T_ERRORCODE = 1 WHERE T_BEGINDATE = ?  AND T_CONTRID = ? AND T_FEETYPE = ? AND T_COMISSNUMBER = ? AND (T_ERRORCODE IS NULL OR T_ERRORCODE = 0)";
             
  cmd = RsdCommand( strQuery );

  cmd.addParam( "", RSDBP_IN, substr(Comment, 1, 256) );
  cmd.addParam( "", RSDBP_IN, SfDefComRec.DatePeriodBegin );
  cmd.addParam( "", RSDBP_IN, SfDefComRec.SfContrID       );
  cmd.addParam( "", RSDBP_IN, SfDefComRec.FeeType         );
  cmd.addParam( "", RSDBP_IN, SfDefComRec.CommNumber      );

  cmd.execute();
  
end;



private macro SfDefChangeStatus( SfDefID, payMethod )

  var status = SFDEFCOM_STATUS_FOR_PAY;

  if( PayMethod == SF_PAY_METHOD_DOCUMENT )
    status = SFDEFCOM_STATUS_PAYED;
  end;

  if( not Opr_ChangeSfDefComStatus(SfDefID, status) )
    return 1;
  else
    return 0;
  end;
end;

//Формирование документов оплаты
private macro SfDefFormPayDoc( OutBuff, AccountDebit, AccountCredit )
  
  var stat = 0;

  var sidebet  = TRecHandler("sfsi.dbt");
  var sicredit = TRecHandler("sfsi.dbt");
  sidebet.Clear;
  sicredit.Clear;

  if( (SfComissRec.InstantPayment == "X") AND (sfdefcomRec.Sum > 0) )
                                     
    if( SfGetSI_Uni( OBJTYPE_SFDEFCOM, sfdefcomRec, sidebet, sicredit ))
      MsgBox("Ошибка при взятии платежных инструкций");
      return 1;
    end;     

    var Comment= "";
    var SfConComPD = SfConComPrimDoc( 83/*DLDOC_SFCONCOM*/, SfConComRec, SfContrRec, SfDefComRec.FIID_Sum);

    //Формирование основания документов оплаты коммиссий
    var Ground = BuildGroundWithVO( SfDefComRec, SfComissRec, SfContrRec, sidebet.rec.PartyID, sicredit.rec.PartyID, SfSrvDocRec.DatePay );

    var IsNVPI:bool = IsNVPICarry( SfContrRec.PayFIID, SfDefComRec.FIID_Sum, SfContrRec.PayRateDateKind );

    if( stat == 0 )
      stat = SfFormDocs( OutBuff, sidebet, sicredit, SfComissRec, SfSrvDocRec.DatePay, Ground, 
        SfDefComRec.Sum, SfDefComRec.SumNDS, SfDefComRec.FIID_Sum, SFCONTR_DOC, SfContrRec.ID, OBJTYPE_SFDEFCOM, SfDefComRec,
        SfContrRec.PayMethod, SfConComPD, SfDefComRec.IsIncluded, isNVPI, 0, null, null, null, null, null, null, null, SfContrRec.PreAcptID);
    end;

    if(stat == 0)
      stat = SfDefChangeStatus( sfdefcomRec.ID, SfContrRec.PayMethod ); 
    end;

    if(stat != 0 )
      Comment = GetSfPay_ErrMsg();
    end;

    if( AccountDebit == "" )
      AccountDebit = sidebet.Rec.Account;
    end;
    
    if( AccountCredit == "" )
      AccountCredit = sicredit.Rec.Account;
    end;

    InsertSfRepAcc( sfdefcomRec, 
                    sfdefcomRec.DatePeriodBegin, sfdefcomRec.DatePeriodEnd, date(0,0,0), 
                    sfdefcomRec.Sum, -1/*sfdefcomRec.InvoiceID*/, 
                    AccountDebit, AccountCredit, Comment, 1, stat);

  end;

  return stat;
end;

macro ExecuteStep( outBuff, primIn )

  var status = 0, stat = 0;
  var Comment= "";
  var AccountDebit:string = "", AccountCredit:string = "", BeneAccCatCode:string;

  setbuff( sfdefcomRec, primIn  );

  //Расчет и доначисление
  if( (sfdefcomRec.Status == SFDEFCOM_STATUS_CALCULATED) AND (SfComissRec.ReceiverID == {HeadBankID}) )
    if( SfAccrueRec.Amount > $0 )
      SfAccrueRec.TransactionDate = {curdate};
      stat = SfCarryAccrue( SfDefComRec, SfAccrueRec, SfContrRec, SfConComRec, @AccountDebit, @AccountCredit );
    end;
  else
    if( SfAccrueRec.Amount != $0 )
      stat = SfCarryAccrueExtra( SfDefComRec, SfAccrueRec, SfContrRec, SfConComRec, @AccountDebit, @AccountCredit );
    end;
  end;

    if( stat == 0 )
      if( not Opr_ChangeSfDefComStatus(sfdefcomRec.ID, SFDEFCOM_STATUS_CALCULATED) )
        stat = 1;
      end;                                       
    end;

  if(stat != 0 )
    Comment = GetSfPay_ErrMsg();
  end;

  if( SfAccrueRec.Amount != $0 )
    InsertSfRepAcc(SfDefComRec, SfAccrueRec.BeginDate, SfAccrueRec.EndDate, SfAccrueRec.TransactionDate, SfAccrueRec.Amount, 
                     0, AccountDebit, AccountCredit, Comment, 0, stat);
  end;

  /*{MEV: Замена счёта в комиссии */
  DebugBreak;
  /* DONE: Достать id клиента из договора обслуживания */
  var ClientID = getClientOnContr(SfDefComRec.ConID), EndPeriodDate;
  /*GetOprDate_Kind( 5100001, EndPeriodDate); /*Дата окончания периода комиссии */   */
  GetOprDate_Kind( 5100002, EndPeriodDate); /*Дата оплаты комиссии */   
  /* EVG 01.12.2010 Добавил передачу ID ДО */
  selectAccountForSfPay( SfDefComRec.CommSum+SfDefComRec.NDSSum, SfDefComRec.Fiid_CommSum, EndPeriodDate, ClientID, SF_FEE_TYPE_PERIOD, SfDefComRec.Id,
                         SfDefComRec.ConID );
  /*MEV}*/
  
  if( stat == 0 )
    stat = SfDefFormPayDoc( outBuff, AccountDebit, AccountCredit );
  end;

  return stat;
end;


macro PostStepAction( message,     /* данный параметр может принимать следующие */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                Number_Step, /* номер шага операции          */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep,    /* вид шага операции                               */
                ID_Step )    /* идентификатор шага операции                     */

  var strQuery = "", cmd;
  setbuff (SfDefComRec, FirstDoc);

  // Происходит откат
  if ((message==OP_EXECUTE_STEP) AND errTrn) 
    UpdateSfRepAcc(SfDefComRec, "Ошибка создания документа оплаты: " + GetErrMsg());
    return 0;    
  end;

  /*if((message==OP_EXECUTE_STEP) AND (errTrn == 0))
    strQuery = "UPDATE DSFREPACC_TMP SFR SET SFR.T_INVOICEID = " +
                " NVL((SELECT CAST( (SELECT T_DOCUMENTID  FROM DOPRDOCS_DBT " +
                " WHERE T_ID_OPERATION = ? AND T_ID_STEP = ?  AND T_DOCKIND = 59 " + 
                "  AND ROWNUM <= 1) "                     +
                "  AS NUMBER) FROM DUAL) , 0)"                +
                " WHERE SFR.T_SFDEFCOMID = ? AND SFR.T_INVOICEID = -1 ";

    cmd = RsdCommand( strQuery );
    cmd.addParam( "", RSDBP_IN, ID_Oper );
    cmd.addParam( "", RSDBP_IN, ID_Step );
    cmd.addParam( "", RSDBP_IN, SfDefComRec.ID );
    cmd.execute();
    
  end;
  */

  return 0;
end;
