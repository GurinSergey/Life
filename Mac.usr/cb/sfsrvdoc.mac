/** 
 * Макрос скроллинга "Сервисные операции начисления и оплаты"
 * Скроллинг строится по файлу sfsrvdoc.dbt
 */
//Гуцу Е. 14.03.2013 Реализована проверка даты окончания при вводе сервисной операции расчёта годовой комиссии (C-18298).
//                   Также добавил проверку, что установлен флажок "Оплата" (а не начисление).





/* Буфер вводимой/редактируемой записи sfsrvdoc.dbt
 * структура записи 
 * ID:integer; 
 * ServKind:integer;      // Вид обслуживания, для ДО которого запускалась операция
 * DocKind:integer;       // Вид ПД сервисной операции
 * Kind:integer;          // Вид операции: 1 - Начисление 2 - Оплата
 * SfContrID:integer;     // Ссылка на ДО, по которому стартована сервисная операция
 * Status:integer;        // Возможные статусы сервисной операции: 1 - Открыта 2 - Закрыта.
 * DatePeriodEnd:date;    // Дата окончания периода сервисной операции.
 * DatePay:date;          // Дата выполнения проводок, формирования документов сервисной операции
 * CommNumber:integer;    // Номер комиссии
 * GroupID:integer;       // Группа комиссий, для которых запущена сервисная операция
 * AttrID:integer;        // 
 * Branch:integer;        // Узел ТС, для объектов которого запущена сервисная операция
 * Executer:integer;      // Операционист запустивший операцию
 * ExecutionDate:date;    // Опер. дата запуска операции
 * SysDate:date;          // Системная дата запуска операции
 * SysTime:time;          // Системное время запуска операции  
 * UnionContrID:integer;  // ссылка на СДО  
 * FeeType:integer;       // Тип взимания (SF_FEE_TYPE_PERIOD || SF_FEE_TYPE_LEVEL)
 * Oper:integer;          // Операционист, по счетам которого выполняется операция
 *
 */
/* EVG 14/03/2013 Добавил импорт, иначе не работают вызовы rsdCommand() и rsdRecordSet()
   из общего списка сервисных операций. */
import rsd;


private record SfSrvDocRec( sfsrvdoc );

/**
 * Функция проверки при сохранении панели ввода/редактирования
 *
 * Входные параметры - значения полей панели ввода/редактирования сервисной операции: 
 * FIID:integer;         // "Валюта"
 * bOnlyForeign:boolean; // "Только ин. валюта"
 * Object:string;        // "Счет"
 * 
 * Возвращаемое значение:
 * true - проверка прошла успешно - панель сохраняем
 * false - проверка не прошла - остаемся в панели
 * 
 */
macro CheckSfSrvDoc( FIID, bOnlyForeign, Object )

  var cmd, rs, query;

  /* EVG 14/03/2013 Проверка даты окончания при вводе сервисной операции расчёта 
     годовой комиссии (период в настройках указывается больше года). */
  query = " Select 1 from DSFCOMISS_DBT  " +
          "  Where t_Feetype         = 1 " +    // Тип взимания = Периодическое
          "    and t_Number          = ? " +
          "    and t_CalcPeriodType  = 4 " +    // Тип периода = Год
          "    and t_CalcPeriodNum   > 1 " ;    // Период > 1 года
  cmd = rsdCommand( query );
  cmd.addParam( "", RSDBP_IN, SfSrvDocRec.CommNumber );
  rs = rsdRecordSet( cmd );
  if( rs and rs.moveNext() )

     /* Проверяем дату окончания периода */

     /* Рассчитаем правильную дату. Для этого используем дату проводки (текущий день). */
     var dd, mm, yy, NeededDate;
     dateSplit( SfSrvDocRec.DatePay, dd, mm, yy );
     NeededDate = date( 1, mm, yy+1 ) -1;

     //msgbox( SfSrvDocRec.DatePay, " - ", NeededDate);
     if( SfSrvDocRec.DatePeriodEnd != NeededDate )
        if( GetTrue( true, "Дата окончания периода годовой комиссии, указанная вами (" + trim(string(SfSrvDocRec.DatePeriodEnd)) + ") " +
                           "не соответствует расчётной (" + trim(string(NeededDate)) + "). Поправить её?") )
           SfSrvDocRec.DatePeriodEnd = NeededDate;
        end;
     end;

  end;


  /* EVG 14/03/2013 Заодно проверим, чтобы пользователь не забыл поставить флажок "Оплата" */
  if( SfSrvDocRec.Kind == 1 )
     if( GetTrue( true, "Выбран флаг \"Начисление\". Исправить на \"Оплата\"?" ) )
        SfSrvDocRec.Kind = 2;
     end;
  end;


  return true;
end;