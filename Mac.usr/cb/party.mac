/*---------------------------------------------------------------------------\
|                       Файл    party.mac                                   |
|                     Для справочника субъектов                             |
\---------------------------------------------------------------------------*/

import  BankInter, PTInter, "pt_lib.mac", "set_rscode.mac", "Send_IC_text.mac";
// EVG
import  CTInter;
//Tikh
Import "Dbkt.mac";
Import lib_compare;
var fl;

record address( adress );
record persnidc( persnidc );

record СтарыйСубъект(party);
record Старый_Код(objcode);
record Новый_Код(objcode);

// EVG 
const GROUPID_Origin = 101,   // ID категории "Происхождение"
      CODE_Origin    = "ФР";  // Код происхождения "Фронт"


private Macro ВходитВГруппу(Oper,IdGroup)
 var stat = true; 
 var select, recSet, command;

 select = "SELECT t_name FROM DACSGROUP_DBT WHERE T_GROUPID = " + IdGroup;
 command = RSDCommand(select);
 command.execute();
 recSet = RSDRecordSet(command, RSDVAL_CLIENT, RSDVAL_STATIC);
 if (recSet and recSet.moveNext())
   select = "select * from DACSGROUPOPER_DBT where T_GROUPID = "+ IdGroup +" and T_OPER = " + Oper;
   command = RSDCommand(select);
   command.execute();
   recSet = RSDRecordSet(command, RSDVAL_CLIENT, RSDVAL_STATIC);
   if (not(recSet and recSet.moveNext()))
    stat = false;
   end;
  else 
   msgbox("Группа с номером " + IdGroup + " не найдена");
   stat = false;
 end;
 return stat;
End;


private macro PartyInspection(Субъект:RsbParty, Вид_Панели:integer)
/*
0 - панель субъекта
1 - панель адреса
2 - панель значения кода субъекта
3 - панель документа регистрации субъекта экономики.
4 - панель удостоверения личности физического лица.
5 - панель примечания субъекта экономики.
6 - панель примечания физического лица.
7 - список признаков категории субъекта экономики.
8 - кроме этого данную функцию вызываем при единичном закрытии одного субъекта по [F8]
*/
  //msgbox("Проверка входа в PartyInspection!!! Субъект.LastName = ", Субъект.LastName, ", Вид_Панели = ", Вид_Панели);
  return 0;
end;

/*Пример функции для проверки необходимости синхронизации
 */
private macro CheckSync(PartyInit:RsbParty, Party:RsbParty)
  //msgbox("Проверка входа в CheckSync!!! PartyInit.LastName = ", PartyInit.LastName, ", Party.LastName = ", Party.LastName);
  return 1;
end;

/*******************************************/
PRIVATE MACRO Новый_Субъект(Субъект:RsbParty, Вид_Кода:integer)

        return 0;
END;

/* Показать панель субъекта? */
private macro Показать_Панель_Субъекта(Субъект:RsbParty, Вид_Кода:integer)
  return PT_EXIT_SUCCESS;
end;

/* ***************************************** */
PRIVATE MACRO Проверить_Субъекта(Режим:integer, Субъект:RsbParty, Вид_Кода:integer, RunFromScroll:bool)
VAR stat = false, error;

/* Режимы проверки:
 PT_DEL      - удаление субъекта;       ┐ При этих значениях "Вид_Кода"
 PT_INS      - ввод субъекта;           ├ инициализируется видом кода, показываемого
 PT_EDIT     - корректировка субъекта;          │ в панели субъекта
 PT_OPEN     - открытие субъекта (из закрытых); │
 PT_CLOSE    - закрытие субъекта;               ┘

 PT_DELCODE  - удаление кода субъекта;    ┐ При этих значениях "Вид_Кода"
 PT_INSCODE  - ввод кода субъекта;        ├ инициализируется видом меняемого(вводимого\удаляемого)
 PT_EDITCODE -корректировка кода субъекта.┘ кода субъекта
*/

 stat = true;

 /* EVG Выполняем поиск значения категории "Происхождение" */
 var attr, code, num;
 var pty = TBFile ("party.dbt");
 record ctgP   ( "objattr" ); //для записи категории

 pty.Clear();
 pty.rec.PartyID = Субъект.PartyID;
 pty.GetEQ();

 if ( GetMainObjAttr( null, OBJTYPE_PARTY, UniID(pty, OBJTYPE_PARTY), GROUPID_Origin,
                      null,
                      code ) )

    /* Если происхождение = Фронт, запрещаем корректировку */
    if (code == CODE_Origin)
       stat = false;
       msgbox ("Внимание!|Данный субъект импортирован из системы \"Фронт\". Корректировка субъекта в RS-Bank запрещена.");
    end;
 end;
 /* EVG  *  -  *  -  *  */

 /* Если стоит флаг "предприниматель" добавляем "ИП"*/ 
// debugbreak;
/*Kozina если стоит категория тип субъекта (нотариус, адв.кабинет), то "ИП" не надо ставить*/
 /*if ( not GetMainObjAttr( null, OBJTYPE_PARTY, UniID(pty, OBJTYPE_PARTY), 16,
                      null,
                      code ))*/
 //Gurin S. 12.03.2016
 var ObjCat = RsbObjCategories( OBJTYPE_PARTY, UniID( pty, OBJTYPE_PARTY ));
 ObjCat.GetFirst( 16/*Тип субъекта*/, {curdate}, ctgP );
 if (not ctgP.attrid)
    if (Субъект.LegalForm == 2)
        var _ShortName = Субъект.ShortName;
        var _FullName = Субъект.FullName;
        /* 20.10.2010 Chesnokov D. Добавил обрезку пробелов, т.к. из фронта может прийти наименование и с пробелами в начале */
        if (Субъект.IsEmployer)
                //Если нет то Добавить ИП
                if (substr(trim(_ShortName), 1, 2) != "ИП")
                        _ShortName = "ИП " + _ShortName;                
                end;
                //Lavrenov: 18.04.2012 I-00168109
                if ((substr(trim(_FullName), 1, 2) != "ИП") and (index(strupr(_FullName),"ИНДИВИДУАЛЬНЫЙ ПРЕДПРИНИМАТЕЛЬ") == 0))
                        _FullName = "ИП " + _FullName;          
                end;
                Субъект.ShortName = _ShortName;
                Субъект.FullName = _FullName;
        end;
        /* 20.10.2010 Chesnokov D. Добавил обрезку пробелов, т.к. из фронта может прийти наименование и с пробелами в начале */
        if (not Субъект.IsEmployer)
                if (substr(trim(_ShortName), 1, 2) == "ИП")
                        _ShortName =  substr(_ShortName, 4);            
                end;
                if (substr(trim(_FullName), 1, 2) == "ИП")
                        _FullName =  substr(_FullName, 4);              
                end;
                Субъект.ShortName = _ShortName;
                Субъект.FullName = _FullName;
        end;
    end;
 end;        
 
if(stat == true) return 0;
else             return 1;
end;

END;

/* ***************************************** */
PRIVATE MACRO Функция_Пользователя(Субъект:RsbParty, Режим:integer)
   var m;
   array mn;
   //Gurin S. 20.03.2015 R-559453-2
   var prompt = "Действие над субъектом (ИД субъекта "+Субъект.partyid+")";
   mn(0)=RSL_RPAD("1. Дебиторы и кредиторы клиентов",strlen(prompt),"");
   mn(1)="2. Отправить сообщение в Интербанк";
   mn(2)="3. Связать банки (факт смены БИКа)";
   mn(3)="4. Генерация РС-кода";
   mn(4)="5. Редактирование уставного капитала";
//   mn(4)="5. Внутренний ИД субъекта "+Субъект.partyid;


   var newBIC, selKKind=PTCK_BIC, selCode;
   var col_chapter = TArray(), ind = 0;
   var rsdcmd,rs;

   macro AddColumn( col, fld, head, width )
      Col.value( ind * 6 + 0 ) = fld;  // fieldName
      Col.value( ind * 6 + 1 ) = head; // header 
      Col.value( ind * 6 + 2 ) = width; // width
      Col.value( ind * 6 + 3 ) = 2;    // fldType (2 = FBT)
      Col.value( ind * 6 + 4 ) = null; // decPoint
      Col.value( ind * 6 + 5 ) = 0;    // reserv
      ind = ind + 1;
   end;

   macro EvProc1 (rs, cmd, id, key)
      var plsql;

      if ((cmd == DLG_KEY) and (key == 13 /* Enter */))

         plsql = RSDCommand("begin usr_merge_bank("+rs.value("partyid")+", "+Субъект.partyid+"); end;");
         plsql.execute();

         msgbox("Банки связаны успешно");
        
         return CM_CANCEL;
      end;

      onerror(er)
         msgbox("произошла ошибка при связываении банков:|"+rsldefenv.error(0).descr);

   end;

  // Tikh Функция при mn=1 не для массовой обработки
  If (Режим <= 3)
    m=menu(mn,null,prompt);
    if (m==3)
      debugbreak;
      if (ВходитВГруппу({oper},175))
      if (gettrue (true,"ВЫ ДЕЙСТВИТЕЛЬНО ХОТИТЕ ИЗМЕНИТЬ КОД РС-СУБЪЕКТА?"))
        debugbreak;
        SetRScode(Субъект);
        DisplayError();
      end;
      else msgbox("Вы не входите в группу 175");
      end;
        //Tikh
    elif (m==0)
      if (StartDBKT());
      DBKT(Субъект.partyid);
      EndDBKT();
      end;
    elif (m==1)
      debugbreak;
      Send_text_ic(Субъект.partyid);

    elif (m==2)
      newBIC = Субъект.Code(PTCK_BIC);
      //Seleznev ListPT не работает здесь, сделал альтернативу
      rsdcmd = RSDCommand  (
                             "select   ob1.t_code bic, ob2.t_code rscode, t.t_name name, t.t_partyid partyid " +
                             "  from   dparty_dbt t, dobjcode_dbt ob1, dobjcode_dbt ob2, dpartyown_dbt ow " +
                             " where       t.t_locked = chr (88) " +
                             "         and t.t_legalform = 1 " +
                             "         and ow.t_partyid = t.t_partyid and t_partykind = 2 " +
                             "         and ob1.t_objectid = t.t_partyid " +
                             "         and ob1.t_codekind = 3 " +
                             "         and ob1.t_bankdate = (select max (t_bankdate) from dobjcode_dbt where t_codekind = ob1.t_codekind and t_objectid = ob1.t_objectid and t_objecttype = 3) " +
                             "         and ob1.t_objecttype = 3 " +
                             "         and ob2.t_objectid = t.t_partyid " +
                             "         and ob2.t_codekind = 1 " +
                             "         and ob2.t_bankdate = (select max (t_bankdate) from dobjcode_dbt where t_codekind = ob2.t_codekind and t_objectid = ob2.t_objectid and t_objecttype = 3) " +
                             "         and ob2.t_objecttype = 3 "
                            );
     AddColumn(col_chapter, "bic"    , "БИК", 9); 
     AddColumn(col_chapter, "rscode" , "Код клиента", 12);
     AddColumn(col_chapter, "name"   , "Наименование", 32);
     AddColumn(col_chapter, "partyid", "ID Субъекта", 8);

     rs = RSDRecordset(rsdcmd, null, RSDVAL_STATIC);

     RunScroll( rs, ind, col_chapter, "locked_bank_scroll", @EvProc1, "Список закрытых банков: ", "ENTER Выбор", true, /*X*/null, /*Y*/null, null, null );
	elif (m==4)
		// ASV редактирование уставного капитала
		ExecMacroFile("set_institut.mac", "inst_main", Субъект.partyid);
		
    end;

  // Для массовой только Дб и кт   
  elIf (Режим == 4)
    if (not StartDBKT())
       fl = 0;
    else
       fl = 1;
    end;
  elif (Режим == 5)
      if (fl)
        DBKT(Субъект.partyid);
      end;
  elif (Режим == 6) 
    if (fl)
        EndDBKT();
    end;
  end;
   
  return 0;
END;

/* ***************************************** */
PRIVATE MACRO ПроверитьАдресСубъекта(Адрес)

  var stat = 0;
  var ind = 0;
  var but;
  var regparm = false;
  var err = 0;
  Array Text;
  Array Button1;
  Array Button2;
  var okato = false;
  var kladr = false;
  var OkatoByKladr = "";

  GetRegistryValue("CB\\PARTY\\КЛАДР\\СВЕРКА ОКАТО", V_BOOL, okato, err);
  if ( err != 0 )
    okato = false;
  end; 
  GetRegistryValue("CB\\PARTY\\КЛАДР\\АДРЕС КЛАДР", V_BOOL, regparm, err );
  if ( err != 0 )
    regparm = false;
  end; 
  GetRegistryValue("CB\\PARTY\\КЛАДР\\КЛАДР", V_BOOL, kladr, err );
  if ( err != 0 )
    kladr = false;
  end; 

  Button1(0) = " Исправить ";
  Button1(1) = " Сохранить ";
  Button2(0) = " Исправить ";

  if(( Адрес.Country == "" ) or ( Адрес.Territory == "" ))
    stat = 1;
    Text(ind) = "Не указана страна или территория.";
    ind = ind + 1;
  end;
  if( Адрес.Country == "RUS" )
    if(( Адрес.Region == "" ) or ( Адрес.PostIndex == "" ))
      stat = 1;
      Text(ind) = "Не указан регион или индекс.";
      ind = ind + 1;
    end;
  end;

  if(( Адрес.Country == "RUS" ) AND ( regparm ) AND ( Адрес.Kladr == "" )) 
    stat = 2;
    Text(ind) = "Не заполнен код КЛАДР.";
    ind = ind + 1;
  end;
  
  if((Адрес.Country == "RUS") and (Адрес.Kladr != "") and (regparm or okato))
    OkatoByKladr = GetOkatoByKladr(Адрес.Kladr);
    if(regparm and (OkatoByKladr == "noKladr"))
      stat = 1;
      Text(ind) = "Код КЛАДР не найден в справочнике КЛАДР.";
      ind = ind + 1;
    elif(kladr and okato and (Адрес.Okato != "") and (OkatoByKladr != "noKladr") and (OkatoByKladr != address.Okato))
      stat = 1;
      Text(ind) = "Код ОКАТО не соответствует коду КЛАДР.";
      ind = ind + 1;
    end;
  end;

  if((index(Адрес.Region  , "*") != 0) or (index(address.Region  , "?") != 0) or
     (index(Адрес.Province, "*") != 0) or (index(address.Province, "?") != 0) or
     (index(Адрес.District, "*") != 0) or (index(address.District, "?") != 0) or
     (index(Адрес.Place   , "*") != 0) or (index(address.Place   , "?") != 0) or
     (index(Адрес.Street  , "*") != 0) or (index(address.Street  , "?") != 0) )
    stat = 2;
    Text(ind) = "Недопустимый символ '*' или '?' в наименовании адресного объекта.";
    ind = ind + 1;
  end;

  if(stat != 0)
    if( stat == 1 )
      but = ConfWin(Text,Button1);
    else
      but = ConfWin(Text,Button2);
    end;
    if(but == 1)
      stat = 0;
    end;
  end;

  return stat;
END;

private macro ЗаполнитьРегион( Адрес )
  setbuff( address, Адрес );
  return 1;
end;

private macro ЗаполнитьРайон( Адрес )
  setbuff( address, Адрес );
  return 1;
end;

private macro ЗаполнитьГород( Адрес )
  setbuff( address, Адрес );
  return 1;
end;

private macro ЗаполнитьПункт( Адрес )
  setbuff( address, Адрес );
  return 1;
end;

private macro ЗаполнитьУлица( Адрес )
  setbuff( address, Адрес );
  return 1;
end;

private macro ПроверитьУдостоверениеСубъекта( Документ, Субъект:RsbParty )
  var stat = false;
  setbuff( persnidc, Документ );
  
  stat = true;

  if(stat == true) return 0;
  else             return 1;
  end;
end;
