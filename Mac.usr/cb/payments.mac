/*
 *   payments.mac
 *
 *   Макрофункции для работы с платежами
 *
 *   Режимы функции проверки:
 *     PMMAC_DELETE    = 1 - удаление
 *     PMMAC_INSERT    = 2 - ввод
 *     PMMAC_UPDATE    = 3 - обновление
 *     PMMAC_GENERATED = 4 - проверка сгенерированного платежа
 *   (константы доступны при подключенном PaymInter)
 *
 *   09.02.99 AS
 *
 */

// Chesnokov D.S.  01.09.2014 C-32168 Вызов функции пользователя. Возможность редактировать категорию по кодам ФМ 

import PaymInter;


record Платеж ( pmpaym );
record Дебет  ( pmprop );
record Кредит ( pmprop );
record МакетR ( pmrmprop );

record СтарыйПлатеж ( pmpaym );
record СтарыйДебет  ( pmprop );
record СтарыйКредит ( pmprop );
record СтарыйМакетR ( pmrmprop );



/*
 *                    Инициализация нового платежа
 */
macro НовыйПлатеж
   return 0;
end;



/*
 *              Проверка платежа при вводе/редактировании
 */
macro ПроверитьПлатеж( режим )
   var RegistrDate, Parms = TArray(), i = 0, mes = "";

   /*Для ценнобумажных платежей, если по ним сформировано поручение депо (есть связка - запись в spdrprop),
     то если при редактировании такого платежа были изменены существенные для депозитарного учета параметры - 
     предупредим пользователя о необходимости поправить эти параметры в поручении вручную.

     Существенные реквизиты платежа :
     - номер счета получателя/отправителя
     - банк получателя/отправителя
     - корсчет банка получателя/отправителя
     - корреспондент банка получателя/отправителя

     См. 104226.
   */
   if ((Платеж.Purpose == BAi) or (Платеж.Purpose == BRi))
      if ( ExecMacroFile("dpdrutl.mac", "SP_GetDraftDate", Платеж.PaymentID, RegistrDate) )

         if (Платеж.PayerAccount != СтарыйПлатеж.PayerAccount)
            Parms(Parms.size) = "номер счета отправителя\n";
         end;

         if (Платеж.PayerBankID != СтарыйПлатеж.PayerBankID)
            Parms(Parms.size) = "банк отправителя\n";
         end;

         if (Дебет.CorrAcc != СтарыйДебет.CorrAcc)
            Parms(Parms.size) = "корсчет банка отправителя\n";
         end;

         if (Дебет.CorrID != СтарыйДебет.CorrID)
            Parms(Parms.size) = "корреспондент банка отправителя\n";
         end;



         if (Платеж.ReceiverAccount != СтарыйПлатеж.ReceiverAccount)
            Parms(Parms.size) = "номер счета получателя\n";
         end;

         if (Платеж.ReceiverBankID != СтарыйПлатеж.ReceiverBankID)
            Parms(Parms.size) = "банк получателя\n";
         end;

         if (Кредит.CorrAcc != СтарыйКредит.CorrAcc)
            Parms(Parms.size) = "корсчет банка получателя\n";
         end;

         if (Кредит.CorrID != СтарыйКредит.CorrID)
            Parms(Parms.size) = "корреспондент банка получателя\n";
         end;


         if (Parms.size > 0)
            mes = "Необходимо выполнить в модуле \"Депозитарий\" корректировку реквизитов: \n";

            while (i < Parms.size)
               mes = mes + Parms(i);
               i = i + 1;
            end;

            mes = mes + "в поручении депо, сформированном по данному платежу";
            MsgBox(mes);

            Parms.size = 0;
         end;
      end;
   end;
   return 0;
   OnError(er) /* Обработка ошибок времени выполнения */
     return 0;
end;



/*
 *                    Пользовательская функция
 *   Режимы функции
 *      UFN_PANEL_INPUT = 1 - панель ввода
 *      UFN_PANEL_EDIT  = 2 - панель редактирования
 *      UFN_SCROL       = 3 - вызов из скроллинга
 *   (константы доступны при подключенном BankInter)
 */
macro ПользФункцияПлатеж( режим )
  debugbreak;
  var m;
  array mn;
  
  mn(asize(mn))="Коды операций для контроля";
  mn(asize(mn))="Коды необычных операций";
  
  m=menu(mn, null, "Выберите действие");

  if(m == -2)
    return 0;
  elif(mn(m)=="Коды операций для контроля")
    execMacroFile("fm_inspaycode.mac", "EditCategory", Платеж, 1);//Обязательный контроль
    return 0;
  elif(mn(m)=="Коды необычных операций")
    execMacroFile("fm_inspaycode.mac", "EditCategory", Платеж, 2);//Необычная операция
    return 0;
  end;
    
  return 0;
end;
