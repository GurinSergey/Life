import globals, dbf2ora, cb_sql, BankInter;

var RecreateTableDBF = false;

macro RunImport( ImportDir, ImportName )

  var ErrText;
  var rs, cmd, ret_val, CountNFB, CountNFS, CountSPC, CountUPD;
  var OperName, OperPost, TotalRow;
  var SubCode, SubName;

  /* Переносим данные из dbf в Oracle */
  if( SubStr( ImportDir, strlen( ImportDir )) != "\\" )
    ImportDir = ImportDir + "\\";
  end;

  /*Таблица subbranch_dbf и таблица протокола subbranch_log должны быть созданы вручную (см. PRBB_2029_README.1ST)*/
  if( not dbf2ora( ImportDir + ImportName, "subbranch_dbf", "Загрузка файла " + ImportName + " в БД", ErrText, RecreateTableDBF ))
    msgbox( ErrText );
    return 1;
  end;

  /*Таблица subbranch_dbf и таблица протокола subbranch_log должны быть созданы вручную (см. PRBB_2029_README.1ST)*/
  SQL_Execute("TRUNCATE TABLE subbranch_log");

  if(RecreateTableDBF)
    SQL_Execute("ALTER PACKAGE rsb_subbranch COMPILE");
  end;

  ret_val = 0;

  cmd = RsdCommand( "begin ? := rsb_subbranch.subbranch_load(" + {oper} + "); end;" );

  cmd.addParam( "ret_val", V_INTEGER, RSDBP_OUT);

  SQL_Execute( cmd, "Выполняется импорт справочника SUBBRANCH ЦАС НСИ. Ждите...", false );

  rs = SQL_ExecuteAndGetRs("SELECT t.t_name_type, p.t_name FROM dperson_dbt p, dtypeac_dbt t WHERE p.t_oper = " + {oper} + 
                           " AND t.t_inumtype = 14 AND t.t_type_account = p.t_ctypeperson");

  rs.MoveNext;

  OperPost = rs.value( 0 );
  OperName = rs.value( 1 );

[           Протокол импорта справочника 91 SUBBRANCH ЦАС НСИ];
[ ];
[ Подразделение: ##### ##########################################################] ( {OperDprt}, {Name_Bank} );
[ Пользователь:  ###################### ##### ###################################] ( OperPost, {oper}, OperName );
[ Дата и время:  ########## ########                                             ] ( date(), time());
[ ];
[ Файл импорта:  ################################################################] ( ImportDir + ImportName );
[ ];
[--------------------------------------------------------------------------------------];
[ ];

  rs = SQL_ExecuteAndGetRs("SELECT COUNT(*) FROM subbranch_dbf");

  rs.MoveNext;

  TotalRow = int( rs.value( 0 ));

  rs = SQL_ExecuteAndGetRs("SELECT COUNT( DECODE( t_TypeRec, 'NFB', 1 ))," +
                                  "COUNT( DECODE( t_TypeRec, 'NFS', 1 ))," +
                                  "COUNT( DECODE( t_TypeRec, 'SPC', 1 ))," +
                                  "COUNT( DECODE( t_TypeRec, 'UPD', 1 )) " +
                             "FROM subbranch_log");
  rs.MoveNext;
  CountNFB = int( rs.value( 0 ));
  CountNFS = int( rs.value( 1 ));
  CountSPC = int( rs.value( 2 ));
  CountUPD = int( rs.value( 3 ));


[ Результаты импорта:                                                            ];
[  - в файле обработано ##### записей                                            ] ( TotalRow );
[  - в базе обновлено ##### записей                                              ] ( CountUPD );
[ ];
  if(( CountNFB + CountNFS ) > 0 )
[--------------------------------------------------------------------------------------];
[ ];
[ В базе не обнаружены ##### записей о структурных подразделениях СБ РФ.         ] ( CountNFB + CountNFS );
[ Из них ##### записей уровня ОСБ и выше:                                        ] ( CountNFB );

    rs = SQL_ExecuteAndGetRs("SELECT t_TerBank_Branch, t_SubName FROM subbranch_log WHERE t_TypeRec = 'NFB'");

    while( rs.MoveNext )

      SubCode = rs.value( 0 );
      SubName = rs.value( 1 );

[  ############## #####################################################################] ( SubCode, SubName );
    end;
  end;
[ ];
  if(( CountSPC ) > 0 )
[--------------------------------------------------------------------------------------];
[ ];
[ В базе установлен код SUBBRANCH для ##### записей о структурных подразделениях СБ РФ:] ( CountSPC );

    rs = SQL_ExecuteAndGetRs("SELECT t_TerBank_Branch, t_SubName FROM subbranch_log WHERE t_TypeRec = 'SPC'");

    while( rs.MoveNext )

      SubCode = rs.value( 0 );
      SubName = rs.value( 1 );

[  ############## #####################################################################] ( SubCode, SubName );
    end;
  end;
[ ];

[--------------------------------------------------------------------------------------];
[ ];
[ #################### _____________________________ ##################################] ( OperPost, OperName );

  return "subbranch.dbf";
end;
