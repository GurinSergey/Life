/* @desc   : Макрос, вызываемый во время выполнения процедуры завершения операционного дня
 * @changes: 2013-02-25 zip_z. R-159169 (проверки на проводки робота Джет: JET_ROBOT_NUMBER = 22222)
 */

import oralib, likepy, lib_const, BankInter, TReport;

// @desc: получить имя файла для вывода отчёта. Инкапсуляция, дабы не плодить var в голове макроса.
//        Никаких глобализмов нам здесь не нужно!
private macro GetReportFileName ()
    return GetTxtFileName ("CloseDate_Report");
end;


// @desc  : проверяет, есть ли проводки пользователя Oper за дату CloseDate в подразделении Department
//          и сбрасывает протокол работы в файл - GetTxtFileName ("CloseDate_Report")
// @return: V_BOOL (true, если проводки есть)
macro CB_CheckOperCarry (CloseDate:date, Department:integer, Oper:integer):bool
    var ret = false;
    
    // уповаем на свежесть собранной статистики. Если её не собрать, по запросу подберется плохой план :(
    // а вообще на тесте отрабатывает за 35 мс даже по самому безнадёжному набору данных

    /* EVG 17/12/2013 Адаптация под 2031.
    var sql = "SELECT count (1) OVER (PARTITION BY NULL) n," +
              "\n       t_numb_document, t_account_payer, t_account_receiver, t_sum, t_ground" +
              "\n FROM (SELECT t_numb_document, t_account_payer, t_account_receiver, t_sum, t_ground, t_date_carry, t_oper, t_department" +
              "\n      FROM darhdoc_dbt" +
              "\n      UNION ALL" +
              "\n      SELECT t_numb_document, t_account_payer, t_account_receiver, t_sum, t_ground, t_date_carry, t_oper, t_department" +
              "\n      FROM darhdoc$_dbt" +
              "\n     )" +
              "\n WHERE t_date_carry = :m_date_carry AND t_department = :m_department AND t_oper = :m_oper";*/

    var sql = "SELECT count (1) OVER (PARTITION BY NULL) n," +
              "\n       t_numb_document, t_account_payer, t_account_receiver, t_sum_payer, t_ground" +
              "\n FROM ( SELECT t_numb_document, t_account_payer, t_account_receiver, t_sum_payer, t_ground, t_date_carry, t_oper, t_department" +
              "\n        FROM dacctrn_dbt )" +
              "\n WHERE t_date_carry = :m_date_carry AND t_department = :m_department AND t_oper = :m_oper";

    var params = makeArray (SQLParam ("m_date_carry", CloseDate  ), 
                            SQLParam ("m_department", Department ), 
                            SQLParam ("m_oper"      , Oper       ));
    sql = execSqlSelect (sql, params, false);
    
    setOutput (GetReportFileName ());
    
    var table = CTableReport();
    table.addColumn ("Номер"     ,   6, AL_RIGHT );
    table.addColumn ("Плательщик",  25, AL_LEFT  );
    table.addColumn ("Получатель",  25, AL_LEFT  );
    table.addColumn ("Сумма"     ,  10, AL_RIGHT );
    table.addColumn ("Основание" , 100, AL_LEFT  );
    table.printHead ();
    
    while (sql.moveNext ())
        ret = true;
        table.printStringTransferByWord (sql.value ("t_numb_document"   ), 
                                         sql.value ("t_account_payer"   ), 
                                         sql.value ("t_account_receiver"), 
                                         sql.value ("t_sum_payer"       ), 
                                         sql.value ("t_ground"          ));
    end;
    
    table.printBottom ();
    setOutput (null, true);
    return ret;
end;


/* Макрофункция вызывается перед процедурой завершения операционного дня */
/* функция должна вернуть:
    0 (RSL_EXIT_SUCCESS) - если процедура завершения операционного дня может быть продолжена
   !0 - в противном случае
 */
macro StartCloseDay(CloseDate:date, Department:integer):integer
    private const JET_ROBOT_NUMBER = string (22222);
    var ret = RSL_EXIT_SUCCESS;
    if (true == CB_CheckOperCarry (CloseDate, Department, JET_ROBOT_NUMBER))
        ret = RSL_EXIT_FAILURE;
        if (getTrue (true, "В балансе найдены проводки робота " + JET_ROBOT_NUMBER + 
                           " за дату " + string (CloseDate) +  
                           "|Закрытие дня запрещено. Показать протокол ?" ))
            ViewFile (GetReportFileName ());
        end;
    end;
    return ret;
end;

/* Макрофункция вызывается в случае ошибочного выполнения процедуры завершения операционного дня */
macro ErrorCloseDay(CloseDate : date, Department : integer)
end;
