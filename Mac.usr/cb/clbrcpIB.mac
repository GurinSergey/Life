/*30.07.2012 LAO Добавил метку для отзыва*/
/*zmp 01.11.2012 I-00278580 добавил ф-ю экранирования кавычек*/
  /*
    Макрос отправки подтверждения в интербанк v6
   */
  import rsd, lib_compare;

  /*
    Параметры настройки на сервере рс-банка для доступа к интербанку
    Подробнее читать документ  "Доступ к источникам данных с помощью
    библиотеки RSD"
   */
  var DSN = "IB6";
  var IB6USER = "IBRS4_2";
  var IB6PASS = "IBRS4_2";

  /* 
    Строковый идентификатор экземпляра АБС(подключения к внешним системам).
  */
  var RSBANK_STRID = "iabs6";

/*SDA - проверка определения настроек соединения в реестре */
   private var err, _DSN, _IB6USER, _IB6PASS, _RSBANK_STRID;

   GetRegistryValue( "PRBB\\INTERBANK\\DSN", V_STRING, _DSN, err );
   if ( err == 0 )
       DSN = _DSN;
   end;

   GetRegistryValue( "PRBB\\INTERBANK\\IB6USER", V_STRING, _IB6USER, err );
   if ( err == 0 )
       IB6USER = _IB6USER;
   end;

   GetRegistryValue( "PRBB\\INTERBANK\\IB6PASS", V_STRING, _IB6PASS, err );
   if ( err == 0 )
       IB6PASS = _IB6PASS;
   end;

   GetRegistryValue( "PRBB\\INTERBANK\\RSBANK_STRID", V_STRING, _RSBANK_STRID, err );
   if ( err == 0 )
       RSBANK_STRID = _RSBANK_STRID;
   end;   
  /*
    Проверить версию подсистемы, загрузившей документ
   */
  macro checkInterBankVersion(id,version)
    var rs = RsdRecordset( string("select t_originalsystem from dclbdoc_dbt where t_docid='",id,"'"));
    if ( rs.MoveNext() )
       if ( rs.value(0)=="BORWP" )
         return version==5;
       elif (rs.value(0)=="IB6")
         return version==6;
       end;
    end;
    return false;
  end;

  /*
    Соответствие статусов документа в рс-банке и интербанке 6
   */
  macro getIBStatus(type)
    if (type == 3) // получен
      return "200809181135349510000-b50f649215962488";
    elif (type == 4) // принят
    elif (type == 5) // отказан
      return "200709111833560620014-f01e3b1aed0d8366";
    elif (type == 6) // проконтролирован
      return "200709111833560620019-f01e3b1aafc40b48";
    elif (type == 7) // удален
      return "200709111833560620018-f01e3b1ae8e259d7";
    elif (type == 8) // загружен в АБС
    elif (type == 9) // проведен
      return "200809181133258690000-b50f6492c4e5e71d";
    elif (type == 10) // помещен в картотеку
      return "200809181133473850000-b50f6492eeb55d48";
    elif (type == 13) // обработан
      return "200709111833560620015-f01e3b1ace9b90b0";
    elif (type == 21) // отозван LAO
      return "200812230834011560000-97e6263584948aec"
    elif (type == 22) // оплата картотеки
      return "200803281035522640400-1a756b7e32e7d924";
    elif (type == 24) // откат в абс
      return "200902191220140510000-97740d77668e306a";
    end;
    return 0;
  end;

  /*
    Соответствие видов документов
  */
  macro getIBType(id)
    var str = string(id);
    if(strlen(str) == 15)
      str = substr(str,1,5);
      if(str == "00201")
        return "PaymentOrder";
      elif(str == "00202")
        return "PaymentOrderCurrency";
      end;
    end;
    return "PaymentDocumentBase";
  end;

  /*
    Отправка подтверждения
   */
  macro clbMakeReceiptIB6(id,type,shortDesc,desc)

/*  
    var con = RsdConnection(DSN,IB6USER,IB6PASS);
    type = getIBStatus(type);
    var interBankType = getIBType(id);
    var cmd = RsdCommand(con,string("insert into ib6_external_event (extid,extstrid,entitykindstrid,status,statusdesc,fulldesc,time,version) values ",
                              "('",id,"','",RSBANK_STRID,"','",interBankType,"','",type,"','",shortDesc,"','",desc,"',TO_TIMESTAMP(CURRENT_DATE),0)"));
*/
    /*zmp 01.11.2012 I-00278580 добавил ф-ю экранирования кавычек*/
    desc = PutDoubleQoute(desc);
    type = getIBStatus(type);
    var interBankType = getIBType(id);
    var cmd = RsdCommand(string("insert into ib6_external_event (extid,extstrid,entitykindstrid,status,statusdesc,fulldesc,time,version) values ",
                              "('",id,"','",RSBANK_STRID,"','",interBankType,"','",type,"','",shortDesc,"','",desc,"',TO_TIMESTAMP(CURRENT_DATE),0)"));

        cmd.execute;
    /* UDA 31.08.2012 по заявке I-00245758-2. Выборка статуса дублируется в таблицу ib6_external_event_log 
	--> begin */
    var cmd_log = RsdCommand(string("insert into ib6_external_event_log (extid,extstrid,entitykindstrid,status,statusdesc,fulldesc,time,version) values ",
                              "('",id,"','",RSBANK_STRID,"','",interBankType,"','",type,"','",shortDesc,"','",desc,"',TO_TIMESTAMP(CURRENT_DATE),0)"));

        cmd_log.execute; 
    /* <-- end UDA 31.08.2012*/
    return true;

    OnError(x)
      if(cmd==null)
        [error N ##:#](x.code,x.message);
        return false;
      end;
      var env=cmd.connection.environment;
      var i=0;
      while(i<env.errorcount)
        [error N ##:#](i,env.error(i).descr);
        i=i+1;
      end;
    return false;

  end;