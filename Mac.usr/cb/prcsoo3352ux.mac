/*
$Name:             Prcsoo3352ux.mac
$Module:           ББ и РКО
$Description:      Печать расходного кассового ордера 3352-У (xls)
*/

import prcs, BnkTemplReport, lib_compare, lib_fg;

debugbreak;
    
private class (BnkTemplReport)PrintOrderReport(PrintOrderData)

  var data = PrintOrderData;

  private macro cutKop(value)
    var pos = regexp_instr (value, "[[:digit:]] коп");
    if(pos != 0)
      if (substr(value, pos - 1 , 2) == "00") return substr(value, 1, pos - 2); end;
    end;
    return value;
  end;

  private macro MakeMoneyStr3352(_Money:money)

  VAR StrRub  :string = "";
  VAR StrKop  :string = "";
  VAR StrSep  :string = "-";
  VAR RetValue:string = ""; 
  if ( _Money != $0 )
    
    if(ValType(_Money) == V_MONEY)
      StrRub = Trim(String((Floor(Double(_Money))):16:0));
      StrKop = Trim(String((100.0 * (Double(_Money) - Floor(Double(_Money)))):16:0));
      if(StrLen(StrKop) < 2)
        StrKop = "0" + StrKop;
      end;
    
      RetValue = StrRub;
      
      if(StrKop == "00") RetValue = StrRub + "=";
      else               RetValue = StrRub + "-" + StrKop; 
      end;

    end;
  end;
  return RetValue;

  end; /* MakeMoneyStr3352 */

  private macro getSimbNameForGround(arr)
   var i = 0, str = " ";
   while (i < arr.size)
      if(trim(arr[i]) != "")         
         str = str + " lpad('" + arr[i] + "', 3, ' '), ";
      end;
      i = i + 1;
   end;
   if (trim(str) != "") 
      str = substr(str, 1, strlen(str) - 2);
      var rs = RsdRecordset("SELECT  LISTAGG(t_name, '; ') WITHIN GROUP (ORDER BY t_name) AS value FROM   dlistsymb_dbt where T_SYMB_CASH in (" +str + ") and t_dockind = 1 ") ;
      if (rs.moveNext()) return rs.value(0, null, v_string); end;
   end;
   return " "; 
  onError
   return " ";
  end;




  MACRO Create()
      
    SetSheetName( data.Number );

    array ArrayClient, ArrayBankName, ArrayGround, ArraypaperGround;
           
   // StrSplit( data.BankName, ArrayClient  , 100, 45, 3);
    StrSplit(ifThenElse(_bank.is_VUZ, data.BankName, data.client), ArrayClient, 100, 45, 3);
    StrSplit( data.BankName, ArrayBankName, 100, 50, 2);
    StrSplit( ifThenElse(_bank.is_VUZ, getSimbNameForGround(data.ArraySym), data.ground), ArraypaperGround, 100, 50, 2);

   // if( ArrayBankName(1) == "" )
   //   ArrayBankName(1) = ArrayBankName(0);
   //   ArrayBankName(0) = "";
   // end; 
    /*Основание платежа*/
    StrSplit(data.Ground, ArrayGround, 65, 47, 3);

    var isDebL = IfThenElse( data.DebitList.size() > 1, true, false );

    SetValues( "Number_0"          , "Отрывной талон к расходному кассовому ордеру №" + data.Number,
               "Number"            ,  data.Number           ,
               "DateStr  "         ,  usr_getFullDate(data.DateStr)     ,
               "ClientStr"         ,  ArrayClient(0)        ,
               "ClientStr_1"       ,  ArrayClient(1)        ,
               "ClientStr_2"       ,  ArrayClient(2)        ,

               "DebAcc"            ,  data.DebitList.value(0).Account   ,
               "FICode"            ,  data.DebitList.value(0).FICode    ,
               "Amount"            ,  MakeMoneyStr3352(strsubst(data.DebitList.value(0).Amount, "-", ".")));

    var index = 1;
    if( isDebL )
      var table = RegisterTable("DEBET");
      while( index < data.DebitList.size())
        table.SetValueCell( "DebAcc"        ,  data.DebitList.value(index).Account );
        table.SetValueCell( "FICode"        ,  data.DebitList.value(index).FICode  );
        table.SetValueCell( "Amount"        ,  data.DebitList.value(index).Amount  );
        table.AddStr();
        index = index + 1;
      end;
      table.EndTable();
    end;

    SetValues(
               "CredAcc"           ,  data.ReceiverAccount              , 
               //"CFICode"           ,  data.ReceiverFICode               ,
               //"CAmount"         ,  data.ReceiverAmountStr            ,
               "BankName"          ,  ArrayBankName(0)                  ,
               "BankName_1"        ,  ArrayBankName(1)                  ,
               "BIC"               ,  data.BIC                          ,
               "AmountStr"         ,  cutKop(data.ArraySum.value(0))            ,
               "AmountStr_1"       ,  cutKop(data.ArraySum.value(1))            ,
               "Symb"              ,  data.ArraySym.value(0)            ,
               "Symb_1"            ,  data.ArraySym.value(1)            ,
               "Symb_2"            ,  data.ArraySym.value(2)            ,
               "SAmount"           ,  MakeMoneyStr3352(strsubst(data.ArrayPartSum.value(0), "-", ".")),
               "SAmount_1"         ,  MakeMoneyStr3352(strsubst(data.ArrayPartSum.value(1), "-", ".")),
               "SAmount_2"         ,  MakeMoneyStr3352(strsubst(data.ArrayPartSum.value(2), "-", ".")),
               "ArrayPaperName"    ,  ArraypaperGround(0)                           ,
               "ArrayPaperName_1"  ,  ArraypaperGround(1)                           ,
               "Sh"                ,  data.ShifrOper                           ,
               "PaperName"         ,  ifThenElse(_bank.is_VUZ, " ", data.ArrayPaperName.value(0)),
               "PaperName_1"       ,  ifThenElse(_bank.is_VUZ, " ", data.ArrayPaperName.value(1)),
               //"PaperName_1"       ,  data.ArrayPaperName.value(1)             ,
               "NameAccount"       ,  data.officerPost                         ,
               "NameAccount_FIO"  ,  data.officerShortName 
               //"NameControl"       ,  "Контролирующий работник"                ,
               //"NameCashier"       ,  "Кассовый работник"
             );

    return TRUE;
  END;

  initBnkTemplReport("DebitCashOrder3352-U.xls");
end; 

macro PrintDocument(ncopy:integer):bool

  var DocKind:integer = pr_pmpaym.rec.DocKind;
  var data : TOutOrderPrintData3352 = TOutOrderPrintData3352();

  if( InList( DocKind, 15, 70, 420, 440 ) )
    data.InitByCashOrder( pr_pmpaym, pr_pmrmprop, pr_pscshdoc );  
  else
    MsgBox("Данный вид документа не поддерживается макросом");
    return FALSE;
  end;

  return PrintOrderReport(data, ncopy).print();

end;
