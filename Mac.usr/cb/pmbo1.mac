//-----------------------------------------------------------------------------
// Блок     : 29010 - "Обработка в БО"
// Шаг      : 10    - "Передача в бэк-офис"
// Описание : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, BankInter, lnpaym, OprInter;
import oralib; //Jushmanov 2014-02-21 C-19151

var PaymentObj:RsbPayment;

MACRO ExecuteStep( doc, paymDoc ):integer

    PaymentObj.PaymStatus = PM_KVITWAIT;

    if( PaymentObj.ToBackOffice == "В" )
        PaymentObj.ReplicationSession = 0;
        PaymentObj.ReplicationBO = "В";
    end;

    return 0;
END;

MACRO CheckStepAction( mes:integer ):integer

    macro SayCantRollback( BOCode:string )
        msgbox( "Невозможно откатить шаг операции,|т.к. платеж обрабатывается в ПС " + LnFindType(BOCode) );
    end;

    if( mes == OP_BACKOUT_STEP )
        // Нельзя откатывать документ, участвующий в репликации
        if( PaymentObj.ReplicationSession )
            SayCantRollback( PaymentObj.ReplicationBO );
            return 1;
        end;
        // Нельзя откатывать документ, по которому что-то сделано в БО
        if( PaymentObj.PaymStatus != PM_KVITWAIT )
            SayCantRollback( PaymentObj.ToBackOffice );
            return 1;
        end;
    end;

    return 0;
END;

//Jushmanov 2014-02-21 C-19151
macro PostStepAction( message,      /* 1 - выполнение шага; 2 - откат шага;   */
                      errTrn,       /* статус выполнения шага. 0 - ошибки нет */
                      FirstDoc,     /* указатель на первичный документ        */
                      ID_Oper,      /* внутренний идентификатор операции      */
                      Number_Step,  /* Номер шага операции (из настроек)      */
                      KindOper,     /* номер вида операции                    */
                      KindDoc,      /* номер вида первичного документа        */
                      KindStep,     /* вид шага операции                      */
                      ID_Step )     /* внутренний идентификатор шага операции */
    private var logquery;

    if(( message == OP_EXECUTE_STEP ) and (errTrn == 0) and (IsOprMultiExec))
        if ((valtype(ID_Oper) != V_INTEGER) and (valtype(ID_Oper) != V_STRING)) ID_Oper = 0; end;
        if ((valtype(ID_Step) != V_INTEGER) and (valtype(id_Step) != V_STRING)) ID_Step = 0; end;

        logquery = "UPDATE usr_doprstep_robot_dbt SET t_is_robot_step = chr(88)" +
                   " WHERE t_id_operation = " + ID_Oper + " AND t_id_step = " + ID_Step;
        ExecSQL(logquery, null, false);
    end;

    return 0;
end;