/******************************************************************************/
/*  Дополняет строку num слева нулями до len знаков                           */
/******************************************************************************/
// KS 02.12.2013 Перенос пользовательских доработок

import "globals.mac", PTInter;
import "adress.mac";

private const RegPathNameClient:string = "REPORT\\REP_CLIENT_NAME";

private var fperson  :TBFile = TBFile( "person.dbt",   "R", 0, "person.dbt",   "bank.def" );
private var faccount :TBFile = TBFile( "account.dbt",  "R", 0, "account.dbt",  "bank.def" );
private var fparty   :TBFile = TBFile( "party.dbt",    "R", 0, "party.dbt",    "bank.def" );
private var ffininstr:TBFile = TBFile( "fininstr.dbt", "R", 0, "fininstr.dbt", "bank.def" );

macro L_Z( num, len )
    var str1, len1;
    str1 = trim( string( num ) );
    len1 = strlen( str1 );
    if ( len1 >= len ) return str1;
    else  return  mkstr("0", len-len1 ) + str1;
    end;
end;

/******************************************************************************/
/* Получить имя операциониста                                                 */
/******************************************************************************/
MACRO GetOperName( oper:integer ):string
  fperson.KeyNum = 0;
  fperson.rec.Oper = oper;
  if( fperson.GetEQ() )
    return fperson.rec.Name;
  else
    return string("Не найден операционист: ", oper);
  end;
END;

/******************************************************************************/
/* Получить буфер лицевого счета                                              */
/******************************************************************************/
MACRO MO_GetAccountBuffer( account:TRecHandler, accnum:string, chapter:integer, accfi:integer ):bool
  faccount.KeyNum = 0;
  faccount.rec.Code_Currency = accfi;
  faccount.rec.Account = accnum;
  faccount.rec.Chapter = chapter;
  if( faccount.GetEQ() )
    Copy( account, faccount );
    return TRUE;
  else
    ClearRecord( account );
    return FALSE;
  end;
END;

/******************************************************************************/
/* Строчку информации о клиенте счета для вывода в мемордер                   */
/******************************************************************************/
MACRO MO_GetClientString( accnum:string, chapter:integer, accfi:integer ):string
  var account:TRecHandler = TRecHandler( "account.dbt", "bank.def" ),
      clientINN :string = "",
      clientName:string = "";
  if( MO_GetAccountBuffer( account, accnum, chapter, accfi ) )
    clientINN  = GetCodeParty( account.rec.Client, PTCK_INN );
    clientName = GetNameClient( RegPathNameClient, accnum, chapter, accfi );
    if( clientINN )
      return string( "ИНН ", clientINN, " ", clientName );
    else
      return clientName;
    end;
  else
    return string( "Не найден счет: ", accnum );
  end;
END;

/******************************************************************************/
/* Получить буфер субъекта экономики                                          */
/******************************************************************************/
MACRO MO_GetPartyBuffer( party:TRecHandler, PartyID:integer ):bool
  fparty.rec.PartyID = PartyID;
  if( fparty.GetEQ() )
    Copy( party, fparty );
    return TRUE;
  else
    ClearRecord( party );
    return FALSE;
  end;
END;

/******************************************************************************/
/* Получить числовой (ISO) код валюты                                         */
/******************************************************************************/
MACRO MO_GetISONumber( FIID:integer ):integer
  if( FIID >= 0 )
    ffininstr.rec.FIID = FIID;
    if( ffininstr.GetEQ() )
      return int(ffininstr.rec.ISO_Number);
    end;
  end;
  return 0;
END;

/******************************************************************************/
/* Получить наименование нашего банка( с адресом или без - по настройке )     */
/******************************************************************************/
MACRO GetOurBankName():string

  var err, RegValNameWithAddr;
  var OurBankName = {Name_Bank};
 
  RECORD addr ( adress );
                     
  GetRegistryValue( "CB\\CARRY\\BANKNAMEWITHADDR", V_BOOL, RegValNameWithAddr, err);

  if( ( err == 0 ) and ( RegValNameWithAddr == true ) )
    if( НайтиЮридическийАдресСубъекта( {OurBank}, addr) )
      if( addr.District != "" )
        OurBankName = OurBankName + " " + addr.CodeDistrict + " " + addr.District;
      end;
    end;
  end;

  return OurBankName;

END;
