/****************************************************************************/
/*                     R-Style SoftLab, RS-Bank V6                          */
/****************************************************************************/
/*                      Библиотеки общих функций                            */
/*         Общие функции, используемые для печати счетов-фактур             */
/*                                                                          */
/*  Имя файла: bilsfprn_lib.mac                                             */
/*  Создан:    13.03.2012                                       Гуцу Е.В.   */
/****************************************************************************/

/* EVG 13/03/2012 Функция перенесена из макросов bilsfprn_new.mac / bilsfprn_old.mac
   для исключения её дублирования */
macro FakturaReceiverAcc( FakturaID );

var select, select2, select3 :string = "";
var rs, rs2, rs3         :object;
var accReceiver = "";
/* EVG */
var CodeClient = "";

        select = "SELECT t_bookentryid FROM dbilbookentry_dbt " +
                     "WHERE t_facturaid = " + FakturaID + "";
        rs = RSDRecordset(select);
        while(rs.moveNext())
                select2 = "SELECT t_bookentryid, t_DocKind, t_DocID FROM dbilreconcile_dbt "+
                             "WHERE t_bookentryid = "+rs.Value(0)+"";
                rs2 = RSDRecordset(select2);
                while(rs2.moveNext())
                        select3 = "SELECT t_payeraccount FROM dpmpaym_dbt "+
                                     "WHERE t_paymentid = "+rs2.Value(2)+"";
                        rs3 = RSDRecordset(select3);

                        while(rs3.moveNext())
                           accReceiver = rs3.Value(0);
                        end;
                end;
        end;

        /* EVG Если счёт найти не удалось, вместо него указываем код клиента */
        if ( trim(accreceiver) == "" )
           select = " Select code.t_Code " +
                    "   From DObjCode_dbt code, DBilFactura_dbt bil " +
                    "  Where bil.t_facturaid = " + FakturaID +
                    "    and code.t_ObjectType = 3 " +          // Субъект
                    "    and code.t_CodeKind   = 1 " +          // Вид кода "Код"
                    "    and code.t_ObjectID   = bil.t_ReceiverID ";
           rs = null;
           rs = rsdRecordSet(select);
           if (rs and rs.MoveNext())
              CodeClient = rs.Value(0, null, V_STRING);
           end;

           return CodeClient;
        end;

//      setparm(accReceiver);
        return (accreceiver);

END;


/* EVG 13/03/2012 Функция перенесена из макросов bilsfprn_new.mac / bilsfprn_old.mac
   для исключения её дублирования */

// KS 03.12.2010 I-097132 Юрадрес субъекта. Переопределение
macro GetAdressBILF( pBillNumber, pBillDate, pBuyerName, pBuyerAddress)
  var query, rs, cmd;

  query = "select t.t_adress " +
          "  from dadress_dbt t " +
          " where t.t_partyid in " +
          "       (select bf.t_receiverid " +
          "          from dbilfactura_dbt bf " +
          "         where bf.t_facturanumber = ? " +
          "           and bf.t_creationdate = ? " +
          "           and bf.t_receivername = ?) " +
          "   and t.t_type = 1 ";

  cmd = RSDCommand( query );
  cmd.addParam( "", RSDBP_IN, pBillNumber );
  cmd.addParam( "", RSDBP_IN, pBillDate );
  cmd.addParam( "", RSDBP_IN, pBuyerName );

  rs = RsdRecordset( cmd );

  if ( rs and rs.MoveNext() )
     return rs.Value(0); // Переопределяем
  end;

  return pBuyerAddress;  // Вернем старый

end;


/* EVG 13/03/2012 Функция перенесена из макросов bilsfprn_new.mac / bilsfprn_old.mac
   для исключения её дублирования */

macro GetAdressBILF_ID( pReceiverId, pBuyerAddress)
  var query, rs, cmd;

  query = "select t.t_adress " +
          "  from dadress_dbt t " +
          " where t.t_partyid = ?" +
          "   and t.t_type = 1 ";

  cmd = RSDCommand( query );
  cmd.addParam( "", RSDBP_IN, pReceiverId );

  rs = RsdRecordset( cmd );

  if ( rs and rs.MoveNext() )
     return rs.Value(0); // Переопределяем
  end;

  return pBuyerAddress;  // Вернем старый

end;


/* EVG 13/03/2012 Функция перенесена из макросов bilsfprn_new.mac / bilsfprn_old.mac
   для исключения её дублирования */

MACRO getISOCode( FIID:integer ):integer
  record fi( "fininstr.dbt", "bank.def" );
  if( FIID >= 0 )
    if( ПолучитьФинИн( FIID, fi ) == 0 )
      return fi.ISO_Number;
    end;
  end;
  return 0;
END;
