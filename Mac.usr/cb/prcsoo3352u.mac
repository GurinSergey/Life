/*
$Name:             Prcsoo3352u.mac
$Module:           ББ и РКО
$Description:      Печать расходного кассового ордера 3352-У
*/

import prcs, globals;
    
/* -----------------------------------------------
   Непосредственно печать по подготовленным данным
   ----------------------------------------------- */

MACRO PrintOutCashOrder(d : TOutOrderPrintData3352, ncopy : integer):bool
  debugbreak;
  var CashSymbCount = 2;
  array ArrayClient, ArrayBankName, ArrayGround, officerPost, officerShortName;
         
  StrSplit( d.Client, ArrayClient, 34, 26, 3);
  StrSplit( d.BankName, ArrayBankName, 44, 40, 2);
  StrSplit(d.officerPost  , officerPost, 13, null, 2);
  StrSplit(d.officerShortName, officerShortName, 10, null, 2);

  if(officerPost(1)      == "") officerPost(1)      = officerPost(0);      officerPost(0)      = ""; end;
  if(officerShortName(1) == "") officerShortName(1) = officerShortName(0); officerShortName(0) = ""; end;

  if( ArrayBankName(1) == "" )
    ArrayBankName(1) = ArrayBankName(0);
    ArrayBankName(0) = "";
  end; 
  /*Основание платежа*/
  StrSplit(d.Ground, ArrayGround, 60, 40, 3);

  /*Печать*/
  while(ncopy > 0)
    ncopy = ncopy - 1;
//-------------------------------------------------------------------------------------------  
      [                                        ┌───────────────────┐  ┌ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ┐
                                               │Код формы документа│  │Отрывной талон к расходному│
                                               │  по ОКУД 0402009  │  │кассовому ордеру № ########│
                                               └───────────────────┘  └ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─ ┘
                                                                      ┌───────────────────────────┐
                                   ┌─────────┐                        │    Место для наклейки     │
        Расходный кассовый ордер N │######## │ #######################│     отрывного талона      │
                                   └─────────┘  ────────────────────  └───────────────────────────┘
                                                       Дата                                        
                                                                      ┌───────────────────────────┐
        Выдать ##########################              ДЕБЕТ          │      Сумма цифрами        │
        ──────────────────────────────────┬───────────────────────────┼─────────────┬─────────────┤
        (фамилия,имя,отчество(при наличии)│счет № ####################│#############│             │    ]
        
       ( d.Number:l, 
         d.Number:c, usr_getFullDate(d.DateStr),
         ArrayClient(0), d.DebitList.value(0).Account:l, d.DebitList.value(0).FICode, d.DebitList.value(0).Amount:r );

      if (d.DebitList.size < 2)
      [ ──────────────────────────────────┴───────────────────────────┤             │             │
        ##################################             КРЕДИТ         │             │             │    ]
        (ArrayClient(1));
      else
        var i : integer = 1;
        while( i < d.DebitList.size )
          if ( i == 1 )
      [ (фамилия,имя,отчество(при наличии)│       ####################│#############│             │    ]
            (d.DebitList.value(i).Account:l, d.DebitList.value(i).FICode, d.DebitList.value(i).Amount:r);
          end;
          if ((i == 2) and (d.DebitList.size > 2 ))
      [  #################################│       ####################│#############│             │    ]
            (ArrayClient(1), d.DebitList.value(i).Account:l, d.DebitList.value(i).FICode, d.DebitList.value(i).Amount:r);
          elif (i == 2)
      [  #################################└───────────────────────────┤             │             │    
                                                   КРЕДИТ             │             │             │    ]
            (ArrayClient(1));
          end;
          if ( i > 2 )
      [                                   │       ####################│#############│             │    ]
         (d.DebitList.value(i).Account:l, d.DebitList.value(i).FICode, d.DebitList.value(i).Amount:r);
          end;
         i = i + 1;
        end;
        if (i > 2)
      [                                   └───────────────────────────┤             │             │    
                                                   КРЕДИТ             │             │             │    ]
        end;                                           
      end;

      [ ──────────────────────────────────┬───────────────────────────┤             │             │
                                          │счет № ####################│#############│             │
        ──────────────────────────────────┴───────────────────────────┼─────────────┴─────────────┤
         Наименование банка ########################################  │  в том числе по символам  │
        ──────────────────────────────────────────────────────────────┼─────────────┬─────────────┤
        ############################################ БИК ##########   │   символ    │    сумма    │
        ──────────────────────────────────────────────────────────────┼─────────────┼─────────────┤
         Сумма прописью ##############################################│    ####     │#############│
         ############################################################ │    ####     │#############│    ]
         
       ( d.ReceiverAccount:l, d.ReceiverAmountStr:r, /*d.ReceiverFICode*/  
         ArrayBankName(0),
         ArrayBankName(1), /*d.BIC zmp*/ {mfo_bank}, 
         d.ArraySum.value(0), d.ArraySym.value(0):c, d.ArrayPartSum.value(0):r,
         d.ArraySum.value(1), d.ArraySym.value(1):c, d.ArrayPartSum.value(1):r );
    
      while( CashSymbCount < d.ArraySym.size )
      [                                                               │    ####     │#############│    ]
        (d.ArraySym.value(CashSymbCount):c, d.ArrayPartSum.value(CashSymbCount):r );
        CashSymbCount = CashSymbCount + 1;
      end;

      [ ──────────────────────────────────────────────────────────────┤             │             │
         Направление выдачи ######################################### │             │             │
         ############################################################ ├─────────────┴──────┬──────┤
         ############################################################ │ Шифр документа     │  ##  │
        ──────────────────────────────────────────────────────────────┴────────────────────┴──────┘
                                                                                                   
        Предъявлен документ, удостоверяющий личность ####################### ┌ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─┐
        ─────────────────────────────────────────────────────────────────────┤                    │
         ################################################################### │      Подпись       │
         ################################################################### │                    │
        ─────────────────────────────────────────────────────────────────────└ ─ ─ ─ ─ ─ ─ ─ ─ ─ ─┘
           (наименование документа, серия, номер, кем и когда выдан)     
           
        Указанную в расходном кассовом ордере сумму получил __________________
                                                           (подпись получателя)
        #############        ############
        #############        ############
         ___________  ________ _________ __________ ________ _________ __________ ________ ________
        (наименование (личная (фамилия,(наименование (личная (фамилия,(наименование(личная(фамилия,
          должности)  подпись) инициалы) должности)   подпись)инициалы) должности) подпись)инициалы)]
       ( ArrayGround(0),
         ArrayGround(1),
         ArrayGround(2), d.ShifrOper:c,   
         d.ArrayPaperName.value(0), 
         d.ArrayPaperName.value(1),
         d.ArrayPaperName.value(2), officerPost(0):c,officerShortName(0):c, officerPost(1):c,officerShortName(1):c);
  end;        
//-------------------------------------------------------------------------------------------          ]
  return TRUE;

END;

MACRO PrintDocument(ncopy:integer):bool

  var DocKind:integer = pr_pmpaym.rec.DocKind;
  var data : TOutOrderPrintData3352 = TOutOrderPrintData3352();

  if( ( DocKind == 420/*CASH_PS_OUTORDER*/ ) or ( DocKind == 440/*CASH_BOF_OUTORDER*/ ) )
    data.InitByCashOrder( pr_pmpaym, pr_pmrmprop, pr_pscshdoc );  
  elif( ( DocKind == CB_MULTYDOC ) or ( DocKind == DLDOC_MEMORIALORDER ) )
    data.InitByMemorialDocument( pr_pmpaym, pr_pmrmprop );
  else
    MsgBox("Данный вид документа не поддерживается макросом");
    return FALSE;
  end;
  if( pr_PrintEA )
    PrintEAHeader();
  end;

  return PrintOutCashOrder(data, ncopy);

END;