/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prpminvi.mac                                       */
/*                                                                      */
/*  Описание       : Печать инкассового поручения (WR)                  */
/*                   (требование-поручение)                             */
/*                                                                      */
/*  Программист    : Дроздов И.Е.                                       */
/*                                                                      */
/*  Создан         : 22.09.04                                           */
/*                                                                      */
/* 13.03.2014 DPN I-00471618-2 исправил отображение суммы прописью      */
/* 13.03.2014 DPN R-344271-2 изменил инкасс. пор. согласно 383-П        */
/************************************************************************/

import "BnkTemplReport.mac";
import prpm;
debugbreak;
class (BnkTemplReport)PayRequest0401071Report(PayerP:TPartyProperties, ReceiverP:TPartyProperties)

   var PayerPr = PayerP;
   var ReceiverPr = ReceiverP;

   var paym   :TRecHandler = TRecHandler( "pmpaym.dbt",   "bank.def" );
   var debet  :TRecHandler = TRecHandler( "pmprop.dbt",   "bank.def" );
   var credit :TRecHandler = TRecHandler( "pmprop.dbt",   "bank.def" );
   var prop   :TRecHandler = TRecHandler( "pmrmprop.dbt", "bank.def" );
   var payord :TRecHandler = TRecHandler( "pspayord.dbt", "bank.def" );
   var paydem :TRecHandler = TRecHandler( "pspaydem.dbt", "bank.def" );
   
   copy( paym   , pr_pmpaym   );
   copy( debet  , pr_debet    );
   copy( credit , pr_credit   );
   copy( prop   , pr_pmrmprop );
   copy( payord , pr_pspayord );
   copy( paydem , pr_pspaydem );

  macro Create()
debugbreak;
    var PDate:date    = date(0, 0, 0);
    var Sum:string = string( paym.rec.Amount );
    var Sum_:string = string( paym.rec.Amount ); //13.03.2014 DPN I-00471618-2 отображение суммы прописью
    var strSum:string; //13.03.2014 DPN R-344271-2  переменная для хранения текстовой записи суммы  

    StrSet(Sum, StrBrk(Sum, ".,"), "-");
    strSum = RubToStrAlt(Sum_);

    if( paym.rec.DocKind == PS_PAYORDER )
      if(payord.rec.DocKind == PSPOKIND_REQUEST)
        PDate = paydem.rec.AcceptDate;
        //13.03.2014 DPN R-344271-2 
        if ((money(sum_) - int(sum_)) == 0 )   
            strSum = RubToStrAlt(int(Sum_));
            strSum = substr(strSum, 1, StrBrk(strSum, "0") - 1);
            sum = int(sum) + "=";
        else
            StrSet(Sum, StrBrk(Sum, ".,"), "-");
        end;
      end;
    end;

    var pINN, pKPP, rINN, rKPP;
    splitINN_KPP(PayerPr.INN, pINN, pKPP);
    splitINN_KPP(ReceiverPr.INN, rINN, rKPP);

     SetSheetName( prop.rec.Number );
    //заполнение фрейма
    SetValues( "DateInto", string(paym.rec.PayerBankEnterDate:f),
               "ChargeOffDate", string(prop.rec.PayerChargeOffDate:f),
               "Data", string(prop.rec.Date:f), 
               "PaymentKind", GetPaymentKind(prop.rec.PaymentKind),
               "Number", prop.rec.Number,
               "TaxAuthor", prop.rec.TaxAuthorState,
               "AmountInLine", strSum, //13.03.2014 DPN I-00471618-2 исправил отображение суммы прописью
               "pINN", pINN, 
               "pKPP", pKPP,
               "Amount", Sum,
               "pName", PayerPr.Name,
               "pAccount", string(PayerPr.Account),
               "pBankName", PayerPr.BankName,
               "pBankBIC", PayerPr.BankBIC,
               "pBankAccount", string(PayerPr.BankCorrAccount),
               "rBankName", ReceiverPr.BankName,
               "rBankBIC", ReceiverPr.BankBIC,
               "rBankAccount", string(ReceiverPr.BankCorrAccount),
               "rINN", rINN,
               "rKPP", rKPP,
               "rAccount", string(ReceiverPr.Account),
               "ShifrOper", prop.rec.ShifrOper,
               "rName", ReceiverPr.Name,
               "ShifrNazn", "",
               "ShifrCode", IfThenElse(prop.rec.Date < Date(31,3,2014), "", prop.rec.UIN),
               "Priority", prop.rec.Priority,
               "RezPole", "",
               "BttTICode", IfThenElse(prop.rec.TaxAuthorState, prop.rec.BttTICode,   ""),
               "OkatoCode", IfThenElse(prop.rec.TaxAuthorState, prop.rec.OKATOCode,   ""),
               "TaxPmGround", IfThenElse(prop.rec.TaxAuthorState, prop.rec.TaxPmGround, ""),
               "TaxPmPeriod", IfThenElse(prop.rec.TaxAuthorState, prop.rec.TaxPmPeriod, ""),
               "TaxPmNumber", IfThenElse(prop.rec.TaxAuthorState, prop.rec.TaxPmNumber, ""),
               "TaxPmDate", IfThenElse(prop.rec.TaxAuthorState, prop.rec.TaxPmDate,   ""),
               "TaxPmType", IfThenElse(prop.rec.TaxAuthorState, IfThenElse(((pr_pmrmprop.rec.Date >= date(01,01,2015)) and (pr_pmrmprop.rec.TaxPmType == "0")),"",pr_pmrmprop.rec.TaxPmType),   ""),/*zmp 12.01.2015 HF2031_19_25\124*/
               "Ground", prop.rec.Ground,
               "I2PlaceDate", strsubst(string(paym.rec.I2PlaceDate), " ", "0") /*zmp 21.11.2014 R-489565-2 добавлено strsubst*/,
               "PayerBankMarkDate", string(paym.rec.PayerBankMarkDate)
             );
    
    
    
     var partPayList :TPartPayList;
     var table:object;
     var i = 0;
     
     if( paym.rec.DocKind == PS_PAYORDER )
       partPayList = TPartPayList(payord.rec.OrderID);
     end;

     if((partPayList != null) and (partPayList.size > 0))
       table = RegisterTable("PartPayments");
       while( i < partPayList.size )
         table.SetValueCell("PartCount",  string(partPayList.Value(i).SubPurp));  
         table.SetValueCell("PartNumber", partPayList.Value(i).Number);           
         table.SetValueCell("PartPayDate", string(partPayList.Value(i).PayDate:f));
         table.SetValueCell("PartAmount", string(partPayList.Value(i).Amount:f)); 
         table.SetValueCell("PartRest", string(partPayList.Value(i).Rest:f));   
         table.AddStr();
         i = i + 1;
       end;
       table.EndTable();
     end;
  end;

   initBnkTemplReport("PayRequest0401071.xls");
end;


MACRO PrintIncashRequestRSF():bool
  debugbreak;
  var PayerPr   :TPartyProperties = TPartyProperties( "Payer",    pr_pmpaym, pr_debet,  pr_pmrmprop);
  var ReceiverPr:TPartyProperties = TPartyProperties( "Receiver", pr_pmpaym, pr_credit, pr_pmrmprop);

  return PayRequest0401071Report( PayerPr, ReceiverPr ).Print();
END;
