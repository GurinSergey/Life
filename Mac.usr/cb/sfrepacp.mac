/*
  Протокол оплаты комиссий
*/

import BankInter, sfrepcommon;

var PrevObject = "";
var PrevDprt = -1;

/* EVG */
var isPaymentK2 = false;

private macro PrintHeader()
  [
  
                      
                     Ведомость оплаты комиссий];
end;

private macro PrintParm( Dprt, Attr, Comiss, EndDate, TrnDate )

[
   Параметры запуска операции оплаты:];
[   Узел ТС - ########################################################################] ( Dprt );
[   Признак - ########################################################################] ( Attr );
[   Комиссия - #######################################################################] ( Comiss );
[   Дата окончания периода - ##########] ( EndDate );
[   Дата проводки - ##########] ( TrnDate );

end;

private macro GetInvDocNum( InvID )

  var sqlString : string;
  var rs : RsdRecordset;

  sqlString = " SELECT T_DocNumber FROM DSFINV_DBT WHERE T_InvoiceID = " + InvID;

  rs = RsdRecordset( sqlString );

  if( rs.moveNext())
    return rs.value( 0 );
  end;

  return "";
end;


private macro PrintObjectPaym( rs : RsdRecordSet )

  var CCurStr = "";
  var InvDocNum = "", Comment = "";

  var ComCode = "";
  if( rs.value(10) != NULLVAL )    
    ComCode =  rs.value(10);
  end;

  var BeginDate;
  if( rs.value(5) == NULLVAL )
    BeginDate = "";
  else
    BeginDate = string( rs.value(5) );
  end;

  var EndDate;
  if( rs.value(6) == NULLVAL )
    EndDate = "";
  else
    EndDate = string( rs.value(6) );
  end;

  var ComSum;
  if( rs.value(7) == NULLVAL )
    ComSum = "";
  else
    ComSum = money( rs.value(7) );
  end;

  var NDSSum;
  if( rs.value(8) == NULLVAL )
    NDSSum = "";
  else
    NDSSum = money( rs.value(8) );
  end;  

  var Debet;
  if( rs.value(2) == NULLVAL )
    Debet = "";
  else
    Debet = rs.value(2);    
  end;

  var Credit;
  if( rs.value(3) == NULLVAL )
    Credit = "";
  else
    Credit = rs.value(3);

    /* EVG Взводим признак картотеки */
    isPaymentK2 = false;
    if ( subStr( Credit, 1, 5 ) == "47423" )
       isPaymentK2 = true;
    end;
  end;
  
  if( rs.value(4) != NULLVAL )  
    CCurStr = SfRep_GetCurrencyShortName( rs.value( 4 ));  
  end;
  
  if( rs.value(1) != NULLVAL )
    InvDocNum = rs.value(1);/*GetInvDocNum( rs.value(1) );*/
  end;    

  var Error = 0;
  if( rs.value(13) != NULLVAL )
    Error = rs.value(13);
  end; 

  /*Печатаем, если сформирован ДО*/
  if( Error == 0 )

[|                              |##############################|##########|##########|#########|  ### |##########|###################################|###################################|] 
                                 ( ComCode,                    BeginDate, EndDate,  ComSum+NDSSum, CCurStr, InvDocNum,  Debet,  Credit );

/* EVG Если платёж попадает в К2, печатается эта строчка*/
if ( isPaymentK2 )
   [|                              |#                                                                                                                                                       |] 
   ("Платёж находится в картотеке 2");
end;

[+------------------------------+------------------------------+----------+----------+---------+------+----------+-----------------------------------+-----------------------------------+];
  else
    if( rs.value(11) != NULLVAL )
      Comment = rs.value(11);
    end;
    if( Comment == "" )
      Comment = "Ошибка формирования документа оплаты";
    end;
[|                              |##############################|##########|##########|##############################################################################################################] 
                                 ( ComCode,                      BeginDate, EndDate,   Comment:w );
[+------------------------------+------------------------------+----------+----------+---------+------+----------+-----------------------------------+-----------------------------------+];
  end;

end;


private macro PrintAccrue( AllDprt, Dprt, rs : RsdRecordSet )

  var DprtStr = "";
  var CCurStr = "";

  if( PrevDprt == -1 )

[
  Результаты выполнения:];
  
  end;

  
  if( (PrevDprt == -1) or ((AllDprt == true) and (PrevDprt != rs.value(12))) )
    PrevDprt = rs.value(12);

    DprtStr = SfRep_GetDepartmentName( PrevDprt );
[
  Филиал - #####################################################################################] ( DprtStr );
[+------------------------------+------------------------------+----------+----------+---------+------+----------+-----------------------------------+-----------------------------------+];
[|Объект договора               |Комиссия                      |   Дата   |  Дата    |  Сумма  |Валюта| Вид,     |       Счет плательщика            |       Счет получателя             |];
[|                              |                              |  начала  |окончания |  c НДС  |      | номер    |                                   |                                   |];
[|                              |                              |  периода | периода  |         |      | документа|                                   |                                   |];
[|                              |                              |          |          |         |      |          |                                   |                                   |];
[+------------------------------+------------------------------+----------+----------+---------+------+----------+-----------------------------------+-----------------------------------+];  
  end;

  if(( strlen( PrevObject ) == 0 ) or ( PrevObject != rs.value( 9 )))
    PrevObject = rs.value( 9 );

[|##############################|                              |          |          |         |      |          |                                   |                                   |
 +------------------------------+------------------------------+----------+----------+---------+------+----------+-----------------------------------+-----------------------------------+]  
   ( rs.value( 9 ));
  end;
  
  PrintObjectPaym( rs );

end;

private macro MakeSqlQuery() : string

  var sqlString : string;

  sqlString = "SELECT " +
              " sfra.t_SfDefComID, " +
              " (SELECT (DECODE (sfc.t_payMethod, 10, '', " +
                " (SELECT max(rm.t_Number) FROM  dpmrmprop_dbt rm, dpmpaym_dbt pm WHERE rm.t_PaymentID = pm.t_PaymentID " +
                " AND pm.t_feeType = sfra.t_feetype AND pm.t_DefComID = sfra.t_sfdefcomid AND pm.t_DefComID <> 0))) FROM DUAL) As paymNum, " +
              " sfra.t_Debit, " +
              " sfra.t_Credit, " +
              " sfdc.t_FIID_CommSum, " +
              " sfra.t_BeginDate, " +
              " sfra.t_EndDate, " +
              " sfdc.t_CommSum, " +
              " sfdc.t_NDSSum, " +
              " sfc.t_Object, " +
              " sfcm.t_Code, " +
              " sfra.t_Comment, sfra.t_Department, sfra.t_ErrorCode " +
              "FROM dsfrepacc_tmp sfra, dsfdefcom_dbt sfdc, dsfcontr_dbt sfc, dsfcomiss_dbt sfcm " +
              "WHERE sfra.t_Kind = 1 AND sfdc.t_ID(+) = sfra.t_SfDefComID AND sfc.t_ID = sfra.t_ContrID " +
              "AND sfcm.t_FeeType = sfra.t_FeeType AND sfcm.t_Number = sfra.t_ComissNumber " +
              "AND (sfra.t_InvoiceID <> 0 OR sfra.t_ErrorCode <> 0)" +
              "ORDER BY sfra.t_Department, sfc.t_Object, sfcm.t_Code, sfra.t_BeginDate";
  
  return sqlString;        

end;

private macro PrintContext( AllDprt, Dprt )

  var strSql : string;
  var cmd : RsdCommand;
  var rs : RsdRecordset;

  strSql = MakeSqlQuery();

  cmd = RsdCommand( strSql );
  cmd.NullConversion = true;
  rs = RsdRecordset( cmd );

  while( rs.MoveNext())
    PrintAccrue( AllDprt, Dprt, rs );
  end;

end;

macro PrintReport( AllDprt, Dprt, Attr, Comiss, EndDate, TrnDate )
  PrintHeader();

  SfRep_PrintTop();

  PrintParm( Dprt, Attr, Comiss, EndDate, TrnDate );

  PrintContext( AllDprt, Dprt );
end;