/*
 *  Макрос шага "Проведение исправительного оборота" операции "Исправительный оборот" по первичному документу "Мемордер"
 *  Изменил: Gurin S. N. I-00217849-2 06.07.2012
// KS 02.12.2013 Перенос пользовательских доработок в 31ю сборку
 */
Import BankInter, OprInter, FIInter, PaymInter, InsCarryDoc, "cortur_lib.mac";
/* EVG */
import Cbsttls;
import oralib; //Jushmanov 2014-02-11 C-19151

var PaymentObj : RsbPayment;
var Date_Rate  : date;

macro ExecuteStep( doc, primdoc )
  var stat = 0;
  var ПроводкаИсправительная = RsbAccTransaction;
  var Memorial : RsbMemorialOrder = RsbMemorialOrder( PaymentObj.DocumentID );
  var Type = "И, К, N, S"; //Gurin 06.07.2012 I-00217849-2

  ПроводкаИсправительная.Chapter          = Memorial.Chapter;
  ПроводкаИсправительная.FIID             = Memorial.Code_Currency;
  ПроводкаИсправительная.Sum              = PaymentObj.PayerAmount;
  ПроводкаИсправительная.AccountPayer     = PaymentObj.PayerAccount;
  ПроводкаИсправительная.AccountReceiver  = PaymentObj.ReceiverAccount;
  ПроводкаИсправительная.Numb_Document    = PaymentObj.Number;
  ПроводкаИсправительная.Date_Carry       = PaymentObj.ValueDate;
  ПроводкаИсправительная.Ground           = PaymentObj.Ground;
  ПроводкаИсправительная.TypeDocument     = Memorial.TypeDocument;
  ПроводкаИсправительная.Number_Pack      = PaymentObj.NumberPack;
  ПроводкаИсправительная.Shifr_Oper       = PaymentObj.ShifrOper;
  if (CompareStrWithMasks (Type, ПроводкаИсправительная.TypeDocument)==0)  //Gurin 06.07.2012 I-00217849-2
      ПроводкаИсправительная.Kind_Oper        = " 6";
  else
      ПроводкаИсправительная.Kind_Oper        = Memorial.Kind_Oper;
  end;
  ПроводкаИсправительная.Department       = PaymentObj.Department;
  ПроводкаИсправительная.UserTypeDocument = Memorial.UserTypeDocument;
  ПроводкаИсправительная.Date_Rate        = Date_Rate;

  if(index(ПроводкаИсправительная.TypeDocument, "S") > 0)
    FillMultyCurrAccTrn(ПроводкаИсправительная, DLDOC_MEMORIALORDER, Memorial.DocumentID);
  end;
 
  if( not ПроводкаИсправительная.Carry() )
    stat = 1;
    msgbox( "Ошибка при вставке проводки" );
  end;

  if((stat == 0) and (index(ПроводкаИсправительная.TypeDocument, "S") > 0))
    /*Установить тип "Сторнирован" на исправляемый документ*/
    UpdateCorrectionalDoc(ПроводкаИсправительная.TypeDocument, PaymentObj.DocKind, PaymentObj.DocumentID);
  end;

  // Статус платежа меняем на "Завершенный платеж"
  PaymentObj.PaymStatus = PM_FINISHED;
  // Изменяем дату списания со счета плательщика
  PaymentObj.PayerChargeOffDate = PaymentObj.ValueDate;

  /* EVG */
  if( УстановитьСтатусыПлатежа( 701 /*Документооборот*/, 2 /*Закрыт*/ ) )
    msgbox("Ошибка при установке сегментов статуса экземпляра операции");
    return 1;
  end;

  return stat;

end;


//Jushmanov 2014-02-11 C-19151
macro PostStepAction( message,  /* данный параметр может принимать следующие       */
                                /* значения: 1 - выполнение шага; 2 - откат шага;  */
                      errTrn,   /* статус выполнения шага. Если параметр не равен  */
                                /* 0, то произошла ошибка                          */
                      FirstDoc, /* указатель на первичный документ                 */
                      ID_Oper,  /* внутренний идентификатор операции               */
                      Num_Step, /* Номер шага операции (из настроек)               */
                      KindOper, /* номер вида операции                             */
                      KindDoc,  /* номер вида первичного документа                 */
                      KindStep, /* вид шага операции                               */
                      ID_Step ) /* внутренний идентификатор шага операции          */

private var query;

    if(( message == OP_EXECUTE_STEP ) and (errTrn == 0) and (IsOprMultiExec))
        if ((valtype(ID_Oper) != V_INTEGER) and (valtype(ID_Oper) != V_STRING)) ID_Oper = 0; end;
        if ((valtype(ID_Step) != V_INTEGER) and (valtype(id_Step) != V_STRING)) ID_Step = 0; end;

        query = "UPDATE usr_doprstep_robot_dbt SET t_is_robot_step = chr(88)" +
                " WHERE t_id_operation = " + ID_Oper + " AND t_id_step = " + ID_Step;
        ExecSQL(query, null, false);
    end;

  return 0;
end;