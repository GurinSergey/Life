//-----------------------------------------------------------------------------
// Блок      : 29001 - "Межфилиальные расчеты ЦАБС"
// Шаг       : 10    - "Перевод по счетам МФР ЦАБС"
// Назначение: Макрос шага
// Описание  : Макрос шага 
//-----------------------------------------------------------------------------

import InsCarryDoc, pm_common, pm_setst, cbsttls, PaymInter, mfrstepmass;
import OprInter, oralib; //Jushmanov 2014-02-21 C-19151

var PaymentObj:RsbPayment;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
MACRO ExecuteStep( doc, primdoc )

    var stat    :integer = 0; // Статус ошибки
    var NumCarry:integer = 0; // Номер проводки

    // Если ничего проводить не надо
    if( (( PaymentObj.StartDepartment == PaymentObj.EndDepartment) and ( PaymentObj.Department == PaymentObj.EndDepartment))
    or   ( PaymentObj.Department      == PaymentObj.EndDepartment ))
        if( УстановитьСтатусыПлатежа( OPR_PAYM_CABS, OPR_PM_ST_MFR_YES ) )
            msgbox("Ошибка при установке сегментов статуса экземпляра операции");
            return 1;
        end;
        return 0;
    end;

    // Если вид предыдущего шага равен "Перенос на незавершенные"
    if( PM_PrevStepIsAccUnclosed(PaymentObj) )
        PaymentObj.ValueDate = {curdate};
    end;

    /*VDN 02.03.2015 HF 143*/
    //входящий кредитовый тут никогда не может быть проведен
    if (not (PaymentObj.IsExternalIncoming and PaymentObj.IsCredit))
      PaymentObj.StatusInfo = "Исполнен";
    end;

    while( ( stat == 0 ) and ( PaymentObj.Department != PaymentObj.EndDepartment ) )
        stat = StepMFR_CABS( PaymentObj.PaymentID );
        NumCarry = NumCarry + 1;
    end;

    // Если сделали хоть одну проводку, то обнуляем ошибку и даем провести то, что есть
    if( ( stat != 0 ) and ( NumCarry > 1 ) )
        stat = 0;
    end;

    // Если все провели
    if( ( stat == 0 ) and ( PaymentObj.Department == PaymentObj.EndDepartment ) )
        if( УстановитьСтатусыПлатежа( OPR_PAYM_CABS, OPR_PM_ST_MFR_YES ) )
            msgbox("Ошибка при установке сегментов статуса экземпляра операции");
            return 1;
        end;
    end;

    return stat;
END;


//Jushmanov 2014-02-21 C-19151
macro PostStepAction( message,      /* 1 - выполнение шага; 2 - откат шага;   */
                      errTrn,       /* статус выполнения шага. 0 - ошибки нет */
                      FirstDoc,     /* указатель на первичный документ        */
                      ID_Oper,      /* внутренний идентификатор операции      */
                      Number_Step,  /* Номер шага операции (из настроек)      */
                      KindOper,     /* номер вида операции                    */
                      KindDoc,      /* номер вида первичного документа        */
                      KindStep,     /* вид шага операции                      */
                      ID_Step )     /* внутренний идентификатор шага операции */
    private var logquery;

    if(( message == OP_EXECUTE_STEP ) and (errTrn == 0) and (IsOprMultiExec))
        if ((valtype(ID_Oper) != V_INTEGER) and (valtype(ID_Oper) != V_STRING)) ID_Oper = 0; end;
        if ((valtype(ID_Step) != V_INTEGER) and (valtype(id_Step) != V_STRING)) ID_Step = 0; end;

        logquery = "UPDATE usr_doprstep_robot_dbt SET t_is_robot_step = chr(88)" +
                   " WHERE t_id_operation = " + ID_Oper + " AND t_id_step = " + ID_Step;
        ExecSQL(logquery, null, false);
    end;

    return 0;
end;