/****************************************************************************/
/*                   R-Style SoftWare Lab, RS-Bank v6.0                     */
/****************************************************************************/
/*                         Подсистема "Главная книга"                       */
/*  Функционал установки кода ФМ из списка всех платежей по счету клиента   */
/*                                                                          */
/*  Имя файла:  fm_inspaycode.mac                                           */
/*  Создан   :  01.09.2014                                   Чесноков Д.С.  */
/*  Заявка   :  C-32168                                                     */
/*  Описание :  Функционал вызывается по сочетанию клавиш Ctrl+Z из списка  */
/*              всех платежей по дебету\кредиту по счету клиента. Доступно  */
/*              для операционистов с АРМ "Сотрудник ФМ". Если АРМ отличается*/
/*              или по платежу сформирована операция в ФМ в статусе не равен*/
/*              "на контроль", "отложен", "отвергнут" то списки             */
/*              предоставляются только на просмотр                          */
/****************************************************************************/
import RSD, PaymInter, "KeyCodes.mac", "lib_oper.mac";

var Paym:RsbPayment, Paym_rec;
var ObjAttr = TRecHandler( "objattr.dbt" );
file attr("objattr.dbt") key 0;

const ARM_FM = 1009;
const OBJTYPE_OPR_FM = 700;
const CODE_OK:string = "Коды операций для контроля";
const CODE_NO:string = "Коды необычных операций";
const CODE_OK_VIEW:string = "[ТОЛЬКО ПРОСМОТР] Коды операций для контроля";
const CODE_NO_VIEW:string = "[ТОЛЬКО ПРОСМОТР] Коды необычных операций";
const PAYM_GR_OK = 11;
const PAYM_GR_NO = 12;
const OPR_GR_OK = 1;
const OPR_GR_NO = 2;

var ProcName = "EvProcScroll", mes_str = "", key_str = "~F2~ Установить основным ~F8~ Удалить ~F9~ Добавить";
var paym_group, opr_group;

macro AddCol (ar,ind, fld, head, width, rdonly)
  ar.value (ind * 6)     = fld;
  ar.value (ind * 6 + 1) = head;
  ar.value (ind * 6 + 2) = width;
  ar.value (ind * 6 + 3 ) = 2;   // fldType
  ar.value (ind * 6 + 4 ) = -1;  // decPoint
  ar.value (ind * 6 + 5 ) = 0;   // reserv
end;

macro EvProcScroll (RecSet, Cmd, id, key )
  
  var CM_FLAG = CM_DEFAULT;
  var cat, stat = 0;
  var rs, command, str;
  
  if (cmd == DLG_KEY)
    if (key == KEY_ESC)//Выход из скроллинга
      cat = RsbObjCategories(OBJTYPE_PAYMENT, UniID (Paym_rec, OBJTYPE_PAYMENT));
      stat = cat.GetMainAttr(paym_group, {curdate}, attr);
      if ((stat != 0) and (Valtype(RecSet.value("t_attrid")) != 26))
        msgbox("У объекта должен быть основной признак");
        CM_FLAG = CM_SELECT;
      end;
    elif (key == KEY_F2)//Установить основным 
      if (RecSet.value("t_general") != "X")//курсор на неосновной категории
        cat = RsbObjCategories(OBJTYPE_PAYMENT, UniID (Paym_rec, OBJTYPE_PAYMENT));
        stat = cat.GetMainAttr(paym_group, {curdate}, attr);
        //if ((stat != 0) and (Valtype(RecSet.value("t_attrid")) != 26))
        if (stat == 0)//если нашли основной признак
          //убираем признак с основного
          str = " UPDATE dobjatcor_dbt " +
                "    SET t_general = CHR (0), " +
                "        t_sysdate = TRUNC (SYSDATE), " +
                "        t_systime = " +
                "        TO_DATE ('01.01.0001 ' || TO_CHAR (SYSDATE, 'hh24:mi:ss'), " +
                "        'dd.mm.yyyy hh24:mi:ss') " +
                "  WHERE t_object = " + UniID (Paym_rec, OBJTYPE_PAYMENT) +
                "    AND t_groupid = 1" + attr.groupid +
                "    AND t_attrid = " + attr.attrid;
          RsdCommand(str).Execute;
          //установливаем признак на выбранную запись
          str = " UPDATE dobjatcor_dbt " +
                "    SET t_general = CHR (88), " +
                "        t_sysdate = TRUNC (SYSDATE), " +
                "        t_systime = " +
                "        TO_DATE ('01.01.0001 ' || TO_CHAR (SYSDATE, 'hh24:mi:ss'), " +
                "        'dd.mm.yyyy hh24:mi:ss') " +
                "  WHERE t_object = " + UniID (Paym_rec, OBJTYPE_PAYMENT) +
                "    AND t_groupid = " + RecSet.value("t_groupid") +
                "    AND t_attrid = " + RecSet.value("t_attrid");
          RsdCommand(str).Execute;
        elif((stat != 0) and (Valtype(RecSet.value("t_attrid")) != 26))//не нашли основной признак, проверим на пустой скроллинг
          str = " UPDATE dobjatcor_dbt " +
                "    SET t_general = CHR (88), " +
                "        t_sysdate = TRUNC (SYSDATE), " +
                "        t_systime = " +
                "        TO_DATE ('01.01.0001 ' || TO_CHAR (SYSDATE, 'hh24:mi:ss'), " +
                "        'dd.mm.yyyy hh24:mi:ss') " +
                "  WHERE t_object = " + UniID (Paym_rec, OBJTYPE_PAYMENT) +
                "    AND t_groupid = " + RecSet.value("t_groupid") +
                "    AND t_attrid = " + RecSet.value("t_attrid");
          RsdCommand(str).Execute;
        end;
        CM_FLAG = CM_SELECT;
      end;
    elif (key == KEY_F8)//Удаление записи
      cat = RsbObjCategories(OBJTYPE_PAYMENT, UniID (Paym_rec, OBJTYPE_PAYMENT));
      if (GetTrue(False, "Вы дейтсвительно хотите удалить?"))
        if ((cat.DisconnectAttr(RecSet.value("t_groupid"), RecSet.value("t_attrid")) == 0) and 
           (cat.Save(UniID (Paym_rec, OBJTYPE_PAYMENT)) == 0))
           CM_FLAG = CM_SELECT;
        else
          msgbox("Ошибка при удалении категории");
          CM_FLAG = CM_SELECT;
        end;
        CM_FLAG = CM_SELECT;  
      end;
    elif (key == KEY_F9)//Добавление записи
      if (ListObjAttr(OBJTYPE_OPR_FM, opr_group, ObjAttr))
        cat = RsbObjCategories(OBJTYPE_PAYMENT, UniID (Paym_rec, OBJTYPE_PAYMENT));
        stat = cat.ConnectAttr(paym_group, ObjAttr.rec.AttrID, null, null, {curdate});
        if (stat == 0)
          stat = cat.Save(UniID (Paym_rec, OBJTYPE_PAYMENT));
          if (stat == 0)
            CM_FLAG = CM_SELECT;
          else
            msgbox("Ошибка при добавлении кода ФМ");
            CM_FLAG = CM_SELECT;
          end;
        else
          msgbox("Ошибка при добавлении кода ФМ");
          CM_FLAG = CM_SELECT;
        end;
      end;
    end;
  end;
  
  return CM_FLAG;
  
end;

macro InsertFMCategory(Payment)
  
  var CmdText, RecordSet, col = TArray;
  
  Paym_rec = Payment;
  Paym = RsbPayment(Payment.PaymentID);
  
  AddCol (col, 0, "t_general",       "Осн.",        3, true);
  AddCol (col, 1, "t_numinlist",     "Номер",       5, true);
  AddCol (col, 2, "t_name",          "Название",    5, true);
  AddCol (col, 3, "t_validfromdate", "Опер. день", 10, true);
  AddCol (col, 4, "t_oper",          "Опер.",       5, true);
  AddCol (col, 5, "t_sysdate",       "Дата",       10, true);
  AddCol (col, 6, "t_systime",       "Время",      10, true);
  AddCol (col, 7, "t_isauto",        "А",           3, true);
  
  CmdText = " SELECT cor.t_general, " +
            "        attr.t_numinlist, " +
            "        attr.t_name, " +
            "        cor.t_validfromdate, " +
            "        cor.t_oper, " +
            "        cor.t_sysdate, " +
            "        TO_CHAR (cor.t_systime, 'hh24:mi:ss') as t_systime, " +
            "        cor.t_isauto, " +
            "        cor.t_groupid, " +
            "        cor.t_attrid " +
            "   FROM dobjatcor_dbt cor, dobjattr_dbt attr " +
            "  WHERE cor.t_objecttype = 501 " +
            "    AND cor.t_object = '" + UniID(Paym_rec, OBJTYPE_PAYMENT) + "'" +
            "    AND cor.t_groupid = " + paym_group+
            "    AND attr.t_objecttype = 700 " +
            "    AND attr.t_groupid = " + opr_group +
            "    AND attr.t_attrid = cor.t_attrid " +
            "  ORDER BY attr.t_name";
  
  RecordSet = RSDRecordSet(CmdText, RSDVAL_CLIENT, RSDVAL_STATIC);
  
  While (runScroll(RecordSet, 8, col, null, ProcName, mes_str, key_str))
    RecordSet = RSDRecordSet(CmdText, RSDVAL_CLIENT, RSDVAL_STATIC);
  end;
  RecordSet.Close();
end;

/*В макросе определяем все переменные*/
macro EditCategory(Payment:strucref, CodeKind:integer)
  
  var str, rset;
  var oper = RSL_Person({oper});
  
  if (CodeKind == 1)
    mes_str = CODE_OK;
    paym_group = PAYM_GR_OK;
    opr_group = OPR_GR_OK;
  else
    mes_str = CODE_NO;
    paym_group = PAYM_GR_NO;
    opr_group = OPR_GR_NO;
  end;
  
  if (not oper.Check_ARM(ARM_FM))
    msgbox("Вы не входите в АРМ \"Сотрудник ФМ\"| Коды доступны только в режиме ПРОСМОТРА ");
    if (CodeKind == 1)
      mes_str = CODE_OK_VIEW;
    else
      mes_str = CODE_NO_VIEW;
    end;
    key_str = "ТОЛЬКО ПРОСМОТР";
    ProcName = null;
    InsertFMCategory(Payment);
    return 0;  //и выходим от греха подальше
  end;

  str = "select t_status from dopcontr_dbt where t_documentid = TO_CHAR(" + Payment.PaymentID + ")";
  rset = trsbdataset(str);
  
  if(rset and rset.movenext)
    if ((rset.value("t_status") != 1) and (rset.value("t_status") != 4) and (rset.value("t_status") != 8)) //операция не на статусе на контроль, отложен
      msgbox("По платежу имеется операция ФМ в статусе отличным от НА КОНТРОЛЬ, ОТЛОЖЕН или ОТВЕРГНУТ!|Список доступен только в режиме ПРОСМОТР");
      if (CodeKind == 1)
        mes_str = CODE_OK_VIEW;
      else
        mes_str = CODE_NO_VIEW;
      end;
      key_str = "ТОЛЬКО ПРОСМОТР";
      ProcName = null;
    end;
  end;
  
  InsertFMCategory(Payment);
end;