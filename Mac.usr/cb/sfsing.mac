/*~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~

    RS-Bank 5.1

    Плата за обслуживание
        новая модель

    Оплата единовременных комиссий
        на шаге операции

    Файл    sfsing.mac
                                                Mikhailov S., 1.08.2001, Wed
                                                
    Изменения:
    18.03.2015 joy R-556258-3 Проставляем статус 30 (К оплате) на УЕК

~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~*/

import OprInter;
import "sfcrpaybatch.mac", "sfcomcat.mac" /*,"sfground.mac", "sfpay.mac"*/;

private const OBJTYPE_OPRSFCOM  = 665; /*Удержанная единовременная комиссия*/
private const RashetCatCode = "+Расчеты"; 
private const RashetNVPI_CatCode = "+Расчеты,НВПИ"; 

macro   SfSingPay(
            OutBuffer,     /*  буфер для формирования выходного документа для Insert...Document */
            PrimKind,   /*  Вид первичного документа */
            PrimBuffer, /*  Буфер первички  */
            OprSfCom_buff, /* Буфер комиссии операции */
            DateKindID,
            CatAccount
          )
   var stat = 0;

   var SfConComPD : SfConComPrimDoc;
   
   var SumPayer = $0;
   var CatCode : string;

   var opdate;
   var isNVPI : bool;

   var bilfDocArray = TArray;

   record  OprSfCom( sfdef );
   record  OprPDParam( "oprpdprm.str" );

   file sfcontr( sfcontr ) key 0;
   file sfcomiss( sfcomiss ) key 0;

   var SfConCom = TBFile( "sfconcom.dbt" );
   var GroundStr = "";

   var sidebet = TRecHandler("sfsi.dbt");
   var sicredit = TRecHandler("sfsi.dbt");

   SetBuff( OprSfCom, OprSfCom_buff );

   /* Получить комисиию */
   sfcomiss.FeeType = OprSfCom.FeeType;
   sfcomiss.Number  = OprSfCom.CommNumber;

   if( not getEQ( sfcomiss ) )
      MsgBox("Не найдена комиссия");
      return 1;
   end;

   if( sfcomiss.InstantPayment == "X" )

     sfcontr.Id = OprSfCom.SfContrID;
     /* Получим договор */
     if( not getEQ( sfcontr ) )
        MsgBox("Не найден договор обслуживания: ", OprSfCom.SfContrID );
        return 1;
     end;

     if( DateKindID > 0 )
       GetOprDate_Kind( DateKindID, opdate );      
       if( opdate == Date(0,0,0) )
         opdate = {curdate};
       end;      
     else
       opdate = {curdate};
     end;

     /*в 2029.3X изменился индекс TSfConComKey0: добавлены поля SfPlanID и dateBegin.
     При SfPlanID = -1 в функции проверяется наличие ТП у SfContr действующего на dateBegin: 
     если ТП есть, то SfConCom ищется с учетом его идентификаторa, если нет - SfPlanID = 0*/
     if( FindSfConCom_OnDate(SfContr.ID, OprSfCom.FeeType, OprSfCom.CommNumber, 659, -1, opdate, SfConCom) )
       MsgBox("Не найдена комиссия ДО");
       return 1;
     end;
    
     if( isDefComNVPI( OBJTYPE_OPRSFCOM, OprSfCom, isNVPI ) != 0 )
       return 1;
     end;
   
     sidebet.Clear;
     sicredit.Clear;
     if(SfGetSI_Uni( OBJTYPE_OPRSFCOM, OprSfCom, sidebet, sicredit ))
        MsgBox("Ошибка при взятии платежных инструкций");
        return 1;
     end;

     /* Формирование основания документов оплаты коммиссий */
     GroundStr = BuildGroundWithVO( OprSfCom, sfcomiss, sfcontr, sidebet.rec.PartyID, sicredit.rec.PartyID, opDate ); 

     SfConComPD = SfConComPrimDoc( 83/*DLDOC_SFCONCOM*/, SfConCom.rec, sfcontr, OprSfCom.FIID_Sum );

     stat = SfFormDocs( OutBuffer, sidebet, sicredit, sfcomiss, opDate, GroundStr, 
              OprSfCom.Sum, OprSfCom.SumNDS, OprSfCom.FIID_Sum, primKind, 0, 
              OBJTYPE_OPRSFCOM, OprSfCom, sfcontr.PayMethod, 
              SfConComPD, OprSfCom.IsIncluded, isNVPI, OprSfCom.FacturaID, bilfDocArray, null, null, null, null, null, null, SfContr.PreAcptID);

     if( (stat == 0) and (sfcontr.PayMethod == SF_PAY_METHOD_DOCUMENT) and (bilfDocArray.Size > 0) )
       
       SfComSetState( OprSfCom, SFDEFCOM_STATUS_PAYED );
       
       if(OprSfCom.FacturaID > 0)  
       
         if(not CreateBilBookEntry(OprSfCom.FacturaID, opdate, bilfDocArray))
           MsgBox( "Не удалось связать оплату комиссии с СФ" );
         end;
       else 
         if(OprSfCom.FacturaID < 0)
          if(not CreateBilBookEntry(OprSfCom, opdate, bilfDocArray))
            MsgBox( "Не удалось связать оплату комиссии с СФ" );
          end;
         end;
       end;
     elif ( stat == 0 ) // 18.03.2015 joy R-556258-3 Проставляем статус 30 (К оплате)
        SfComSetState( OprSfCom, SFDEFCOM_STATUS_FOR_PAY );
     end;
   end;

   return stat;
end;/*SfSingPay*/


macro SfSingPayBatch(FeeType, BlockID, Number_Step)

  return SFCreateDocPayBatch(FeeType, BlockID, Number_Step, NULL);

end;
