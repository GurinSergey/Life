//-----------------------------------------------------------------------------
// Блок      : 29011 - "Возврат из БО"
// Шаг       : 10    - "Обработка выполненных проводок"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import PaymInter, cbsttls, OprInter, pm_tools;
import oralib; //Jushmanov 2014-02-21 C-19151

var PaymentObj:RsbPayment;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
MACRO ExecuteStep( doc, paymDoc, DocKind, ID_Operation )

    if( PaymentObj.PayerGroup != PAYMENTS_GROUP_EXTERNAL )
        PaymentObj.FreeReserve( PaymentObj.PayerAccount, 1/*CHAPT1*/ , PaymentObj.PayerFIID );
    end;

    var text;
    if( IsClaimByOperStep(ID_Operation) )
        text = "По платежному документу имеется документ, изменяющий претензию. ";
        if(not IsOprMultiExec())
            if( RsbGetTrue( false, false, text + "|Вы действительно хотите выполнить возврат платежного документа?" ) == false ) 
                return 1;
            end;
        else
            msgbox(text + "Платежный документ должен быть обработан в единичном режиме.");
            return 1;  
        end;
    end;

    // Удаляем проводки
    if( not OprDeleteDocumentsPaymObject(PaymentObj.PaymentID) )
        msgbox("Ошибка при удалении проводок");
        return 1;
    end;

    if( УстановитьСтатусыПлатежа( OPR_PAYM_STATE, OPR_PM_ST_REJECT ) )
        msgbox("Ошибка при установке сегментов статуса экземпляра операции");
        return 1;
    end;

    return 0;
END;

//-----------------------------------------------------------------------------
// Постобработка шага
//-----------------------------------------------------------------------------
//Jushmanov 2014-02-21 C-19151
macro PostStepAction( message,      /* 1 - выполнение шага; 2 - откат шага;   */
                      errTrn,       /* статус выполнения шага. 0 - ошибки нет */
                      FirstDoc,     /* указатель на первичный документ        */
                      ID_Oper,      /* внутренний идентификатор операции      */
                      Number_Step,  /* Номер шага операции (из настроек)      */
                      KindOper,     /* номер вида операции                    */
                      KindDoc,      /* номер вида первичного документа        */
                      KindStep,     /* вид шага операции                      */
                      ID_Step )     /* внутренний идентификатор шага операции */
    private var logquery;

    if(( message == OP_EXECUTE_STEP ) and (errTrn == 0) and (IsOprMultiExec))
        if ((valtype(ID_Oper) != V_INTEGER) and (valtype(ID_Oper) != V_STRING)) ID_Oper = 0; end;
        if ((valtype(ID_Step) != V_INTEGER) and (valtype(id_Step) != V_STRING)) ID_Step = 0; end;

        logquery = "UPDATE usr_doprstep_robot_dbt SET t_is_robot_step = chr(88)" +
                   " WHERE t_id_operation = " + ID_Oper + " AND t_id_step = " + ID_Step;
        ExecSQL(logquery, null, false);
    end;

    return 0;
end;