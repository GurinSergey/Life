/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prpmaci.mac                                        */
/*                                                                      */
/*  Описание       : Печать аккредитива.                                */
/*                                                                      */
/*  Программист    : Смирнов С.В.                                       */
/*                                                                      */
/*  Создан         : 10.10.02                                           */
/*                                                                      */
// KS 03.12.2013 Перенос пользовательских доработок в 31ю сборку
/************************************************************************/

import prpm, gnd120p;

PRIVATE MACRO GetTypeAkkr( type:string ):string

  var query:string =  " SELECT t.T_CONTENS "
                        " FROM dtypeac_dbt t "
                       " WHERE t.t_inumtype = 160 "
                         " AND t.T_TYPE_ACCOUNT = '" + type + "'";
                              
  var rs:RsdRecordset = execSQLselect( query, null, true );
  if( rs and rs.moveNext() )
    return string( rs.value(0) );
  end;

  return "";
END;

/* Печать аккредитива */
MACRO PrintAkkreditiv(ncopy:integer):bool
  var PayerProps   :TPartyProperties = TPartyProperties("Payer",    pr_pmpaym, pr_debet,  pr_pmrmprop);
  var ReceiverProps:TPartyProperties = TPartyProperties("Receiver", pr_pmpaym, pr_credit, pr_pmrmprop);

  var PayerString       :string,
      ReceiverString    :string,
      ground            :string,
      condition         :string;

  record rpspayord( "pspayord.dbt", "bank.def" );
  Copy(rpspayord, pr_pspayord);
      
  condition = IfThenElse( pr_pmakkr.rec.PayCondition == 0, НазваниеСАкцептом, НазваниеБезАкцепта );
  
  PayerString    = MakeINN_KPP_String(PayerProps.INN)    + " " + PayerProps.Name;
  ReceiverString = MakeINN_KPP_String(ReceiverProps.INN) + " " + ReceiverProps.Name;
  ground         = InterpretGround( pr_pmrmprop.rec.Ground );
  
  ARRAY SS, SG, SBP, SBR, SP, SR, SA;

  strsplit( PayerString,            SP,    50, 46, 5 );
  strsplit( ReceiverString,         SR,    50, 50, 5 );
  strsplit( PayerProps.BankName,    SBP,   50, 50, 2 );
  strsplit( ReceiverProps.BankName, SBR,   50, 50, 2 );
  strsplit( ground,                 SG,    90, 90, 3 );
  strsplit( akkr_adddocs,           SA,    90, 90, 9 );
  strsplit( RubToStrAlt(pr_pmpaym.rec.Amount), SS, 76, 76, 3 );
  if( pr_PrintEA )
    PrintEAHeader();
  end;

  while(ncopy != 0)
    [                                                                                         ┌─────────┐
                                                                                              │ 0401063 │
                                                                                              └─────────┘

       Аккредитив N ###############
                                                               ############     #############
                                                               ────────────     ─────────────
                                                                  (Дата)        (Вид платежа)

       Сумма   │ ####################################################################################
       прописью│ ####################################################################################
               │ ####################################################################################
      ─────────┴──────────────────────────────────────────┬───────────┬────────────────────────────────
       ИНН ############################################## │ Сумма     │ #############################
       ################################################## │           │
       ################################################## │           │
       ################################################## ├───────────┼────────────────────────────────
       ################################################## │ Сч.N.     │ #############################
                                                          │           │
       Плательщик                                         │           │
      ────────────────────────────────────────────────────┼───────────┤
       ################################################## │ БИК       │ #############################
       ################################################## ├───────────┤
                                                          │ Сч.N.     │ #############################
       Банк плательщика                                   │           │
      ────────────────────────────────────────────────────┼───────────┼────────────────────────────────
       ################################################## │ БИК       │ #############################
       ################################################## ├───────────┤
                                                          │ Сч.N.     │ #############################
       Банк получателя                                    │           │
      ────────────────────────────────────────────────────┼───────────┤
       ИНН ############################################## │ Сч.N.     │ #############################
       ################################################## │ (40901)   │
       ################################################## ├───────────┼──────┬────────────┬────────────
       ################################################## │  Вид оп.  │ #### │ Срок дейст.│ ##########
       ################################################## ├───────────┤      │ аккредит.  │
                                                          │ Наз. пл.  │      │            │
                                                          ├───────────┤      ├────────────┤
       Получатель                                         │ Код       │      │ Рез. поле  │
      ─────────────┬──────────────────────────────────────┴───────────┴──────┴────────────┴────────────
       Вид         │
       аккредитива │  ###############################################################################
      ─────────────┤
       Условие     │
       оплаты      │  ####################################
       ────────────┴───────────────────────────────────────────────────────────────────────────────────
       Наименование товаров (работ, услуг), № и дата договора, срок отгрузки товаров (выполнения работ,
       оказания услуг), грузополучатель и место назначения
       ###############################################################################################
       ###############################################################################################
       ###############################################################################################

       Платеж по представлению (вид документа)
       ###############################################################################################

       Дополнительные условия
       ###############################################################################################

       № счета получателя  ###########################################################################
      ─────────────────────────────────────────────────────────────────────────────────────────────────
                                                Подписи                        Отметки банка
                                                                               ##################
            М.П.
                                                ───────────────────────────────

                                                ───────────────────────────────
       ############################################################################################### 
       ###############################################################################################
       ###############################################################################################
       ###############################################################################################
       ###############################################################################################
       ###############################################################################################
       ###############################################################################################
       ###############################################################################################
       ###############################################################################################
    
        ]
     (pr_pmrmprop.rec.Number:l, pr_pmrmprop.rec.Date:f:c, GetPaymentKind(pr_pmrmprop.rec.PaymentKind):c,
      SS(0):l, SS(1):l, SS(2),
      SP(0):l, pr_pmpaym.rec.Amount:l:f, SP(1):l, SP(2):l, SP(3), SP(4),
      PayerProps.Account:l,
      SBP(0):l, PayerProps.bankBIC:l, SBP(1):l, PayerProps.BankCorrAccount:l,
      SBR(0):l, ReceiverProps.BankBIC:l, SBR(1):l, ReceiverProps.BankCorrAccount:l, SR(0),
      ReceiverProps.Account:l, SR(1), SR(2), SR(3),
      pr_pmrmprop.rec.ShifrOper:l, pr_pmakkr.rec.Date:f:l, SR(4),
      GetTypeAkkr( pr_pmakkr.rec.Type ):l, condition:l,
      SG(0):l, SG(1):l, SG(2):l, 
      pr_pmakkr.rec.Representation:l, pr_pmakkr.rec.AddCondition:l, pr_pmakkr.rec.AccRealReceiver:l,
      pr_pmpaym.rec.PayerBankMarkDate:f,
      SA(0):l,SA(1):l,SA(2):l,SA(3):l,SA(4):l,SA(5):l,SA(6):l,SA(7):l,SA(8):l
     );

    ncopy = ncopy - 1;
  end;
  
  return TRUE;
  
END;
