/*
 $Name:        Prpmstval3352u.mac
 $Module:      ББ
 $Description: Печать мемордера 0402102 по 3352-У
*/

//zmp 18.11.2014 немного видоизменен, смотреть оригинал в папке \mac

import globals;//, FIInter, PTInter, PaymInter;
import prpmbuff, prpmboi, likepy, prcs, prpm_common;

debugbreak;

var pr_cb_doc :TRecHandler = TRecHandler("cb_doc.dbt", "bank.def");
var pr_pmvaltr:TRecHandler = TRecHandler( "pmvaltr.dbt" );
var pr_pmstval:TRecHandler = TRecHandler( "pmstval.dbt" );

PRIVATE MACRO FillPayAmountStr( Amount, FIID, ArraySum )

  var CURtext = ""; 
  
  if( FIID != 0 )
    CURtext = CurToStrAlt(Amount, NULL, NULL, GetISOCode(FIID));    
  else  
    CURtext = RubToStrAlt(Amount);
  end;
  var rub = subStr(CURtext, 1, index(CURtext, "коп") - 4);
  var cop = subStr(CURtext, index(CURtext, "коп")    - 3, 2);

  if(cop != "00") CURtext = rub + cop + " коп.";
  else            CURtext = rub;
  end;


  StrSplit(CURtext, ArraySum, 80, 80, 2);
   
END;

PRIVATE CLASS TPayInstruction
  var Account    : string,
      FIID       : integer,
      Amount     : string;
END;




/* Заполнение счетов и сумм из разноски или если нет разноски из платежа Pmpaym */
private macro FillPIList( PIList:TArray, pmpaym:TRecHandler, DebetCredit:integer )
  
  var i : integer = 0;
  var pi : TPayInstruction;
  
  // ищем разноску в pmaddpi
  var payment : RsbPayment = RsbPayment(pmpaym.rec.PaymentID),
      pmaddpi : TRecHandler = TRecHandler( "pmaddpi.dbt" ),
      stat    : integer = payment.PIList( DebetCredit ).First();

  if ( not stat ) // если есть, заполняем из разноски
    i = 0;
    while( not stat )
      stat = payment.PIList( DebetCredit ).Current( pmaddpi );
      if( not stat )
        pi = TPayInstruction();
        pi.Account    = pmaddpi.rec.Account;
        pi.FIID       = pmaddpi.rec.FIID;
        pi.Amount     = string(pmaddpi.rec.Amount:f);
        PIList.value( i ) = pi;
        i = i + 1;
        stat = payment.PIList( DebetCredit ).Next();
      end;
    end;
  else // если нет разноски, берем из pmpaym
    pi = TPayInstruction();
    if (DebetCredit == PRT_Debet)
      pi.Account = pmpaym.rec.PayerAccount;
      pi.FIID    = pmpaym.rec.FIID;
      pi.Amount  = string(pmpaym.rec.Amount:f);
    else
      pi.Account = pmpaym.rec.ReceiverAccount;
      pi.FIID    = pmpaym.rec.PayFIID;
      pi.Amount  = string(pmpaym.rec.PayAmount:f);
    end;
    PIList.value(0) = pi;       
  end;
end;

private macro usr_getFullDate(_d) 
   var dd, mm, yyyy, mon;
   dateSplit (date(_d), dd, mm, yyyy);
   if   (mm == 1)  mon = "января";
   elif (mm == 2)  mon = "февраля";
   elif (mm == 3)  mon = "марта";
   elif (mm == 4)  mon = "апреля";
   elif (mm == 5)  mon = "мая";
   elif (mm == 6)  mon = "июня";
   elif (mm == 7)  mon = "июля";
   elif (mm == 8)  mon = "августа";
   elif (mm == 9)  mon = "сентября";
   elif (mm == 10) mon = "октября";
   elif (mm == 11) mon = "ноября";
   elif (mm == 12) mon = "декабря";
   end;
   return string (string(dd:o:2),  " ", mon, " ", yyyy, " года");
end;


private macro MakeMoneyStr3352(_Money:money)

  VAR StrRub  :string = "";
  VAR StrKop  :string = "";
  VAR StrSep  :string = "-";
  VAR RetValue:string = ""; 
  if ( _Money != $0 )
    
    if(ValType(_Money) == V_MONEY)
      StrRub = Trim(String((Floor(Double(_Money))):16:0));
      StrKop = Trim(String((100.0 * (Double(_Money) - Floor(Double(_Money)))):16:0));
      if(StrLen(StrKop) < 2)
        StrKop = "0" + StrKop;
      end;
    
      RetValue = StrRub;
      
      if(StrKop == "00") RetValue = StrRub + "=";
      else               RetValue = StrRub + "-" + StrKop; 
      end;

    end;
  end;
  return RetValue;

end; /* MakeMoneyStr3352 */

private macro getRegDocStr(d)
   var str = " ", i = 0;
   while(i < d.size)
      if (d[i] != "") str = str + d[i]; end;
      i = i + 1;
   end;
   return  str;
onError
return str;
end;



private macro getBankName(pmpaym:TRecHandler,Account )
  file acc ("account.dbt" ) key 0;
  file dp_dep("dp_dep.dbt") key 0;
  file party("party.dbt");
  record pt(party);
  var bankName = " ";
  acc.Chapter       = pmpaym.rec.Chapter;
  acc.Account       = Account;
  acc.Code_Currency = pmpaym.rec.FIID;
  if( getEQ( acc ) )
    if (acc.Department != acc.Branch)
      dp_dep.Code = acc.Branch;
      if(getEQ(dp_dep))
        if(dp_dep.PartyID)
          party.PartyID = dp_dep.PartyID;
          if(getEQ(party)) BankName = party.Name end;
        end;
      end;
    end;
  end;  
  return bankName;
end;



MACRO PrintOrderPrintData( data )
  debugbreak;
  array aBankName, aAmount;

  
  var ground,
      oper_name,
      passport,
      rsd,
      cmd,
      operation,
      values,
      Number,
      officerPost,
      officerName,
      Sum; //количество
  
  /* Подготовка строк к печати */
  ARRAY SS, SG, SBP, DD, pp, ArrayofficerPost, ArrayofficerName, arrayVal, arrayOpearation ; 
  var arrayValue = Tarray();


  if(not inlist(substr(pr_pmpaym.rec.payeraccount, 1, 5), "99999", "99998"))
      StrSplit ( getBankName(pr_pmpaym,pr_pmpaym.rec.payeraccount   ), aBankName, 38, 32, 2 );
  else
      StrSplit ( getBankName(pr_pmpaym,pr_pmpaym.rec.receiveraccount), aBankName, 38, 32, 2 );
  end;
  if(aBankName(1) == "") aBankName(1) = {name_bank}; aBankName(2) = " ";
  else                   aBankName(2) = {name_bank}; 
  end;


  macro getOfficerData(id, officerPost:@string,officerName:@string )
   var rs = "  SELECT   t_Post post, (SELECT   PT.T_SHORTNAME "+
            "  FROM   DPARTY_DBT PT                           "+
            "  WHERE   PT.T_PARTYID = P.T_PARTYID) name       "+
            "  FROM   dofficer_dbt o,                         "+
            " dperson_dbt p,                                  "+
            " dpmpaym_dbt pm,                                 "+
            "  ddp_dep_dbt dep                                "+
            " WHERE       P.T_OPER = PM.T_OPER                "+
            " AND P.T_PARTYID = o.t_PersonID                  "+
     //     " AND P.T_CODEDEPART = DEP.T_CODE                 "+  LVV I-00550229 11.02.15 Если пользователь не головного узла ТС, то ничего запрос не найдет
            " AND O.T_PARTYID = DEP.T_PARTYID                 "+
            " AND PM.T_PAYMENTID =                            " + id;
   rs = rsdRecordset(rs);
   if(rs and rs.movenext())  
      officerPost = rs.value("post", null, v_string);
      officerName = rs.value("name", null, v_string);
   else
      officerPost = "";
      officerName = "";
   end;    
   end;

  private macro fillEmptyValueArray(a, s:integer)
     var i = 0;
     for(i, 0 , s - 1) if(a[i] == null) a[i] = " "; end; end;
  onError
  end;

  var DebetList : TArray, CreditList : TArray;
  DebetList  = TArray;
  FillPIList( DebetList, pr_pmpaym, PRT_Debet );
  CreditList = TArray;
  FillPIList( CreditList, pr_pmpaym, PRT_Credit );
  // Если нет разноски и по дебету и по кредиту, то поле CredAmount не заполняется
  if ( (DebetList.size < 2) and (CreditList.size < 2) )
    CreditList.value(0).Amount = "";
  end;

  Sum = pr_pmpaym.rec.Amount;

  getOfficerData(pr_pmpaym.rec.paymentid, @officerPost, @officerName);

  strSplit(officerPost, ArrayofficerPost, 24, 24, 2);
  strSplit(officerName, ArrayofficerName, 43, 43, 2);

  if(ArrayofficerPost(1) == "") ArrayofficerPost(1) = ArrayofficerPost(0); ArrayofficerPost(0) = ""; end;
  if(ArrayofficerName(1) == "") ArrayofficerName(1) = ArrayofficerName(0); ArrayofficerName(0) = ""; end;

  fillEmptyValueArray(ArrayofficerPost, 2);
  fillEmptyValueArray(ArrayofficerName, 2);

 if ( DebetList.size < 2 ) FillPayAmountStr( pr_pmpaym.rec.Amount, pr_pmpaym.rec.FIID, aAmount );
 else                      FillPayAmountStr( pr_pmpaym.rec.PayAmount, pr_pmpaym.rec.PayFIID, aAmount );
 end;

 ground = pr_pmrmprop.rec.ground;
 
 
 strsplit( data.Content,             arrayOpearation, 94, 74, 2 );
 strsplit( data.OwnerName,           SS,              38, 14, 3 );
 strsplit( getRegDocStr(data.RegDoc),pp,              95, 50, 2 );
  
    [
                                         ┌────────────┐                          ┌───────────────────┐
          Ордер по передаче ценностей  № │##########  │ #                        │Код формы документа│    
                                         └────────────┘ ________________________ │ по ОКУД 0402102   │
                                                                Дата             └───────────────────┘
                                                                                ┌────────────────────┐
      Наименование банка ################################       ДЕБЕТ           │    Сумма (цифрами) │
       ####################################### ┌────────────────────────────────┼──────────┬─────────┤
       ####################################### │счет № #########################│##########│         │]
    (  pr_pmrmprop.rec.Number:l,
       usr_getFullDate(pr_pmrmprop.rec.Date),
       aBankName(0),
       aBankName(1), aBankName(2), DebetList.value(0).Account:l, MakeMoneyStr3352(strsubst(DebetList.value(0).Amount, "-", ".")):l );
       
    var i : integer = 1;
    while( i < DebetList.size )

    [                                          ├────────────────────────────────┤          │         │
                                               │счет № #########################│##########│         │]
      (DebetList.value(i).Account:l, MakeMoneyStr3352(strsubst(DebetList.value(i).Amount, "-", ".")):l);
      i = i + 1;
    end;
      
    [                                          └────────────────────────────────┤          │         │
                                                                                │          │         │
      Кому принадлежат ценности ##############                  КРЕДИТ          │          │         │
       ####################################### ┌────────────────────────────────┤          │         │
       ####################################### │счет № #########################│##########│         │]
    ( SS(0), SS(1), SS(2), CreditList.value(0).Account:l, ifThenElse(CreditList.size == 0, " ", MakeMoneyStr3352(strsubst(CreditList.value(0).amount,  "-", ".")):l) );

    i = 1;   
    while( i < CreditList.size )

    [                                          ├────────────────────────────────┤          │         │
                                               │счет № #########################│##########│         │]
      (CreditList.value(i).Account:l, MakeMoneyStr3352(strsubst(CreditList.value(i).Amount, "-", ".")):l);
      i = i + 1;
    end;                                                                        
    [                                          │                                ├──────────┴────┬────┤
                                               │                                │Шифр документа │ ## │
                                               └────────────────────────────────┴───────────────┴────┘
      Содержание операции  ##########################################################################]
       ( data.Sh:l, arrayOpearation(0):l);

    if(trim(arrayOpearation(1)) != "")
    [  ##############################################################################################]
    (arrayOpearation(1):l);
    end;

    [ ╔══════════════════════════════════════════════════════╤══════════════════╤════════════════════╗
      ║           Наименование ценностей                     │    Количество    │    Сумма (цифрами) ║
      ╟──────────────────────────────────────────────────────┼──────────────────┼────────────────────╢];
   
    i = 0;
    var valuables:TArray = data.order.ValuableList.AsTArray();

    while( i < valuables.size)
    strsplit(valuables[i].rec.NameValue,       arrayVal, 52, 52, 3 );   


    [ ║ #################################################### │ ################ │ ################## ║]
    (arrayVal[0], valuables[i].rec.CountValue, MakeMoneyStr3352(strsubst(string(valuables[i].rec.AmountValue), "-", ".")):l);
    if(trim(arrayVal(1)) != "")
    [ ║ #################################################### │                  │                    ║]
    (arrayVal(1));
    end;
    if(trim(arrayVal(2)) != "")
    [ ║ #################################################### │                  │                    ║]
    (arrayVal(2));
    end;
    i = i + 1;                                                                                       
    if((arrayValue.size > 1) and (i != arrayValue.size))                                             
    [ ║──────────────────────────────────────────────────────┼──────────────────┼────────────────────║]
    end;                                                                       
     asize(arrayVal, 0);
    end;         
    
    


    [ ╚══════════════════════════════════════════════════════╧══════════════════╧════════════════════╝
      Сумма прописью ################################################################################
                     ################################################################################

      ########################                           ###########################################
      ########################                           ###########################################
      ________________________ ________________________  ___________________________________________
      (наименование должности)     (личная подпись)             (фамилия, инициалы)

      Ценности переданы                                  
      ________________________ ________________________  ___________________________________________
      (наименование должности)     (личная подпись)             (фамилия, инициалы)

                                                         ###########################################
      ________________________ ________________________  ___________________________________________
      (наименование должности)     (личная подпись)             (фамилия, инициалы)

      Предъявлен документ, удостоверяющий личность:##################################################
      ###############################################################################################

    ]
      (
       aAmount(0),
       aAmount(1), 
       ArrayOfficerPost(0):c,
       ArrayOfficerName(0):c,
       ArrayOfficerPost(1):c,
       ArrayOfficerName(1):c,
       data.FIO:c,
       pp(0), pp(1)
       ); 

return true;

END;


macro PrintDocument( ncopy:integer ):bool
  var stat : bool = TRUE,
      data : TInOutOrderPrintData = TInOutOrderPrintData();

  if( pr_pmpaym.rec.DocKind == 288 )
    data.InitByCashOrder( pr_pmpaym, pr_pmrmprop, pr_pmstval, pr_pmvaltr );
    stat = PrintOrderPrintData( data );
  else
    MsgBox("Данный вид документа не поддерживается макросом");
    stat = FALSE;
  end;

  return stat;
end;
