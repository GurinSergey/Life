/******************************************************************/
/*         Автоматизированная банковская система RS-Bank v6.0     */
/*                 Copyright (c) R-Style Software Lab 2005        */
/*  Имя файла        : cbsfinv10.mac                              */
/*  Описание         : Оплата ТО. Шаг Предъявления ТО             */
/*  Программист      : Чернов А.Г.                                */
/*  Создан           : 09-06-2005                                 */
/******************************************************************/
// KS 26.11.2013 Адаптация под 31ю сборку

import InsCarryDoc, "sfcommon.mac", "sfcomcat.mac", "cbsfinvac.mac", "sfpay.mac";
/* EVG */
import "CommissLib.mac";

/* EVG Если счёт валютный, возвращает код валюты, иначе - код нац. валюты */
macro GetAccountFIID( Acc )
  var query, rs;
  //Gurin S. 30.01.2014 Адаптация 2031
  query = " Select t_Code_Currency From daccount_dbt Where t_Account = '" + Acc + "'";
  rs = rsdRecordSet(query);
  if (rs and rs.moveNext())
     return rs.Value(0, null, V_INTEGER);
  end;

  return NatCur;
end;


private macro FillDebetCredit( sidebet, sicredit, sfinv, sfcomiss, SfContr, SfConComPD:SfConComPrimDoc, IsNVPI, defComID )

  const PlusNDSCatCode = "+НДС"; 
  const MinusNDSCatCode = "-НДС";

  var settacc  = TRecHandler("settacc.dbt");
  settacc.Clear();
  SfSelectSETTACC( sfinv.InvoiceFIID, OBJTYPE_SFCONTR, SfContr, settacc );
  
  /*sidebet*/
  sidebet.rec.PartyId  = sfinv.PayerID;
  sidebet.rec.PartyCodeKind = sfinv.PayerCodeKind;          
  sidebet.rec.PartyCode     = sfinv.PayerCode;          
  sidebet.rec.PartyName     = sfinv.PayerName;
  sidebet.rec.PartyINN = sfinv.PayerINN;
  sidebet.rec.BankID = sfinv.PayerBankID;
  sidebet.rec.BankCodeKind = sfinv.PayerBankCodeKind;
  sidebet.rec.BankCode = sfinv.PayerBankCode;
  sidebet.rec.BankName = sfinv.PayerBankName;
  sidebet.rec.CorrAcc  = sfinv.PayerCorrAcc;
  sidebet.rec.BankCorrCodeKind = sfinv.PayerCorrBankCodeKind;
  sidebet.rec.BankCorrCode     = sfinv.PayerCorrBankCode;
  sidebet.rec.BankCorrName     = sfinv.PayerCorrBankName;
  /* EVG Брать валюту СПИ по ДО неправильно, т.к. комиссия может
     оплачиваться с совершенно иного счёта.
  sidebet.rec.FIID     = sfinv.PayerAccountFIID; 
  */
  sidebet.rec.FIID = GetAccountFIID( sfinv.PayerAccount );
  sidebet.rec.Account  = sfinv.PayerAccount;
  sidebet.rec.NoAccept = sfinv.NoAccept;
  sidebet.rec.Department = CheckAccountDprt( sidebet.rec.Account, sidebet.rec.FIID, 1 );
 
  /*sicredit*/
  sicredit.rec.PartyId  = sfinv.BeneID;
  sicredit.rec.PartyCodeKind = sfinv.BeneCodeKind;          
  sicredit.rec.PartyCode     = sfinv.BeneCode;          
  sicredit.rec.PartyName     = sfinv.BeneName;
  sicredit.rec.PartyINN = sfinv.BeneINN;
  sicredit.rec.BankID = sfinv.BeneBankID;
  sicredit.rec.BankCodeKind = sfinv.BeneBankCodeKind;
  sicredit.rec.BankCode = sfinv.BeneBankCode;
  sicredit.rec.BankName = sfinv.BeneBankName;
  sicredit.rec.BankCorrCodeKind = sfinv.BeneCorrBankCodeKind;
  sicredit.rec.BankCorrCode     = sfinv.BeneCorrBankCode;
  sicredit.rec.BankCorrName     = sfinv.BeneCorrBankName;
  sicredit.rec.FIID     = sfinv.BeneAccountFIID;
  sicredit.rec.Account  = sfinv.BeneAccount;  
  sicredit.rec.Department = CheckAccountDprt( sicredit.rec.Account, sicredit.rec.FIID, 1 );
  
  if( sfinv.NDSAmount > $0 )
    if( ((sfinv.BeneBankID == {HeadBankID}) OR (sfinv.BeneBankID == {OurBank})) AND (sfinv.IsIncluded == "X") )
      if( isNVPI == true )
        sicredit.rec.ReceiverNDSAccount
          = SfConComPD.GetDefComSfSiAccount( defComID, CALCNDS_SFSI_KIND, PlusCalcNDS_NVPI_CatCode, sfinv.InvoiceDate, sfinv.PayFIID );
      else
        sicredit.rec.ReceiverNDSAccount
          = SfConComPD.GetDefComSfSiAccount( defComID, CALCNDS_SFSI_KIND, PlusCalcNDS_CatCode, sfinv.InvoiceDate, sfinv.PayFIID );
      end;
    else
      if( (sfinv.BeneBankID == {HeadBankID}) OR (sfinv.BeneBankID == {OurBank}) )      
      sicredit.rec.ReceiverNDSAccount = 
        SfConComPD.GetDefComSfSiAccount( defComID, NDS_SFSI_KIND, MinusNDSCatCode, sfinv.InvoiceDate, NATCUR );
    else
      if( (sfinv.PayerBankID == {HeadBankID}) OR (sfinv.BeneBankID == {OurBank}) )
        /*Дт.   "+НДС" 
         Кт.     СПИ комиссии*/
        sidebet.rec.ReceiverNDSAccount = 
          SfConComPD.GetDefComSfSiAccount( defComID, NDS_SFSI_KIND, PlusNDSCatCode, sfinv.InvoiceDate, NATCUR );
      end;
    end;
  end;  
  end;

  return true;

end;

private macro getSfInvNotPaidSums( sfinv_buf, NotPaidInvSum:@money, NotPaidNDSSum:@money )
  var  invRests:TArray;
  var retPayFIID, retDirection;
  
  if( sfinv_buf.InvoiceFIID != sfinv_buf.PayFIID )
    GetInvoiceRest( sfinv_buf.InvoiceID, sfinv_buf.InvoiceDate, retPayFIID, retDirection, invRests );
    if( invRests.size > 0 )
      NotPaidInvSum = invRests[0].rec.amount;
      if( invRests.size > 1 )
        NotPaidNDSSum = invRests[1].rec.amount;
      end; 
    end;
  else
    NotPaidInvSum = sfinv_buf.TotalAmount - sfinv_buf.PaidAmount - (sfinv_buf.NDSAmount - sfinv_buf.PaidNDS);
    NotPaidNDSSum = sfinv_buf.NDSAmount - sfinv_buf.PaidNDS;
  end;
end;

private macro fillSfInvLnk( sfinv_buf, NotPaidInvSum, NotPaidNDSSum, sfinvlnk:TRecHandler )

  //При формировании платежа по ТО необходимо заполнять для него в SfInvLnk суммы  PayAmount, PayFIID, PaidNDS и LinkStatus.
  sfinvlnk.Clear;
  sfinvlnk.rec.InvoiceID = sfinv_buf.InvoiceID;
  sfinvlnk.rec.PayAmount = NotPaidInvSum;
  sfinvlnk.rec.PayFIID   = sfinv_buf.PayFIID;
  sfinvlnk.rec.PaidNDS   = NotPaidNDSSum;
  sfinvlnk.rec.LinkStatus = 0; /*связь не отражена*/
  sfinvlnk.rec.InvAmount = $0;
  sfinvlnk.rec.InvNDS    = $0;
end;

private macro fillSfInvPayParams( sfinv_buf, SfContr, payParams:TSfPayParams )
  
  payParams.ground = sfinv_buf.Details;
  payParams.payDate = sfinv_buf.InvoiceDate;
  payParams.FIID = sfinv_buf.PayFIID;
  payParams.objectType = OBJTYPE_SFINVOICE;
  payParams.objectBuf = sfinv_buf;
  payParams.payMethod = SfContr.payMethod;
  payParams.IsIncluded = sfinv_buf.IsIncluded;  
  payParams.FacturaID = sfinv_buf.FacturaID;
  payParams.PreAcptID = SfContr.PreAcptID;

end;

private macro SfInvFormPayDocs( d, sfinv_buf, SfContr, SfConComPD, sidebet, sicredit, sfcomiss, IsNVPI )

  var stat = 0;

  var NotPaidInvSum:money = $0;
  var NotPaidNDSSum:money = $0;
  
  var SumInInvFIID = sfinv_buf.TotalAmount - sfinv_buf.PaidAmount - (sfinv_buf.NDSAmount - sfinv_buf.PaidNDS);
  getSfInvNotPaidSums( sfinv_buf, @NotPaidInvSum, @NotPaidNDSSum );
  
  var sfinvlnk = TRecHandler( "sfinvlnk.dbt" );
  fillSfInvLnk( sfinv_buf, NotPaidInvSum, NotPaidNDSSum, sfinvlnk );

  var payParams = TSfPayParams;
  fillSfInvPayParams( sfinv_buf, SfContr, payParams );
  payParams.paySum = NotPaidInvSum;
  payParams.taxSum = NotPaidNDSSum;
  payParams.SfComPD = SfConComPD;
  payParams.isNVPI = IsNVPI;

  var bilfDocArray = TArray;

  stat = SfInvFormDocs( d, sidebet, sicredit, sfcomiss, payParams, sfinvlnk, SumInInvFIID, @bilfDocArray );

  if(stat == 0)                   
   if( (sfinv_buf.FacturaID > 0) AND (bilfDocArray.Size > 0) )       
     if(not CreateBilBookEntry(sfinv_buf.FacturaID, sfinv_buf.InvoiceDate, bilfDocArray) )  
       MsgBox("Не удалось связать оплату комиссии с СФ");
     end;
   end;
  end;    
  
  /* Если работаем без РКО */
  if( not Sf_IsInstalledRKO() OR (SfContr.payMethod == SF_PAY_METHOD_DOCUMENT) )
    /*Меняем статус ТО на "Оплачено", обновляем в ТО оплаченные суммы sfinv.PayAmount и sfinv.PayNDS, 
    меняем статусы включенных комиссий на "Оплачено".*/
    if( not SfInvSetState( sfinv_buf.InvoiceID, SFINV_STATUS_PAYED, true ) ) 
      msgbox( "Ошибка при установке статуса ТО" );
      return 1;
    end;

    if( not InsertOprStatus(SFINV_OPRPAYSTATUS, SFINV_STATUS_PAYED ) )
      msgbox( "Ошибка при установке статуса операции" );
      return 1;
    end;
  else
    if( sfinv_buf.InvoiceStatus == SFINV_STATUS_NOPRODUCED )
      if( not SfInvSetState( sfinv_buf.InvoiceID, SFINV_STATUS_NOTPAYED ) ) 
        msgbox( "Ошибка при установке статуса ТО" );
        return 1;
      end;  
    end;

    if( not InsertOprStatus(SFINV_OPRPAYSTATUS, SFINV_STATUS_NOTPAYED ) ) 
      msgbox( "Ошибка при установке статуса операции" );
      return 1;
    end;
  end;
  return stat;
end;

macro ExecuteStep( d, sfinv )
  var stat = 0;

  record sfinv_buf("sfinv.dbt");
  setbuff( sfinv_buf, sfinv );

  var SfConComOnDate = {BranchCurDate}, defComID = 0;

  /*Получить комисиию */
  file sfcomiss( sfcomiss ) key 0;
  sfcomiss.number  = GetDefComissParm( sfinv_buf.InvoiceID, @SfConComOnDate, @defComID, @sfcomiss.FeeType );
  if( not getEQ( sfcomiss ) )
    MsgBox("Не найдена комиссия");
    return 1;
  end;

  file SfContr( "sfcontr.dbt") key 0;
  SfContr.Id = sfinv_buf.ContractID;
  if( not getEQ( SfContr ) )
    MsgBox("Не найден договор обслуживания: ", sfinv_buf.ContractID);
    return 1;
  end;

  if ((sfinv_buf.InvMethod == SFINV_INVMETHOD_ACCOUNT) or (sfinv_buf.InvMethod == SFINV_INVMETHOD_NOPAYDOC))
    /* для способа оплаты по счету печатаем счет после выполнения шага */
    if( not InsertOprStatus(SFINV_OPRCORRECTPAYMSTATUS, SFINV_CORRECTPAYM_UNNEEDED) ) 
      msgbox( "Ошибка при установке статуса операции" );
      return 1;
    end;
  else // иначе вставляем документ

    /* заполним  sidebet, sicredit */
    var sidebet  = TRecHandler("sfsi.dbt");
    var sicredit = TRecHandler("sfsi.dbt"); 
    sidebet.Clear;
    sicredit.Clear;

    /*в 2029.3X изменился индекс TSfConComKey0: добавлены поля SfPlanID и dateBegin.
    При SfPlanID = -1 в функции проверяется наличие ТП у SfContr действующего на dateBegin: 
    если ТП есть, то SfConCom ищется с учетом его идентификаторa, если нет - SfPlanID = 0*/
    var SfConCom = TRecHandler( "sfconcom.dbt" );
    if( FindSfConCom_OnDate(sfinv_buf.ContractID, sfcomiss.FeeType, sfcomiss.number, 659, -1, SfConComOnDate, SfConCom) )
      MsgBox("Не найдена комиссия ДО");
      return 1;
    end;  

    var SfConComPD = SfConComPrimDoc( 83/*DLDOC_SFCONCOM*/, SfConCom.rec, SfContr, sfinv_buf.InvoiceFIID );
    var IsNVPI = IsNVPICarry( sfinv_buf.PayFIID, sfinv_buf.InvoiceFIID, sfinv_buf.PayRateDateKind );

    FillDebetCredit( sidebet, sicredit, sfinv_buf, sfcomiss, SfContr, SfConComPD, IsNVPI, defComID );

    stat = SfInvFormPayDocs( d, sfinv_buf, SfContr, SfConComPD, sidebet, sicredit, sfcomiss, IsNVPI );
  end;
  
  return stat;
end;

macro PrintStepDocs( ID_Oper,    /* внутренний идентификатор операции        */
                     ID_Step,    /* внутренний идентификатор шага операции   */
                     KindDoc,    /* номер вида первичного документа          */
                     KindStep )  /* вид шага операции                        */

  var oproper = TBFile("oproper");
  var sfinv = TBFile("sfinv");

  oproper.rec.ID_Operation = ID_Oper;
  if( not GetEQ(oproper) )
    MsgBox("Не найден экземпляр операции");
    return 0;
  end;

  sfinv.rec.InvoiceID = int(oproper.rec.DocumentID);
  if( not GetEQ(sfinv) )
    MsgBox("Не найден первичный документ <sfinv.dbt>");
    return 0;
  end;

  SfPrintInvoice( sfinv.rec );

  return 0;
end;

macro PostStep( message,     /* данный параметр может принимать следующие       */
                             /* значения: 1 - выполнение шага; 2 - откат шага;  */
                errTrn,      /* статус выполнения шага. Если параметр не равен 0*/
                             /* произошла ошибка                                */
                FirstDoc,    /* указатель на первичный документ                 */
                ID_Oper,     /* внутренний идентификатор операции               */
                ID_Step,     /* внутренний идентификатор шага операции          */
                KindOper,    /* номер вида операции                             */
                KindDoc,     /* номер вида первичного документа                 */
                KindStep )   /* вид шага операции                               */

  record sfinv("sfinv");
  setbuff (sfinv, FirstDoc);

  if (errTrn OR (message==2)) 
    /* Произошла ошибка или происходит откат */
    return;
  end;

  SfPrintInvoice(sfinv);

  return 0;
end;
