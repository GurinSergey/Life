/************************************************************************/
/*         Автоматизированная банковская система RS-Bank                */
/*                 Copyright (c) R-Style Software Lab 2002              */
/*                                                                      */
/*  Имя файла      : prpmrqi.mac                                        */
/*                                                                      */
/*  Описание       : Печать платежного поручения.                       */
/*                                                                      */
/*  Программист    : Смирнов С.В.                                       */
/*                                                                      */
/*  Создан         : 09.10.02                                           */
/*                                                                      */
/*SDA 05/03/2012 - убран лишний разрыв страницы при пакетной печати     */
// KS 04.11.2013 Перенос пользовательских доработок в 31ю сборку
/************************************************************************/

import prpm, gnd120p, prpmboi, "pmbudg_lib.mac";
import OprInter;

Private macro MakeStamp(pr_pmpaym);
 macro opername(id)
    var cmd = rsdcommand("select t_name from dperson_dbt where t_oper = ?");
        cmd.addparam("id", RSdBP_IN, id);
        cmd.execute();
    var rsd = rsdrecordset(cmd);
    if (rsd.movenext())
    return rsd.value(0);
    else
    return "";
    end;
  end;

  var town_bank="";
  RECORD addr ( adress );
                    
  if( НайтиЮридическийАдресСубъекта( {OurBank}, addr) )
    town_bank = addr.CodeDistrict + ". " + addr.District;
  end;

[                                                       ┌────────────────────────────────┐
                                                        │################################│
                                                        │            ПРОВЕДЕНО           │
                                                        │################################│
                                                        │################################│
                                                        │ #################БИК ######### │
                                                        │ к/с ###########################│
                                                        └────────────────────────────────┘]
                    ({Name_Bank}:c, 
                     date(pr_pmpaym.rec.valuedate ):c:f, 
                     opername(pr_pmpaym.rec.oper):c,   
                     town_bank:c,  
                     {mfo_bank}:c,
                     {corac_bank}:c);

end;

/* Печать платежного поручения до вступления в силу 1256-У */
MACRO PrintPayReqBefore1256(ncopy:integer):bool

  var PayerProps   :TPartyProperties = TPartyProperties("Payer",    pr_pmpaym, pr_debet,  pr_pmrmprop);
  var ReceiverProps:TPartyProperties = TPartyProperties("Receiver", pr_pmpaym, pr_credit, pr_pmrmprop);

  var PayerString   :string, 
      ReceiverString:string,
      ground        :string,
      valAmount     :numeric;                           //AAN 29.11.2013 - I-00447003-2
      valAmount = pr_pmpaym.rec.Amount;
      
  PayerString    = MakeINN_KPP_String(PayerProps.INN)    + " " + PayerProps.Name;
  ReceiverString = MakeINN_KPP_String(ReceiverProps.INN) + " " + ReceiverProps.Name;
  ground         = InterpretGround( pr_pmrmprop.rec.Ground );
  
  ARRAY SS, SG, SBP, SBR, SP, SR, UIN;

  strsplit( PayerString,            SP,    50, 46, 5 );
  strsplit( ReceiverString,         SR,    50, 50, 5 );
  strsplit( PayerProps.BankName,    SBP,   50, 50, 2 );
  strsplit( ReceiverProps.BankName, SBR,   50, 50, 2 );
  strsplit( ground,                 SG,    85, 85, 3 );

  //DPN 25.11.2013 - I-00445667-3 
  if (pr_pmpaym.rec.Amount == pr_pmpaym.rec.Payamount)
      valAmount = pr_pmpaym.rec.Amount
  else 
      valAmount = pr_pmpaym.rec.Payamount
  end;
  strsplit( RubToStrAlt(valAmount), SS, 76, 76, 3 );
  strsplit( pr_pmrmprop.rec.UIN,   UIN, 13, 13, 2 );

  while(ncopy != 0)
    [                                                                                         ┌─────────┐
                                                                                              │ 0401060 │
                                                                                              └─────────┘
           ############
       ────────────────────
       Поступ. в банк плат.
                                                    ############     #############
       Платежное поручение N ###############        ────────────     ─────────────
                                                       (Дата)        (Вид платежа)

       Сумма   │ ####################################################################################
       прописью│ ####################################################################################
               │ ####################################################################################
      ─────────┴──────────────────────────────────────────┬───────────┬───────────────────────────────────
       ИНН ############################################## │ Сумма     │ #############################
       ################################################## │           │
       ################################################## │           │
       ################################################## ├───────────┼───────────────────────────────────
       ################################################## │ Сч.N.     │ #############################
                                                          │           │
       Плательщик                                         │           │
      ────────────────────────────────────────────────────┼───────────┼───────────────────────────────────
       ################################################## │ БИК       │ #############################
       ################################################## ├───────────┤
                                                          │ Сч.N.     │ #############################
       Банк плательщика                                   │           │
      ────────────────────────────────────────────────────┼───────────┼───────────────────────────────────
       ################################################## │ БИК       │ #############################
       ################################################## ├───────────┤
                                                          │ Сч.N.     │ #############################
       Банк получателя                                    │           │
      ────────────────────────────────────────────────────┼───────────┤
       ИНН ############################################## │ Сч.N.     │ #############################
       ################################################## │           │
       ################################################## ├───────────┼─────────────┬──────────┬───────────
       ################################################## │  Вид оп.  │    ####     │Срок плат.│ ##########
       ################################################## ├───────────┤             ├──────────┤
                                                          │ Наз. пл.  │             │Очер плат.│ ##########
                                                          ├───────────┤             ├──────────┤
                                                          │Код        │#############│Рез. поле │
       Получатель                                         │           │#############│          │
      ────────────────────────────────────────────────────┴───────────┴─────────────┴──────────┴───────────
       Назначение платежа:
       ###############################################################################################
       ###############################################################################################
       ###############################################################################################

      ─────────────────────────────────────────────────────────────────────────────────────────────────────
                                                Подписи                        Отметки банка
                                                                               ####################
            М.П.
                                                ───────────────────────────────

                                                ───────────────────────────────


    ]
    (pr_pmrmprop.rec.Date:f:c, pr_pmrmprop.rec.Date:f:c, 
     GetPaymentKind(pr_pmrmprop.rec.PaymentKind):c, pr_pmrmprop.rec.Number:l,
     SS(0):l, SS(1):l, SS(2),
     SP(0):l, valAmount:l:f, //DPN 25.11.2013 - I-00445667-3 
     SP(1):l, SP(2):l, SP(3), SP(4),
     PayerProps.Account:l,
     SBP(0):l, GetCodeBik(pr_pmpaym.rec.valuedate,PayerProps.BankBIC):l, SBP(1):l, PayerProps.BankCorrAccount:l,
     SBR(0):l, GetCodeBik(pr_pmpaym.rec.valuedate,ReceiverProps.BankBIC):l, SBR(1):l, 
     ReceiverProps.BankCorrAccount:l, SR(0), ReceiverProps.Account:l,
     SR(1), SR(2), SR(3),
     pr_pmrmprop.rec.ShifrOper:l, "", SR(4), pr_pmrmprop.rec.Priority:z,
     IfThenElse(pr_pmrmprop.rec.Date < Date(31,3,2014), "", UIN(0)),
     IfThenElse(pr_pmrmprop.rec.Date < Date(31,3,2014), "", UIN(1)),
     SG(0):l, SG(1):l, SG(2):l, pr_pmpaym.rec.PayerBankMarkDate:f:c
    );

    //DAI разрыв страницы только если действительно массовая печать
    if ((ncopy > 0) or (IsOprMultiExec()))
       PrintEOF();
    end;

    ncopy = ncopy - 1;
  end;
  
  return TRUE;
  
END;

/* Печать платежного поручения по 1256-У */
MACRO PrintPayReq1256(ncopy:integer):bool

  var PayerProps   :TPartyProperties = TPartyProperties("Payer",    pr_pmpaym, pr_debet,  pr_pmrmprop);
  var ReceiverProps:TPartyProperties = TPartyProperties("Receiver", pr_pmpaym, pr_credit, pr_pmrmprop);

  var pINN   :string, 
      pKPP   :string,
      rINN   :string, 
      rKPP   :string,
      ground   :string,
      IsBudg :bool = IsBudgetPaym(pr_pmrmprop.rec.TaxAuthorState, pr_pmpaym.rec.PayType),
      valAmount:numeric;                             //AAN 29.11.2013 - I-00447003-2
      valAmount = pr_pmpaym.rec.Amount;
      
  ARRAY SS, SG, SBP, SBR, SP, SR, UIN;

  var err,pagebreak,terminator=""; // KS 16.11.2011 Разделитель
  if(IsOprmultiExec())
    GetRegistryValue("PRBB\\REPORT\\ПЕЧАТЬ\\PAYORDER\\PAGE BREAK",V_BOOL,pagebreak,err);
    if (( err == 0 ) and (pagebreak))
      terminator = "";
    end;
  end;
  ground        = InterpretGround( pr_pmrmprop.rec.Ground );
  
  splitINN_KPP(PayerProps.INN,    pINN, pKPP);
  splitINN_KPP(ReceiverProps.INN, rINN, rKPP);

  strsplit( PayerProps.Name,        SP,    50, 50, 4 );
  strsplit( ReceiverProps.Name,     SR,    50, 50, 4 );
  strsplit( PayerProps.BankName,    SBP,   50, 50, 2 );
  strsplit( ReceiverProps.BankName, SBR,   50, 50, 2 );
  strsplit( ground,                 SG,    85, 85, 3 );
  
  //DPN 25.11.2013 - I-00445667-3 
  if (pr_pmpaym.rec.Amount == pr_pmpaym.rec.Payamount)
      valAmount = pr_pmpaym.rec.Amount
  else 
      valAmount = pr_pmpaym.rec.Payamount
  end;
  strsplit( RubToStrAlt(valAmount), SS, 76, 76, 3 );
  strsplit( pr_pmrmprop.rec.UIN,   UIN, 13, 13, 2 );
  
  while(ncopy != 0)
    [                                                                                  ┌─────────┐
                                                                                       │ 0401060 │
                                                                                       └─────────┘
          ############            ##########
      ────────────────────  ──────────────────────
      Поступ. в банк плат.   Списано со сч. плат.
                                                   ############     #############           ┌────┐
      Платежное поручение N ###############        ────────────     ─────────────           │ ## │
                                                      (Дата)        (Вид платежа)           └────┘

      Сумма   │ ####################################################################################
      прописью│ ####################################################################################
              │ ####################################################################################
     ─────────┴────────┬─────────────────────────────────┬───────────┬────────────────────────────────
      ИНН ############ │ КПП #########                   │ Сумма     │ #############################
     ──────────────────┴─────────────────────────────────┤           │
      ################################################## │           │
      ################################################## │           │
      ################################################## ├───────────┼────────────────────────────────
      ################################################## │ Сч.N.     │ #############################
                                                         │           │
      Плательщик                                         │           │
     ────────────────────────────────────────────────────┼───────────┼────────────────────────────────
      ################################################## │ БИК       │ #############################
      ################################################## ├───────────┤
                                                         │ Сч.N.     │ #############################
      Банк плательщика                                   │           │
     ────────────────────────────────────────────────────┼───────────┼────────────────────────────────
      ################################################## │ БИК       │ #############################
      ################################################## ├───────────┤
                                                         │ Сч.N.     │ #############################
      Банк получателя                                    │           │
     ──────────────────┬─────────────────────────────────┼───────────┤
      ИНН ############ │ КПП #########                   │ Сч.N.     │ #############################
     ──────────────────┴─────────────────────────────────┤           │
      ################################################## │           │
      ################################################## ├───────────┼─────────────┬──────────┬───────────
      ################################################## │  Вид оп.  │    ####     │Срок плат.│ ##########
      ################################################## ├───────────┤             ├──────────┤
                                                         │ Наз. пл.  │             │Очер плат.│ ##########
                                                         ├───────────┤             ├──────────┤
                                                         │Код        │#############│Рез. поле │
      Получатель                                         │           │#############│          │
     ─────────────────────┬─────────────┬────┬───────────┴┬──────────┴──────┬──────┴─────┬────┴──────────
      ####################│ ########### │ ## │ ########## │ ############### │ ########## │ ##
     ─────────────────────┴─────────────┴────┴────────────┴─────────────────┴────────────┴───────────────
      Назначение платежа:
      ###############################################################################################
      ###############################################################################################
      ###############################################################################################

     ─────────────────────────────────────────────────────────────────────────────────────────────────
                                               Подписи                        Отметки банка
                                                                              #####################
           М.П.
                                               ───────────────────────────────

                                               ───────────────────────────────


    #] //TAM 05.06.2012 I-00203428-2
    //Gurin S. 14.10.2014 I-00522071-2
    (IfThenElse((pr_pmrmprop.rec.PayerChargeOffDate != date(0,0,0)) and (pr_pmpaym.rec.PayerBankEnterDate >= pr_pmrmprop.rec.PayerChargeOffDate), pr_pmrmprop.rec.PayerChargeOffDate:f:c, pr_pmpaym.rec.PayerBankEnterDate:f:c), pr_pmrmprop.rec.PayerChargeOffDate:f:c, pr_pmrmprop.rec.Date:f:c, 
     GetPaymentKind(pr_pmrmprop.rec.PaymentKind):c, pr_pmrmprop.rec.Number:l,
     pr_pmrmprop.rec.TaxAuthorState,
     SS(0):l, SS(1):l, SS(2),
     Str0(pINN, IsBudg), Str0(pKPP, IsBudg), valAmount:l:f,  //DPN 25.11.2013 - I-00445667-3 
     SP(0), SP(1):l, SP(2):l, SP(3),
     PayerProps.Account:l,
     SBP(0):l, GetCodeBik(pr_pmpaym.rec.valuedate,PayerProps.BankBIC):l, SBP(1):l, PayerProps.BankCorrAccount:l,
     SBR(0):l, GetCodeBik(pr_pmpaym.rec.valuedate,ReceiverProps.BankBIC):l, SBR(1):l, 
     ReceiverProps.BankCorrAccount:l, Str0(rINN, IsBudg), Str0(rKPP, IsBudg), ReceiverProps.Account:l,
     SR(0), SR(1), SR(2),
     pr_pmrmprop.rec.ShifrOper:l, "", SR(3), pr_pmrmprop.rec.Priority:l, //Gurin S. 07.05.2015 R-579391-2
     IfThenElse(pr_pmrmprop.rec.Date < Date(31,3,2014), "", UIN(0)),
     IfThenElse(pr_pmrmprop.rec.Date < Date(31,3,2014), "", UIN(1)),
     IfThenElse(pr_pmrmprop.rec.TaxAuthorState, Str0(pr_pmrmprop.rec.BttTICode,   IsBudg), ""),
     IfThenElse(pr_pmrmprop.rec.TaxAuthorState, Str0(pr_pmrmprop.rec.OKATOCode,   IsBudg), ""),
     IfThenElse(pr_pmrmprop.rec.TaxAuthorState, Str0(pr_pmrmprop.rec.TaxPmGround, IsBudg), ""),
     IfThenElse(pr_pmrmprop.rec.TaxAuthorState, Str0(pr_pmrmprop.rec.TaxPmPeriod, IsBudg), ""),
     IfThenElse(pr_pmrmprop.rec.TaxAuthorState, Str0(pr_pmrmprop.rec.TaxPmNumber, IsBudg), ""),
     IfThenElse(pr_pmrmprop.rec.TaxAuthorState, Str0(pr_pmrmprop.rec.TaxPmDate,   IsBudg), ""),
     //Gurin S. 22.01.2015 I-00546582-2
     //IfThenElse(pr_pmrmprop.rec.TaxAuthorState, Str0(IfThenElse(((pr_pmrmprop.rec.Date >= date(01,01,2015)) and (pr_pmrmprop.rec.TaxPmType == "0")),"",pr_pmrmprop.rec.TaxPmType),   (IsBudg and (pr_pmrmprop.rec.Date < date(01,01,2015))) ), ""),/*zmp 12.01.2015 HF2031_19_25\124*/
     IfThenElse(pr_pmrmprop.rec.TaxAuthorState, Str0(pr_pmrmprop.rec.TaxPmType,   IsBudg), ""),
     SG(0):l, SG(1):l, SG(2):l, pr_pmpaym.rec.PayerBankMarkDate:f,
     ""/*terminator*/ // KS 16.11.2011 Разделитель
    );

    //DAI разрыв страницы только если действительно массовая печать
/*    if (ncopy > 1)*/
    /*SDA - или массовая печать в пакетном режиме */
    if ((ncopy > 1) or (IsOprMultiExec()))
      if ((pr_pmpaym.rec.dockind == 320) or (pr_pmpaym.rec.dockind == 322))
       MakeStamp(pr_pmpaym);
      end;
//SDA 05.03.2012  - два раза вставляется разрыв страницы
//    PrintEOF();  
      Println(terminator);
    end;

    ncopy = ncopy - 1;
  end;
  
  return TRUE;
  
END;

MACRO PrintPayReq(ncopy:integer):bool
/*  var isPrintEOF:bool;               // Печать символа окончания листа
  if( not GetParm( 1, isPrintEOF ) ) // по умолчанию печатать надо
    isPrintEOF = true;
  end;        */
  if( pr_PrintEA )
    PrintEAHeader();
  end;
  var stat = 0;

  if( pr_pmrmprop.rec.ShifrOper == "17" )
    stat = PrintBankOrder( ncopy );
  elif(is1256UPayment(pr_pmrmprop))
    stat = PrintPayReq1256(ncopy);
  else
    stat = PrintPayReqBefore1256(ncopy);
  end;
/*  if( isPrintEOF )
    PrintEOF();
  end;*/
  return stat;
END;
