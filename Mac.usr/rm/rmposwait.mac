//-----------------------------------------------------------------------------
// Блок     : 29050 - "Ожидание перепозиционирования"
// Шаг      : 10    - "Ожидание перепозиционирования"
// Описание : Макрос шага
//-----------------------------------------------------------------------------

import InsCarryDoc, PaymInter, PTInter, OprInter, BankInter, "rmtools.mac", "cbsttls.mac", "pmcarfun.mac";
import oralib; //Jushmanov 2014-02-25 C-19151

var PaymentObj:RsbPayment;


private macro IsUnfinAcc( FIID, Account ):bool
    var select = " select 1 " +
                   " from dmcaccdoc_dbt doc, dmccateg_dbt cat " +
                  " where doc.t_Chapter = 1 " +
                    " and doc.t_Currency = :FIID " +
                    " and doc.t_Account = :Account " +
                    " and doc.t_CatID  = cat.t_ID " +
                    " and cat.t_Number = 103 ";

    var params = makeArray( SQLParam( "FIID", FIID ), 
                            SQLParam( "Account", Account ) );
    var rs = execSQLselect( select, params, TRUE );

    if( rs and rs.moveNext() )
        return true;
    else
        return false;
    end;
end;


//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
macro ExecuteStep( doc, first )
    var rs:RsdRecordset;  
    record Corschem(corschem);

    if( НайтиКорсхему(PaymentObj.OutCorschem, PaymentObj.OutCorschemFIID, Corschem))
        MsgBox("Не найдена исходящая корсхема");
        return 1;
    end;

    if(ПолучитьВсеПроводки(PaymentObj.PaymentID,@rs, 1/*CHAPT1*/) and rs.moveLast())
        /* Если в последней проводке счет кредита (дебета для дебетового платежа) равен корсчету из исходящей корсхемы платежа 
           или является счетом СНР (имеет связь с категорией учета СНР)*/
        if( ( (PaymentObj.DbFlag == UNSET_CHAR) and ( (rs.value("t_ReceiverAccount") == Corschem.Account) or 
                                                      ( IsUnfinAcc( rs.value("t_ReceiverFIID"),rs.value("t_ReceiverAccount") ) ) )
            ) OR
            ( (PaymentObj.DbFlag == SET_CHAR)   and ( (rs.value("t_PayerAccount")    == Corschem.Account) or 
                                                      ( IsUnfinAcc( rs.value("t_PayerFIID"),rs.value("t_PayerAccount") ) ) ) 
            )
          )
            if(УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_DISCHARGE))
                MsgBox( "Возникла ошибка при смене статусов операции платежа" );
                return 1;
            end;
        else
            PaymentObj.ValueDate = ValueDateForRmPosition(PaymentObj, Corschem);
            SetOprDate(29000000, PaymentObj.ValueDate);
            if(УстановитьСтатусыПлатежа( OPR_PAYM_DO, OPR_PM_ST_ENTER))
                MsgBox( "Возникла ошибка при смене статусов операции платежа" );
                return 1;
            end;
        end;
    end;

    return 0;
end;


//-----------------------------------------------------------------------------
// Постобработка шага
//-----------------------------------------------------------------------------
//Jushmanov 2014-02-25 C-19151
macro PostStepAction( message,      /* 1 - выполнение шага; 2 - откат шага;   */
                      errTrn,       /* статус выполнения шага. 0 - ошибки нет */
                      FirstDoc,     /* указатель на первичный документ        */
                      ID_Oper,      /* внутренний идентификатор операции      */
                      Number_Step,  /* Номер шага операции (из настроек)      */
                      KindOper,     /* номер вида операции                    */
                      KindDoc,      /* номер вида первичного документа        */
                      KindStep,     /* вид шага операции                      */
                      ID_Step )     /* внутренний идентификатор шага операции */
    private var logquery, idStep = ID_Step, idOper = ID_Oper;

    if(( message == OP_EXECUTE_STEP ) and (errTrn == 0) and (IsOprMultiExec))
        if ((valtype(idOper) != V_INTEGER) and (valtype(idOper) != V_STRING)) idOper = 0; end;
        if ((valtype(idStep) != V_INTEGER) and (valtype(idStep) != V_STRING)) idStep = 0; end;

        logquery = "UPDATE usr_doprstep_robot_dbt SET t_is_robot_step = chr(88)" +
                   " WHERE t_id_operation = " + idOper + " AND t_id_step = " + idStep;
        ExecSQL(logquery, null, false);
    end;

    return 0;
end;