//-----------------------------------------------------------------------------
// Блок      : 29027 - "Картотека ЛОРО"
// Шаг       : 135   - "Средств достаточно?"
// Назначение: Макрос шага
// Описание  : Макрос шага
//-----------------------------------------------------------------------------
import BankInter, OprInter, CurrInter, WldInter, "rmtools.mac", "cbsttls.mac", pm_common;
import oralib; //Jushmanov 2014-02-24 C-19151

RECORD Corschem( corschem );
var PaymentObj:RsbPayment;

//-----------------------------------------------------------------------------
// Выполнение шага
//-----------------------------------------------------------------------------
macro ExecuteCaseStep( Kind_Operation, Number_Step, first, KindDoc )

    var AccLORO  :string  = PaymentObj.FuturePayerAccount;
    var ChaptLORO:integer = 1;
    var SkipArest;

    RECORD oprstep("oprstep");
    StepInfoEx(Number_Step, oprstep);

    if(CheckDateStartOpr(oprstep.ID_Operation))
        return 1;
    end;

    // Установить дату начала операции равной дате операционного дня пользователя
    SetOprDate(29000000, {curdate});
    PaymentObj.ValueDate = PM_GetOperDay_Balance(PaymentObj.Department);

    if( GetOprStatus(OPR_PAYM_PERMISSION) == OPR_PAYM_ST_PERMISSION_YES )
        SkipArest = true;
    else
        SkipArest = false;
    end;

    if( PaymentObj.CheckRest(AccLORO, ChaptLORO, Corschem.FIID, PaymentObj.FuturePayerAmount, {curdate},
                             false, false, false, false, SkipArest) 
      )
        msgbox("Недостаточно средств на счете ЛОРО");
        return 1;
    elif( UnpaidDocIsExist(PaymentObj.FuturePayerAccount, PaymentObj.Priority, false) ) 
        msgbox("В картотеке ЛОРО есть платежи с более высоким приоритетом");
        return 1;
    end;

    return string( НомерШагаСписаниеСВнебаланса );
end;


//Jushmanov 2014-02-24 C-19151
macro PostStepAction( message,      /* 1 - выполнение шага; 2 - откат шага;   */
                      errTrn,       /* статус выполнения шага. 0 - ошибки нет */
                      FirstDoc,     /* указатель на первичный документ        */
                      ID_Oper,      /* внутренний идентификатор операции      */
                      Number_Step,  /* Номер шага операции (из настроек)      */
                      KindOper,     /* номер вида операции                    */
                      KindDoc,      /* номер вида первичного документа        */
                      KindStep,     /* вид шага операции                      */
                      ID_Step )     /* внутренний идентификатор шага операции */
    private var logquery, idStep = ID_Step, idOper = ID_Oper;

    if(( message == OP_EXECUTE_STEP ) and (errTrn == 0) and (IsOprMultiExec))
        if ((valtype(idOper) != V_INTEGER) and (valtype(idOper) != V_STRING)) idOper = 0; end;
        if ((valtype(idStep) != V_INTEGER) and (valtype(idStep) != V_STRING)) idStep = 0; end;

        logquery = "UPDATE usr_doprstep_robot_dbt SET t_is_robot_step = chr(88)" +
                   " WHERE t_id_operation = " + idOper + " AND t_id_step = " + idStep;
        ExecSQL(logquery, null, false);
    end;

    return 0;
end;