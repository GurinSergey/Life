// -------------------------------------------------------------------------------------------------
// @filename: xr_category.mac v.1
// @author  : 2013-05-01 zip_z. 
// @desc    : ТК Life - категории
// @changes : 05.12.2014 Chesnokov D.S. R-504788 Исправил запрос, нельзя ставить CHR(0), а так же
//                       при повторной установке признака "основной" он не должен сбрасываться
// -------------------------------------------------------------------------------------------------
import ctinter;
import soa_core;
import soa_client_checkdata;

Class SOACategory()
  var GroupId:  integer;
  var AttrId:   integer;
  var AttrCode: string;
  var General:  integer;

  macro Init(v_groupid, v_attrid, v_attrcode, v_general)
    if ((v_attrid == 0) and (v_attrcode != V_UNDEF)) v_attrid = this.getAttrID(v_groupid, v_attrcode, 501); end;

    this.GroupId  = v_groupid;
    this.AttrId   = v_attrid;
    this.AttrCode = v_attrcode;
    this.General  = v_general;
  end;

  macro getAttrID(v_groupid, v_codelist, v_objecttype)
    var sql   = "SELECT attr.t_attrid attr FROM dobjattr_dbt attr WHERE attr.t_objecttype = :m_obj AND attr.t_groupid = :m_group AND attr.t_codelist = :m_code";
    var param = makeArray (SqlParam ("m_obj"  , v_objecttype )
                          ,SqlParam ("m_group", v_groupid    )
                          ,SqlParam ("m_code" , v_codelist   )
                          );

    sql = execSqlSelect (sql, param, false);
    if (sql.MoveNext())
      return sql.Value("attr", null, V_INTEGER);
    end;
  return 0;
  end;
end;

PRIVATE macro set_mainattr (p_objecttype, p_objectid, p_groupid, p_attrid, p_uniobjectid, p_general)
    var sql = "select 1 from dobjgroup_dbt " + 
              " where t_objecttype = :m_objecttype and t_groupid = :m_groupid and t_type = chr (88)";
    var param = makeArray (SqlParam ("m_objecttype", p_objecttype )
                          ,SqlParam ("m_groupid"   , p_groupid    )
                          );
    sql = execSqlSelect (sql, param, false);
    // категория исключительная - установка главной (основной) категории не требуется
    if (sql.moveNext ()) return; end;
    
    // категория неисключительная - проверяем, есть ли главная (основная) категория
    sql   = "select count ( 1 ) n from dobjatcor_dbt where t_objecttype = :m_objecttype " + 
            "   and t_groupid = :m_groupid and t_object = :m_object and t_general = chr (88)";
    param = makeArray (SqlParam ("m_objecttype", p_objecttype  )
                      ,SqlParam ("m_groupid"   , p_groupid     )
                      ,SqlParam ("m_object"    , p_uniobjectid )
                      );
    sql   = execSqlSelect (sql, param, false);
    if ((sql.moveNext ()) and (sql.value ("n", null, V_INTEGER) == 0))
        // нет основной категории, проставляем :)
        sql = "update dobjatcor_dbt set t_general = chr (88) where t_objecttype = :m_objecttype "
              "   and t_groupid = :m_groupid and t_object = :m_object and t_attrid = :m_attrid";
        param = makeArray (SqlParam ("m_objecttype", p_objecttype  )
                          ,SqlParam ("m_groupid"   , p_groupid     )
                          ,SqlParam ("m_object"    , p_uniobjectid )
                          ,SqlParam ("m_attrid"    , p_attrid      )
                          );
        execSql (sql, param, false);
    end;
    /*VDN 28.10.2014 С-33844 Простановка основной категории*/
    /*05.12.2014 Chesnokov D.S. R-504788 Исправил запрос, нельзя ставить CHR(0), а так же при     */
    /*                          повторной установке признака "основной" он не должен сбрасываться */
    if (p_general)
        sql = "UPDATE (SELECT t_general, t_oper, t_sysdate, t_systime, t_attrid "+
              "        FROM dobjatcor_dbt "+
              "        WHERE t_objecttype = :m_objecttype AND t_groupid = :m_groupid AND t_object = :m_object "+
              "          AND (t_general = CHR (88) OR t_attrid = :m_attrid0)) "+
              "SET t_general   = (CASE WHEN ((t_general = CHR (88)) and (t_attrid = :m_attrid1)) "+
              "                          THEN CHR (88) "+
              "                        WHEN ((t_general = CHR (88)) and (t_attrid != :m_attrid2)) "+
              "                          THEN CHR (0) "+
              "                        ELSE CHR (88) END), "+
              "    t_oper      = rsbsessiondata.oper, "+
              "    t_sysdate   = TRUNC (SYSDATE), "+
              "    t_systime   = TO_DATE ('01.01.0001 ' || TO_CHAR (SYSDATE, 'hh24:mi:ss'), 'dd.mm.yyyy hh24:mi:ss')";
        param = makeArray (SqlParam ("m_objecttype", p_objecttype  )
                          ,SqlParam ("m_groupid"   , p_groupid     )
                          ,SqlParam ("m_object"    , p_uniobjectid )
                          ,SqlParam ("m_attrid0"   , p_attrid      )
                          ,SqlParam ("m_attrid1"   , p_attrid      )
                          ,SqlParam ("m_attrid2"   , p_attrid      )
                          );
        execSql (sql, param, false);
    end;                                
end;

// @desc: сервис установки категории
// DONE
macro usr_set_category (p_objecttype:integer, p_objectid:string, v_cat:object):bool
    // Vaschenko D. обнуляем опера
    initSessionData();
                        
    var uid  = makeFastUniId (p_objecttype, p_objectid);
    // var cat  = RsbObjCategories (p_objecttype, uid);
    var stat = true;
    // cat.DisconnectAttr (v_cat.groupid,0);
    
    if (v_cat.attrid == 0)
        var sql = "select t_attrid from dobjattr_dbt where t_objecttype = :m_objecttype and t_groupid = :m_groupid and t_nameobject = :m_attrcode";
        sql = execSqlSelect (sql, makeArray (SqlParam ("m_objecttype", p_objecttype   )
                                            ,SqlParam ("m_groupid"   , v_cat.GroupID  )
                                            ,SqlParam ("m_attrcode"  , v_cat.AttrCode )));
        if (sql.moveNext ())
            v_cat.attrid = sql.value ("t_attrid", null, V_INTEGER);
        end;
    end;

/*    
    if (cat.ConnectAttr(v_cat.groupid, v_cat.attrid, null, null, {curdate}) == EXIT_SUCCESS) 
        stat = cat.Save (uid); 
        set_mainattr (p_ObjectType, p_ObjectID, v_cat.GroupID, v_cat.AttrID, uid);
    end;
*/    

    // Golovkin 24.09.2014 R-456372 почему-то cat.ConnectAttr не отработал как надо
    ConnectObjAttr(stat,p_objecttype,uid,v_cat.groupid,v_cat.attrid,null,null,{curdate});

    if( stat == EXIT_SUCCESS )
        set_mainattr (p_ObjectType, p_ObjectID, v_cat.GroupID, v_cat.AttrID, uid, v_cat.general);
    end;

    if( stat == EXIT_SUCCESS )
        return true;
    else
       /*VDN 10.04.2015 R-567769*/ 
        runError("Категория: " + v_cat.GroupID + ", Атрибут: " + v_cat.AttrID + getErrMsg);
    end;

//    return (stat==EXIT_SUCCESS);
// пользовательский перехват исключений
onerror (e) catchUserError (e);
end;

// @desc: сервис удаления категории
// DONE
macro usr_del_category (p_objecttype:integer, p_objectid:string, c:object):bool
    // Vaschenko D. обнуляем опера
    initSessionData();

    var uid  = makeFastUniId (p_objecttype, p_objectid);
    var cat  = RsbObjCategories (p_objecttype, uid);

    //VDN надо искать ид признака по его значению, иначе постоянно все удаляет
    if ((c.attrcode) and (c.attrid == 0))
      var sql = "select t_attrid from dobjattr_dbt where t_objecttype = :m_objecttype and t_groupid = :m_groupid and t_nameobject = :m_attrcode";
      sql = execSqlSelect (sql, makeArray (SqlParam ("m_objecttype", p_objecttype)
                                          ,SqlParam ("m_groupid"   , c.GroupID   )
                                          ,SqlParam ("m_attrcode"  , c.AttrCode  )));
      if (sql.moveNext ()) 
        c.attrid = sql.value ("t_attrid", null, V_INTEGER);
      else
        runError("не найдена категория по значению " + c.attrcode);
      end;
    end;   

    // если DisconnectAttr () а затем Save () возвратят EXIT_SUCCESS, вылетит true
    return ((cat.DisconnectAttr (c.groupid, c.attrid) == EXIT_SUCCESS) and (cat.Save(uid) == EXIT_SUCCESS));

// пользовательский перехват исключений
onerror (e) catchUserError (e);
end;

/*VDN установка категории для платежа, в рамках проекта интеграции ИК*/
macro usr_set_category_payment( p:object )
  var rs, AttrCode = "", attrID = 0, p_objecttype = 501, stat;
  var v_cat = SOACategory();

  if ((p.doc_kind == 2011) or (p.doc_kind == 202))
    /*VDN категория Код страны нерезидента-отправителя*/
    stat = isNotResident(p.payer_inn, 16, AttrCode);
    if (stat == 1) RunError("Ошибка при поиске кода страны нерезидента-отправителя"); end;
    if (AttrCode != "") v_cat.Init(118, attrID, AttrCode, 0); usr_set_category (p_objecttype, p.payment_id, v_cat); end;

    if(p.doc_kind == 202) 
      /*VDN категория Код страны банка нерезидента*/ 
      stat = isNotResident(p.receiver_bank_bic, 3, AttrCode);
      if (stat == 1) RunError("Ошибка при поиске кода банка нерезидента"); end;
      if (AttrCode != "") v_cat.Init(117, attrID, AttrCode, 0); usr_set_category (p_objecttype, p.payment_id, v_cat); end;

      /*VDN категория Код страны нерезидента-получателя*/  
      stat = isNotResident(p.receiver_inn, 16, AttrCode);
      if (stat == 1) RunError("Ошибка при поиске кода страны нерезидента-получателя"); end;
      AttrCode = trim(p.userfield2);
      if (AttrCode != "") v_cat.Init(119, attrID, AttrCode, 0); usr_set_category (p_objecttype, p.payment_id, v_cat); end;     
  
      /*VDN категория Перечень видов услуг для 402ф*/
      if (ValType(p.serviceKindCode) != 26)
        v_cat.Init(3, attrID, p.serviceKindCode, 0); 
        usr_set_category (p_objecttype, p.payment_id, v_cat);
      end;
    end;
  end;

  if (((p.doc_kind == 2001)  or
       (p.doc_kind == 2002)  or
       (p.doc_kind == 2003)) and
       (p.ratetype == 1100))
    /*VDN категория Автоматическое проведение = ДА*/
    v_cat.Init(1003, attrID, 2, 0); 
    usr_set_category (p_objecttype, p.payment_id, v_cat);
  end;
  return "no_error"; 
end;
