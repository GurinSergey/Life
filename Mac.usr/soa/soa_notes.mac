// -------------------------------------------------------------------------------------------------
// @filename: soa_note.mac v.3
// @author  : 2013-05-01 zip_z. 
// @desc    : ТК Life - примечания
// @changes : none
// -------------------------------------------------------------------------------------------------
import bankinter;
import soa_core;

// @desc  : разбор форматной строки значения примечания для объекта системы. 
// @param :  p_objecttype - IN    V_INTEGER - тип объекта
//           p_notekind   - IN    V_INTEGER - вид примечания
// @return: значение примечения в соответствии с типом данных согласно системному справочнику ГК
private macro parseFormatString ( p_objecttype:integer, p_notekind:integer, p_notevalue:string ):variant
    var ret = p_notevalue;
    var sql = "SELECT m_note_valtype FROM usr_vw_soa_notes_chk where m_object_type = :m_object_type and m_note_kind = :m_note_kind";
    sql = execSqlSelect (sql, makeArray (SQLParam ("m_object_type", p_objecttype), SQLParam ("m_note_kind", p_notekind)), true);
    if (sql.moveNext ())
        var type = trim (sql.value ("m_note_valtype", null, V_STRING));
        if   (type == "V_STRING" ) ret = string (p_notevalue);
        elif (type == "V_DOUBLE" ) ret = double (p_notevalue);
        elif (type == "V_MONEY"  ) ret = money (p_notevalue);
        elif (type == "V_BOOL"   ) ret = (strlwr (trim (p_notevalue)) == "true");
        elif (type == "V_INTEGER") ret = int (p_notevalue);
        elif (type == "V_DATE"   ) ret = date (p_notevalue);
        else
            runError ("Добавление вида примечания " + p_notekind + " к объекту системы " + p_objecttype + " не поддерживается");
        end;
    else // вид примечания не существует
        runError ("Вид примечания " + p_notekind + " к объекту системы " + p_objecttype + " не существует");
    end;
    return ret;
end;

// @desc  : существует ли вид примечания p_notekind для типа объекта p_objecttype ?
// @return: V_BOOL (true, если существует)
private macro checkExistsNote ( p_objecttype:integer, p_notekind:integer ):bool
    var sql = "SELECT 1 FROM usr_vw_soa_notes_chk where m_object_type = :m_object_type and m_note_kind = :m_note_kind";
    sql = execSqlSelect (sql, makeArray (SQLParam ("m_object_type", p_objecttype), SQLParam ("m_note_kind", p_notekind)), true);
    return (sql.moveNext ());
end;

// @desc  : установка примечания для объекта системы
// @return: V_INTEGER (идентификатор созданного примечания) или V_BOOL (false), если возникли ошибки
//          в ходе работы AddNoteForObject (). В случае невалидности параметров выбрасывается runError ()
macro usr_set_note (p_objecttype:integer // тип объекта  
                   ,p_objectid  :string  // идентификатор объекта
                   ,note        :object  // описание примечаний
                   ):variant
    // Vaschenko D. обнуляем опера
    initSessionData();

    var noteKind  = safeGetProp (note, "notekind", V_INTEGER);
    var noteValue = safeGetProp (note, "notevalue", V_STRING);
    
    // проверка входящих параметров
    if (isNull (p_objecttype )) runError ("Не задан обязательный параметр objectType (тип объекта)");          end;
    if (isNull (p_objectid   )) runError ("Не задан обязательный параметр objectId (идентификатор объекта)");  end;
    if (isnull (noteKind     )) runError ("Не задан обязательный параметр noteKind (вид примечания)");         end;
    if (isnull (noteValue    )) runError ("Не задан обязательный параметр noteValue (значение примечания)");   end;
    
    // т.к. значение noteValue всегда прилетает в string (ТЗ!), а в БД мы можем хранить примечания разных
    // типов, выполняем разбор строки и приведение типов
    noteValue = parseFormatString (p_objecttype, notekind, noteValue);
    //TAM 10.04.2014 C-28570
    if ((p_objecttype == 501/*Платеж*/) and (noteKind == 53))
        ExecMacroFile("lib_pm_check.mac", "SetPaymentUIN", p_objectid, noteValue);
        return true;
    else
        // проверки пройдены
        p_objectid = makeFastUniId ( p_objecttype, p_objectid );
    
        var stat = addNoteForObject (p_objecttype, p_objectid, notekind, notevalue, {curdate});
        if (EXIT_SUCCESS != stat)
            return false;
        end;

        var sql = "select max (t_id) t_noteid from dnotetext_dbt where t_objecttype = :m_objecttype and t_documentid = :m_objectid and t_notekind = :m_notekind";
        sql = execSqlSelect (sql, makeArray (SqlParam ("m_objecttype", p_objecttype)
                                            ,SqlParam ("m_objectid"  , p_objectid  )
                                            ,SqlParam ("m_notekind"  , notekind    )), false);
        if (sql.moveNext ()) 
            return sql.value ("t_noteid", null, V_INTEGER);
        end;
        return false;
    end;
end;

// @desc: удаление примечания для объекта системы
// DONE
macro usr_delete_note (p_objecttype      :integer // тип объекта
                      ,p_objectid        :string  // идентификатор объекта
                      ,p_notekind        :integer // вид примечания
                      ):bool
    // Vaschenko D. обнуляем опера
    initSessionData();

    if (not checkExistsNote (p_objecttype, p_notekind))
        runError ("Вид примечания " + p_notekind + " к объекту системы " + p_objecttype + " не существует");
    end;
    return RemoveNoteForObject (p_objecttype, makeFastUniId (p_objecttype, p_objectid), p_notekind);
end;