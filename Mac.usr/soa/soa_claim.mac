// -------------------------------------------------------------------------------------------------
// @filename: xr_claim.mac v.1
// @author  : 2013-05-01 zip_z. 
// @desc    : ТК Life - претензии
// @changes : none
// -------------------------------------------------------------------------------------------------

import soa_core;

// @desc: вставка претензии
// DONE
//LAO переделал с использованием объекта, так как опер и комментарий не обязательный параметр.
macro usr_insert_claim (p_account         :string
                        ,p_sum            :money
                        ,p_comment        :STRING
                        ,p_oper           :INTEGER
                       )
   
    // дозаполнение параметров по умолчанию
    p_oper    = nvl_ex (p_oper, {oper});
    p_comment = nvl_ex (p_comment, "Блокировка остатка инициирована внешней системой");
    
    // транзакция вставки претензии
     var cmd = RSDCommand("begin usr_claims.insert_claim (?, ?, ?, ?, ?); end;");
    cmd.addParam ("m_account", RSDBP_IN , p_account );
    cmd.addParam ("m_oper"   , RSDBP_IN , p_oper    );
    cmd.addParam ("m_comment", RSDBP_IN , p_comment );
    cmd.addParam ("m_sum"    , RSDBP_IN , p_sum     );
    cmd.addParam ("m_error"  , RSDBP_OUT, V_STRING, 1024);
    cmd.execute();
    
    if (cmd.param("m_error").value == EXIT_NOERROR)
        var sql = "select t_claimid"         + "\n" +
                  "from ( select t_claimid"  + "\n" +
                  "       from dacclaim_dbt" + "\n" +
                  "       where t_account = :m_account and t_startamount = :m_startamount " + "\n" + 
                  "         and t_claimkind = 3 /*ACCLAIM_KIND_RESERVE*/ and t_restkind = 3 /*ACCLAIM_TYPE_AMOUNT*/ and t_initiator = 5 /*ACCLAIM_INIT_BANK*/ " + "\n" +
                  "       order by t_claimid desc )" + "\n" +
                  "where rownum = 1";
        sql = execSqlSelect (sql, makeArray (SqlParam ("m_account", p_account), SqlParam ("m_startamount", p_sum)));
        if (sql.moveNext ()) 
            return (sql.value ("t_claimid", null, V_INTEGER));
        end;
    else
        runError (cmd.param("m_error").value); //LAO возвращаем нормальную ошибку
    end;
// пользовательский перехват исключений
onerror (e) catchUserError (e);    
end;

// @desc: удаление претензии
// DONE
macro usr_delete_claim (p_account         :string
                       ,p_sum             :money
                       ,p_comment         :string
                       )
    
    // дозаполнение параметров по умолчанию //LAO переписал комментарий, должен быть вот этот по умолчанию.
    p_comment = nvl_ex (p_comment, "Блокировка остатка инициирована внешней системой");
    
    // транзакция удаления претензии
    var cmd = RSDCommand("begin usr_claims.delete_claim (?, ?, ?, ?); end;");
    
    cmd.addParam ("m_account", RSDBP_IN , p_account );
    cmd.addParam ("m_sum"    , RSDBP_IN , p_sum     );
    cmd.addParam ("m_comment", RSDBP_IN , p_comment );
    cmd.addParam ("m_error"  , RSDBP_OUT, V_STRING, 1024);
    cmd.execute();
    if  (cmd.param("m_error").value == EXIT_NOERROR)
     return true
    else 
     runError (cmd.param("m_error").value); //LAO 
    end;
  
// пользовательский перехват исключений
onerror (e) catchUserError (e);    
end;
