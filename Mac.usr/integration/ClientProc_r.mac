/******************************************************************************
   Имя файла.:       ClientProc.mac
   Назначение:       Реализация интерфейса "Клиент"
   Дата......:       22.08.2008
   Автор.....:       Стадник Александр
******************************************************************************/
import PTInter, globals, "CommonInt.mac", "ProcInit.inc", "LogProc.mac", "set_rscode.mac";
import BankInter;                             

//Создание клиента
macro CreateClient(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client = RsbParty();       
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);


    Client.LegalForm = int(ArrayFields(crtClientFldLegalForm));
    
    if (Client.LegalForm == 1)
        Client.ShortName  = ArrayFields(crtClientFldShortName);
        Client.FullName   = ArrayFields(crtClientFldFullName);                

        Client.CharterDate    = date(ArrayFields(crtClientFldCharterDate));
        Client.CapitalFI      = ArrayFields(crtClientFldCapitalFI);
        Client.DeclareCapital = strsubst(ArrayFields(updClientFldDeclareCapital), ",",".");
        Client.RealCapital    = strsubst(ArrayFields(updClientFldRealCapital),    ",",".");
        Client.OKPO      = ArrayFields(crtClientFldOKPO);    

    else
        Client.ShortName  = ArrayFields(crtClientFldShortName);
//        Client.ShortName  = ArrayFields(crtClientFldLastName);
        Client.FirstName  = ArrayFields(crtClientFldFirstName);
        Client.Patronymic = ArrayFields(crtClientFldSecondName);
        Client.LastName   = ArrayFields(crtClientFldLastName);
        Client.FullName   = ArrayFields(crtClientFldFullName);
//        Client.FullName   = Client.LastName + " " + Client.FirstName + " " + Client.Patronymic; 
        Client.BirthDate  = date(ArrayFields(crtClientFldBirthDate));
        Client.BirthPlace = ArrayFields(crtClientFldBirthPlace);
        Client.IsMale     = (ArrayFields(crtClientFldIsMale) == "X");
        Client.Ethnos     = ArrayFields(crtClientFldNationality);
        Client.IsEmployer = (ArrayFields(crtClientFldIsEmployer) == "X");
        Client.PlaceWork  = ArrayFields(crtClientFldWorkPlace);                        
        if (Client.IsEmployer)
          Client.OKPO      = ArrayFields(crtClientFldOKPO);
        end;
    end;
    
    Client.AddName        = ArrayFields(crtClientFldAddName);
    Client.NRCountry      = ArrayFields(crtClientFldCountry);    
    Client.SuperiorID     = int(ArrayFields(crtClientFldSuperiorID));
    
    if (strlen(Client.NRCountry) == 0)
        Client.NRCountry = "RUS";
    end;
    
    var imode = SetDialogFlag(0);
    stat = Client.Update();
    SetDialogFlag(imode);

    if (stat == true)             
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при создании клиента. " + GetErrMsg();    
    end;
    
    LogCreateClient(Str, ArrayFields, Client.PartyID, ErrorMessage);
 
    return string(Client.PartyID) + chrDelimiter + ErrorMessage;
end;

  

// Обновление основных свойств клиента
macro UpdateClient(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, PartyID;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);
    
    PartyID = int(ArrayFields(updClientFldPartyID));
    
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    Client = RsbParty(PartyID);
    
    if (Client.LegalForm == 1)
        Assign(Client.ShortName, ArrayFields(updClientFldShortName));
        Assign(Client.FullName, ArrayFields(updClientFldFullName));

        Assign(Client.CharterDate,    date(ArrayFields(updClientFldCharterDate)));
        Assign(Client.CapitalFI,      ArrayFields(updClientFldCapitalFI));
        Assign(Client.DeclareCapital, strsubst(ArrayFields(updClientFldDeclareCapital), ",","."));
        Assign(Client.RealCapital,    strsubst(ArrayFields(updClientFldRealCapital),    ",","."));


    else
        Assign(Client.ShortName,  ArrayFields(updClientFldLastName));
        Assign(Client.FirstName,  ArrayFields(updClientFldFirstName));
        Assign(Client.Patronymic, ArrayFields(updClientFldSecondName));
        Assign(Client.LastName,   ArrayFields(updClientFldLastName));

        Assign(Client.ShortName, ArrayFields(updClientFldShortName));
        Assign(Client.FullName,  ArrayFields(updClientFldFullName));

        //Client.FullName = Client.LastName + " " + Client.FirstName + " " + Client.Patronymic; 
        Assign(Client.BirthDate,  date(ArrayFields(updClientFldBirthDate)));
        Assign(Client.BirthPlace, ArrayFields(updClientFldBirthPlace));
        Assign(Client.IsMale,     ArrayFields(updClientFldIsMale));
        Assign(Client.Ethnos,     ArrayFields(updClientFldNationality));
        Assign(Client.IsEmployer, ArrayFields(updClientFldIsEmployer));
        Assign(Client.PlaceWork,  ArrayFields(updClientFldWorkPlace));                
    end;
    
    Assign(Client.AddName,    ArrayFields(updClientFldAddName));
    Assign(Client.OKPO,       ArrayFields(updClientFldOKPO));
    Assign(Client.NRCountry,  ArrayFields(updClientFldCountry));  
    Assign(Client.SuperiorID, int(ArrayFields(updClientFldSuperiorID)));  
          

    var imode = SetDialogFlag(0);
    stat = Client.Update();
    SetDialogFlag(imode);

    if (stat == true)        
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при обновлении клиента. " + GetErrMsg();    
    end;
   
    LogUpdateClient(Str, ArrayFields, ErrorMessage);
        
    return ErrorMessage;
end;


// Удаление клиента
macro DeleteClient(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, PartyID;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);
    
    PartyID = int(ArrayFields(delClientFldPartyID));
    
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    Client = RsbParty(PartyID);
                
    stat = Client.Lock();

    if (stat == true)                
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при удалении клиента. " + GetErrMsg();    
    end;
   
    LogDeleteClient(Str, ArrayFields, ErrorMessage);
        
    return ErrorMessage;
end;
                                                         

// Постановка клиента на обслуживание
macro SetClientService(Str : string)
    var stat;
    var ErrorMessage = "";
    var PartyID, ServKind, Oper, StartDate, Department, Branch;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);
    
    PartyID    = int(ArrayFields(setClientServiceFldPartyID));
    ServKind   = int(ArrayFields(setClientServiceFldServKind));
    Oper       = int(ArrayFields(setClientServiceFldOper));
    StartDate  = date(ArrayFields(setClientServiceFldStartDate));
    Department = int(ArrayFields(setClientServiceFldDepartment));

    //I-012048
    if (ArrayFields(setClientServiceFldBranch) != "")
       Branch     = int(ArrayFields(setClientServiceFldBranch));
    else
       Branch     = Department;
    end;
    
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    if (CheckServKind(ServKind, ErrorMessage) == false)        
        return ErrorMessage;
    end;    

    var imode = SetDialogFlag(0);
    stat = PT_BindClientWithBranch(PartyID, ServKind, Branch, Oper, StartDate);
    SetDialogFlag(imode);
	
    if (not stat)
        ErrorMessage = "Ошибка при постановке клиента на обслуживание. " + GetErrMsg();	    
    else
        ErrorMessage = no_error_msg;
    end;
	
    LogSetClientService(Str, ArrayFields, ErrorMessage);
	    
    return ErrorMessage;	
end;


// Снятие клиента с обслуживания
macro DelClientService(Str : string)
    var stat;
    var ErrorMessage = "";
    var PartyID, ServKind;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);
    
    PartyID = int(ArrayFields(delClientServiceFldPartyID));
    ServKind  = int(ArrayFields(delClientServiceFldServKind));
        
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    if (CheckServKind(ServKind, ErrorMessage) == false)        
        return ErrorMessage;
    end;    

    var imode = SetDialogFlag(0);    
    stat = PT_CloseClientService(PartyID, ServKind, {curdate}, true);
    SetDialogFlag(imode);	

    if (not stat)
        ErrorMessage = "Ошибка при снятии клиента с обслуживания. " + GetErrMsg();	    
    else
        ErrorMessage = no_error_msg;
    end;	
	
    LogDelClientService(Str, ArrayFields, ErrorMessage);
    
    return ErrorMessage;	
end;



// Установка кода клиента
macro SetClientCode(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, ClientCode;
    var PartyID, CodeKind, CodeValue;
    var ArrayFields = TArray;

    private var rs;
    
    ParseStringToArray(Str, ArrayFields);
    
    PartyID   = int(ArrayFields(setClientCodeFldPartyID));
    CodeKind  = int(ArrayFields(setClientCodeFldCodeKind));
    CodeValue = ArrayFields(setClientCodeFldCodeValue);
        
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;

    Client = RsbParty(PartyID);    

    if ((CodeKind == 1)  and ((valtype(CodeValue) == V_UNDEF) or (CodeValue == "")))
       if (SetRScode(Client))
          ClientCode = Client.Code;
          ErrorMessage = no_error_msg+chrDelimiter+Client.Code(1);
       else
          ErrorMessage = GetErrMsg(0)+chrDelimiter;
       end;
    else
       ClientCode = Client.Code;
       
          ClientCode.CloseCode(CodeKind); //Закрывем старый код

       stat = ClientCode.SetCode(CodeKind, CodeValue);    

       if (stat == true)
           stat = Client.Update();
       end;

       if (stat == true)        
           ErrorMessage = no_error_msg;
       else
           rs = RSDRecordset("select t_name from DOBJKCODE_DBT where T_OBJECTTYPE = 3 and t_codekind = "+CodeKind);
           if (rs.movenext)
              ErrorMessage = "Ошибка при создании кода клиента\n вид кода:"+CodeKind+" значение:"+ CodeValue+" "+GetErrMsg();    
           else
              ErrorMessage = "Ошибка при создании кода клиента\n код вида "+CodeKind+" не найден";    
           end;

       end;
    end;
    LogSetClientCode(Str, ArrayFields, ErrorMessage);
        
    return ErrorMessage;
end;



// Удаление кода клиента
macro DelClientCode(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, ClientCode;
    var PartyID, CodeKind;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);
    
    PartyID   = int(ArrayFields(delClientCodeFldPartyID));
    CodeKind  = int(ArrayFields(delClientCodeFldCodeKind));    
        
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    Client     = RsbParty(PartyID);
    ClientCode = Client.Code;
    
    stat = ClientCode.DeleteCode(CodeKind);    
    
    if (stat == true)
        stat = Client.Update();
    end;

    if (stat == true)        
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при удалении кода клиента. " + GetErrMsg();    
    end;
        
    LogDelClientCode(Str, ArrayFields, ErrorMessage);
    
    return ErrorMessage;
end;



// Добавление адреса клиента
macro SetClientAddress(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, ClientAddress;
    var PartyID, AddressType;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);
    
    PartyID     = int(ArrayFields(setClientAddressFldPartyID));    
    AddressType = int(ArrayFields(setClientAddressFldAddressType));    
        
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    Client        = RsbParty(PartyID);
    ClientAddress = Client.Address(AddressType);
    
    ClientAddress.Country       = ArrayFields(setClientAddressFldCountry);
    ClientAddress.Address       = ArrayFields(setClientAddressFldAddress);
    ClientAddress.PhoneNumber   = ArrayFields(setClientAddressFldPhoneNumber);
    ClientAddress.PhoneNumberAd = ArrayFields(setClientAddressFldPhoneNumberAd);
    ClientAddress.FaxNumber     = ArrayFields(setClientAddressFldFaxNumber);
    ClientAddress.EMail         = ArrayFields(setClientAddressFldEMail);
    
    
    stat = Client.Update();    

    if (stat == true)        
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при создании адреса клиента. " + GetErrMsg();    
    end;
   
    LogSetClientAddress(Str, ArrayFields, ErrorMessage);
        
    return ErrorMessage;
end;



// Удаление адреса клиента
macro DelClientAddress(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, ClientAddress;
    var PartyID, AddressType;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);
    
    PartyID     = int(ArrayFields(delClientAddressFldPartyID));    
    AddressType = int(ArrayFields(delClientAddressFldAddressType));    
        
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    Client        = RsbParty(PartyID);
    ClientAddress = Client.Address(AddressType);      
    
    stat = ClientAddress.Delete();
    
    if (stat == true)
        stat = Client.Update();    
    end;

    if (stat == true)        
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при удалении адреса клиента. " + GetErrMsg();    
    end;
    
    LogDelClientAddress(Str, ArrayFields, ErrorMessage);
        
    return ErrorMessage;
end;



// Добавление регистрационного документа клиента
macro SetClientRegDoc(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, ClientRegDoc;
    var PartyID, RegPartyKind, RegDocKind;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);
    
    PartyID      = int(ArrayFields(setClientRegDocFldPartyID));    
    RegPartyKind = int(ArrayFields(setClientRegDocFldRegPartyKind));    
    RegDocKind   = int(ArrayFields(setClientRegDocFldRegDocKind));    
        
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    Client       = RsbParty(PartyID);
    ClientRegDoc = Client.PartyRegDoc(RegPartyKind, RegDocKind);
    
    ClientRegDoc.RegPartyID = int(ArrayFields(setClientRegDocFldRegPartyID));
    ClientRegDoc.StartDate  = date(ArrayFields(setClientRegDocFldStartDate));
    ClientRegDoc.Series     = ArrayFields(setClientRegDocFldSeries);
    ClientRegDoc.Number     = ArrayFields(setClientRegDocFldNumber);
    ClientRegDoc.DocDate    = date(ArrayFields(setClientRegDocFldDocDate));
    ClientRegDoc.IsMain     = (ArrayFields(setClientRegDocFldIsMain) == "X");

    stat = Client.Update();    

    if (stat == true)        
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при создании регистрации клиента. " + GetErrMsg();
    end;
     
    LogSetClientRegDoc(Str, ArrayFields, ErrorMessage);
        
    return ErrorMessage;
end;



// Удаление регистрационного документа клиента
macro DelClientRegDoc(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, ClientRegDoc;
    var PartyID, RegPartyKind, RegDocKind;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);
    
    PartyID      = int(ArrayFields(delClientRegDocFldPartyID));    
    RegPartyKind = int(ArrayFields(delClientRegDocFldRegPartyKind));    
    RegDocKind   = int(ArrayFields(delClientRegDocFldRegDocKind));    
        
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    Client       = RsbParty(PartyID);
    ClientRegDoc = Client.PartyRegDoc(RegPartyKind, RegDocKind);
    
    stat = ClientRegDoc.Delete();
        
    if (stat == true)
        stat = Client.Update();    
    end;

    if (stat == true)        
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при удалении регистрации клиента. " + GetErrMsg();
    end;
    
    LogDelClientRegDoc(Str, ArrayFields, ErrorMessage);
        
    return ErrorMessage;
end;



// Добавление сотрудника клиента
macro SetClientOfficer(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, ClientOfficer, Person, PersonCode, pers_code;
    var PartyID;
    var ArrayFields = TArray;         
    var pers = TRecHandler("party.dbt");

    ParseStringToArray(Str, ArrayFields);
    
    PartyID = int(ArrayFields(setClientOfficerFldPartyID));    
            
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    Client        = RsbParty(PartyID);    
    ClientOfficer = Client.Officer();    
    ClientOfficer.New();
           
    ClientOfficer.PersonID       = int(ArrayFields(setClientOfficerFldPersonID));
    ClientOfficer.IsFirstPerson  = (ArrayFields(setClientOfficerFldIsFirstPerson) == "X");
    ClientOfficer.IsSecondPerson = (ArrayFields(setClientOfficerFldIsSecondPerson) == "X");
    ClientOfficer.Post           = ArrayFields(setClientOfficerFldPost);

    ClientOfficer.DateFrom       = date(ArrayFields(setClientOfficerFldDateFrom));
    ClientOfficer.DateTo         = date(ArrayFields(setClientOfficerFldDateTo));
/* A.Gregeradsky - 21.12.2009 - Доработка по установке кода RS-Bank сотруднику (ФЛ) клиента РКО (ЮЛ) */
    Person = RsbParty(ClientOfficer.PersonID);

    if(ПолучитьКодСубъекта (Person.PartyID, 1) == "")
      pers.rec.partyid = Person.PartyID;
      if(GenerateReference(110, pers_code, 3, pers) == 0);
        PersonCode = Person.Code;
        stat = PersonCode.SetCode(1, pers_code);    
        if(stat == true)
          stat = Person.Update();
        else
          ErrorMessage = "Ошибка при установке кода сотрудника. " + GetErrMsg();
          stat = false;
        end;
      else
        ErrorMessage = "Ошибка при получении кода сотрудника. " + GetErrMsg();
        stat = false;
      end;
    else
     stat = true;
    end;
/* End Gregeradsky */

    if(stat == true)
      stat = Client.Update();    
    end;

    if (stat == true)        
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при создании сотрудника клиента. " + GetErrMsg();
    end;
        
    LogSetClientOfficer(Str, ArrayFields, ErrorMessage);
         
    return ErrorMessage;
end;


// Удаление сотрудника клиента
macro DelClientOfficer(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, ClientOfficer;
    var PartyID, PersonID;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);

    PartyID  = int(ArrayFields(delClientOfficerFldPartyID));    
    PersonID = int(ArrayFields(delClientOfficerFldPersonID));
            
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    Client       = RsbParty(PartyID);
    ClientOfficer = Client.Officer();
    
    stat = ClientOfficer.DeletePerson(PersonID);
           
    if (stat == true)     
        stat = Client.Update();    
    end;

    if (stat == true)        
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при удалении сотрудника клиента. " + GetErrMsg();
    end;
    
    LogDelClientOfficer(Str, ArrayFields, ErrorMessage);
        
    return ErrorMessage;
end;



// Добавление документа клиента
macro SetClientPaper(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, ClientPaper;
    var PartyID, PaperKind;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);

    PartyID   = int(ArrayFields(setClientPaperFldPartyID));    
    PaperKind = int(ArrayFields(setClientPaperFldPaperKind)); 
            
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    Client      = RsbParty(PartyID);
    ClientPaper = Client.PersonPaper(PaperKind);
    
    ClientPaper.Series     = ArrayFields(setClientPaperFldSeries);
    ClientPaper.Number     = ArrayFields(setClientPaperFldNumber);
    ClientPaper.IssuedDate = date(ArrayFields(setClientPaperFldIssuedDate));
    ClientPaper.Issuer     = ArrayFields(setClientPaperFldIssuer);
    ClientPaper.IssuerCode = ArrayFields(setClientPaperFldIssuerCode);

    ClientPaper.IsMain     = (ArrayFields(setClientPaperFldIsMain) == "X");
    /* A.Gregeradsky - 21.12.2009 - Признак "Основной документ" для паспорта гр.РФ физлица, не ИП */
//    if ((Client.LegalForm == 2) and (not Client.IsEmployer) and (PaperKind == 0))
//         ClientPaper.IsMain = true;
//    end;
    /* End Gregeradsky */

    stat = Client.Update();    

    if (stat == true)        
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при создании документа клиента. " + GetErrMsg();
    end;
    
    LogSetClientPaper(Str, ArrayFields, ErrorMessage);
        
    return ErrorMessage;
end;



// Удаление документа клиента
macro DelClientPaper(Str : string)
    var stat;
    var ErrorMessage = "";
    var Client, ClientPaper;
    var PartyID, PaperKind;
    var ArrayFields = TArray;         
    
    ParseStringToArray(Str, ArrayFields);
    
    PartyID   = int(ArrayFields(delClientPaperFldPartyID));    
    PaperKind = int(ArrayFields(delClientPaperFldPaperKind)); 
            
    if (CheckPartyID(PartyID, ErrorMessage) == false)        
        return ErrorMessage;
    end;
    
    Client      = RsbParty(PartyID);
    ClientPaper = Client.PersonPaper(PaperKind);
    
    stat = ClientPaper.Delete();
    
    if (stat == true)        
        stat = Client.Update();    
    end;

    if (stat == true)        
        ErrorMessage = no_error_msg;
    else
        ErrorMessage = "Ошибка при удалении документа клиента. " + GetErrMsg();
    end;
    
    LogDelClientPaper(Str, ArrayFields, ErrorMessage);
        
    return ErrorMessage;
end;
