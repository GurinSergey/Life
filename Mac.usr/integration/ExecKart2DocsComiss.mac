// -------------------------------------------------------------------------------------------------
// @filename: ExecKart2DocsComiss.mac
// @author  : FIV 
// @desc    : Массовое списание документов в картотеке 2, порожденных комиссиями
// @changes : KS           26.04.2012 Адаптация под банковский ордер
//            Gurin S.     С-21207    26.07.2013
//            Teleshova A. R-236841-2 27.08.2013
//            Gurin S.     С-30420    17.06.2014
//            Gurin S.     C-31277-7  16.07.2014
//            VDN          C-41253    27.07.2015
// -------------------------------------------------------------------------------------------------

import PTInter, Календарь, rsd, globals, "opr_engine.mac", "gstname.mac", "PmReportComissK2.mac";
import commonint, "lib_access.mac", "lib_fg.mac";

var rs, cmd, cmd1;
var opr = opr_engine;
var intM = 5, time1, time2, flag, mes;

var path, SelectMenu, sql, time_max;
record party(party);
record oper(person);
array M;
GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,path);
//Gurin S. C-31277-7 16.07.2014
GetRegistryValue("PRBB\\MASS_EXEC\\TIME_MAX",2,time_max);
if (time_max == "")
    time_max = "21:00";
end;

//24.07.2013 Gurin S. логгирование запуска
ExecmacroFile("lib_log.mac", "LogProcedure", "ExecKart2DocsComiss.mac");

var filen = "trnsord.lbr";
var pathfile = FindPath(filen,path);
if (not pathfile)
   msgbox("Не найдена LBR");
   exit();
end;

var dlg  = TRecHandler("cmsk2",pathfile,True);
const  KEY_ENTER = 13, KEY_F2 = 316, KEY_F3 = 317, KEY_F5 = 319, KEY_ALTF3 = 362, KEY_CTRLF3 = 352, KEY_SPACE = 32, EXITFIELD = 530, KEY_ESC = 27;

m[0] = "Весь банк";
m[1] = "Филиалы Москвы: (000,006,002,011,012)";
m[2] = "Иваново: 817-825";
m[3] = "Регионы: все кроме 000,006,002,011,012,817-825";


opr.oper = {oper};
opr.kindoperation = 24001;
opr.dockind = 201;

macro is_enable_pipe(p_pipe)
   private  var stat,cmd;
   cmd=rsdcommand("begin ?:=is_enable_pipe(:p_pipe); end;");
   cmd.addParam("Stat",   RSDBP_OUT, V_STRING);
   cmd.addParam("p_pipe",   RSDBP_IN, p_pipe);
   cmd.execute;
   stat = cmd.Param("Stat").value;
   if (stat=="0")
      return true;
   else
      return false;
   end;
end;

macro EvProcComissK2 (dlg, cmd, id, key) 
   var mess = "~F2~ Продолжить ~F3~ Список ~Space~ Установить ВСЕ ~ESC~ Выход ", code;
   
   if(cmd == DLG_INIT)
      dlg.rec.d_date    = {curdate};
      dlg.rec.d_oper    = "Все";
      dlg.rec.d_Client  = "Все";
      dlg.rec.d_branch  = "Весь банк";
      ClearRecord(party);
      ClearRecord(oper);
      UpdateFields(dlg); 
   end;

   if ((cmd==DLG_KEY) and (KEY==KEY_F2))
      return CM_SAVE;
   end;

   if ((cmd==DLG_KEY) and (key == KEY_ENTER))
      SetFocus(dlg,id+1);
      return CM_IGNORE;
   end;
   if ((cmd==DLG_KEY) and (KEY==KEY_F3))
      if (id == 3)
         if (ListPT(party, 1, code, PTLIST_PSCLIENT, 0, PTCK_ALL))
            dlg.rec.d_client  = party.partyid;
            dlg.rec.d_client2  = party.shortname;
         end;
      elif (id == 1) 
         if (ListOper(oper, 1, 0))
            dlg.rec.d_oper  = oper.oper;
            dlg.rec.d_oper2  = oper.name;
         end;
      elif (id == 5)
         SelectMenu = Menu(M);
            if (SelectMenu == 0)
               dlg.rec.d_branch = m[0];
            elif (SelectMenu == 1)
               dlg.rec.d_branch = m[1];
            elif (SelectMenu == 2)
               dlg.rec.d_branch = m[2];
            elif (SelectMenu == 3)
               dlg.rec.d_branch = m[3];
            end;
      elif (id == 0) 
         dlg.rec.d_date = GetDateByCalendar(dlg.rec.d_date);
      end;
      UpdateFields(dlg); 
   end;
   if ((cmd==DLG_KEY) and (KEY==KEY_SPACE))
      if (id == 1)
         dlg.rec.d_oper    = "Все";
         dlg.rec.d_oper2    = "";
         ClearRecord(oper);
      elif (id == 3)  
         dlg.rec.d_Client  = "Все";
         dlg.rec.d_Client2  = "";
         ClearRecord(party);
      elif (id == 5)  
         dlg.rec.d_branch  = "Весь банк";
      end;
      UpdateFields(dlg); 
   end;
end;


private macro catch(x)
   var err_count = 0;
   var lasterror = "oшибка:"+x.module + "[" + x.line + "]:" + x.message;

   while( err_count < RSLDefEnv.ErrorCount )
      lasterror = lasterror + "|"+RSLDefEnv.error(err_count).descr;
      err_count = err_count + 1;
   end;

   if (RSLDefCon.IsinTrans)
      RSLDefCon.RollbackTrans;
   end;
   msgbox(lasterror);
end;

macro getSKSClause()
    var cond = " substr(pa.t_account, 11, 1) = '9' ";

    if (_bank.is_EXV) cond = " (substr(pa.t_account, 11, 1) = '9' or substr(pa.t_account, 12, 1) = '9') " end; 
    if (_bank.is_SLD) cond = " (substr(pa.t_account, 11, 1) = '9' or substr(pa.t_account, 14, 1) = '8') " end; 

    return " and 1 = case "+
                      "when ( instr(pa.t_usertypeaccount, 'D') <> 0 and instr(pa.t_usertypeaccount, 'H') <> 0)  then 1 "+
                      "when ( " + cond + " and instr(pa.t_usertypeaccount, 'H') <> 0 )                          then 1 "+
                      "when not ( instr(pa.t_usertypeaccount, 'D') <> 0 or " + cond + " )                       then 1 "+
                      "else 0 "+
                    "end ";  
 
end;

if (not ACS_CheckGroupOper(181,{oper},false))
   msgbox("Вы не включены в группу \"Процедура массовой оплаты картотеки 2 по комиссиям\" ");
   return;
end;


if (not RunDialog(dlg, "EvProcComissK2")) //название панели
   return; 
end;

//TAM 27.08.2013 R-236841-2
set_pipe_server(pipe_server_k2_comission);

Private Macro StartExecute()
   var cond;
   initprogress(-1,"обработка платежей комиссий в картотеке 2");
   cmd = RSDCommand("truncate table doprtemp_tmp");
   cmd.execute;

   //В программном канале могут находится необработанные сообщения. Чистим очередь
   //cmd = RSDCommand("begin dbms_pipe.purge('rsbank.service.k2_'); end;");
   //cmd.Execute;

   rsldefcon.BeginTrans;
   //VDN 05.08.2015 Таблица счетов определяется всегда C-41253
   /*if ((oper.oper != 0 )or (party.partyid != 0 ) or ( SelectMenu == 1 ) or ( SelectMenu == 2 ) or ( SelectMenu == 3 ) )*/
      cond = "from   dpmpaym_dbt pm, doproper_dbt op, dpmrmprop_dbt pr, dpmaddpi_dbt dpi, daccount_dbt pa ";
   /*else
      cond = "from   dpmpaym_dbt pm, doproper_dbt op, dpmrmprop_dbt pr, dpmaddpi_dbt dpi ";
   end;*/

   // KS 26.04.2012 Адаптация под банковский ордер
   sql = "insert into doprtemp_tmp (t_id_operation, " +
   "                                t_sort, " +
   "                                t_dockind, " +
   "                                t_documentid, " +
   "                                t_orderid, " +
   "                                t_kind_operation, " +
   "                                t_start_date, " +
   "                                t_isnew, " +
   "                                t_id_step) " +
   "        select   op.t_id_operation, " +
   "                 rownum, " +
   "                 pm.t_dockind, " +
   "                 lpad (to_char (pm.t_paymentid), 34, '0'), " +
   "                 pm.t_paymentid, " +
   "                 decode ( " +
   "                         op.t_kind_operation, " +
   "                         0, " +
   "                         (select   min (t_kind_operation) " +
   "                            from   doprkoper_dbt " +
   "                           where   t_dockind = 29 and t_notinuse = chr (0) and t_kind_operation > 0), " +
   "                          op.t_kind_operation), " +
   "                 rsbsessiondata.curdate, " +
   "                 chr (0), " +
   "                (SELECT   MIN(t_id_step)  " +
   "                   FROM   doprstep_dbt  " +
   "                  WHERE   t_id_operation = op.t_id_operation AND t_isexecute = 'R') ";
   sql = sql + cond;
   sql = sql + " WHERE       pr.t_paymentid = pm.t_paymentid "
   "                    AND pm.t_dockind IN (201, 286) "
   "                    AND op.t_documentid = LPAD (TO_CHAR (pm.t_paymentid), 34, '0') "
   "                    AND op.t_dockind = pm.t_dockind "
   "                    AND pm.t_paymentid = DPI.T_PAYMENTID(+) "
   //VDN 05.08.2015 C-41253
   "                    AND pm.t_payeraccount = pa.t_account AND usr_common.get_chapter(pm.t_payeraccount) = pa.t_chapter "
   "                    AND (pm.t_receiveraccount LIKE '47423____3%' "
   "                         OR (pm.t_receiveraccount = CHR (1) "
   "                             AND dpi.t_account LIKE '47423____3%')";
   if (_bank.is_SLD)
       sql = sql + "OR (pm.t_origin = 10000 AND SUBSTR (pm.t_receiveraccount, 1, 5) IN ('47423', '70601'))";
   end;
   sql = sql + "                    ) AND usr_get_rest_current (pm.t_payeraccount, "
   "                                              TO_DATE ('"+string(dlg.rec.d_date)+"', 'dd.mm.yyyy'), "
   "                                              pr.t_priority) > 0 "
   "                    AND pm.t_paymstatus = 2000 ";
   if(oper.oper != 0 )
      sql = sql + " AND pa.t_account = pm.t_payeraccount AND pa.t_oper = "+string(oper.oper);
   end;
   if(party.partyid != 0 )
      sql = sql + " AND pa.t_account = pm.t_payeraccount AND pa.t_client = "+string(party.partyid);
   end;
   if( SelectMenu == 1 )
      sql = sql + " AND pa.t_account = pm.t_payeraccount AND pa.t_branch in (1,8,4,55,168)";
   elif( SelectMenu == 2 )
      sql = sql + " AND pa.t_account = pm.t_payeraccount AND pa.t_branch in (139,140,141,142,143,144,153,156,170)";
   elif( SelectMenu == 3 )
      sql = sql + " AND pa.t_account = pm.t_payeraccount AND pa.t_branch NOT IN (1,8,4,55,168,139,140,141,142,143,144,153,156,170)";
   end;

   /*VDN 27.07.2015 C-41253*/
   sql = sql + getSKSClause();

   sql = sql + " ORDER BY pr.t_priority, pm.t_valuedate ";
   cmd = RSDCommand(sql);
   cmd.execute;


   cmd=RSDCommand("begin ? := wld_common.definemessagereleaseforpmmass (false); end;");
   cmd.addParam("stat", RSDBP_RETVAL, V_INTEGER);
   cmd.Execute();

   rsldefcon.CommitTrans;

   //выполнение предобработки
   cmd=RSDCommand("begin usr_operations.exec_steps_for_temp; end;");
   cmd.Execute();

   remprogress;
   //печать отчета
   //PrintReport("списания картотеки 2 за РКО", dlg.rec.d_date);
   //SaveEndTime (10); //Закончили

   onerror(er)
   catch(er); 
end;


while (1)
   mes = "F2 - Запуск, Ecs - Выход, Ожидание:";
   time1 = time();
   flag = true;
   while (flag)
      time2 = time();
      message(string(mes,(time(0,intM,0,0) - (time2 - time1))));
      //Gurin S. C-31277-7 16.07.2014
      if (time2 >= time(time_max))
          exit();
      elif  ( (time2 - time1) > time(0,intM,0,0))
         //Gurin S. Из-за очереди в пайпе - комментирую
         //if (is_enable_pipe("rsbank.service.k2_"))
         StartExecute();
         flag = false;
         //else
         //    msgbox("Внимание! Подозрение на зависание обработчиков. | Документы проведены не будут. | Обратитесь в HelpDesk");
         //    exit();
         //end;
      elif (TestEvent == KEY_F2)
         //if (is_enable_pipe("rsbank.service.k2_"))
         StartExecute();
         flag = false;
         //else
         //    msgbox("Внимание! Подозрение на зависание обработчиков. | Документы проведены не будут. | Обратитесь в HelpDesk");
         //    exit();
         //end;
      elif (TestEvent == KEY_ESC)
         exit();
      end;
   end;
end;