/*=====================================================================================================================*/
/*  @filename: ExecLoroReport.mac                                                                                      */
/*  @author  : Vashchenko D. N. C-25805                                                                                */
/*  @desc    : Автоматическое формирование выписки LORO                                                                */
/*  @changes :                                                                                                         */
/*                                                                                                                     */
/*                                                                                                                     */
/*=====================================================================================================================*/
import BankInter,
       Reporting,
       FIInter,
       PTInter,
       RsbDataSet,
       RsbFormsInter,
       Globals,
       KeyCodes,
       Lib_SqlTools,
       VBAConst, KeyCodes,
       CashInter;

private const	FT_INTEGER = 0;
private const	FT_STRING  = 7;
private const	FT_DATE    = 9;
private const FT_TIME    = 10;

var statusString    = "~F2~ Запустить ~F3~ Выбор счетов ~ESC~ Выход",
    statusStringNew = "~F3~ Остановить";


Array timeArray;
var s_time = time (9, 0);

for (var s, 1, 27)
   s_time = s_time + time (0, 30);
   timeArray (s) = s_time;
end;

class ( TRsbPanel ) TReportPanel
    InitTRsbPanel();
    SetSize( 43, 15 );

    var editFieldsPosition = 28;
    var labelsPosition     = 2;

    class ( TRsbEditField ) EditField( fldName: string, typeFld: integer, x: integer, y: integer, width: integer, height: integer, bindVal, active: bool, edittable: bool, panel )
        var bindString = string( bindVal );
        
        initTRsbEditField( typeFld       );
        setPosition      ( x    , y      );
        setSize          ( width, height );
        name = FldName;

        if( active  == false     ) editable = focusable = false;    end;
        if ( edittable == false  ) editable = false;                end;
        if( typeFld == FT_STRING ) bindValue( this, "bindString", 100 );
      elif( bindVal != null      ) value    = bindVal;              end;
        if( panel   != null      ) panel.addControl( this );        end;
    end; 

    addLabel( TRsbLabel( labelsPosition, 1 , "Время работы                       :" ) );
    addLabel( TRsbLabel( labelsPosition, 2 , "Время следующего опроса            :" ) );
    addLabel( TRsbLabel( labelsPosition, 3 , "Время выполнения последнего опроса :" ) );
    addLabel( TRsbLabel( labelsPosition, 5 , "Статус обработки                   :" ) );
    addLabel( TRsbLabel( labelsPosition, 7 , "Результат последнего опроса  :"       ) );
    addLabel( TRsbLabel( labelsPosition, 11, "Путь хранения файлов выписки :"       ) );    
    
    var TimeJob   = EditField( "TimeJob"  , FT_TIME  , editFieldsPosition, 1, 7 , 1, ""          , true, false,  this );
    var TimeNext  = EditField( "TimeNext" , FT_TIME  , editFieldsPosition, 2, 7 , 1, ""          , true, false,  this );
    var TimeResp  = EditField( "TimeResp" , FT_TIME  , editFieldsPosition, 3, 7 , 1, ""          , true, false,  this );
    var StatusObr = EditField( "StatusObr", FT_STRING, editFieldsPosition, 5, 14, 1, "Не запущен", true, false,  this );
    var Error     = EditField( "Error"    , FT_STRING, 2, 8 , 40, 2, "no_error", true, false,  this );
    var Path      = EditField( "Path"     , FT_STRING, 2, 12, 40, 2, "\\\\prbs-mow-fl-b00.life.corp\\departs\\swift\\sprav-cred\\", true, true,  this ); 
        

    addEventHandler ( RSB_EV_KEY_PRESSED, R2M( this, "KeyPressed" ) );

    macro GetNextTime ( timeResp ) 
       for (var j, 1, 26) 
           if ( ( timeResp >= timeArray (j) ) and ( timeResp < timeArray (j + 1) ) )
             if ( j == 26 ) return timeArray ( 1 ); end;
             return timeArray (j + 1);
          end;
       end;        
    end;

    Macro GetAccounts (accounts)
       var cmd = RSDRecordset ("SELECT T_ACCOUNT FROM USR_CORRACCOUNT_VLORO_DBT WHERE T_ACTIVE = CHR(88)" );
       while (cmd and cmd.MoveNext)
          accounts = accounts + "'" + cmd.value(0) + "', ";
       end;  
       SetParm (1, substr (accounts, 1, strlen(accounts) - 2)); 
    end;

    Macro CorAccScroll()
       var RecordSet, CmdText, Command, cmd;
       var col = TArray;       

       macro AddCol (ar ,ind , fld, head, width, rdonly)
          ar.value (ind * 6)     = fld;
          ar.value (ind * 6 + 1) = head;
          ar.value (ind * 6 + 2) = width;  
       end; 

       AddCol (col, 0, "T_BANKNAME", "Банк"       , 20, true);
       AddCol (col, 1, "T_ACCOUNT" , "Корр. счет" , 15, true);
       AddCol (col, 2, "T_ACTIVE"  , "Активен"    , 5 , true); 
 
       CmdText = "SELECT T_BANKNAME, T_ACCOUNT, T_ACTIVE FROM USR_CORRACCOUNT_VLORO_DBT";
       Command = RSDCommand ( CmdText );
       Command.Execute();     

       macro EvProc (RecordSet, Command, id, key )             
       end;

       RecordSet = RSDRecordSet(Command, RSDVAL_CLIENT, RSDVAL_STATIC);
       runScroll(RecordSet, 3, col, null, @EvProc, "Список счетов для выписки", "~F9~ Ввод ~Enter~ Изменить ~F8~ Удалить", false, -1, -1, 50, 20); 
    end; 

    macro StartAgent()
       if (Path.Value == "")
          msgbox("Необходимо задать путь хранения выписки!");
          return KEY_F3;
       end;
       private var _key = KEY_F2,
                   timeResps   = time,
                   timeStart   = time,
                   timeObrPrev = time,
                   timeRespPrev,
                   cmd,
                   hour, minute,
                   accounts = "";

       StatusObr.Value = "Запущено";  debugbreak;
       GetAccounts(accounts);

       while ( 1 == 1 )
          _key = testevent(100);
          TimeJob.Value = time - timeStart;
          TimeSplit(time, hour, minute);

          if ( ( time () <= time ( 22, 0, 0 ) ) and ( time () >= time ( 9, 0, 0 ) ) )
             TimeNext.Value = GetNextTime ( timeResps );
          
             if ( time (hour, minute) == TimeNext.Value)
                timeRespPrev = time;
                StatusObr.Value = "Обработка данных";
                cmd = RsdCommand ("BEGIN SEND_REQUEST_IIS_VLORO (?, ?); END;");
                cmd.AddParam ( "path", RSDBP_IN, Path.Value );
                cmd.AddParam ( "acc" , RSDBP_IN, accounts   );
 
                message ("Ожидайте, идет выгрузка выписки");
                cmd.execute;
                message (statusStringNew);

                TimeResp.Value = time - timeRespPrev;
                cmd = TRsbDataSet ("SELECT * FROM VLORO_IIS_LOG_DBT WHERE T_DATEJOB = (SELECT MAX(T_DATEJOB) FROM VLORO_IIS_LOG_DBT)");

                while ( cmd.MoveNext )
                   Error.Value = cmd.err;
                end;
             end;
          else
             Error.Value = "C 22:00 до 9:00 - остановка";
          end;

          if ( ( time() - timeObrPrev) >= time (0, 0, 2) )
             StatusObr.Value = StatusObr.Value + ".";
             if ( (time () - timeObrPrev) >= time(0, 0, 3) )
                StatusObr.Value = "Запущено";
                timeObrPrev = time;
             end; 
          end;

          if ( _key == KEY_F3 )
             StatusObr.Value = "Остановлено";
             TimeNext.Value = 0;
             message (statusString);
             return _key;
          end;
       end;
    end;

    macro keyPressed ( RsbEvent:object )  
       if ( rsbEvent.KeyCode == KEY_F2 )
          Error.Value = "no_error";
          message (statusStringNew); 
          rsbEvent.KeyCode = StartAgent();
       elif ( rsbEvent.KeyCode == KEY_F3 )
          CorAccScroll();
       end;
    end;   
end;

/**********ТОЧКА ВХОДА*************/

var panel = TReportPanel;
    panel.setPosition(40, 15); 
    panel.setStatus(statusString);
    panel.setCaption("Автоматическое формирование выписки");
    panel.run;

exit(1);