/*************************************************************************/
/*     Дополнение к автоматизированной банковской системе RS-Bank        */
/*                                                                       */
/*    Имя файла        : usr_me_time_panel_pm.mac                        */
/*                                                                       */
/*    Описание         : Креглосуточное проведение документов            */
/*                       Работа с пользовательской таблицей              */
/*                       usr_me_time_paym_dbt                            */
/*                                                                       */
/*    Программист      : Котов С.С. использовал наработки Лавренова А.А. */
/*                                                                       */
/*                       C-15088 Новое расписание                        */
/*                                                                       */
/*    Создан           : 11.08.2011                                      */
/*                 //  KS 05.09.2012                                     */
/*                     Переделано на usr_me_time_panel_pm                */
/*                 //  KS 22.10.2012                                     */
/*                     Переделано на usr_me_time_panel_paym              */
/*                                                                       */
/*************************************************************************/

import rsbdataset, PTInter;
import ldmklbr, "KeyCodes.mac";

 var   v_corschem,v_bic,
       v_timebegin_out_our,v_timeend_out_our,v_is_holiday_out_our,
       v_timebegin_out,v_timeend_out,v_is_holiday_out,
       v_timebegin_in,v_timeend_in,v_is_holiday_in,
       v_id;

 var choice;
 var exitscroll;
 var RecordSet;

 var isupdate, ischanged=false;

 private const time_postfix = ":00.00";

macro getCorschemName(id)
 if (id == 0)
  return "Все корсхемы";
 end;
 if ((valtype(id) == 0) or (valtype(id) == 26))
  return "Неизвестно";
 end;
 private var str_cmd = "select t_name from dcorschem_dbt where t_fiid = 0 and t_number = "+id;
 private var rs = trsbdataset(str_cmd);
 while (rs and rs.movenext)
  return rs.t_name;
 end;
 return "Неизвестно";
end;

macro getCorschemType(id)
 if (id == 0)
  return "Все";
 end;
 if ((valtype(id) == 0) or (valtype(id) == 26))
  return "Неизвестно";
 end;
 private var str_cmd = "select t_isnostro from dcorschem_dbt where t_fiid = 0 and t_number = "+id;
 private var rs = trsbdataset(str_cmd);
 while (rs and rs.movenext)
   if (rs.t_isnostro == "X")
     return "Ностро";
   else
     return "Лоро";
   end;
 end;
 return "Неизвестно";
end;

//Является ли строка числом
macro Is_Digit( str )
  var isdigit, digit="1234567890";
  var i, n, cur_symb;

  isdigit = true;
  i = 1;
  n = strlen(str);
  while( (isdigit) and (i <= n) )
     cur_symb = SubStr(str, i, 1);
     if(index(digit,cur_symb) == 0)
       isdigit = false;
     end;
     i = i+1;
  end;
  return isdigit;
end;

//Проверка правильности заполнения поля
private macro check_time(tm)
  var hh,mm,ss,ms;
  var pos;

  if (substr(tm,3,1)!= ":")
    msgbox("Не правильно задано время!|| Формат: ЧЧ:ММ");
    return false;
  end;

  hh = int(substr(tm,1,2));
  mm = int(substr(tm,4,2));

  if(not is_digit(substr(tm,1,2)))
    msgbox("В часах есть недопустимые символы!");
    return false;
  end;

  if((hh < 0) or (hh > 23))
    msgbox("Часы должны быть в диапазоне 0-23!");
    return false;
  end;

  if(not is_digit(substr(tm,4,2)))
    msgbox("В минутах есть недопустимые символы!");
    return false;
  end;

  if((mm < 0) or (mm > 59))
    msgbox("Минуты должны быть в диапазоне 0-59!");
    return false;
  end;

  return true;
End;

//Выбор типа документов
MACRO Get_mass_index(act, mas)

 var cnt, i;
 cnt = asize(mas);
 i=0;
 while (i<cnt)
    if(act == mas(i))
       return i;
    end;
    i = i+1;
 end;
 return -1;
end; 


//Обновление записи в таблице
  MACRO Update_usr_me_time(id, corschem, bic,
                               timebegin_out_our, timeend_out_our, is_holiday_out_our,
                               timebegin_out, timeend_out, is_holiday_out,
                               timebegin_in, timeend_in, is_holiday_in)
     var rs, str;


     str = "UPDATE usr_me_time_paym_dbt " +
           "   set t_timebegin_out_our = '" + timebegin_out_our + time_postfix + "' ," +
           "       t_timeend_out_our  = '"  + timeend_out_our   + time_postfix + "' ,";

     if(is_holiday_out_our == "X") 
      str = str + "       t_is_holiday_out_our = '" + is_holiday_out_our + "', " ;
     else
      str = str + "       t_is_holiday_out_our = chr(0), " ;
     end;

     str = str +
           "       t_timebegin_out = '" + timebegin_out + time_postfix + "' ," +
           "       t_timeend_out  = '"  + timeend_out   + time_postfix + "' ,";

     if(is_holiday_out == "X") 
      str = str + "       t_is_holiday_out = '" + is_holiday_out + "', " ;
     else
      str = str + "       t_is_holiday_out = chr(0), " ;
     end;

     str = str +
           "       t_timebegin_in  = '" + timebegin_in  + time_postfix + "' ," +
           "       t_timeend_in   = '"  + timeend_in    + time_postfix + "' ,";

     if(is_holiday_in  == "X") 
      str = str + "       t_is_holiday_in = '" + is_holiday_in  + "', " ;
     else
      str = str + "       t_is_holiday_in = chr(0), " ;
     end;

     str = str +
           "       t_corschem  = "   + corschem  + " ," +
           "       t_bic       = '"  + bic    + "' ";

     str = str + " WHERE t_id = "+ int(id);

     rs = rsdcommand(str);

     if(rs)
        rs.execute;
        return true;
     else 
        return false;
     end;
  END;


//Следующее значение идентификатора в таблице
  MACRO GetNextVal()
     var rs, str;
     str = "SELECT NVL (MAX (t_id), 0) id " +
           "  FROM usr_me_time_paym_dbt " ;
     rs = trsbdataset(str);
     if (rs and rs.movenext)
        return int(rs.id+1);
     end;
     return 1;
  END;

//Вставка записи в таблицу
  MACRO insert_usr_me_time(corschem, bic,
                           timebegin_out_our, timeend_out_our, is_holiday_out_our,
                           timebegin_out, timeend_out, is_holiday_out,
                           timebegin_in, timeend_in, is_holiday_in)
     var rs, str;

     // Проверка уникальности
     str = "SELECT 1 " +
           "  FROM usr_me_time_paym_dbt where t_corschem="+corschem ;
     rs = trsbdataset(str);
     if (rs and rs.movenext)
        msgbox("Расписание для данной корсхемы уже настроено!");
        return false;
     end;

     // Вставка
     str = "INSERT INTO usr_me_time_paym_dbt " +
           "       (t_corschem, t_bic, " +
           "        t_timebegin_out_our, t_timeend_out_our, " +
           "        t_timebegin_out,     t_timeend_out, " +
           "        t_timebegin_in,      t_timeend_in, " + 
           "        t_is_holiday_out_our, t_is_holiday_out, t_is_holiday_in, t_id) " +
           "VALUES (" + corschem + ",'" + bic + "'," +
                    "'" + timebegin_out_our + time_postfix + "','" + timeend_out_our + time_postfix + "'," +
                    "'" + timebegin_out     + time_postfix + "','" + timeend_out     + time_postfix + "'," +
                    "'" + timebegin_in      + time_postfix + "','" + timeend_in      + time_postfix + "',";

     if(is_holiday_out_our == "X") 
      str = str + " 'X', " ;
     else
      str = str + " chr(0), " ;
     end;


     if(is_holiday_out == "X") 
      str = str + " 'X', " ;
     else
      str = str + " chr(0), " ;
     end;


     if(is_holiday_in == "X") 
      str = str + " 'X', " ;
     else
      str = str + " chr(0), " ;
     end;

     str = str + GetNextVal()+" ) ";

     rs = rsdcommand(str);

     if(rs)
        rs.execute;
        return true;
     else 
        return false;
     end;            
  END;



//Удаление записи из таблицы
  MACRO delete_usr_me_time(id)
     var rs, str;
     str = " delete from usr_me_time_paym_dbt " +
           "  where t_id = "+ id;

     rs = rsdcommand(str);

     if(rs)
        rs.execute;
        return true;
     else 
        return false;
     end;            
  END;



/*********************************************************************************************************/

Class (TRecHandler) TParm(LbrName)

    InitTRecHandler ("metime", (LbrName), true);

    this.("corschem_isnostro")  = getCorschemType(v_corschem);
    this.("corschem_id")        = v_corschem;
    this.("corschem_name")      = getCorschemName(v_corschem);
    this.("bic")                = v_bic;
    this.("timeBegin_out_our")  = v_timebegin_out_our;
    this.("timeEnd_out_our")    = v_timeend_out_our;
    this.("is_holiday_out_our") = v_is_holiday_out_our;
    this.("timeBegin_out")      = v_timebegin_out;
    this.("timeEnd_out")        = v_timeend_out;
    this.("is_holiday_out")     = v_is_holiday_out;
    this.("timeBegin_in")       = v_timebegin_in;
    this.("timeEnd_in")         = v_timeend_in;
    this.("is_holiday_in")      = v_is_holiday_in;

//Обработчик панели
    Macro Handler ( dlg, cmd, id, key )

      record Corschem  ("Corschem.dbt");
      var    isnostro;

      if ( cmd == DLG_INIT )
        dlg.("corschem_isnostro")  = getCorschemType(v_corschem);
        dlg.("corschem_id")        = v_corschem;
        dlg.("corschem_name")      = getCorschemName(v_corschem);
        dlg.("bic")                = v_bic;
        dlg.("timeBegin_out_our")  = v_timebegin_out_our;
        dlg.("timeEnd_out_our")    = v_timeend_out_our;
        dlg.("is_holiday_out_our") = v_is_holiday_out_our;
        dlg.("timeBegin_out")      = v_timebegin_out;
        dlg.("timeEnd_out")        = v_timeend_out;
        dlg.("is_holiday_out")     = v_is_holiday_out;
        dlg.("timeBegin_in")       = v_timebegin_in;
        dlg.("timeEnd_in")         = v_timeend_in;
        dlg.("is_holiday_in")      = v_is_holiday_in;
        UpdateFields(dlg);

      elif ((cmd == DLG_REMFOCUS))
        if( (id == FldIndex ("timeBegin_out_our")))
          if(not check_time(dlg.("timeBegin_out_our"))) 
            return CM_CANCEL;
          end;
          return CM_DEFAULT;
        elif( (id == FldIndex ("timeEnd_out_our")))
          if(not check_time(dlg.("timeEnd_out_our"))) 
            return CM_CANCEL;
          end;
          return CM_DEFAULT;
        elif( (id == FldIndex ("timeBegin_out")))
          if(not check_time(dlg.("timeBegin_out"))) 
            return CM_CANCEL;
          end;
          return CM_DEFAULT;
        elif( (id == FldIndex ("timeEnd_out")))
          if(not check_time(dlg.("timeEnd_out"))) 
            return CM_CANCEL;
          end;
          return CM_DEFAULT;
        elif( (id == FldIndex ("timeBegin_in")))
          if(not check_time(dlg.("timeBegin_in"))) 
            return CM_CANCEL;
          end;
          return CM_DEFAULT;
        elif( (id == FldIndex ("timeEnd_in")))
          if(not check_time(dlg.("timeEnd_in"))) 
            return CM_CANCEL;
          end;
          return CM_DEFAULT;
        end;
      elif (cmd == DLG_KEY) 
        if ((key == Key_F2) or (key == Key_F9))
           v_corschem           = dlg.("corschem_id");
           v_bic                = dlg.("bic");
           v_timebegin_out_our  = dlg.("timeBegin_out_our");
           v_timeend_out_our    = dlg.("timeEnd_out_our");
           v_is_holiday_out_our = dlg.("is_holiday_out_our");
           v_timebegin_out      = dlg.("timeBegin_out");
           v_timeend_out        = dlg.("timeEnd_out");
           v_is_holiday_out     = dlg.("is_holiday_out");
           v_timebegin_in       = dlg.("timeBegin_in");
           v_timeend_in         = dlg.("timeEnd_in");
           v_is_holiday_in      = dlg.("is_holiday_in");

           if((not check_time(dlg.("timeEnd_out_our"))) or
              (not check_time(dlg.("timeBegin_out_our"))) or
              (not check_time(dlg.("timeEnd_out"))) or
              (not check_time(dlg.("timeBegin_out"))) or
              (not check_time(dlg.("timeEnd_in"))) or
              (not check_time(dlg.("timeBegin_in"))))
               return CM_IGNORE;
           end;

           if(isupdate)//Обновляем запись
              if (not Update_usr_me_time(v_id, v_corschem, v_bic,
                                         v_timebegin_out_our, v_timeend_out_our, v_is_holiday_out_our,
                                         v_timebegin_out, v_timeend_out, v_is_holiday_out,
                                         v_timebegin_in, v_timeend_in, v_is_holiday_in))
                 msgbox("Не обновилось!!!");
              end;
              ischanged = true;
           else//Вставляем запись 
              if (not Insert_usr_me_time( v_corschem, v_bic,
                                          v_timebegin_out_our, v_timeend_out_our, v_is_holiday_out_our,
                                          v_timebegin_out, v_timeend_out, v_is_holiday_out,
                                          v_timebegin_in, v_timeend_in, v_is_holiday_in))
                 msgbox("Не обновилось!!!");
              end;
              ischanged = true;
           end;
           return CM_SAVE;
        elif (key == Key_F3)
         if ((id == FldIndex ("corschem_id")) or
             (id == FldIndex ("corschem_name")))

           if (dlg.("corschem_isnostro") == "Лоро")
             isnostro = strfor(0);
           else
             isnostro = strfor(88);
           end;
           if (ListCorschem (Corschem,"Выберите Корсхему", isnostro))
              UpdateFields(dlg);
            else
              if (getCorschemName(corschem.number) != "Неизвестно")
                dlg.("corschem_isnostro") = getCorschemType(corschem.number);
                dlg.("corschem_id") = corschem.number;
                dlg.("corschem_name") = getCorschemName(corschem.number);
              else
                msgbox("Данная схема не является рублевой");
              end;
              UpdateFields(dlg);
           end;
         end;
        elif (key == Key_ESC)
          return CM_SAVE;
        elif (key == KEY_SPACE)
         if (id == FldIndex ("is_holiday_out_our"))
           if (dlg.("is_holiday_out_our") == "X") 
             dlg.("is_holiday_out_our") = strfor(0); 
           else 
             dlg.("is_holiday_out_our") = "X"; 
           end;
         elif (id == FldIndex ("is_holiday_out"))
           if (dlg.("is_holiday_out") == "X") 
             dlg.("is_holiday_out") = strfor(0); 
           else 
             dlg.("is_holiday_out") = "X"; 
           end;
         elif (id == FldIndex ("is_holiday_in"))
           if (dlg.("is_holiday_in") == "X") 
             dlg.("is_holiday_in") = strfor(0); 
           else 
             dlg.("is_holiday_in") = "X"; 
           end;
         elif ((id == FldIndex ("corschem_id")) or
               (id == FldIndex ("corschem_name")))
           dlg.("corschem_isnostro") = "Все";
           dlg.("corschem_id") = 0;
           dlg.("corschem_name") = getCorschemName(0);
           UpdateFields(dlg);
         elif (id == FldIndex ("corschem_isnostro"))
           if (dlg.("corschem_isnostro") == "Ностро")
             dlg.("corschem_isnostro") = "Лоро";
           else
             dlg.("corschem_isnostro") = "Ностро";
           end;
           UpdateFields(dlg);
         end;
        end;
      end;
       return CM_DEFAULT;
    End;

    Macro Run
        return RunDialog (this, R2M (this, "Handler"));
    End;
End;


//Формируем панельку
Macro MkDatePanel( lbr )

Var	rc = lbr.AddPanel( "metime", 40, 12, 50, 15, lbr.EColour.SHLP, lbr.EFRame.UNARY ),
	elem;

       rc.StatusLine = "Пробел - Поменять,  F3 - Справочник,  F9 - Сохранить,  ESC - Выход";
       rc.title = "Ввод/Редактирование интервала";

	elem = rc.AddLabel( 3, 2, "Корсхема:");
	elem = rc.AddField( 10, 2, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "corschem_isnostro", 6 );
	elem = rc.AddField( 17, 2, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "corschem_id", 4 );
	elem = rc.AddField( 22, 2, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "corschem_name", 25, 1, 100 );

	elem = rc.AddLabel( 3, 3, "БИКи:");
	elem = rc.AddField( 10, 3, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "bic", 37, 1, 100 );

	elem = rc.AddLabel( 3, 5, "Начало для исходящих внутренних:");
	elem = rc.AddField( 27, 5, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "timebegin_out_our",12 );

	elem = rc.AddLabel( 3, 6, "Конец для исходящих внутренних:");
	elem = rc.AddField( 27, 6, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "timeend_out_our",12 );
	
	elem = rc.AddLabel( 3, 7, "Признак выходного дня");
	elem = rc.AddField( 20, 7, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "is_holiday_out_our",1 );

	elem = rc.AddLabel( 3, 9, "Начало для исходящих внешних:");
	elem = rc.AddField( 27, 9, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "timebegin_out",12 );

	elem = rc.AddLabel( 3, 10, "Конец для исходящих внешних:");
	elem = rc.AddField( 27, 10, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "timeend_out",12 );
	
	elem = rc.AddLabel( 3, 11, "Признак выходного дня");
	elem = rc.AddField( 20, 11, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "is_holiday_out",1 );

	elem = rc.AddLabel( 3, 13, "Начало для входящих:");
	elem = rc.AddField( 27, 13, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "timebegin_in",12 );

	elem = rc.AddLabel( 3, 14, "Конец для входящих:");
	elem = rc.AddField( 27, 14, lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "timeend_in",12 );
	
	elem = rc.AddLabel( 3, 15, "Признак выходного дня");
	elem = rc.AddField( 20, 15, lbr.EFieldType.NOEDIT, lbr.EFocus.YES, lbr.EDataType.V_STRING, "is_holiday_in",1 );

end;

Macro MkLbrFile()

Var  LbrName = "..\\txtfile\\panel"+usernumber()+".lbr",
     lbr = TLbrFile( LbrName, "C" );
     MkDatePanel( lbr );
     lbr.Save();
     lbr = NULL;
     return LbrName;
end;

/*********************************************************************************************************/

 Var LbrName = MkLbrFile();
 Var Parm = TParm(LbrName);


/*********************************************************************************************************/
//Скроллинг
MACRO me_time_scroll ()
  var CmdText,Command, deleting=false;
  var col = TArray;


  //Обработчик для скроллинга
  macro EvProc (RecordSet, Command, id, key )
    if ((Command == DLG_KEY) and (key == 13)) //Enter

       isupdate = true;           
       //Панель изменения записи
       v_corschem           = RecordSet.value(0);
       v_bic                = RecordSet.value(1);
       v_timebegin_out_our  = RecordSet.value(2);
       v_timeend_out_our    = RecordSet.value(3);
       v_is_holiday_out_our = RecordSet.value(4);
       v_timebegin_out      = RecordSet.value(5);
       v_timeend_out        = RecordSet.value(6);
       v_is_holiday_out     = RecordSet.value(7);
       v_timebegin_in       = RecordSet.value(8);
       v_timeend_in         = RecordSet.value(9);
       v_is_holiday_in      = RecordSet.value(10);
       v_id                 = RecordSet.value(11);

       Parm.Run(); 

       if(ischanged)
          return CM_Select;
       else
          return CM_IGNORE;
       end;                       

    elif ((Command == DLG_KEY) and (key == 323))//F9

       v_corschem           = "0";
       v_bic                = "*";
       v_timebegin_out_our  = "00:00";
       v_timeend_out_our    = "00:00";
       v_is_holiday_out_our = strfor(0);
       v_timebegin_out      = "00:00";
       v_timeend_out        = "00:00";
       v_is_holiday_out     = strfor(0);
       v_timebegin_in       = "00:00";
       v_timeend_in         = "00:00";
       v_is_holiday_in      = strfor(0);

       isupdate = false;
       //Панель ввода новой записи
       Parm.Run(); 
    
       if(ischanged)
          return CM_Select;
       else
          return CM_IGNORE;
       end;                         
    elif ((Command == DLG_KEY) and (key == KEY_F8))

       gettrue(deleting,"Удалить запись?");
       if(deleting)
          delete_usr_me_time(RecordSet.value(11)); 
       end;  
       return CM_Select;
    elif ((Command == DLG_KEY) and (key == KEY_CTRL_R))
       return CM_Select;
    elif ((Command == DLG_KEY) and (key == 27))
       exitscroll = true;
    end;

 end;

  //Добавляем колонки в скроллинг
  macro AddCol (ar,ind, fld, head, width, rdonly)
    ar.value (ind * 6)     = fld;
    ar.value (ind * 6 + 1) = head;
    ar.value (ind * 6 + 2) = width;
    ar.value (ind * 6 + 3 ) = 2;   // fldType
    ar.value (ind * 6 + 4 ) = -1;  // decPoint
    ar.value (ind * 6 + 5 ) = 0;   // reserv
  end;

  AddCol (col, 0, "t_corschem", "Корсхема", 8, true);
  AddCol (col, 1, "t_bic", "БИК", 15, true);
  AddCol (col, 2, "t_timebegin_out_our", "Начало внут", 9, true);
  AddCol (col, 3, "t_timeend_out_our", "Конец внут", 9, true);
  AddCol (col, 4, "t_is_holiday_out_our", "Выходной", 7, true);
  AddCol (col, 5, "t_timebegin_out", "Начало внеш", 9, true);
  AddCol (col, 6, "t_timeend_out", "Конец внеш", 9, true);
  AddCol (col, 7, "t_is_holiday_out", "Выходной", 7, true);
  AddCol (col, 8, "t_timebegin_in", "Начало вход", 9, true);
  AddCol (col, 9, "t_timeend_in", "Конец вход", 9, true);
  AddCol (col,10, "t_is_holiday_in", "Выходной", 8, true);

  //Выбираем данные из таблицы
  CmdText = "SELECT t.t_corschem, " +
            "       t.t_bic, " +
            "	    substr(t.t_timebegin_out_our,1,5) t_timebegin_out_our, " +
            "       substr(t.t_timeend_out_our,1,5)   t_timeend_out_our, "+
            "	    t.t_is_holiday_out_our, "+
            "	    substr(t.t_timebegin_out,1,5) t_timebegin_out, " +
            "       substr(t.t_timeend_out,1,5)   t_timeend_out, "+
            "	    t.t_is_holiday_out, "+
            "	    substr(t.t_timebegin_in,1,5) t_timebegin_in, " +
            "       substr(t.t_timeend_in,1,5)   t_timeend_in, "+
            "	    t.t_is_holiday_in, "+
            "	    t.t_id " +
            "	 FROM usr_me_time_paym_dbt t " +
            " ORDER BY t_corschem " ;

  Command = RSDCommand(CmdText);
  Command.Execute();
  RecordSet = RSDRecordSet(Command, RSDVAL_CLIENT, RSDVAL_STATIC);

  //Запускаем скроллинг
  runScroll(RecordSet, 11, col, null, @EvProc, "Временные интервалы", "Enter - Редактировать,  F9 - Ввод,  ESC - Выход", false);


END;


/******************************************************/
/*                   Точка входа                      */
/******************************************************/

//Пока не нажмем Esc не выйдем
MACRO Run_panel() 
  while(not exitscroll)
     me_time_scroll();
  end;
  exit(1);
end;

//Run_panel();


