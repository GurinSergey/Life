/**********************************************************************/
/* Проверка соответствия дат валютирования и срока платежа            */
/* в клиентском п/п, установка соответствия по дате валютирования     */
/* RSSL - A.Gregeradsky - 24.11.2009                                  */
/**********************************************************************/

import rsd;

/* Обновление даты "Срок платежа" */
/* Используется явное управление транзакциями - по любой ошибке транзакция откатывается */
macro ProcessPmDates(pmid,valdat)

var cmd, vldat;

  vldat = string(valdat);

  RslDefCon.BeginTrans();

  cmd = RsdCommand("UPDATE dpmrmprop_dbt SET t_paydate=to_date('"+vldat+"','DD.MM.YYYY') WHERE t_paymentid="+pmid);
  cmd.execute();

  RslDefCon.CommitTrans();

  return 0;

OnError

  if(RslDefCon.IsInTrans)
    RslDefCon.RollBackTrans();
  end;

  return 1;

end;


/* Проверка соответствия дат */
macro TestDates(pmid)

var cmd, rs, valdat, paydat, stat;

stat = 0;

 cmd = RsdCommand("SELECT paym.t_valuedate as valdat, rm.t_paydate as paydat FROM dpmpaym_dbt paym, dpmrmprop_dbt rm WHERE rm.t_paymentid=paym.t_paymentid AND paym.t_paymentid=?");
 cmd.AddParam("", RSDBP_IN, pmid);
 cmd.execute();

 rs = RsdRecordSet(cmd);

 if(rs.MoveNext())
   valdat = date(rs.value("valdat"));
   paydat = date(rs.value("paydat"));
   if(valdat != paydat)
     stat = ProcessPmDates(pmid,valdat);
   else
     stat = 0;
   end;
 else
   return 1;
 end;
 
 return stat;

end;