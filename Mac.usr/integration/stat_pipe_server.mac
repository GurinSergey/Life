/*****************************************************************************/
/* Макрос сохраняет информацию для монитора обработчиков                     */
/*                                                                           */
/* 22.10.12 Golovkin поменял Hello message на ready, изменил вывод ошибки    */
/* 23.10.12 Golovkin удалил закомментированный код, написал комментарий выше */
/* 24.10.12 Golovkin обрезал MsgValue, ибо не помещается в поле t_msgvalue   */
/*****************************************************************************/

IMPORT RSD,"ProcInit.inc","LogProc.mac", "OperationsProc.mac", globals, LikePy;

CLASS	StatPaym()

   PRIVATE VAR SID,
               StartTime,
               TimeBefore,
               TimeAfter,
               DeltaTime,
               ErrorMessage,
               CurrentTime,
               LastTime,
               DayDocCount,
               DayDocTime;

   MACRO DiffDate(Date1, Date2)
      VAR d1 = Date(Date1),
          d2 = Date(Date2),
          t1 = int(Time(Date1)),
          t2 = int(Time(Date2)),
          DDay  = (d2 - d1)*8640000,
          DTime = t2 - t1;
      RETURN DDay + DTime;
   END;

   MACRO TimeBeginOperation()    
      TimeBefore = DtTm(Date, Time);
   END;

   MACRO TimeEndOperation()
      TimeAfter  = DtTm(Date, Time);
   END;

   MACRO RegisterOperationForMonitor(Stat,PipeName,MsgValue)
      VAR rs, cmd;

      if((Stat == "") or (ValType(Stat) != V_STRING))
         Stat = "unknown";
      end;
      DeltaTime = DiffDate(TimeBefore, TimeAfter);

      MsgValue = SubStr(MsgValue,1,500); /* 24.10.12 Golovkin */

      if(substr(MsgValue,1,1) == 7)
         ErrorMessage = "ready"; /* 22.10.12 Golovkin */
      else
         ErrorMessage = stat;
      end;

      cmd = RsdCommand(" SELECT * FROM USR_FIXDOC_DBT WHERE T_SID = ? AND T_STARTTIME = ? ");

      cmd.addparam("t_sid", RSDBP_IN, SID);
      cmd.addparam("t_starttime", RSDBP_IN, StartTime);
      rs = RsdRecordSet(cmd,RSDVAL_CLIENT,RSDVAL_STATIC);
      if(rs.movenext())
         /* обновляем запись в usr_fixdoc_dbt */
         CurrentTime = DtTm(Date,Time);
         LastTime = rs.value("t_lasttime");
         if( Int( Date( CurrentTime ) ) > Int( Date( LastTime ) ) )
            /* если новый день */
            DayDocCount = 1;
            DayDocTime = DeltaTime;
         else
            DayDocCount	= rs.value("t_daydoc") + 1;
            DayDocTime = rs.value("t_daydocavertime") + DeltaTime;                 
         end;
         rs.Edit();
         rs.value("t_lasttime") = CurrentTime;
         rs.value("t_errmsg") = ErrorMessage;
         rs.value("t_alldoc") = rs.value("t_alldoc") + 1;
         rs.value("t_daydoc") = DayDocCount;
         rs.value("t_alldocavertime") = rs.value("t_alldocavertime") + DeltaTime;
         rs.value("t_daydocavertime") = DayDocTime;
         rs.value("t_sender_pipename") = PipeName;
         rs.update();
      end;
      cmd = RsdCommand(" INSERT INTO USR_ROBOTSLOG_DBT VALUES (?,?,SYSDATE,?,?,?,?) ");
      cmd.addparam("t_sid", RSDBP_IN, SID);
      cmd.addparam("t_starttime", RSDBP_IN, StartTime);
      cmd.addparam("t_doctime", RSDBP_IN, DeltaTime);
      cmd.addparam("t_robot", RSDBP_IN,	{oper});
      cmd.addparam("t_msgvalue", RSDBP_IN, MsgValue);
      cmd.addparam("t_error", RSDBP_IN, ErrorMessage);
      cmd.execute();

      ONERROR(x) /* 22.10.12 Golovkin */
         [=================];
         [Возникла ошибка при обновлении данных для монитора обработчиков.];
         [ ];
         [Сообщение:];
         [#](MsgValue);
         [ ];
         [Ответное сообщение обработчика:];
         [#](Stat);
         [ ];
         [Ошибка:];
         [#](x.Message);
         [=================];
         return 0;
   END;

//---------------------------------
/*          конструктор          */
//---------------------------------
	
   /* получаем id сессии и время запуска обработчика */	

   Var rs = RSDRecordSet(" SELECT SID,LOGON_TIME,AUDSID,SERIAL# FROM V$SESSION WHERE SID = (SELECT RSBSESSIONDATA.CNUM FROM DUAL) ");
       rs.movenext(0);
   Var AudSid = rs.value(2),
       Serial = rs.value(3);
       SID = int(rs.value(0));
       StartTime = rs.value(1);
       CurrentTime = DtTm(Date,Time);

   /* добавляем запись в таблицу usr_fixdoc_dbt */

   Var cmd = RsdCommand(" INSERT INTO USR_FIXDOC_DBT VALUES (?,?,?,?,?,?,?,?,?,?,?,?,?) ");

       cmd.addparam("t_sid", RSDBP_IN, SID);
       cmd.addparam("t_starttime", RSDBP_IN, StartTime);
       cmd.addparam("t_lastTime", RSDBP_IN, CurrentTime);
       cmd.addparam("t_logfile", RSDBP_IN, LogPathRunOperationStep);
       cmd.addparam("t_errmsg", RSDBP_IN, "no_error");
       cmd.addparam("t_alldoc", RSDBP_IN, 0);
       cmd.addparam("t_daydoc", RSDBP_IN, 0);
       cmd.addparam("t_alldocavertime", RSDBP_IN, 0);
       cmd.addparam("t_daydocavertime", RSDBP_IN, 0);
       cmd.addparam("t_robot", RSDBP_IN, {oper});
       cmd.addparam("t_sender_pipename", RSDBP_IN, "no_pipename");
       cmd.addparam("t_audsid", RSDBP_IN, AudSid);
       cmd.addparam("t_serial", RSDBP_IN, Serial);
       cmd.execute();
		
//---------------------------------
/*      конец конструктора       */
//---------------------------------
ONERROR(x)
println("Возникла ошибка при создании объекта класса StatPaym");
Println(x.Message);
[=================];
END;
