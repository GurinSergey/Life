/******************************************************************************
   Имя файла.:       NotesProc.mac
   Назначение:       Реализация интерфейса "Примечания"
   Дата......:       22.08.2008
   Автор.....:       Стадник Александр
******************************************************************************/
import "CommonInt.mac", "ProcInit.inc", "LogProc.mac";

file notes("notekind.dbt") key 0;

// Преобразование значения категории в стандартный тип
macro ConvertValueToNoteType(ObjectType, NoteKind, NoteValue : string, ErrorMessage : @string)
   var Type;
   var val;

   notes.ObjectType = ObjectType;
   notes.NoteKind   = NoteKind;

   ErrorMessage = "";

   if (GetEQ(notes))
      Type = notes.NoteType;

      if ((Type == FT_INT) or (Type == FT_LONG))
         val = Int(NoteValue);
      elif ((Type == FT_FLOAT) or (Type == FT_FLOATG) or (Type == FT_DOUBLE) or (Type == FT_DOUBLEG))
         val = Double(NoteValue);
      elif ((Type == FT_LDMONR) or (Type == FT_NUMERIC))
         val = Money(NoteValue);
      elif (Type == FT_DATE)
         val = Date(NoteValue);
      elif ((Type == FT_TIME) or (Type == FT_SHTM))
         val = Time(NoteValue);
      elif ((Type == FT_CHR) or (Type == FT_UCHR) or (Type == FT_STRING) or (Type == FT_SNR) or (Type == FT_NUMSTR))
         val = String(NoteValue);
      else
         val = String(NoteValue);
      end;
   else
      ErrorMessage = "Не найдено примечание вида " + NoteKind;        
   end;

   return val;
end;

// Создание примечания
macro rslSetNoteValue(Str : string)
    
   var ArrayFields = TArray;     
   var stat;
   var ErrorMessage : string = "";
    
   ParseStringToArray(Str, ArrayFields);
    
   var ObjectType = Int(ArrayFields(setNoteFldObjectType));
   var ObjectID   = ArrayFields(setNoteFldObjectID);
   var NoteKind   = Int(ArrayFields(setNoteFldNoteKind));    
   var NoteValue  = ArrayFields(setNoteFldNoteValue);     

   //TAM 10.04.2014 C-28570 - УИН пишем не в примечание
   if ((ObjectType == 501/*Платеж*/) and (NoteKind == 53))
      ExecMacroFile("lib_pm_check.mac", "SetPaymentUIN", ObjectID, NoteValue);
   else
      var UniObjectID = GetUniID(ObjectType, ObjectID, @ErrorMessage);
      var Value;

      if (strlen(UniObjectID) != 0) 
         Value = ConvertValueToNoteType(ObjectType, NoteKind, NoteValue, @ErrorMessage);
         if (ValType(Value) != V_UNDEF)
            stat = AddNoteForObject(ObjectType, UniObjectID, NoteKind, Value);
            if (stat != 0)
               ErrorMessage = "Ошибка создания примечания для объекта " + ObjectID;
            else
               ErrorMessage = no_error_msg;
            end;                      
         end;
      end;
   end;
   
   LogSetNoteValue(Str, ArrayFields, ErrorMessage);    
   return ErrorMessage;     
end;


// Удаление примечания 
macro rslDelNoteValue(Str : string)
    
   var ArrayFields = TArray;     
   var stat;
   var ErrorMessage : string = "";
    
   ParseStringToArray(Str, ArrayFields);
    
   var ObjectType = Int(ArrayFields(delNoteFldObjectType));
   var ObjectID   = ArrayFields(delNoteFldObjectID);
   var NoteKind   = Int(ArrayFields(delNoteFldNoteKind));    

   var UniObjectID = GetUniID(ObjectType, ObjectID, @ErrorMessage);

   if (strlen(UniObjectID) != 0)
      stat = RemoveNoteForObject(ObjectType, UniObjectID, NoteKind);
   
      if (stat == false)
         ErrorMessage = "Ошибка удаления примечания для объекта " + ObjectID;
      else
         ErrorMessage = no_error_msg;
      end;          
   end;
   
   LogDelNoteValue(Str, ArrayFields, ErrorMessage);    

   return ErrorMessage;     
end;