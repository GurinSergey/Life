// -------------------------------------------------------------------------------------------------
// @filename: ExecERCDocs_Report.mac
// @author  : Zlenko M.P. 
// @desc    : ОТЧЕТ ПО АВТОМАТИЧЕСКОМУ СПИСАНИЮ ДОКУМЕНТОВ РОБОТОМ ЕРЦ
// @request : C-17511 
// -------------------------------------------------------------------------------------------------
Import lib_sqltools, TReport, RSD;

macro ViewReportERC()
var SQL, rs;
var ReportName = getTxtFileName ("ReportERC");
var table = CTableReport();
    table.addColumn ("№ п/п ",               3,  AL_RIGHT);
    table.addColumn ("ID Платежа ",          10, AL_RIGHT);
    table.addColumn ("№ Документа ",         10, AL_RIGHT);
    table.addColumn ("Счет плательщика",     20, AL_RIGHT);    
    table.addColumn ("Сумма п/п",            32, AL_RIGHT);
    table.addColumn ("№ пачки",              7,  AL_RIGHT);    
    table.addColumn ("Состояние",            30, AL_RIGHT);    
    
    startQueryCapture ();
[/* Formatted on 04.04.2013 13:46:47 (QP5 v5.115.810.9015) */
SELECT   DISTINCT PM.T_PAYMENTID PaymentID,
                  PROP.T_NUMBER NumDoc,
                  PM.T_PAYERACCOUNT PayerAccount,
                  pm.t_amount Amount,
                  PM.T_NUMBERPACK NumPack,
                  CASE st.t_isexecute
                     WHEN 'R'
                     THEN
                        CASE st.t_symbol
                           WHEN 'J' THEN 'отвергнут'
                           WHEN 'К' THEN 'на визуальном контроле'
                           WHEN '8' THEN 'на согласовании'
                           WHEN 'f' THEN 'ожидает выгрузки во Фронт'
                           WHEN '7' THEN 'на валютном контроле'
                           WHEN 'Ф' THEN 'ожидает выгрузки в МБР'
                           WHEN 'У' THEN 'приостановлен(террорист)'
                           WHEN 'Ж' THEN 'ожидание в К2'
                           ELSE 'открыт'
                        END
                     WHEN 'X'
                     THEN
                        'закрыт'
                  END
                     List
  FROM   usr_ReportERC tmp,
         doprstep_dbt st,
         dpmpaym_dbt pm,
         dpmrmprop_dbt prop
 WHERE   st.t_id_operation = tmp.t_id_operation
         AND st.t_id_step =
               (SELECT   MAX (t_id_step)
                  FROM   doprstep_dbt t
                 WHERE   t_id_operation = st.t_id_operation
                         AND t_isexecute <> CHR (0))
         AND pm.t_paymentid = tmp.t_paymentid
         AND PROP.T_PAYMENTID = pm.t_paymentid
         ORDER BY PayerAccount, NumPack
];
    SQL = endQueryCapture ();
    rs = RsdRecordset(SQL);
    SetOutput (reportName, false);
    table.printHead ("                                                       Документы проведенные роботом ЕРЦ ");
    var i = 1;
    while (rs and rs.movenext())
        table.printStringTransferByWord ( i,
                                          rs.value ("PaymentID"), 
                                          rs.value ("NumDoc"), 
                                          rs.value ("PayerAccount"), 
                                          rs.value ("Amount"),  
                                          rs.value ("NumPack"), 
                                          rs.value ("List")
                                        );
        i = i + 1;
    end;            
    table.printBottom ("Всего :" + (i - 1));
    SetOutput (NULL, true);
    ViewFile(ReportName);
end;
//ViewReportERC();
