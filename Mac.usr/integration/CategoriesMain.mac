/**********************************************************************/
/* Проверка необходимости и установка отметки "Основной"              */
/* на признак неисключительной категории                              */
/* RSSL - A.Gregeradsky - 23.11.2009                                  */
/**********************************************************************/

import rsd;

/* Проверка признака - вызов из основного макроса работы с категориями в интеграции */
macro TestMainAttr(ObjectType, ObjectID, GroupID, AttrID, UniObjectID)

  var cmd, rs;

  /* Проверка исключительности категории */
  cmd = RsdCommand("SELECT * FROM dobjgroup_dbt WHERE t_objecttype = ? AND t_groupid = ?");
  cmd.AddParam("", RSDBP_IN, ObjectType);
  cmd.AddParam("", RSDBP_IN, GroupID);
  cmd.execute();

  rs = RsdRecordSet(cmd);

  if((rs.MoveNext()) and (rs.value("t_type") == "X"))
    return 0; /* Если категория исключительная - в признаке нет необходимости */
  end;

  /* Проверка на то, установлены ли уже какие-либо признаки в этой категории на объекте */
  cmd = RsdCommand("SELECT count(*) FROM dobjatcor_dbt WHERE t_objecttype=? AND t_groupid=? AND t_object=?");
  cmd.AddParam("", RSDBP_IN, ObjectType);
  cmd.AddParam("", RSDBP_IN, GroupID);
  cmd.AddParam("", RSDBP_IN, UniObjectID);
  cmd.execute();

  rs = RsdRecordSet(cmd);

  if((rs.MoveNext()) and (int(rs.value(0)) > 0))
    return 0; /* Уже есть ранее установленный признак, текущий не будет основным */
  end;

  return 1;   /* Нет признаков в неисключительной категории, установить отметку "Основной" */

end;

/*VDN 29.10.2014 C-33844 Удаление признака "Основной" */
macro DelGeneralAttr (ObjectType, GroupID, UniObjectID)
  var cmd, AttrID;
  
  /*Находим основную*/
  cmd = RsdCommand("SELECT t_attrid FROM dobjatcor_dbt WHERE t_objecttype=? AND t_groupid=? AND t_object=? AND t_general = CHR(88)");
  cmd.AddParam("", RSDBP_IN, ObjectType);
  cmd.AddParam("", RSDBP_IN, GroupID);
  cmd.AddParam("", RSDBP_IN, UniObjectID);
  cmd.execute();

  AttrID = RsdRecordSet(cmd);
  if (AttrID.MoveNext())
    RslDefCon.BeginTrans();
    /*Удаляем основную*/
    cmd = RsdCommand("UPDATE dobjatcor_dbt "+
                     "SET t_general = chr(0), "+
                     "    t_oper = rsbsessiondata.oper, "+
                     "    t_sysdate = trunc(sysdate), "+
                     "    t_systime = to_date('01.01.0001 '|| to_char(sysdate, 'hh24:mi:ss'), 'dd.mm.yyyy hh24:mi:ss') "+
                     "WHERE t_objecttype=? AND t_groupid=? AND t_object=? AND t_attrid=?");
    cmd.AddParam("", RSDBP_IN, ObjectType);
    cmd.AddParam("", RSDBP_IN, GroupID);
    cmd.AddParam("", RSDBP_IN, UniObjectID);
    cmd.AddParam("", RSDBP_IN, AttrID.value(0));

    cmd.execute();
    RslDefCon.CommitTrans();
  end;
onError
  if(RslDefCon.IsInTrans)
    RslDefCon.RollBackTrans();
  end;
end;
/* Установка признака - вызов из основного макроса работы с категориями в интеграции */
/* Используется явное управление транзакциями - по любой ошибке транзакция откатывается */
macro SetMainAttr(ObjectType, ObjectID, GroupID, AttrID, UniObjectID)

  var cmd;
  
  RslDefCon.BeginTrans();
  /*VDN 29.10.2014 Переписал запрос для возможности пренудительного вызова установки признака*/
  cmd = RsdCommand("UPDATE dobjatcor_dbt "+
                   "SET t_general = chr(88), "+
                   "    t_oper = rsbsessiondata.oper, "+
                   "    t_sysdate = trunc(sysdate), "+
                   "    t_systime = to_date('01.01.0001 '|| to_char(sysdate, 'hh24:mi:ss'), 'dd.mm.yyyy hh24:mi:ss') "+
                   "WHERE t_objecttype=? AND t_groupid=? AND t_object=? AND t_attrid=?");
  cmd.AddParam("", RSDBP_IN, ObjectType);
  cmd.AddParam("", RSDBP_IN, GroupID);
  cmd.AddParam("", RSDBP_IN, UniObjectID);
  cmd.AddParam("", RSDBP_IN, AttrID);

  cmd.execute();

  RslDefCon.CommitTrans();

return 0;

onError

  if(RslDefCon.IsInTrans)
    RslDefCon.RollBackTrans();
  end;

return 1;

end;