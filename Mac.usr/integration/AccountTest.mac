/* A.Gregeradsky - 27.11.2009 - Проверка параметров открываемого лицевого счета */

import rsd;

/* Вспомогательный класс для передачи параметров по ссылке */
private class Node

 var code:   integer;
 var kind:   integer;
 var parent: integer;

end;

var  point: Node;


/* Параметры вышестоящего узла */
macro ПолучитьВышестоящий(point)

var cmd, rs; 

cmd = RsdCommand("SELECT t_code as dep, t_nodetype as kind, t_parentcode as par FROM ddp_dep_dbt WHERE t_code = ?");
cmd.AddPAram("code", RSDBP_IN, point.parent);
cmd.execute();

rs  = RsdRecordSet(cmd);

  if(rs.MoveNext())
    point.code = rs.value("dep");
    point.kind = rs.value("kind");
    point.parent = rs.value("par");
  end;

return point;

end;

/* Проверка на опера */
macro Oper_in_base(op)

var cmd, rs; 

cmd = RsdCommand("SELECT pers.t_oper FROM dperson_dbt pers WHERE pers.t_oper = ?");
cmd.AddPAram("op", RSDBP_IN, op);
cmd.execute();

rs  = RsdRecordSet(cmd);

  if(rs.MoveNext())
   return 0;
  else 
   return 1;
  end;

end;


/* Определение филиала (узел ТС типа "ФИЛИАЛ"), к которому будет отноиться л/с - по узлу ТС операциониста, открывающего счет */
Macro DepartmentByOper(oper)

var cmd, rs; 
var kind, parent;

cmd = RsdCommand("SELECT pers.t_codedepart as code, dep.t_nodetype as kind, dep.t_parentcode as par FROM dperson_dbt pers, ddp_dep_dbt dep WHERE pers.t_oper = ? AND pers.t_codedepart = dep.t_code");
cmd.AddPAram("oper", RSDBP_IN, oper);
cmd.execute();

rs  = RsdRecordSet(cmd);

  if(rs.MoveNext())

    point.code = rs.value("code");
    point.kind = rs.value("kind");
    point.parent = rs.value("par");

    while (point.kind == 2)
      point = ПолучитьВышестоящий(point);
    end;

  end;

  if(point.kind == 1)
    return point.code;
  end;

end;
