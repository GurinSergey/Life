/*******************************************************************************/
/*  RS-Bank 6.0                                          R-Style Software Lab  */
/*                                                                             */
/*  File Name   : loadConformityTable.mac v.1                                  */
/*  Created     : 19.11.2015                                                   */
/*  Programmer  : Головкин В.Н.                                                */
/*  Description : Загрузчик таблиц соответствия.                               */
/*                Используется в процедурах переноса данных при закрытии       */
/*                филиалов ЭВ                                                  */
/*  Изменен     :                                                              */
/*******************************************************************************/

import rsd, rcw;
import "KeyCodes.mac";
/*
CREATE TABLE USR_CONFORMITY_ACC_TABLE_DBT
(
   t_frontid          NUMBER,
   t_client_name      VARCHAR2 (4000),
   t_client_inn       VARCHAR2 (50),
   t_rsid_branch      NUMBER,
   t_rscode_branch    VARCHAR2 (50),
   t_account_branch   VARCHAR2 (50),
   t_rsid             NUMBER,
   t_rscode           VARCHAR2 (50),
   t_account          VARCHAR2 (50),
   t_branch           NUMBER
)
*/
private macro openExcel
    var exObj;
    var comObj;
      
    exObj  = CreateObject ("rsax","TRsAxServer","RsAxServer",IsStandAlone());
    comObj = ExObj.CreateComObject ("Excel.Application", False);
    comObj.Application.DisplayAlerts = True;
    comObj.Workbooks.Add();

    SetParm( 0, comObj );

    return true;
onerror
    return false;
end;


macro insertRow(frontid,
                client_name,
                client_inn,
                rsid_branch,
                rscode_branch,
                account_branch,
                rsid,
                rscode,
                account,
                branch)
    private var sql = " INSERT INTO USR_CONFORMITY_ACC_TABLE_DBT VALUES (:p_frontid,"
                                                                        ":p_client_name,"
                                                                        ":p_client_inn,"
                                                                        ":p_rsid_branch,"
                                                                        ":p_rscode_branch,"
                                                                        ":p_account_branch,"
                                                                        ":p_rsid,"
                                                                        ":p_rscode,"
                                                                        ":p_account,"
                                                                        ":p_branch) ";
    private var cmd = rsdCommand(sql);
    cmd.addParam("p_frontid", RSDBP_IN, frontid);
    cmd.addParam("p_client_name", RSDBP_IN, client_name);
    cmd.addParam("p_client_inn", RSDBP_IN, client_inn);
    cmd.addParam("p_rsid_branch", RSDBP_IN, rsid_branch);
    cmd.addParam("p_rscode_branch", RSDBP_IN, rscode_branch);
    cmd.addParam("p_account_branch", RSDBP_IN, account_branch);
    cmd.addParam("p_rsid", RSDBP_IN, rsid);
    cmd.addParam("p_rscode", RSDBP_IN, rscode);
    cmd.addParam("p_account", RSDBP_IN, account);
    cmd.addParam("p_branch", RSDBP_IN, branch);
    cmd.execute();
end;

macro loadTable(branch:integer)
    private var excelDocument;
    private var maskDescription = "Все файлы (*.*),*.*";
    private var multiSelect     = false;
    private var path;
    private var excelSheet;
    private var xlCellTypeLastCel = 11;
    private var rowsCount;
    private var i;

    private var frontid,
                client_name,
                client_inn,
                rsid_branch,
                rscode_branch,
                account_branch,
                rsid,
                rscode,
                account;  

    if(openExcel(excelDocument))
        path = excelDocument.application.GetOpenFileName(maskDescription, null, "Выберите файл для загрузки", null, MultiSelect);
        excelDocument.workbooks.open( path );

        excelSheet = excelDocument.sheets(1);
        rowsCount = excelSheet.Cells(1,1).SpecialCells(xlCellTypeLastCel).Row;
        initProgress(rowsCount, "Загрузка таблицы соответствия", "Загрузка таблицы соответствия" );

        var cmd = rsdCommand("delete from USR_CONFORMITY_ACC_TABLE_DBT where t_branch = :p_branch");
        cmd.addParam("p_branch", RSDBP_IN, branch);
        cmd.execute();

        for(i, 3, rowsCount)
            frontid = excelSheet.range( "A" + i).value;
            client_name = excelSheet.range( "B" + i).value;
            client_inn = excelSheet.range( "D" + i).value;
            rsid_branch = excelSheet.range( "E" + i).value;
            rscode_branch = excelSheet.range( "F" + i).value;
            account_branch = excelSheet.range( "I" + i).value;
            rsid = excelSheet.range( "K" + i).value;
            rscode = excelSheet.range( "L" + i).value;
            account = excelSheet.range( "O" + i).value;

            insertRow(frontid,
                      client_name,
                      client_inn,
                      rsid_branch,
                      rscode_branch,
                      account_branch,
                      rsid,
                      rscode,
                      account,
                      branch);

            useProgress( i );
        end;

        remProgress;
        excelDocument.quit;
    end;

onerror
debugbreak;    
    if(excelDocument)
        excelDocument.quit;
    end;
end;

macro AddCol (ar, ind, fld, head, width, rdonly, DecPoint)
   ar.value (ind * 6)     = fld;
   ar.value (ind * 6 + 1) = head;
   ar.value (ind * 6 + 2) = width;
   if(rdonly)
      ar.value (ind * 6 + 3 ) = 2;
   else
      ar.value (ind * 6 + 3 ) = 5;
   end;
   ar.value (ind * 6 + 4 ) = decPoint;
   ar.value (ind * 6 + 5 ) = 0;
end;

macro confTableScroll(branch:integer)
    private var ESC_KEY_FLAG = false;
    private var sql = " select * from USR_CONFORMITY_ACC_TABLE_DBT where t_branch = :p_branch ";
    private var cmd = rsdCommand(sql);
    cmd.addParam("p_branch", RSDBP_IN, branch);
    
    private var rs = RsdRecordSet(cmd,RSDVAL_CLIENT,RSDVAL_STATIC);

    MACRO EvProc(rs, cmd, id, key)

        if ((cmd == DLG_KEY) and (key == KEY_F9))
           loadTable(branch);
           return CM_SELECT;
        end;

        if ((cmd == DLG_KEY) and (key == KEY_F2))
           return CM_CANCEL;
        end;

        if ((cmd == DLG_KEY) and (key == KEY_ESC))
           ESC_KEY_FLAG = true;
           return CM_CANCEL;
        end;

    END;

    var col = TArray();
        AddCol (col, 0, "t_frontid", "ИД Клиента во Фронт", 10, false, 0);
        AddCol (col, 1, "t_client_name", "Наименование", 30, false);  
        AddCol (col, 2, "t_client_inn", "ИНН", 15, false);
        AddCol (col, 3, "t_rsid_branch", "ИД в РС(филиал)", 10, false, 0);    
        AddCol (col, 4, "t_rscode_branch", "РС КОД(филиал)", 10, false);
        AddCol (col, 5, "t_account_branch", "Счет(филиал)", 25, false);
        AddCol (col, 6, "t_rsid", "ИД в РС", 10, false, 0);
        AddCol (col, 7, "t_rscode", "РС КОД", 10, false);
        AddCol (col, 8, "t_account", "Счет", 25, false);

    while(RunScroll(rs, 9, col, null, "EvProc","Счета","~F2~ Продолжить ~F9~ Загрузить новую таблицу соответствия"))//,null,null,null,null,null,10))
        cmd = rsdCommand(sql);
        cmd.addParam("p_branch", RSDBP_IN, branch);
        rs = RsdRecordSet(cmd,RSDVAL_CLIENT,RSDVAL_STATIC);
    end;

    return not ESC_KEY_FLAG;
end;

macro branchScroll()
    private var sql = 
    " select 0,'ЭВ-Ставрополь' branch from dual " +
    " union all                                 " +                                  
    " select 1,'ЭВ-Воронеж'    branch from dual " +
    " union all                                 " +
    " select 2,'ЭВ-Волгоград'  branch from dual " ;

    private var rs = RsdRecordSet(sql,RSDVAL_CLIENT,RSDVAL_STATIC);

    MACRO EvProc(rs, cmd, id, key)
        if ((cmd == DLG_KEY) and (key == 13 ))
           confTableScroll(rs.value(0,null,V_INTEGER));
           return CM_IGNORE;
        end;
    END;

    Var col = TArray();
        AddCol (col, 0, "branch", "Филиал", 30, false, 0);

    if(RunScroll(rs, 1, col, null, "EvProc","Выберите филиал","~ENTER~ Выбрать",null,null,null,null,10))
        return true;
    else
        return false
    end;

    return false;
end;

/**
 * 0 - ЭВ-Ставрополь
 * 1 - ЭВ-Воронеж
 * 2 - ЭВ-Волгоград
 */
macro conformityTableEditor(branch:integer)
    if(branch == null)
        branchScroll();
        return true;
    else
        return confTableScroll(branch);
    end;
end;

/*conformityTableEditor();

exit(1);
*/