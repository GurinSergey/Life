/*Seleznev
генерация кодов для вида 1 и 101 для субъекта */

import BankInter, PTInter, rsd;



macro SetRScode(Субъект);

   /*Константы из пакета usr_rscode*/
   const c_person_type_artificial = 1;
   const c_person_type_foreign = 2;
   const c_person_type_bank = 4;

   var persontype = 0;
   var cmd:object, stat;
   var ClientCode:object;
   var errormsg;

   if (ВидСубъекта(Субъект.PartyID,PTK_CLIENT))
      if (ВидСубъекта(Субъект.PartyID,PTK_BANK))
         persontype = c_person_type_bank;
      else
         if (Субъект.legalform == 1) //юр лицо
           persontype = c_person_type_artificial;
         end;

         if (Субъект.IsNotResident)
            persontype = persontype + c_person_type_foreign;
         end;
      end;

      rsldefcon.begintrans;

      cmd = RSDCommand("begin usr_rscode.get_rscode (?, ?, ?); end;");
      cmd.addParam("persontype", RSDBP_IN,  persontype);
      cmd.addParam("rscode",  RSDBP_OUT, V_INTEGER);
      cmd.addParam("errors",  RSDBP_OUT, V_STRING,1024);
      cmd.Execute();

      if (cmd.Param("errors").value == "no_error")

         ClientCode = Субъект.Code;
//         stat = ClientCode.SetCode(1, "0100000"+cmd.Param("rscode").value);
         // KS 02.05.2012 I-00182999 Длина кода - 12 символов
         stat = ClientCode.SetCode(1, "01" + mkstr("0",10-strlen(string(cmd.Param("rscode").value)))
                                           + cmd.Param("rscode").value);


         if (stat == true)
            stat = ClientCode.SetCode(101, cmd.Param("rscode").value);    

            if (stat == true)
                stat = Субъект.Update();
               if (stat == true)
                  rsldefcon.committrans;
                  return true;
               else
                  rsldefcon.rollbacktrans;
                  MemoryError(null,"Ошибка при обновлении субъекта");
                  return false;
               end;
            else
               rsldefcon.rollbacktrans;
               MemoryError(null,"Ошибка при установке кода вида 101");
               return false;
            end;
         else
            rsldefcon.rollbacktrans;
            MemoryError(null,"Ошибка при установке кода вида 1");
            return false;
         end;
      else            
         rsldefcon.rollbacktrans;
         MemoryError(null,"ошибка определения кода:|"+cmd.Param("errors").value);
         return false;
      end;
   else
      MemoryError(null, "субъект не является клиентом|установка кода возможна только для клиента");
      return false;
   end;
end;