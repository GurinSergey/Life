/*──────────────────────────────────────────────────────────────────────────┐
  RS-Bank 5.00                                          АКБ "Пробизнесбанк" 

  Module      : МОДБ
  File Name   : exp_scan.mac
  Description : Макрос 
  Comment     : Макрос экспорта справочников для ПО сканирования
  History     : 05.02.2002 г. Байков Д.В.
              : 08.09.2008 г. R-Style Softlab,Селезнёв адаптировал для V6
/*SDA 06.12.2011 - переписал на клоны,т.к. при апрейде слетают FMT
  для работы необходимо в OBJ положить expacc.dbf,expbank.dbf,expourb.dbf */
└───────────────────────────────────────────────────────────────────────────*/
Import BankInter, rsexts, rsbdataset, globals;


var err:integer;
var TempDir;
var expacc, expbank, out; // KS 05.04.2012 из шаблонов

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, TempDir, err);

if( err != 0)
   msgbox("Не определена директория временных файлов");
   exit(1);
end;

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\USERTEMPLSDIR", V_STRING, out, err);

if( err != 0)
   msgbox("Не определена директория шаблонов");
   exit(1);
end;

file SRC() dbf;

file fBankDbf() write dbf ;

 expbank = FindPath("expbank.dbf", out);

 if( not Open(SRC,expbank))
     MsgBox( "Невозможно открыть эталонный файл " );
    return 1;
  end;

  if( not clone(SRC, TempDir+"\\expbank." + UserNumber()) )
    MsgBox("Ошибка создания файла для выгрузки банков") ;
    return false ;
  end;
  close(SRC);

  if( not open(fBankDbf, TempDir+"\\expbank." + UserNumber()) )
    MsgBox("Ошибка открытия файла для выгрузки банков") ;
    return false ;
  end ;

file fOurBankDbf() write dbf ;

// expbank = FindPath("expbank.dbf", out);

 if( not Open(SRC,expbank))
     MsgBox( "Невозможно открыть эталонный файл " );
    return 1;
  end;

  if( not clone(SRC, TempDir+"\\expourb." + UserNumber()) )
    MsgBox("Ошибка создания файла для выгрузки реквизитов нашего банков") ;
    return false ;
  end ;
  close(SRC);
  if( not open(fOurBankDbf, TempDir+"\\expourb." + UserNumber()) )
    MsgBox("Ошибка открытия файла для выгрузки реквизитов нашего банков") ;
    return false ;
  end ;


file fAccDbf() write dbf ;

  expacc = FindPath("expacc.dbf", out);

  if( not Open(SRC,expacc))
    MsgBox( "Невозможно открыть эталонный файл " );
    return 1;
  end;
  if( not clone(SRC, TempDir+"\\expacc." + UserNumber()) )
    MsgBox("Ошибка создания файла для выгрузки банков") ;
    return false ;
  end ;
  close(SRC);
  if( not open(fAccDbf, TempDir+"\\expacc." + UserNumber()) )
    MsgBox("Ошибка открытия файла для выгрузки банков") ;
    return false ;
  end ;

Const ExpPath = "$C:\\RSPAR\\" ;

  /* выгрузка банков */
var fBank = TRSBDataSet("select ob.t_code MFO_Depart, prt.t_name Name_Depart, bnk.t_coracc Corr_Acc " +
       " from dbankdprt_dbt bnk, dparty_dbt prt, dobjcode_dbt ob " +
       " where bnk.t_partyid = prt.t_partyid and ob.t_objecttype = 3 and ob.t_codekind = 3 and ob.t_objectid = prt.t_partyid ");

While( fBank.next )     
  Message(fBank.MFO_Depart) ;
  clearrecord(fBankDbf) ;
  fBankDbf.BIC = fBank.MFO_Depart ;
  fBankDbf.CORR_ACC = fBank.Corr_Acc ;
  fBankDbf.NAME = fBank.Name_Depart ;
  if( not Insert(fBankDbf) )
    MsgBox("Ошибка при записи в файл банков !!!") ;
  end ;

  if( fBank.MFO_Depart == {MFO_Bank} )
    fOurBankDbf.BIC = fBank.MFO_Depart ;
    fOurBankDbf.CORR_ACC = fBank.Corr_Acc ;
    fOurBankDbf.NAME = fBank.Name_Depart ;
    if( not Insert(fOurBankDbf) )
      MsgBox("Ошибка при записи в файл банков !!!") ;
    end ;
  end ;
end ;

/* выгрузка счетов клиентов */
/* A.Gregeradsky - 04.12.2009 - исключены р/счета СКС из перечня счетов для выгрузки в Верификатор FR */
/* A.Gregeradsky - 15.01.2010 - поставлено условие на выгрузку в Верификатор FR только действующего кода "ИНН/КПП" (state = 0) */

var rs = TRSBDataSet(
                     "select account,client, name_client, inn, kpp from(  " +
                     "select t_account account, t_client client, prt.t_name name_client,   " +
                     "       decode( instr( ob.t_code, '/', 1 ), 0, ob.t_code, substr( ob.t_code, 1, instr( ob.t_code, '/', 1 )-1 ) ) inn,  " +
                     "       decode( instr( ob.t_code, '/', 1 ), 0,'0', substr( ob.t_code, instr( ob.t_code, '/', 1 )+1 ) )  kpp  " +
                     "  from daccount_dbt ac, dparty_dbt prt, dobjcode_dbt ob  " +
                     " where ac.t_open_close <> 'З'  " +
                     "   and ac.t_chapter = 1  " +
                     "   and ac.t_type_account not like '%П%'  " +
                     "   and ac.t_account > '40100'  " +
                     "   and ac.t_account < '41000'  " +
                     "   and prt.t_partyid = ac.t_client  " +
                     "   and ob.t_objecttype = 3  " +
                     "   and ob.t_codekind = 16  " +
                     "   and ob.t_state = 0 " +
                     "   and ob.t_objectid = prt.t_partyid " +
                     "   and ac.t_nameaccount not like 'СКС% по % карт%')"
                    );
while (rs.next)

   fAccDbf.Account = rs.Account ;
   fAccDbf.Name = rs.Name_Client ;
   fAccDbf.INN = rs.inn;
   fAccDbf.Kpp = rs.kpp;

   if( not Insert(fAccDbf) )
     MsgBox("Ошибка при записи в файл счетов клиентов !!!") ;
   end;

end;

close(fBankDbf) ;
close(fOurBankDbf) ;
close(fAccDbf) ;

if( not CopyFile(FileName(fBankDbf), ExpPath + "expbank.dbf") )
  MsgBox("Ошибка при копированиии файла expbank.dbf") ;
end ;

if( not CopyFile(FileName(fOurBankDbf), ExpPath + "expourb.dbf") )
  MsgBox("Ошибка при копированиии файла expourb.dbf") ;
end ;

if( not CopyFile(FileName(fAccDbf), ExpPath + "expacc.dbf") )
  MsgBox("Ошибка при копированиии файла expacc.dbf") ;
end ;
//msgbox(ExpPath + "expacc.dbf");

//RemoveFile("C:\\RSPAR\\impdoc.dbf");

DelFile(TempDir+"\\expacc." + UserNumber());
DelFile(TempDir+"\\expbank." + UserNumber());
DelFile(TempDir+"\\expourb." + UserNumber());