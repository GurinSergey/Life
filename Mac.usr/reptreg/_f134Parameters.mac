/**
 * RS-Bank 6.0                                            R-Style Software Lab
 *
 * File Name   : f134Parameters.mac
 * Description : Параметры, используемые в выпуске отчета
 * Comment     : 
 * @since      : 09.11.2010
 * @version    : 6.00.020.29
 */

import ReptCbInter;
import BankInter;
import RcbCoreInter;

/**
 * Глобальная ссылка для хранения параметров.
 */    
private var m_parameters = null;

class Tf134Parameters()
    var ОПФ_КО_АО,             /*форма кредитной организации АО/ООО*/
        ДатаАудПодтв_ТекГ,
        ДатаАуд_ТекГ,
        ДатаАудПодтв_ПрошлыйГ,
        ДатаАуд_ПрошлыйГ,
        ПрибыльТекущегоГодаПодтверждена,
        ПрибыльПрошлогоГодаПодтверждена,
        ПрибыльПредшествующегоГодаНеПодтверждена,
        iRealPlan = ПолучитьРеальныйНомерПлана(ЛогическийПланСчетов, "А"),
        isCalculateACForPosting,
        unconfirmedProfitPreviousYear,
        isEndDateLess0107,
        isEndDateLastDayInMonth,
        isUserInput;

    var ПрибыльПредшествующихЛетНеПодтверждена : Bool;
    var spodEndDate : Date;

    MACRO GetRegVal(Path, Type, Val)

        GetRegistryValue(Path, Type, Val, NULL);

        if (Val == NULL)
           MsgBox("Не задано значения для настройки реестра " + Path);
           установитьФлагВозврата(STOP_PROC_FLG);
           exit(1);
        end;

        SetParm(3, Val);
    END;

    private macro isLastDayInMonth(day : Date)
        var m;
        var y;
        dateSplit(day, NULL, m, y);

        if (m == 12)
            m = 1;
            y = y + 1;
        else
            m = m + 1;
        end;

        return day == Date(1, m, y) - 1;
    end;

    private macro getSpodEndDate(): Date
        var dataSet = TRsbDataSet("SELECT t_dateFinalDay, t_dateFinalMon"
            +"\n"+                "  FROM dobchaptr_dbt"
            +"\n"+                " WHERE t_chapter = 1");

        if (dataSet.moveNext())
            var currentYear : Integer;

            DateSplit(RcbApplication().currentReport.context.period.endDate, NULL, NULL, currentYear);

            return Date(dataSet.dateFinalDay, dataSet.dateFinalMon, currentYear);
        end;

        return null;
    end;

    MACRO Constructor()

        var CurrYear;
        var endDate = RcbApplication().currentReport.context.period.endDate;

        DateSplit(endDate, NULL, NULL, CurrYear);

        GetRegVal("REPTREG\\REP_GROUPS\\COMMON\\ОПФ_КО_АО",                              V_BOOL,   ОПФ_КО_АО                    );
        GetRegVal("REPTREG\\REP_GROUPS\\COMMON\\ДАТААУДПОДТВ_ПРОШЛЫЙГ",                  V_DATE,   ДатаАудПодтв_ПрошлыйГ        );
        GetRegVal("REPTREG\\REP_GROUPS\\COMMON\\ДАТААУД_ПРОШЛЫЙГ",                       V_DATE,   ДатаАуд_ПрошлыйГ             );
        GetRegVal("REPTREG\\REP_GROUPS\\COMMON\\ДАТААУДПОДТВ_ТЕКГ",                      V_DATE,   ДатаАудПодтв_ТекГ            );
        GetRegVal("REPTREG\\REP_GROUPS\\COMMON\\ДАТААУД_ТЕКГ",                           V_DATE,   ДатаАуд_ТекГ                 );
        GetRegVal("REPTREG\\REP_GROUPS\\РАСЧЕТ КАПИТАЛА\\РАСЧЕТ_УК_ПО_ПРОВОДКАМ",        V_BOOL,   isCalculateACForPosting      );
        GetRegVal("REPTREG\\REP_GROUPS\\РАСЧЕТ КАПИТАЛА\\НЕПОДТВ_ПРИБЫЛЬ_ПРЕДШЕСТВ_ЛЕТ", V_STRING, unconfirmedProfitPreviousYear);
        GetRegVal("REPTREG\\REP_GROUPS\\РАСЧЕТ КАПИТАЛА\\ПОЛЬЗОВАТЕЛЬСКИЙ ВВОД",         V_Bool,   isUserInput);

        ДатаАудПодтв_ТекГ                        = date(ДатаАудПодтв_ТекГ);
        ДатаАуд_ТекГ                             = date(ДатаАуд_ТекГ);
        ДатаАудПодтв_ПрошлыйГ                    = date(ДатаАудПодтв_ПрошлыйГ);
        ДатаАуд_ПрошлыйГ                         = date(ДатаАуд_ПрошлыйГ);
        ПрибыльТекущегоГодаПодтверждена          = date(ПрибыльТекущегоГодаПодтверждена);
        ПрибыльПрошлогоГодаПодтверждена          = date(ПрибыльПрошлогоГодаПодтверждена);
        ПрибыльПредшествующегоГодаНеПодтверждена = date(ПрибыльПредшествующегоГодаНеПодтверждена);

        ПрибыльПредшествующихЛетНеПодтверждена = NOT ((Date(1, 1, CurrYear)  <=   ДатаАудПодтв_ПрошлыйГ) and
                                                      (ДатаАудПодтв_ПрошлыйГ <=   ДатаАуд_ПрошлыйГ     ) and
                                                      (ДатаАуд_ПрошлыйГ      <= (endDate + 1)          ));
        

        ПрибыльТекущегоГодаПодтверждена = ((Date(1, 1, CurrYear) <= ДатаАудПодтв_ТекГ) and
                                           (ДатаАудПодтв_ТекГ    <= ДатаАуд_ТекГ     ) and
                                           (ДатаАуд_ТекГ         <= (endDate + 1)    ));

        ПрибыльПрошлогоГодаПодтверждена = ((Date(1, 1, CurrYear)  <=   ДатаАудПодтв_ПрошлыйГ) and
                                           (ДатаАудПодтв_ПрошлыйГ <=   ДатаАуд_ПрошлыйГ     ) and
                                           (ДатаАуд_ПрошлыйГ      <= (endDate + 1)          ));

        ПрибыльПредшествующегоГодаНеПодтверждена = ((Date(1, 1, CurrYear)  <= ДатаАудПодтв_ПрошлыйГ ) and
                                                    (ДатаАудПодтв_ПрошлыйГ <= ДатаАуд_ПрошлыйГ      ) and
                                                    (ДатаАуд_ПрошлыйГ      <= (endDate + 1 )        ) and
                                                    (endDate               <= Date(1, 7, CurrYear)  ));
        isEndDateLess0107  = (endDate <= Date(1, 7, CurrYear));
        isEndDateLastDayInMonth = isLastDayInMonth(endDate);

        spodEndDate = getSpodEndDate();
    END;

    Constructor();
end;

/**
 * Точка доступа.
 */ 
macro f134Parameters() : Tf134Parameters
    if (m_parameters == null)
        m_parameters = Tf134Parameters();
    end;
    return m_parameters;
end;

