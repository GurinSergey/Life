import RcbCoreInter;
import RcbProtocolView;
import RcbKlikoView;
/*  I-00263854-2  05.10.2012 AAN  при выгрузке если счёт типа "0"    */
/*   то ставим тип "А" так просили                                  */
/*                                                                   */
/*                                                                   */
/*                                                                   */


class TExportKliko101(dayly, choice)

    private var m_report    : RcbReport;
    private var m_klikoView : TRcbKlikoView = null;
    private var m_dayly = dayly;
    private var m_choice = choice;
    private var m_byWorkPlan;
    private var m_precision;

    private macro initialize()
        m_report = RcbApplication.currentReport;

        getRegistryValue("REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ТОЧНОСТЬ ДЛЯ ГЛАВЫ Д", V_INTEGER, m_precision, null);
        getRegistryValue( "REPTREG\\REP_GROUPS\\BALANCE_ACCOUNTS\\ЭКСПОРТ ПО РАБОЧЕМУ ПЛАНУ", V_BOOL, m_byWorkPlan, NULL );

        m_klikoView = TRcbKlikoView("101", m_report.context.period.endDate);
    end;

    macro getVal(value, chapter)
        if (value == NULL)
            return 0;
        end;
        if (chapter == 5)
            return value.exact;
        else
            return value.scaled;
        end;
    end;

    macro exportChapter(chapter)
        var strHead;
        var inRestRub, inRestCur, inRestItog;
        var debetRub, debetCur, debetItog;
        var creditRub, creditCur, creditItog;
        var outRestRub, outRestCur, outRestItog;

        // var dataSet = TRsbDataSet("SELECT t_balance, t_kind_account"
        var dataSet = TRsbDataSet("SELECT t_balance, replace ( t_kind_account, '0', 'А' ) t_kind_account"      // I-00263854-2  05.10.2012 AAN
            +"\n"+                "  FROM dbalance_dbt"
            +"\n"+                " WHERE t_iNumPlan = " + RcbApplication.Parameters.balancePlanNumber
            +"\n"+                "   AND length(t_balance) = 5"
            +"\n"+                "   AND t_chapter = " + chapter);

        if (m_dayly)
            strHead = "F_101_" + chapter + "DO";
        else
            strHead = "Ф_" + chapter;
        end;

        m_klikoView.printHead(strHead);

        while (dataSet.moveNext())
            if (RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "__Д") != NULL)
                if (dataSet.t_kind_account == "АП")
                    outRestRub = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "РуА"), chapter);
                    outRestRub = outRestRub + getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "РуП"), chapter);

                    outRestCur = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "ПоА"), chapter);
                    outRestCur = outRestCur + getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "ПоП"), chapter);

                    outRestItog = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "__А"), chapter);
                    outRestItog = outRestItog + getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "__П"), chapter);
                elif (dataSet.t_kind_account == "А")
                    outRestRub  = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "РуА"), chapter);
                    outRestCur  = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "ПоА"), chapter);
                    outRestItog = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "__А"), chapter);
                else
                    outRestRub  = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "РуП"), chapter);
                    outRestCur  = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "ПоП"), chapter);
                    outRestItog = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "__П"), chapter);
                end;

                debetRub  = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "РуД"), chapter);
                debetCur  = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "ПоД"), chapter);
                debetItog = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "__Д"), chapter);

                creditRub  = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "РуК"), chapter);
                creditCur  = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "ПоК"), chapter);
                creditItog = getVal(RcbApplication.currentReport.attributeValue("Бн" + dataSet.t_balance + "__К"), chapter);

                 if (dataSet.t_kind_account == "А")
                    inRestRub = outRestRub  - debetRub  + creditRub;
                    inRestCur = outRestCur  - debetCur  + creditCur;
                    inRestItog = outRestItog - debetItog + creditItog;
                else
                    inRestRub = outRestRub  + debetRub  - creditRub;
                    inRestCur = outRestCur  + debetCur  - creditCur;
                    inRestItog = outRestItog + debetItog - creditItog;
                end;

                if ((m_byWorkPlan == true) or ((inRestRub  != 0) or (inRestCur  != 0) or (inRestItog  != 0) or
                                               (debetRub   != 0) or (debetCur   != 0) or (debetItog   != 0) or
                                               (creditRub  != 0) or (creditCur  != 0) or (creditItog  != 0) or
                                               (outRestRub != 0) or (outRestCur != 0) or (outRestItog != 0) ))
                    if (chapter == 5)
                        inRestRub  = "0";
                        inRestCur  = "0";
                        inRestItog = execExp("String(inRestItog:0:" + m_precision + ")");

                        debetRub  = "0";
                        debetCur  = "0";
                        debetItog = execExp("String(debetItog:0:" + m_precision + ")");

                        creditRub  = "0";
                        creditCur  = "0";
                        creditItog = execExp("String(creditItog:0:" + m_precision + ")");

                        outRestRub  = "0";
                        outRestCur  = "0";
                        outRestItog = execExp("String(outRestItog:0:" + m_precision + ")");
                    else
                        inRestRub  = string(inRestRub:0:0);
                        inRestCur  = string(inRestCur:0:0);
                        inRestItog = string(inRestItog:0:0);

                        debetRub  = string(debetRub:0:0:0);
                        debetCur  = string(debetCur:0:0);
                        debetItog = string(debetItog:0:0);

                        creditRub  = string(creditRub:0:0);
                        creditCur  = string(creditCur:0:0);
                        creditItog = string(creditItog:0:0);

                        outRestRub  = string(outRestRub:0:0);
                        outRestCur  = string(outRestCur:0:0);
                        outRestItog = string(outRestItog:0:0);
                    end;

                    m_klikoView.printRow(dataSet.t_balance, dataSet.t_kind_account,
                                         inRestRub, inRestCur, inRestItog,
                                         debetRub,  debetCur,  debetItog,
                                         creditRub, creditCur, creditItog,
                                         outRestRub, outRestCur, outRestItog);
                end;
            end;
        end;

    end;

    macro execute()
        var i = 0;
        m_klikoView.setoutputFile();

        if (m_choice == 5)
            while (i < 5)
                exportChapter(i + 1);
                i = i + 1;
            end;
        else
            exportChapter(m_choice + 1)
        end;

        m_klikoView.resetoutputFile();

        var protocolView = TProtocolView("ПРОТОКОЛ ЭКСПОРТА В kliko.exe");

        protocolView.setProtocolOutput();
        protocolView.printHead();

        protocolView.printLine("Файл экспорта: " + m_klikoView.getFileName());
        protocolView.printLine(" ");

        protocolView.printLine("Выгружено строк: " + String(m_klikoView.getHeadsCount() + m_klikoView.getRowsCount()) + ", в т.ч.:");
        protocolView.printLine("    количество служебных строк: " + m_klikoView.getHeadsCount());
        protocolView.printLine("    количество содержательных строк:" + m_klikoView.getRowsCount());


        protocolView.resetProtocolOutput();

        protocolView.show();

    end;

    initialize();

end;

// //////////////////////////////////////////////////////////////////////////////////////////////////////

var par = cmdargs();
var dayly = (par == "DAYLY");

/* --- Меню --- */
array MenuItem;
MenuItem(0) = "Балансовые счета";
MenuItem(1) = "Счета доверительного управления";
MenuItem(2) = "Внебалансовые счета";
MenuItem(3) = "Счета срочных операций";
MenuItem(4) = "Счета ДЕПО";
MenuItem(5) = "Все главы";

var choice;

if (РасчетВКопейках)
    msgbox("Данные для экспорта в kliko.exe должны быть представлены в тысячах");
    exit(1);
end;

/* Выбор экспортируемых данных */
if ((choice = menu(menuItem,
                   "Выберите вид выгружаемой информации",
                   "Экспорт данных")) < 0)
    exit(1);
end;

TExportKliko101(dayly, choice).execute();

УстановитьФлагВозврата( OK_MACRO_FLAG);
exit(1);