/*───────────────────────────────────────────────────────────────────────────┐
  RS-Bank version 5.10                                        R-Style Softlab
  Файл подсистемы "Отчеты ЦБ"

  Печать 9-го приложения.

  Создан: 04.02.2004 - Sal.

  Комментарии:
     1. В качестве параметра макросу передается номер печатаемой главы.
        Название параметра: "ch".
└───────────────────────────────────────────────────────────────────────────*/
import prn_tpl, rcb_date, RsbDataSet, RcbCoreInter;
import lib_lang;
import rcbZone;

import param;
import rcbSignatureZone;

/**
 * Данные для печати подписей
 * @author ABP
 * @since 6.20.030.32
 */
private class (TSignatureZone) TSignatureData()
    /**
     * Должность первого лица (из анкеты субъектов).
     */
    macro getFirstPersonAppointment() : String
        return m_firstPersonAppointment;
    end;

    /**
     * ФИО первого лица (из анкеты субъектов).
     */
    macro getFirstPersonName() : String
        return m_firstPersonName;
    end;

    /**
     * Должность второго лица (из анкеты субъектов).
     */
    macro getSecondPersonAppointment() : String
        return m_secondPersonAppointment;
    end;

    /**
     * ФИО второго лица (из анкеты субъектов).
     */
    macro getSecondPersonName() : String
        return m_secondPersonName;
    end;

    private macro constructorTSignatureData()
        initTSignatureZone();
    end;

    constructorTSignatureData();
end;

/* Настройка для печати приложения ( 0 - все счета, 1 - по РПС, 2 - только ненулевые ) */
private const PRINT_REGPATH = "REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ПЕЧАТЬ ФОРМЫ БАЛАНСОВЫЕ СЧЕТА";
private const PRINT_ALL     = 0; /* все счета */
private const PRINT_BWP     = 1; /* по РПС    */
private const PRINT_NOZ     = 2; /* ненулевые */

private var   PrnKind = PRINT_ALL;

private var report = RcbApplication().currentReport;

var printPril = true;
var printNote = true;
/* Удовлетворяет ли переменная необходимым условиям */
MACRO PRB_GetStatusMask( varsd )

   var mask_ok = false;
   var av      = null;

   /* Рассматриваем только остатки */
   if ( (varsd.cVarKind == RXVK_BAL_ACTIVE) or (varsd.cVarKind == RXVK_BAL_PASSIVE) )
      mask_ok = true;

      if ( mask_ok )
         if ( PrnKind == PRINT_BWP )
            if ( not РабочаяПеременная(varsd.iFormId,varsd.szVarName) )
               mask_ok = false;
            end;
         end;
      end;

      if ( mask_ok )
         if ( PrnKind == PRINT_NOZ )
            if (report.form.attribute(varsd.szVarName) == null)
               MsgBox("Ошибка чтения значения переменной " + varsd.szVarName);
               Exit(1);
            else
               av = report.attributeValue(varsd.szVarName);
               if ((av == null) or (av.exact == 0.0))
                   mask_ok = false;
               end;
            end;
         end;
      end;
   end;

   return mask_ok;

END;

/* Обработать специальные поля (шапка и подвал отчета) */
MACRO PRB_SetExternNames( RepForm )

   var signatureData = TSignatureData();

   TPL_SetField( RepForm, "Ext_NameBank",  nvl(TZone().party().rec.name, ""));
   TPL_SetField( RepForm, "Ext_Name_Boss", signatureData.getFirstPersonAppointment());
   TPL_SetField( RepForm, "Ext_FIO_Boss",  signatureData.getFirstPersonName());
   TPL_SetField( RepForm, "Ext_Name_Book", signatureData.getSecondPersonAppointment());
   TPL_SetField( RepForm, "Ext_FIO_Book",  signatureData.getSecondPersonName());
   TPL_SetField( RepForm, "Ext_DateStr",   "на " + String(GetCorrectPrnRepDate():m) );
   TPL_SetField( RepForm, "Ext_RepDate",   string(GetCorrectPrnRepDate():m) );

   if (RcbApplication().currentReport.context.isSummaryMode())
      TPL_SetField( RepForm, "Ext_KindBalance", "Сводный баланс" );
   else
      TPL_SetField( RepForm, "Ext_KindBalance", "Баланс" );
   end;
End;

/* Получить тип печати отчета */
Private
MACRO GetPrintKind()
   var val_type = V_UNDEF,
       err_code = 0,
       val      = PRINT_ALL;

   val_type = GetRegistryValue( PRINT_REGPATH, V_INTEGER, val, err_code );

   if ( (err_code != 0) or (val_type != V_INTEGER) )
      MsgBox( "Ошибка чтения настройки|", PRINT_REGPATH );
      Exit(1);
   else
      if ( (val < PRINT_ALL) or (val > PRINT_NOZ) )
         MsgBox( "Некорректное значение настройки|", PRINT_REGPATH, "|будут напечатаны все счета");
         val = PRINT_ALL;
      end;
   end;

   return val;
END;

/* Определить номер печатаемой главы */
Private
MACRO GetChNum( StrParm )
   var ind = Index( StrParm, "-ch:" );

   if ( ind <= 0 )
      MsgBox("Неверно задана буква главы в параметрах шага операции");
      Exit(1);
   end;

   return SubStr(StrParm,ind+4,1);
END;

/***************
   Точка входа
***************/
PrnKind = GetPrintKind();

TPL_ProcSetExternNames = "PRB_SetExternNames";
TPL_ProcGetStatusMask  = "PRB_GetStatusMask";

private var chapter = GetChNum(CmdArgs);
private var dataSet = NULL;

printPril = true;
if (chapter == 0)
    dataSet = TRsbDataSet("SELECT t_chapter"
        +"\n"+            "  FROM dobchaptr_dbt"
        +"\n"+            " WHERE t_excludefromformalacnt <> 'X'"
        +"\n"+            "ORDER BY CASE"
        +"\n"+            "             WHEN t_chapter = 2"
        +"\n"+            "                 THEN 9999"
        +"\n"+            "             ELSE t_chapter"
        +"\n"+            "         END");

    while (dataSet.moveNext())
        TPL_PrintReport("prbl" + dataSet.chapter, NULL, true, false, dataSet.chapter);
        printPril = false;
    end;
else
    printNote = false;
    TPL_PrintReport("prbl" + chapter, NULL, true, false, chapter);
end;

УстановитьФлагВозврата( OK_MACRO_FLAG );

Exit(0);

OnError(err);
if ( err.Code == 17 )
   MsgBox("Печать отчета прервана пользователем");
else
   RunError();
end;
Exit(1);

/* eof */
