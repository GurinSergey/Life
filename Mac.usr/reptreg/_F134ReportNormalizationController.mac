/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 134. Контроллер нормализации отчета.

   CREATED : 17.07.12 Ser.
└────────────────────────────────────────────────────────────────────────── */
class F134ReportNormalizationController
    private var m_balanceRcbReport = null;
    private var m_auditedBalanceRcbReport = null;
    private var m_parameters = f134Parameters();

    private var m_protocolView;

    private macro checkReports()

        if (not m_balanceRcbReport.isCalculated)
            m_protocolView.printReportAbsence("Балансовые счета", m_balanceRcbReport);
            m_balanceRcbReport = null;
        end;

    end;

    private macro findAuditedReports()
        if (m_parameters.ДатаАудПодтв_ТекГ != date(0,0,0))
            var report = RcbApplication().currentReport;
            var context = report.context;
            var findReportsQuery = "SELECT t_bdPrevDate AS t_beginDate"
                          + "\n" + "  FROM dcy_rdate_dbt rdate,"
                          + "\n" + "       dcy_forms_dbt forms"
                          + "\n" + " WHERE forms.t_szFormName  = 'Балансовые счета'"
                          + "\n" + "   AND rdate.t_iFormId     = forms.t_iFormId"
                          + "\n" + "   AND rdate.t_organizationStructure = " + context.organizationStructure
                          + "\n" + "   AND rdate.t_issueMode   = " + context.issueMode
                          + "\n" + "   AND rdate.t_isSummary   = " + rcbSqlBool(context.isSummaryMode)
                          + "\n" + "   AND rdate.t_iNumDprt    = " + context.departmentCode
                          + "\n" + "   AND rdate.t_cVarKind    = CHR(0)"
                          + "\n" + "   AND rdate.t_bdRepDate   = " + getSqlDate(m_parameters.ДатаАудПодтв_ТекГ - 1)
                          + "\n" + "   AND rdate.t_cCalculated = 'X'"
                          + "\n" + "   AND rdate.t_iCalcDimension = " + report.dimension
                          + "\n" + "   AND rdate.t_bdPrevDate != TO_DATE('01.01.0001', 'DD.MM.YYYY')"
                          + "\n" + " ORDER BY t_bdPrevDate ASC";

            var reportsDataSet = TRsbDataSet(findReportsQuery);

            if (reportsDataSet.next())
                context = RcbReportContext(RcbPeriod(date(reportsDataSet.beginDate), m_parameters.ДатаАудПодтв_ТекГ - 1), context.departmentCode, context.issueMode, context.organizationStructure, context.isSummaryMode);
                m_auditedBalanceRcbReport = RcbApplication().objectFactory().createReport("Балансовые счета", context);
            else
                m_protocolView.printAuditedReportAbsence("Балансовые счета", m_parameters.ДатаАудПодтв_ТекГ - 1);
                m_auditedBalanceRcbReport = null;
            end;
        else
            m_protocolView.printLine("Не определена ДатаАудПодтв_ТекГ. В процессе нормализации сходимость с данными формы \"Балансовые счета\" за ДатаАудПодтв_ТекГ не реализована.\n");
            m_auditedBalanceRcbReport = null;
        end;
    end;

    private macro initialize()

        m_protocolView     = F134NormalizationProtocolView();
        m_balanceRcbReport = rcbApplication.currentReport.createOtherReport("Балансовые счета");

    end;

    macro normalize()

        m_protocolView.beginProtocol();

        checkReports();

        findAuditedReports();

        initProgress(null, "Выполнение нормализации данных", "Выполнение нормализации данных");
        F134ReportNormalizer(m_protocolView, m_balanceRcbReport, m_auditedBalanceRcbReport).normalize();
        remProgress();

        RcbApplication.TransactionManager.commit();

        m_protocolView.endProtocol();

    end;

    initialize();
end;