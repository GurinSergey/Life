/* --------------------------------------------------------------------------┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 134. Раздел отчета.

   CREATED : 17.07.12 Ser.
L-------------------------------------------------------------------------- */
class TSorter()
    macro isLess(v1, v2)
         return (v1.fieldValue("part").current < v2.fieldValue("part").current)
             or (     (v1.fieldValue("part").current == v2.fieldValue("part").current)
                  and (v1.fieldValue("code").current < v2.fieldValue("code").current));
    end;
end;

class (RcbPartBase) F134Part(id : Integer, compositeValue /*: RcbAttributeValue или RcbCompositeValue*/)
    initRcbPartBase(id, compositeValue);

    private var m_reportAttributeValue =    global.getRcbReport().attributeValue("ДанныеОтчета");
    private var m_userinputAttributeValue = global.getRcbReport().attributeValue("ПользовательскийВвод");
    private var m_ReserveStringAttributeValue = global.getRcbReport().attributeValue("Строка_недосозд_рез_ввод");
    private var m_isEmpty = true;

    private macro isUserAttribute(attributeId : String) : Bool
        if (index(attributeId, "ввод") > 0)
            return true;
        end;

        if (in(attributeId, "Eq2_1_7", "Eq3_1", "Eq3_3", "Eq3_6", "Eq3_8", "Eq5_1", "Eq5_3"))
            return true;
        end;
        
        return false;
    end;

    private macro getCompositeValue(attributeId : String)
        if (attributeId == "Строка_недосозд_рез_ввод")
            return m_ReserveStringAttributeValue;
        elif (isUserAttribute(attributeId))
            return m_userinputAttributeValue;
        end;

        return m_reportAttributeValue;
    end;

    private macro processAttributeValue(attributeValue : RcbAttributeVAlue)
        
        var iterator = attributeValue.createValueIterator();

        iterator.setSortOrder(TSorter());

        iterator.moveFirst();
        while (not iterator.isDone())
            addAttribute(iterator.currentItem.fieldValue("code").currentAsString);

            if (not in(iterator.currentItem.fieldValue("moneyValue").current, null, $0.0))
                m_isEmpty = false;
            end;

            iterator.moveNext();
        end;

    end;

    macro initialize()
        processAttributeValue(m_reportAttributeValue);
        processAttributeValue(m_userinputAttributeValue);
        addAttribute("Строка_недосозд_рез_ввод");

        return m_isEmpty;
    end;

    macro getAttribute(attributeId : String) : RcbAttributeBase
        m_attributes.moveFirst();
        while (m_attributes.moveNext())
            if (m_attributes.getCurrentItem().getId() == attributeId)
                return m_attributes.getCurrentItem();
            end;
        end;

        return m_attributes.push_back(objectFactoryInstance.createAttribute(attributeId, this, getCompositeValue(attributeId)));
    end;
end;
