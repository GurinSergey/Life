/*GSP 18.03.2014 реализация печати в копейках */

import rcbimport;
import balanceAttribute;

import f101ExcelSignatureZone;

import rsexts;

/**
 * Класс представления отчетной формы.
 */
class (RcbReportView)TF101ReportView(formName, formOkudCode, formPeriodicity, periodFormat)
    const ReportWidth = 174;
    private var m_isPrintZeroChapter5 : Bool;
    private var m_chapter : Integer;

    /**
     * Точность для главы Д. 
     */
    private var m_precisionChapter5   : Integer;

    /**
     * Настройка вида печати. (0 - все, 1 - по РПС, 2 - ненулевые) 
     */
    private const PRINT_REGPATH = "REPTREG/REP_GROUPS/BALANCE_ACCOUNTS/ПЕЧАТЬ ФОРМЫ 101";

    /**
     * Все счета.
     */
    private const PRINT_KIND_ALL  = 0;

    /**
     * По РПС.
     */
    private const PRINT_KIND_BWP  = 1;

    /**
     * Ненулевые.
     */
    private const PRINT_KIND_NOZ  = 2;

    /**
     * Вид печати счетов. (0 - все, 1 - по РПС, 2 - ненулевые) 
     */
    private var   m_isPrintKind = PRINT_KIND_ALL;

    /**
     * Получить вид печати отчета.
     * @return Значение настройки. (0 - все, 1 - по РПС, 2 - ненулевые)
     */
    private macro getPrintKind()
        var val_type = V_UNDEF,
            err_code = 0,
            val      = PRINT_KIND_ALL;
      
        val_type = GetRegistryValue( PRINT_REGPATH, V_INTEGER, val, err_code );
      
        if ( (err_code != 0) or (val_type != V_INTEGER) )
            MsgBox( "Ошибка чтения настройки|", PRINT_REGPATH );
            Exit(1);
        else
            if ( (val < PRINT_KIND_ALL) or (val > PRINT_KIND_NOZ) )
                MsgBox( "Некорректное значение настройки|", PRINT_REGPATH, "|будут напечатаны все счета");
                val = PRINT_KIND_ALL;
            end;
        end;
    
        return val;
    end;

    private macro TReportViewConstructor(formName, formOkudCode, formPeriodicity, periodFormat)
        initRcbReportView(NVL(formName,        "ОБОРОТНАЯ ВЕДОМОСТЬ ПО СЧЕТАМ БУХГАЛТЕРСКОГО УЧЕТА КРЕДИТНОЙ ОРГАНИЗАЦИИ"),
                          NVL(formOkudCode,    "0409101"),
                          NVL(formPeriodicity, arrCreate(RCB_PK_HALFYEAR, RCB_PK_QUARTER, RCB_PK_MONTH, RCB_PK_DAY)),
                          NVL(periodFormat,    DATE_IN_PERIOD_FORMAT));

        setReportWidth(ReportWidth);
        m_isPrintZeroChapter5 = balanceParameters().getIsPrintZeroInChapter5();
        m_precisionChapter5   = balanceParameters().getPrecisionChapter5();
        m_isPrintKind  = getPrintKind();
    end;

    macro printTableHeader()
/*        [─────┬─────────────────────────────────────────╥───────────────────────────────────────────────────────────────────────────────────╥─────────────────────────────────────────┐];
        [     │                                         ║                              Обороты за отчетный период                           ║                                         │];
        [Номер│            Входящие остатки             ╟─────────────────────────────────────────┬─────────────────────────────────────────╢            Исходящие остатки            │];
        [счета│                                         ║               по дебету                 │               по кредиту                ║                                         │];
        [вто- ├─────────────┬─────────────┬─────────────╫─────────────┬─────────────┬─────────────┼─────────────┬─────────────┬─────────────╫─────────────┬─────────────┬─────────────┤];
        [рого │     руб.    │   ин.вал.,  │    итого    ║     руб.    │   ин.вал.,  │    итого    │     руб.    │   ин.вал.,  │    итого    ║     руб.    │   ин.вал.,  │    итого    │];
        [по-  │             │    драг.    │             ║             │    драг.    │             │             │    драг.    │             ║             │    драг.    │             │];
        [рядка│             │   металлы   │             ║             │   металлы   │             │             │   металлы   │             ║             │   металлы   │             │];
        [─────┼─────────────┼─────────────┼─────────────╫─────────────┼─────────────┼─────────────┼─────────────┼─────────────┼─────────────╫─────────────┼─────────────┼─────────────┤];
        [  1  │      2      │      3      │      4      ║      5      │      6      │      7      │      8      │      9      │     10      ║      11     │     12      │     13      │];
        [─────┴─────────────┴─────────────┴─────────────╨─────────────┴─────────────┴─────────────┴─────────────┴─────────────┴─────────────╨─────────────┴─────────────┴─────────────┘];
*/
        [─────┬──────────────────────────────────────────────────────╥─────────────────────────────────────────────────────────────────────────────────────────────────────────────╥──────────────────────────────────────────────────────┐];
        [     │                                                      ║                                         Обороты за отчетный период                                          ║                                                      │];
        [Номер│                  Входящие остатки                    ╟──────────────────────────────────────────────────────┬──────────────────────────────────────────────────────╢                  Исходящие остатки                   │];
        [счета│                                                      ║                        по дебету                     │                    по кредиту                        ║                                                      │];
        [вто- ├─────────────────┬─────────────────┬──────────────────╫─────────────────┬─────────────────┬──────────────────┼─────────────────┬─────────────────┬──────────────────╫─────────────────┬─────────────────┬──────────────────┤];
        [рого │       руб.      │     ин.вал.,    │       итого      ║       руб.      │     ин.вал.,    │       итого      │       руб.      │     ин.вал.,    │      итого       ║       руб.      │     ин.вал.,    │       итого      │];
        [по-  │                 │      драг.      │                  ║                 │      драг.      │                  │                 │      драг.      │                  ║                 │      драг.      │                  │];
        [рядка│                 │     металлы     │                  ║                 │     металлы     │                  │                 │     металлы     │                  ║                 │     металлы     │                  │];
        [─────┼─────────────────┼─────────────────┼──────────────────╫─────────────────┼─────────────────┼──────────────────┼─────────────────┼─────────────────┼──────────────────╫─────────────────┼─────────────────┼──────────────────┤];
        [  1  │        2        │        3        │         4        ║        5        │        6        │         7        │        8        │        9        │       10         ║        11       │       12        │        13        │];
        [─────┴─────────────────┴─────────────────┴──────────────────╨─────────────────┴─────────────────┴──────────────────┴─────────────────┴─────────────────┴──────────────────╨─────────────────┴─────────────────┴──────────────────┘];

    end;

    macro printSeparator()
//        [──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
        [──────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────];
    end;

    macro printChapterHeader(chapterName : String)
        var currentBeginPos = 1;
        var currentEndPos;
        var limit = StrLen(chapterName);
        var correction = 1;

        while (currentBeginPos <= limit)
            currentEndPos = StrBrk(substr(chapterName, currentBeginPos), "\n");
            if (currentEndPos == 0)
                currentEndPos = StrLen(substr(chapterName, currentBeginPos));
                correction = 0;
            end;
            PrintLn(strAlign(substr(chapterName, currentBeginPos, currentEndPos - correction), ReportWidth, STR_ALIGN_CENTER));
            currentBeginPos = currentBeginPos + currentEndPos;
        end;
    end;

    macro printTableString(balance, inRub, inCur, inItog, dtRub, dtCur, dtItog, ktRub, ktCur, ktItog, outRub, outCur, outItog)
        if ((m_chapter == 5) AND (m_isPrintZeroChapter5 != TRUE))
//            [##### ############# ############# ############# ############# ############# ############# ############# ############# ############# ############# ############# ############# ]
            [##### ################# ################# ################## ################# ################# ################## ################# ################# ################## ################# ################# ##################]
            ( balance, "",  "",  inItog:r:0:*,m_precisionChapter5,
                       "",  "",  dtItog:r:0:*,m_precisionChapter5,
                       "",  "",  ktItog:r:0:*,m_precisionChapter5,
                       "",  "",  outItog:r:0:*,m_precisionChapter5);
        elif (m_chapter == 5)
//            [##### ############# ############# ############# ############# ############# ############# ############# ############# ############# ############# ############# ############# ]
            [##### ################# ################# ################## ################# ################# ################## ################# ################# ################## ################# ################# ##################]
            ( balance, inRub :r:0:*,m_precisionChapter5, inCur :r:0:*,m_precisionChapter5, inItog :r:0:*,m_precisionChapter5,
                       dtRub :r:0:*,m_precisionChapter5, dtCur :r:0:*,m_precisionChapter5, dtItog :r:0:*,m_precisionChapter5,
                       ktRub :r:0:*,m_precisionChapter5, ktCur :r:0:*,m_precisionChapter5, ktItog :r:0:*,m_precisionChapter5,
                       outRub:r:0:*,m_precisionChapter5, outCur:r:0:*,m_precisionChapter5, outItog:r:0:*,m_precisionChapter5);
        else
//            [##### ############# ############# ############# ############# ############# ############# ############# ############# ############# ############# ############# ############# ]
            [##### ################# ################# ################## ################# ################# ################## ################# ################# ################## ################# ################# ##################]
            ( balance, inRub :r:0:2, inCur :r:0:2, inItog :r:0:2,
                       dtRub :r:0:2, dtCur :r:0:2, dtItog :r:0:2,
                       ktRub :r:0:2, ktCur :r:0:2, ktItog :r:0:2,
                       outRub:r:0:2, outCur:r:0:2, outItog:r:0:2);
        end;
    end;

    macro printActiveHeader()
        [ Актив                                                                                                                                                                        ];
    end;

    macro printActiveBottom()
        printSeparator();

        [ Итого по активу (баланс)                                                                                                                                                     ];
    end;

    macro printPassiveHeader()
        [ Пассив                                                                                                                                                                       ];
    end;

    macro printPassiveBottom()
        printSeparator();

        [ Итого по пассиву (баланс)                                                                                                                                                    ];
    end;

    macro printTableBottom()

    end;

    macro getValue(value, chapter)
        if (value == NULL)
            return 0;
        end;
        if (chapter == 5)
            return value.exact;
        else
            return value.current;
        end;
    end;

    macro getValueAsString(value, chapter)
        if (value == NULL)
            return 0;
        end;
        if (chapter == 5)
            return value.exactAsString;
        else
            return value.currentAsString;
        end;
    end;

    /**
     * Нулевое значение по Активу 
     * @param value Стурктурная перменная TBalanceAttribute().
     * @param chapter Глава.
     * @return true, если все значения нулевые, иначе false.
     */
    macro isZeroActive(value, chapter)
        if ((getValue(value.getInRestNatCurActive(), chapter)  == 0) AND
            (getValue(value.getInRestCurActive(), chapter)     == 0) AND
            (getValue(value.getInRestActive(), chapter)        == 0) AND
            (getValue(value.getDebetNatCur(), chapter)         == 0) AND
            (getValue(value.getDebetCur(), chapter)            == 0) AND
            (getValue(value.getDebet(), chapter)               == 0) AND
            (getValue(value.getCreditNatCur(), chapter)        == 0) AND
            (getValue(value.getCreditCur(), chapter)           == 0) AND
            (getValue(value.getCredit(), chapter)              == 0) AND
            (getValue(value.getOutRestNatCurActive(), chapter) == 0) AND
            (getValue(value.getOutRestCurActive(), chapter)    == 0) AND
            (getValue(value.getOutRestActive(), chapter)       == 0))

            return true;
        end;
        return false;
    end;

    /**
     * Нулевое значение по Пассиву
     * @param value Стурктурная перменная TBalanceAttribute().
     * @param chapter Глава.
     * @return true, если все значения нулевые, иначе false.
     */
    macro isZeroPassive(value, chapter)
        if ((getValue(value.getInRestNatCurPassive(), chapter)  == 0) AND
            (getValue(value.getInRestCurPassive(), chapter)     == 0) AND
            (getValue(value.getInRestPassive(), chapter)        == 0) AND
            (getValue(value.getDebetNatCur(), chapter)          == 0) AND
            (getValue(value.getDebetCur(), chapter)             == 0) AND
            (getValue(value.getDebet(), chapter)                == 0) AND
            (getValue(value.getCreditNatCur(), chapter)         == 0) AND
            (getValue(value.getCreditCur(), chapter)            == 0) AND
            (getValue(value.getCredit(), chapter)               == 0) AND
            (getValue(value.getOutRestNatCurPassive(), chapter) == 0) AND
            (getValue(value.getOutRestCurPassive(), chapter)    == 0) AND
            (getValue(value.getOutRestPassive(), chapter)       == 0))

            return true;
        end;
        return false;
    end;

    macro printChapter(chapter, chapterName)
        var av = TBalanceAttribute("БАЛАНС");
        var sIn, sInR, sInV, sD, sDr, sDv, sK, sKr, sKv, sOut, sOutR, sOutV;

        m_chapter = chapter;
        printChapterHeader(chapterName);

        av.createBalanceIterator(chapter, KIND_ACTIVE, false, false);
        av.sortIteratorByBalance();

        printActiveHeader();

        sIn = 0.00; sInR = 0; sInV = 0; sD = 0; sDr = 0; sDv = 0; sK = 0; sKr = 0; sKv = 0; sOut = 0; sOutR = 0; sOutV = 0;

        while (av.next())

            if (((m_isPrintKind == PRINT_KIND_BWP) AND (NOT av.isBwp())) OR
                ((m_isPrintKind == PRINT_KIND_NOZ) AND (isZeroActive(av, chapter))))
                continue;
            end;

/*            printTableString(av.getBalance(), getValue(av.getInRestNatCurActive(), chapter), getValue(av.getInRestCurActive(), chapter), getValue(av.getInRestActive(), chapter),
                                              getValue(av.getDebetNatCur(), chapter), getValue(av.getDebetCur(), chapter), getValue(av.getDebet(), chapter),
                                              getValue(av.getCreditNatCur(), chapter), getValue(av.getCreditCur(), chapter), getValue(av.getCredit(), chapter),
                                              getValue(av.getOutRestNatCurActive(), chapter), getValue(av.getOutRestCurActive(), chapter), getValue(av.getOutRestActive(), chapter));
*/
            printTableString(av.getBalance(), getValueAsString(av.getInRestNatCurActive(), chapter), getValueAsString(av.getInRestCurActive(), chapter), getValueAsString(av.getInRestActive(), chapter),
                                              getValueAsString(av.getDebetNatCur(), chapter), getValueAsString(av.getDebetCur(), chapter), getValueAsString(av.getDebet(), chapter),
                                              getValueAsString(av.getCreditNatCur(), chapter), getValueAsString(av.getCreditCur(), chapter), getValueAsString(av.getCredit(), chapter),
                                              getValueAsString(av.getOutRestNatCurActive(), chapter), getValueAsString(av.getOutRestCurActive(), chapter), getValueAsString(av.getOutRestActive(), chapter));

            sInR  = sInR  + getValue(av.getInRestNatCurActive(), chapter);
            sInV  = sInV  + getValue(av.getInRestCurActive(), chapter);
            sIn   = sIn   + getValue(av.getInRestActive(), chapter);
            sDr   = sDr   + getValue(av.getDebetNatCur(), chapter);
            sDv   = sDv   + getValue(av.getDebetCur(), chapter);
            sD    = sD    + getValue(av.getDebet(), chapter);
            sKr   = sKr   + getValue(av.getCreditNatCur(), chapter);
            sKv   = sKv   + getValue(av.getCreditCur(), chapter);
            sK    = sK    + getValue(av.getCredit(), chapter);
            sOutR = sOutR + getValue(av.getOutRestNatCurActive(), chapter);
            sOutV = sOutV + getValue(av.getOutRestCurActive(), chapter);
            sOut  = sOut  + getValue(av.getOutRestActive(), chapter);
        end;

        printActiveBottom();
//msgbox(sInR +",\n"+ sInV+",\n"+ sIn+",\n"+ sDr+",\n"+ sDv+",\n"+  sD+",\n"+ sKr+",\n"+ sKv+",\n"+ sK+",\n"+ sOutR+",\n"+ sOutV+",\n"+ sOut);
        printTableString("", sInR, sInV, sIn, sDr, sDv,  sD, sKr, sKv, sK, sOutR, sOutV, sOut);
        printSeparator();

        av.createBalanceIterator(chapter, KIND_PASSIVE, false, false);
        av.sortIteratorByBalance();

        printPassiveHeader();

        sIn = 0; sInR = 0; sInV = 0; sD = 0; sDr = 0; sDv = 0; sK = 0; sKr = 0; sKv = 0; sOut = 0; sOutR = 0; sOutV = 0;

        while (av.next())

            if (((m_isPrintKind == PRINT_KIND_BWP) AND (NOT av.isBwp())) OR
                ((m_isPrintKind == PRINT_KIND_NOZ) AND (isZeroPassive(av, chapter))))
                continue;
            end;

/*            printTableString(av.getBalance(), getValue(av.getInRestNatCurPassive(), chapter), getValue(av.getInRestCurPassive(), chapter), getValue(av.getInRestPassive(), chapter),
                                              getValue(av.getDebetNatCur(), chapter), getValue(av.getDebetCur(), chapter), getValue(av.getDebet(), chapter),
                                              getValue(av.getCreditNatCur(), chapter), getValue(av.getCreditCur(), chapter), getValue(av.getCredit(), chapter),
                                              getValue(av.getOutRestNatCurPassive(), chapter), getValue(av.getOutRestCurPassive(), chapter), getValue(av.getOutRestPassive(), chapter));
*/
           printTableString(av.getBalance(), getValueAsString(av.getInRestNatCurPassive(), chapter), getValueAsString(av.getInRestCurPassive(), chapter), getValueAsString(av.getInRestPassive(), chapter),
                                              getValueAsString(av.getDebetNatCur(), chapter), getValueAsString(av.getDebetCur(), chapter), getValueAsString(av.getDebet(), chapter),
                                              getValueAsString(av.getCreditNatCur(), chapter), getValueAsString(av.getCreditCur(), chapter), getValueAsString(av.getCredit(), chapter),
                                              getValueAsString(av.getOutRestNatCurPassive(), chapter), getValueAsString(av.getOutRestCurPassive(), chapter), getValueAsString(av.getOutRestPassive(), chapter));

            sInR  = sInR  + getValue(av.getInRestNatCurPassive(), chapter);
            sInV  = sInV  + getValue(av.getInRestCurPassive(), chapter);
            sIn   = sIn   + getValue(av.getInRestPassive(), chapter);
            sDr   = sDr   + getValue(av.getDebetNatCur(), chapter);
            sDv   = sDv   + getValue(av.getDebetCur(), chapter);
            sD    = sD    + getValue(av.getDebet(), chapter);
            sKr   = sKr   + getValue(av.getCreditNatCur(), chapter);
            sKv   = sKv   + getValue(av.getCreditCur(), chapter);
            sK    = sK    + getValue(av.getCredit(), chapter);
            sOutR = sOutR + getValue(av.getOutRestNatCurPassive(), chapter);
            sOutV = sOutV + getValue(av.getOutRestCurPassive(), chapter);
            sOut  = sOut  + getValue(av.getOutRestPassive(), chapter);

        end;

        printPassiveBottom();
        printTableString("", sInR, sInV, sIn, sDr, sDv,  sD, sKr, sKv, sK, sOutR, sOutV, sOut);
        printSeparator();


    end;

    TReportViewConstructor(formName, formOkudCode, formPeriodicity, periodFormat);
end;


class (TF101ReportView)TF101ReportViewExcel(formName, formOkudCode, formPeriodicity, periodFormat)

    private var
        /* Перечисления для коллекции Borders */
        xlEdgeTop          = 8,
        xlEdgeBottom       = 9,
        xlEdgeRight        = 10,
        xlEdgeLeft         = 7,
        xlDiagonalDown     = 5,
        xlDiagonalUp       = 6,
        xlInsideVertical   = 11,
        xlInsideHorizontal = 12,

        /* Толщина линий */
        xlHairline = 1,     /* Очень тонкая */
        xlThin     = 2,     /* Тонкая */
        xlMedium   = -4138, /* Нормальная */
        xlThick    = 4,     /* Толстая */

        /* Константы выравнивания */
        xlTop    = -4160,
        xlCenter = -4108,
        xlBottom = -4107,
        xlRight  = -4152,
        xlLeft   = -4131,

        /* тип сохраняемого документа */
        xlWorkBookType_97_2003 = -4143;   //Excel 97-2003

    private var m_reportFileName : String;
    private var m_currentUseSystemSeparatorsValue;
    private var C_EE_EXCEL_NUMBER_FORMAT = "# ##0"; /* целочисленный формат */
    private var EXCEL_ws = NULL;                    /* EXCEL worksheet */
    private var C_EE_EXCEL_POINT = ",";             /* разделитель для дробной части чисел */
    private var m_rowCurrent     = 22;              // текущая строка на листе
    var l_ob;                                       // объект Excel.Application
    var startAX;

    private macro TReportViewExcelConstructor(formName, formOkudCode, formPeriodicity, periodFormat)
        initTF101ReportView(formName, formOkudCode, formPeriodicity, periodFormat);
        m_precisionChapter5   = balanceParameters().getPrecisionChapter5();
    end;

    macro getUnitName(formDimension)
        var formUnit = "";

        if (formDimension == RCB_DIM_MILLION)
            formUnit = "млн. руб.";
        elif (formDimension == RCB_DIM_NATIONAL)
            formUnit = "руб.";
        elif (formDimension == RCB_DIM_ROUBLE)
            formUnit = "руб.";
        elif (formDimension == RCB_DIM_THOUSAND)
            formUnit = "тыс. руб.";
        end;

        return formUnit;
    end;

    macro defineFileName()
        var prefix = SubStr(getNamesZone().getOkudString(ReportWidth), strLen(getNamesZone().getOkudString(ReportWidth)) - 2);
        m_reportFileName = getNameLog(prefix, "xls");
    end;

    macro getReportFileName() : String
        defineFileName();
        return m_reportFileName;
    end;

    /* Преобразовать путь в полный */
    macro _processPath(p_path, path)
      var l_str, l_path, l_p;
          p_path = trim(p_path);

       if(substr(p_path, 1, 2 ) == "..") /* ссылка на вышестоящий каталог */
            p_path = substr(p_path, 3);
            l_str  = path;
            l_path = "";
            l_p    = strbrk(l_str, "\\");
            while(l_p != 0)
                l_path = l_path + substr(l_str, 1, l_p - 1) + "\\";
                l_str  = substr(l_str, l_p + 1);
                l_p    = strbrk(l_str, "\\");
            end;
            return (substr(l_path, 1, strlen(l_path) - 1) + p_path);
       elif(substr(p_path, 1, 1) == ".") /* ссылка на текущий каталог */
            return (path + substr(p_path, 2));
       end;

       return path + "\\" + p_path;
    end;

    /*
          Открытие шаблона отчета в EXCEL
           p_name: Opt: полный путь к файлу шаблона: Def - C_EE_TEMPLATE_NAME
    */
    macro ee_EXCEL_template_init(p_excel_ob, p_name)
        var l_wb,
            l_line;

        l_wb     = p_excel_ob.Workbooks.Add(p_name);
        EXCEL_ws = l_wb.Sheets(1);
        p_excel_ob.Visible = TRUE;

        m_currentUseSystemSeparatorsValue = p_excel_ob.useSystemSeparators;
        p_excel_ob.useSystemSeparators = false;
        C_EE_EXCEL_POINT = p_excel_ob.application.DecimalSeparator;
    end;

    /*
      Код колонки EXCEL по номеру
       p_idx: Req: номер колонки (нумерация с 1)
    */
    macro ee_cl_code(p_idx)
     var l_s,
         s;

     p_idx = p_idx - 1; /* 0 == "A" */
     if(p_idx <= 25)
        return strfor(codefor("A") + p_idx);
     end;
     l_s = ee_cl_code(p_idx / 26 - 1);
     l_s = l_s + ee_cl_code(p_idx - (p_idx / 26) * 26);

     return s;
    end;

    /*
      Код фрагмента таблицы в EXCEL - для вызова EXCEL-процедур
       p_start_cl: Req: номер первой колонки
       p_start_ln: Req: номер первой строки
       p_end_cl  : Req: номер последней колонки
       p_end_ln  : Req: номер последней строки
    */
    macro ee_range_code(p_start_cl, p_start_ln, p_end_cl, p_end_ln)
        return string(ee_cl_code(p_start_cl), p_start_ln, ":", ee_cl_code(p_end_cl), p_end_ln);
    end;

    /*
      Установить формат заданного фрагмента таблицы EXCEL
       p_align : Req: код выравнивания  "l" или "c" или "r" (аналогично RSL)
       p_points: Opt: количество точек после запятой -
                      для форматирования чисел: Def - 0
       p_s_cl, p_s_ln, p_e_cl, p_e_ln: Req: параметры, определяющие границы
                                            форматируемой области (нач.колонка,
                                            нач. строка, кон. колонка и строка)
    */
    macro ee_EXCEL_set_cells_fmt(p_align, p_points, p_s_cl, p_s_ln, p_e_cl, p_e_ln)
        var l_r,
        l_NumberFormat;

        l_r = EXCEL_ws.Range(ee_range_code(p_s_cl, p_s_ln, p_e_cl, p_e_ln));

        p_align = strlwr(p_align);
        if (p_align == "r")
            l_r.HorizontalAlignment = xlRight;
        elif(p_align == "c")
            l_r.HorizontalAlignment = xlCenter;
        else
            l_r.HorizontalAlignment = xlLeft;
        end;

        if(valtype(p_points) != V_INTEGER)
            if(p_points=="txt")
                l_r.NumberFormat = "@";
            end;
            return;
        end;

        l_NumberFormat = C_EE_EXCEL_NUMBER_FORMAT;
        if(p_points > 0)
            l_NumberFormat = l_NumberFormat + C_EE_EXCEL_POINT + mkstr("0", p_points);
        end;
        l_r.NumberFormat = l_NumberFormat;
    end;

    /*
      Преобразование STRING -> DATE
       p_date: Req: Строка с датой
    */
    macro ee_str2date(p_date)
        var str_date = trim(p_Date),
            p,
            dd, mm, yy;

        p = strbrk(str_date, "-/.:");

        if(p == 0)
            return date(0,0,0);
        end;

        dd = int(substr(str_date, 1, p - 1));
        str_date = substr(str_date, p + 1, strlen(str_date) - p);
        p = strbrk(str_date, "-/.:" );

        if(p == 0 )
            return date(0,0,0 );
        end;

        mm = int(substr(str_date, 1, p - 1));
        str_date = substr(str_date, p + 1, strlen(str_date) - p);
        yy = int(str_date );

        return date(dd, mm, yy);
    end;

    /*
       Установить точность числа в ячейке EXCEL
       x    : колонка
       y    : строка
       prec : количество точек после запятой
    */
    private macro ee_EXCEL_set_cell_prec(x, y, prec)
        var l_r,
            l_NumberFormat;

        l_r = EXCEL_ws.Cells(y, x);
        l_NumberFormat = C_EE_EXCEL_NUMBER_FORMAT;
        if (prec > 0)
            l_NumberFormat = l_NumberFormat + C_EE_EXCEL_POINT + mkstr("0", prec);
        end;
        l_r.NumberFormat = l_NumberFormat;
    end;

    /*
      Преобразовать STRING-значение к заданному в качестве параметра типу
       p_value: Req: строка содержащая значение для преобразования
       p_type:  Req: RSL-тип значения
    */
    macro ee_convtotype(p_value, p_type)
        p_type = int(p_type );

        if  (p_type == V_INTEGER)
            return int(p_value);
        elif ((p_type == V_MONEY  ) or
              (p_type == V_MONEYL ) or
              (p_type == V_DOUBLE ) or
              (p_type == V_DOUBLEL))
            return double(p_value);
        elif (p_type == V_DATE)
            return ee_str2date(p_value);
        end;

        return p_value;
    end;

    /*
       Код ячейки таблицы в EXCEL - для вызова EXCEL-процедур
        p_cl: Req: номер колонки
        p_ln: Req: номер строки
    */
    macro ee_cell_code(p_cl, p_ln)
        return string(ee_cl_code(p_cl), p_ln);
    end;

    /*
      Печать значения в заданную ячейку таблицы Excel
       p_value : Req: STRING-значение
       p_type  : Req: RSL-тип значения
       p_column: Req: колонка
       p_row   : Req: строка
    */
    macro ee_EXCEL_print_value(p_value, p_type, p_column, p_row)
        EXCEL_ws.Range(ee_cell_code(p_column, p_row)).FormulaR1C1 = ee_convtotype(p_value, p_type);
    end;

    macro setOutputFile()
        var path    = "",
            exlname = "";

        path = GetCurDir(false);
        exlname = "tmpl_101d.xls";

        /*Ищем файл шаблона*/
        var pathXls = FindPath(exlname, NULL, "xls");

        if(pathXls == "" )
            msgbox("Не найден файл-шаблон EXCEL ", + exlname, " в каталоге макрофайлов на сервере приложений.");
            return;
        end;

        if (isStandAlone())
            l_ob   = ActiveX("Excel.Application", NULL, TRUE);
            path = _processPath(pathXls, path);
        else
            if (startAX == null)
                startAX = CreateObject("rsax", "TRsAxServer", "ReportAxServer", isStandalone());
            end;
            l_ob = startAX.CreateComObject("Excel.Application");

            if(not copyFile(pathXls, "$" + exlname))
                msgbox("Невозможно скопировать файл-шаблон EXCEL ", exlname, ".");
                return;
            end;

            GetFileInfo("$" + exlname, null, null, null, path);

            if (strBrk(path, "$") == 1)
                path = subStr(path, 2);
            end;
        end;

        ee_EXCEL_template_init(l_ob, path);
        //l_ob.Workbooks.Activate();
    end;

    macro printLendingAgencyCodeZone()
    end;

    macro printNamesZone()
        //выводим код формы по ОКУД
        ee_EXCEL_print_value(getNamesZone().getOkudString(ReportWidth), V_STRING, 13, 14);
        //выводим периодичность формы
        ee_EXCEL_print_value(getNamesZone().getFormPeriodicity(), V_STRING, 13, 15);
        //выводим единицы измерения
        ee_EXCEL_print_value(getUnitName(rcbApplication.currentReport.form.dimension), V_STRING, 13, 16);
        //выводим дату формы
        ee_EXCEL_print_value(getNamesZone().getPeriodName(), V_STRING, 7, 11);
        //выводим название банка
        ee_EXCEL_print_value(getNamesZone().getLendingAgencyName(), V_STRING, 10, 12);
        //выводим почтовый адрес банка
        ee_EXCEL_print_value(getNamesZone().getLendingAgencyPostalAdress(), V_STRING, 10, 13);
    end;

    macro printTableHeader()
    end;

    //вывод заголовка раздела
    private macro printChapterHeader(ChapterName)
        ee_EXCEL_set_cells_fmt("c", "txt", 7, m_rowCurrent, 7, m_rowCurrent);
        ee_EXCEL_print_value(ChapterName, V_STRING, 7, m_rowCurrent);
        m_rowCurrent = m_rowCurrent + 1;
    end;

    private macro printActiveHeader(ChapterName)
        ee_EXCEL_set_cells_fmt("l", "txt", 1, m_rowCurrent, 1, m_rowCurrent);
        ee_EXCEL_print_value("Актив", V_STRING, 1, m_rowCurrent);
        m_rowCurrent = m_rowCurrent + 1;
    end;

    private macro printPassiveHeader(ChapterName)
        ee_EXCEL_set_cells_fmt("l", "txt", 1, m_rowCurrent, 1, m_rowCurrent);
        ee_EXCEL_print_value("Пассив", V_STRING, 1, m_rowCurrent);
        m_rowCurrent = m_rowCurrent + 1;
    end;

    private macro printActiveBottom()
        //printSeparator();
        ee_EXCEL_set_cells_fmt("l", "txt", 1, m_rowCurrent, 1, m_rowCurrent);
        ee_EXCEL_print_value("Итого по активу (баланс)", V_STRING, 1, m_rowCurrent);
        m_rowCurrent = m_rowCurrent + 1;
    end;

    private macro printPassiveBottom()
        //printSeparator();
        ee_EXCEL_set_cells_fmt("l", "txt", 1, m_rowCurrent, 1, m_rowCurrent);
        ee_EXCEL_print_value("Итого по пассиву (баланс)", V_STRING, 1, m_rowCurrent);
        m_rowCurrent = m_rowCurrent + 1;
    end;

    private macro printTableString(balance, inRub, inCur, inItog, dtRub, dtCur, dtItog, ktRub, ktCur, ktItog, outRub, outCur, outItog)
        if (m_chapter == 5)
            if (m_isPrintZeroChapter5 != TRUE)
                ee_EXCEL_print_value(balance, V_STRING, 1,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(4, m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(inItog,  V_MONEY,  4,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(7, m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(dtItog,  V_MONEY,  7,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(10, m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(ktItog,  V_MONEY,  10, m_rowCurrent);
                ee_EXCEL_set_cell_prec(13, m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(outItog, V_MONEY,  13, m_rowCurrent);
            else
                ee_EXCEL_print_value(balance, V_STRING, 1,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(2,  m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(inRub,   V_MONEY,  2,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(3,  m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(inCur,   V_MONEY,  3,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(4,  m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(inItog,  V_MONEY,  4,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(5,  m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(dtRub,   V_MONEY,  5,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(6,  m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(dtCur,   V_MONEY,  6,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(7,  m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(dtItog,  V_MONEY,  7,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(8,  m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(ktRub,   V_MONEY,  8,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(9,  m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(ktCur,   V_MONEY,  9,  m_rowCurrent);
                ee_EXCEL_set_cell_prec(10, m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(ktItog,  V_MONEY,  10, m_rowCurrent);
                ee_EXCEL_set_cell_prec(11, m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(outRub,  V_MONEY,  11, m_rowCurrent);
                ee_EXCEL_set_cell_prec(12, m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(outCur,  V_MONEY,  12, m_rowCurrent);
                ee_EXCEL_set_cell_prec(13, m_rowCurrent, m_precisionChapter5);
                ee_EXCEL_print_value(outItog, V_MONEY,  13, m_rowCurrent);
            end;

            m_rowCurrent = m_rowCurrent + 1;
            return;
        end;

        ee_EXCEL_print_value(balance, V_STRING, 1,  m_rowCurrent);
        ee_EXCEL_print_value(inRub,   V_MONEY,  2,  m_rowCurrent);
        ee_EXCEL_print_value(inCur,   V_MONEY,  3,  m_rowCurrent);
        ee_EXCEL_print_value(inItog,  V_MONEY,  4,  m_rowCurrent);
        ee_EXCEL_print_value(dtRub,   V_MONEY,  5,  m_rowCurrent);
        ee_EXCEL_print_value(dtCur,   V_MONEY,  6,  m_rowCurrent);
        ee_EXCEL_print_value(dtItog,  V_MONEY,  7,  m_rowCurrent);
        ee_EXCEL_print_value(ktRub,   V_MONEY,  8,  m_rowCurrent);
        ee_EXCEL_print_value(ktCur,   V_MONEY,  9,  m_rowCurrent);
        ee_EXCEL_print_value(ktItog,  V_MONEY,  10, m_rowCurrent);
        ee_EXCEL_print_value(outRub,  V_MONEY,  11, m_rowCurrent);
        ee_EXCEL_print_value(outCur,  V_MONEY,  12, m_rowCurrent);
        ee_EXCEL_print_value(outItog, V_MONEY,  13, m_rowCurrent);

        m_rowCurrent = m_rowCurrent + 1;
    end;

    macro printChapter(chapter, ChapterName)
        var av = TBalanceAttribute("БАЛАНС");
        var sIn, sInR, sInV, sD, sDr, sDv, sK, sKr, sKv, sOut, sOutR, sOutV;

        m_chapter = chapter;
        printChapterHeader(chapterName);

        av.createBalanceIterator(chapter, KIND_ACTIVE, false, false);
        av.sortIteratorByBalance();

        printActiveHeader();

        sIn = 0; sInR = 0; sInV = 0; sD = 0; sDr = 0; sDv = 0; sK = 0; sKr = 0; sKv = 0; sOut = 0; sOutR = 0; sOutV = 0;

        while (av.next())

            if (((m_isPrintKind == PRINT_KIND_BWP) AND (NOT av.isBwp())) OR
                ((m_isPrintKind == PRINT_KIND_NOZ) AND (isZeroActive(av, chapter))))
                continue;
            end;

            printTableString(av.getBalance(), getValue(av.getInRestNatCurActive(), chapter), getValue(av.getInRestCurActive(), chapter), getValue(av.getInRestActive(), chapter),
                                              getValue(av.getDebetNatCur(), chapter), getValue(av.getDebetCur(), chapter), getValue(av.getDebet(), chapter),
                                              getValue(av.getCreditNatCur(), chapter), getValue(av.getCreditCur(), chapter), getValue(av.getCredit(), chapter),
                                              getValue(av.getOutRestNatCurActive(), chapter), getValue(av.getOutRestCurActive(), chapter), getValue(av.getOutRestActive(), chapter));

            sInR  = sInR  + getValue(av.getInRestNatCurActive(), chapter);
            sInV  = sInV  + getValue(av.getInRestCurActive(), chapter);
            sIn   = sIn   + getValue(av.getInRestActive(), chapter);
            sDr   = sDr   + getValue(av.getDebetNatCur(), chapter);
            sDv   = sDv   + getValue(av.getDebetCur(), chapter);
            sD    = sD    + getValue(av.getDebet(), chapter);
            sKr   = sKr   + getValue(av.getCreditNatCur(), chapter);
            sKv   = sKv   + getValue(av.getCreditCur(), chapter);
            sK    = sK    + getValue(av.getCredit(), chapter);
            sOutR = sOutR + getValue(av.getOutRestNatCurActive(), chapter);
            sOutV = sOutV + getValue(av.getOutRestCurActive(), chapter);
            sOut  = sOut  + getValue(av.getOutRestActive(), chapter);
        end;

        printActiveBottom();
        printTableString("", sInR, sInV, sIn, sDr, sDv,  sD, sKr, sKv, sK, sOutR, sOutV, sOut);
        //printSeparator();

        av.createBalanceIterator(chapter, KIND_PASSIVE, false, false);
        av.sortIteratorByBalance();

        printPassiveHeader();

        sIn = 0; sInR = 0; sInV = 0; sD = 0; sDr = 0; sDv = 0; sK = 0; sKr = 0; sKv = 0; sOut = 0; sOutR = 0; sOutV = 0;

        while (av.next())

            if (((m_isPrintKind == PRINT_KIND_BWP) AND (NOT av.isBwp())) OR
                ((m_isPrintKind == PRINT_KIND_NOZ) AND (isZeroPassive(av, chapter))))
                continue;
            end;

            printTableString(av.getBalance(), getValue(av.getInRestNatCurPassive(), chapter), getValue(av.getInRestCurPassive(), chapter), getValue(av.getInRestPassive(), chapter),
                                              getValue(av.getDebetNatCur(), chapter), getValue(av.getDebetCur(), chapter), getValue(av.getDebet(), chapter),
                                              getValue(av.getCreditNatCur(), chapter), getValue(av.getCreditCur(), chapter), getValue(av.getCredit(), chapter),
                                              getValue(av.getOutRestNatCurPassive(), chapter), getValue(av.getOutRestCurPassive(), chapter), getValue(av.getOutRestPassive(), chapter));

            sInR  = sInR  + getValue(av.getInRestNatCurPassive(), chapter);
            sInV  = sInV  + getValue(av.getInRestCurPassive(), chapter);
            sIn   = sIn   + getValue(av.getInRestPassive(), chapter);
            sDr   = sDr   + getValue(av.getDebetNatCur(), chapter);
            sDv   = sDv   + getValue(av.getDebetCur(), chapter);
            sD    = sD    + getValue(av.getDebet(), chapter);
            sKr   = sKr   + getValue(av.getCreditNatCur(), chapter);
            sKv   = sKv   + getValue(av.getCreditCur(), chapter);
            sK    = sK    + getValue(av.getCredit(), chapter);
            sOutR = sOutR + getValue(av.getOutRestNatCurPassive(), chapter);
            sOutV = sOutV + getValue(av.getOutRestCurPassive(), chapter);
            sOut  = sOut  + getValue(av.getOutRestPassive(), chapter);
        end;

        printPassiveBottom();
        printTableString("", sInR, sInV, sIn, sDr, sDv,  sD, sKr, sKv, sK, sOutR, sOutV, sOut);
        //printSeparator();

    end;

    macro printTableBottom()
        //msgbox("printTableBottom()");
    end;

    macro printSignatureZone()
        var signatureZone : TXlsSignatureZone_101;

        //отступ
        m_rowCurrent = m_rowCurrent + 3;

        //выводим первое лицо
        ee_EXCEL_set_cells_fmt("l", "txt", 1, m_rowCurrent, 1, m_rowCurrent);
        ee_EXCEL_print_value(signatureZone.getFirstPersonAppointment(), V_STRING, 1,  m_rowCurrent);
        m_rowCurrent = m_rowCurrent + 1;

        //выводим второе лицо
        ee_EXCEL_set_cells_fmt("l", "txt", 1, m_rowCurrent, 1, m_rowCurrent);
        ee_EXCEL_print_value(signatureZone.getSecondPersonAppointment(), V_STRING, 1,  m_rowCurrent);
        m_rowCurrent = m_rowCurrent + 1;

        //выводим "М.П."
        ee_EXCEL_set_cells_fmt("l", "txt", 1, m_rowCurrent, 2, m_rowCurrent);
        ee_EXCEL_print_value("М.П.", V_STRING, 1,  m_rowCurrent);
        m_rowCurrent = m_rowCurrent + 1;

        //выводим исполнителя
        ee_EXCEL_set_cells_fmt("l", "txt", 1, m_rowCurrent, 1, m_rowCurrent);
        ee_EXCEL_set_cells_fmt("r", "txt", 3, m_rowCurrent, 2, m_rowCurrent);
        ee_EXCEL_print_value(signatureZone.getExecutorName(), V_STRING, 3,  m_rowCurrent);
        ee_EXCEL_print_value("Исполнитель", V_STRING, 1,  m_rowCurrent);
        m_rowCurrent = m_rowCurrent + 1;

        //выводим телефон исполнителя
        ee_EXCEL_set_cells_fmt("l", "txt", 1, m_rowCurrent, 2, m_rowCurrent);
        ee_EXCEL_print_value(signatureZone.getExecutorPhoneNumber(), V_STRING, 1,  m_rowCurrent);
        ee_EXCEL_print_value("телефон:", V_STRING, 1,  m_rowCurrent);
        m_rowCurrent = m_rowCurrent + 1;

        //выводим дату отчета
        ee_EXCEL_set_cells_fmt("l", "txt", 1, m_rowCurrent, 1, m_rowCurrent);
        ee_EXCEL_print_value(signatureZone.getDatePrintableReport, V_STRING, 1,  m_rowCurrent);
    end;

    macro show()
    end;

    macro resetOutputFile()
        var absPath : String = GetCurDir(false);
        var relPath : String = getReportFileName();
        var i : Integer;

        //определяем абсолютный путь к файлу отчета
        if (SubStr(relPath, 1, 3) == "..\\")
            relPath = SubStr(relPath, 4);
            i = StrLen(absPath);
            while (SubStr(absPath, i, 1) != "\\")
                i = i - 1;
            end;
            absPath = SubStr(absPath, 1, i) + relPath;
        else
            absPath = absPath + relPath;
        end;
        //удаляем старый файл

        l_ob.useSystemSeparators = m_currentUseSystemSeparatorsValue;
        RemoveFile(absPath);
    end;

    TReportViewExcelConstructor(formName, formOkudCode, formPeriodicity, periodFormat);
end;
