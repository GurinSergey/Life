/**
 * RS-Bank 6.0                                            R-Style Software Lab
 *
 * File Name   : f134Parameters.mac
 * Description : Параметры, используемые в выпуске отчета
 * Comment     :
 * @since      : 09.11.2010
 * @version    : 6.00.020.29
 */

import ReptCbInter;
import BankInter;
import RcbCoreInter;

/**
 * Глобальная ссылка для хранения параметров.
 */
private var m_parameters = null;

class Tf134Parameters()
    var ОПФ_КО_АО,             /*форма кредитной организации АО/ООО*/
        ДатаАудПодтв_ТекГ,
        ДатаАуд_ТекГ,
        ДатаАудПодтв_ПрошлыйГ,
        ДатаАуд_ПрошлыйГ,
        ПрибыльТекущегоГодаПодтверждена,
        ПрибыльПрошлогоГодаПодтверждена,
        ПрибыльПредшествующегоГодаНеПодтверждена,
        iRealPlan = ПолучитьРеальныйНомерПлана(ЛогическийПланСчетов, "А"),
        isCalculateACForPosting,
        unconfirmedProfitPreviousYear,
        isEndDateLess0107,
        isEndDateLastDayInMonth,
        isUserInput;

    var ПрибыльПредшествующихЛетНеПодтверждена : Bool;
    var spodEndDate : Date;

    private macro getDateFromRegistry(path : String)
debugbreak;
        var errorCode : Integer = 0;
        var value : string = "";
        var valueType : Integer = V_UNDEF;

        valueType = getRegistryValue(path, V_STRING, value, errorCode);

        if ((errorCode != 0) or (valueType != V_STRING))
            RcbApplication.TransactionManager.rollBack();
            msgbox("Ошибка чтения даты из настройки реестра " + path);
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
        /*
        var position = 0;
        var day   = 0;
        var month = 0;
        var year  = 0;

        position = strBrk(value, "./-");
        day      = Int(subStr(value, 1, position));
        value    = subStr(value, position + 1);

        position = strBrk(value, "./-");
        month    = Int(subStr(value, 1, position));
        value    = subStr(value, position + 1);

        year = Int(value);

        if (    (day   == 0)
             or (month == 0)
             or (year  == 0)
           )
            RcbApplication.TransactionManager.rollBack();
            msgbox("Ошибка чтения даты из настройки реестра " + path);
            установитьФлагВозврата(STOP_PROC_FLG);
            exit(1);
        end;
        */
        return Date(value); //day, month, year);
    end;

    MACRO GetRegVal(Path, Type, Val)

        var errorCode = 0;

        GetRegistryValue(Path, Type, Val, errorCode);

        if (errorCode != 0)
           msgbox("Ошибка чтения значения настройки реестра " + Path);
           установитьФлагВозврата(STOP_PROC_FLG);
           exit(1);
        end;

        SetParm(3, Val);
    END;

    private macro isLastDayInMonth(day : Date)
        var m;
        var y;
        dateSplit(day, NULL, m, y);

        if (m == 12)
            m = 1;
            y = y + 1;
        else
            m = m + 1;
        end;

        return day == Date(1, m, y) - 1;
    end;

    private macro getSpodEndDate(): Date
        var spodDate = null;

        var currentYear : Integer;
        dateSplit(RcbApplication().currentReport.context.period.endDate, NULL, NULL, currentYear);

        var dataSet = TRsbDataset("SELECT t_finalDate"
                         + "\n" + "  FROM dchptspod_dbt"
                         + "\n" + " WHERE TO_CHAR(t_finalDate, 'YYYY') = " + getSqlString(String(currentYear))
                                 );
        dataSet.setFieldType("t_finalDate", V_DATE);

        if (dataSet.moveNext())
            spodDate = dataSet.finalDate;
        else
            spodDate = Date(0, 0, 0);
        end;

        return spodDate;
    end;

    MACRO Constructor()

        var CurrYear;
        var endDate = RcbApplication().currentReport.context.period.endDate;

        DateSplit(endDate, NULL, NULL, CurrYear);

        GetRegVal("REPTREG\\REP_GROUPS\\COMMON\\ОПФ_КО_АО",                              V_BOOL,   ОПФ_КО_АО                    );
        GetRegVal("REPTREG\\REP_GROUPS\\РАСЧЕТ КАПИТАЛА\\РАСЧЕТ_УК_ПО_ПРОВОДКАМ",        V_BOOL,   isCalculateACForPosting      );
        GetRegVal("REPTREG\\REP_GROUPS\\РАСЧЕТ КАПИТАЛА\\НЕПОДТВ_ПРИБЫЛЬ_ПРЕДШЕСТВ_ЛЕТ", V_STRING, unconfirmedProfitPreviousYear);
        GetRegVal("REPTREG\\REP_GROUPS\\РАСЧЕТ КАПИТАЛА\\ПОЛЬЗОВАТЕЛЬСКИЙ ВВОД",         V_BOOL,   isUserInput);

        ДатаАудПодтв_ПрошлыйГ = getDateFromRegistry("REPTREG\\REP_GROUPS\\COMMON\\ДАТААУДПОДТВ_ПРОШЛЫЙГ");
        ДатаАуд_ПрошлыйГ = getDateFromRegistry("REPTREG\\REP_GROUPS\\COMMON\\ДАТААУД_ПРОШЛЫЙГ");
        ДатаАудПодтв_ТекГ = getDateFromRegistry("REPTREG\\REP_GROUPS\\COMMON\\ДАТААУДПОДТВ_ТЕКГ");
        ДатаАуд_ТекГ = getDateFromRegistry("REPTREG\\REP_GROUPS\\COMMON\\ДАТААУД_ТЕКГ");

        ПрибыльПредшествующихЛетНеПодтверждена = NOT ((Date(1, 1, CurrYear)  <=   ДатаАудПодтв_ПрошлыйГ) and
                                                      (ДатаАудПодтв_ПрошлыйГ <=   ДатаАуд_ПрошлыйГ     ) and
                                                      (ДатаАуд_ПрошлыйГ      <= (endDate + 1)          ));

        ПрибыльТекущегоГодаПодтверждена = ((Date(1, 1, CurrYear) <= ДатаАудПодтв_ТекГ) and
                                           (ДатаАудПодтв_ТекГ    <= ДатаАуд_ТекГ     ) and
                                           (ДатаАуд_ТекГ         <= (endDate + 1)    ));

        ПрибыльПрошлогоГодаПодтверждена = ((Date(1, 1, CurrYear)  <=   ДатаАудПодтв_ПрошлыйГ) and
                                           (ДатаАудПодтв_ПрошлыйГ <=   ДатаАуд_ПрошлыйГ     ) and
                                           (ДатаАуд_ПрошлыйГ      <= (endDate + 1)          ));

        ПрибыльПредшествующегоГодаНеПодтверждена = ((Date(1, 1, CurrYear)  <= ДатаАудПодтв_ПрошлыйГ ) and
                                                    (ДатаАудПодтв_ПрошлыйГ <= ДатаАуд_ПрошлыйГ      ) and
                                                    (ДатаАуд_ПрошлыйГ      <= (endDate + 1 )        ) and
                                                    (endDate               <= Date(1, 7, CurrYear)  ));
        isEndDateLess0107  = (endDate <= Date(1, 7, CurrYear));
        isEndDateLastDayInMonth = isLastDayInMonth(endDate);

        spodEndDate = getSpodEndDate();
    END;

    Constructor();
end;

/**
 * Точка доступа.
 */
macro f134Parameters() : Tf134Parameters
    if (m_parameters == null)
        m_parameters = Tf134Parameters();
    end;
    return m_parameters;
end;
