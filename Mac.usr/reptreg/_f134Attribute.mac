/* ──────────────────────────────────────────────────────────────────────────┐
   RS-Bank V6                                                 R-Style Softlab
   Файл подсистемы "Регламентированная отчетность"

   Форма 134. Атрибут отчета.

   CREATED : 17.07.12 Ser.
└────────────────────────────────────────────────────────────────────────── */
class (RcbAttributeBase) F134Attribute(id : String, part : RcbPartBase, compositeValue : Object /*RcbCompositeValue или RcbAttributeValue*/)
    initRcbAttributeBase(id, part, compositeValue);

    private macro getCompositeValue(isFind : Bool) : RcbCompositeValue
        if (getId() =="Строка_недосозд_рез_ввод")
            return m_attributeCompositeValue;
        end;

        var keyValue = m_attributeCompositeValue.createKeyValue();

        keyValue.fieldValue("part") = m_part.getId();
        keyValue.fieldValue("code") = getId();

        var compositeValue = m_attributeCompositeValue.findVAlue(keyValue);

        if (compositeValue != null)
            return compositeValue;
        elif (isFind)
            return null;
        end;

        compositeValue = m_attributeCompositeValue.addValue();
    
        compositeValue.reset();

        compositeValue.fieldValue("part").exact = m_part.getId();
        compositeValue.fieldValue("code").exact = getId();

        return compositeValue;
    end;

    macro getRcbValue(isFind : Bool) : RcbValue
        if (getId() =="Строка_недосозд_рез_ввод")
            return m_attributeCompositeValue;
        end;

        var compositeValue = getCompositeValue(isFind);

        if (compositeValue == null)
            return null;
        end;

        return compositeValue.fieldValue("moneyValue");
    end;

    macro getValue()
        return getRcbValue().exact;
    end;

    macro getScaledValue()
        return getRcbValue().scaled;
    end;

    macro findValue(debtorsGroup : String, debtor : String, valueType : Integer) /*тип определяется параметром valueType*/
        var val = getRcbValue(true);

        if (val == null)
            return null;
        end;

        return val.exact;
    end;

    macro findScaledValue() /*тип определяется параметром valueType*/
        var val = getRcbValue(true);

        if (val == null)
            return null;
        end;

        return val.scaled;
    end;

    macro getValueAsString() : String
        var val = getRcbValue();

        if ((val == null) or val.isUndefined())
            return null;
        end;

        return val.currentAsString;
    end;

    macro setValue(value : Variant)
        var val = getRcbValue();

        if (isEqClass("TValue", value))
            val.exact  = value.exact;
            val.scaled = value.scaled;
        else
            if (value != null)
                val.exact = value;
                val.recalculateScaled();
            else
                val.setUndefined;
            end;
        end;


        
        return this;
    end;

    /** 
     * установить округленное значение
     *
     * @param keyValue  ключевое значение атрибута
     * @param value     значение для присваивания
     * @return ссылка на себя
     * @See RcbAttributeBase
     */
    macro setScaledValue(value : Double) : RcbAttributeBase
        getRcbValue().scaled = value;

        return this;
    end;

    //пришлось ввести дополнительный метод, так как иначе при обращении к plus() внутри переопределенного метода, вызывается метод базового класса
    private macro plus_(value : Variant) : RcbAttributeBase
        var val = getRcbValue();

        if (isEqClass("TValue", value))
            val.exact  = val.exact + value.exact;
            val.scaled = val.exact + value.scaled;
        else
            val.exact = val.exact + value;
            val.recalculateScaled();
        end;

        return this;
    end;

    macro plus(value : Variant) : RcbAttributeBase
        return plus_(value);
    end;

    macro minus(value) : RcbAttributeBase
        var val = getRcbValue();

        if (isEqClass("TValue", value))
            plus(TValue(-value.exact, -value.scaled));
        else
            plus(-value);
        end;
        
        return this;
    end;
end;