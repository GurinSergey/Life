/*************************************************************************/
/*     Дополнение к автоматизированной банковской системе RS-Bank        */
/*                                                                       */
/*    Имя файла        : CKC_rest_tk.mac                                 */
/*                                                                       */
/*    Программист      : Котов С.С.                                      */
/*    Заточка под ТК   : Гуцу Е.В.                                       */
/*                                                                       */
/*    Создан           : 28.12.2012                                      */
/*                                                                       */
/*************************************************************************/

import Globals;
import oralib, likepy;

var dt1={curdate};
var i=0;
var mask="405*,406*,407*,40820*";

/* EVG 27/12/2012 Доработка для GVB */
var ReportBegTime = "10:00", 
    ReportEndTime = Time(),
    sumAvailableSum = $0;


/* EVG 27/12/2012 Функция вычисляет приход по счёту за выбранный промежуток времени, исходя из
       даты и времени проведения шага "Зачисление". */
macro getAvailableSum( Account, RepDate )
  var SQL, cmd, rs;

  SQL = "      SELECT NVL ( SUM (paym.t_amount), 0 )                                    ";
  SQL = SQL + "  FROM dpmpaym_dbt paym, doproper_dbt oper, doprstep_dbt step            ";
  SQL = SQL + " WHERE paym.t_receiveraccount    = ?                                     ";
  SQL = SQL + "   AND paym.t_paymstatus         = 32000                                 ";
  SQL = SQL + "   AND oper.t_dockind            = paym.t_dockind                        ";
  SQL = SQL + "   AND oper.t_documentid         = LPAD (paym.t_documentid, 34, '0')     ";
  SQL = SQL + "   AND step.t_id_operation       = oper.t_id_operation                   ";
  SQL = SQL + "   AND step.t_kind_operation     = oper.t_kind_operation                 ";
  SQL = SQL + "   AND step.t_blockid            = 10000130                              ";      // Блок "Зачисление"
  SQL = SQL + "   AND TO_DATE ( TO_CHAR (step.t_syst_date, 'dd.mm.rrrr') || ' ' ||      ";
  SQL = SQL + "                 TO_CHAR (step.t_syst_time, 'hh24:mi:ss'),               ";
  SQL = SQL + "                 'dd.mm.rrrr hh24:mi:ss' ) BETWEEN ? AND ?               ";

  cmd = rsdcommand( SQL );
  cmd.AddParam( "", RSDBP_IN, Account       );
  cmd.AddParam( "", RSDBP_IN, ReportBegTime );
  cmd.AddParam( "", RSDBP_IN, ReportEndTime );

  rs = rsdRecordSet( cmd );
  if( rs and rs.moveNext() )
     return rs.Value( 0, null, V_MONEY );
  end;

  return $0;
end;


private macro PrintTop(dt1)
  [Отчет "Сравнение остатков и лимитов по овердрафтом СКС <Таможенная карта> в RS-Bank V6"
   Дата ##########
   ┌─────┬────────────────────┬───────────────┬───────────────┬───────────────┬────────────────┐
   │  №  │       № СКС        │  Остаток RS,  │Доступный лимит│Общая доступная│      Сумма     │
   │ п/п │                    │      руб      │    RS, руб    │ сумма RS, руб │   поступлений  │
   ├─────┼────────────────────┼───────────────┼───────────────┼───────────────┼────────────────┤](date(dt1));
end;

var sumrest=0,sumlimit=0;
private macro PrintLine(dt1,acc,rest,client)
  i=i+1;
// Определим доступный лимит
 var limit = 0,
     // EVG 27/12/2012
     availableSum = 0;
 var query:string = "select t_account, " +
                    //"       rsb_account.resta(t_account,:dt1,t_chapter,t_r0) " + TAM 31.01.2014 Adaptation_2031
                    "       rsb_account.restac(t_account, t_code_currency, :dt1, t_chapter, 0) " +
                    "  from daccount_dbt sks \n" +
                    /* EVG 27/12/2012 Изменение отчёта для GVB
                    " where t_usertypeaccount like '%G%' \n" +*/
                    " where t_usertypeaccount like '%T%' \n" +
                    "   and t_client = :client \n" +
                    "   and t_chapter = 3 \n" +
                    "   and substr(t_account,10,2)='09' \n" +
                    "   and substr(t_account,12,1)='2'";
 var params:TArray = makeArray(SQLParam("dt1",dt1),
                               SQLParam("client",client));
 var rs:RsdRecordSet = execSQLselect( query, params );
 if ((rs)and(rs.moveNext()))
  if (rs and (ValType(rs.value(0)) != 26))
   limit = rs.value(1);
  end;
 end;

 availableSum = getAvailableSum( acc, dt1 );


  [│#####│####################│###############│###############│###############│################│]
   (i:r,acc:c:w,money(rest):r,money(limit):r,money(rest+limit):r,money(availableSum));

 sumrest         = sumrest + rest;
 sumlimit        = sumlimit + limit;
 sumAvailableSum = sumAvailableSum + AvailableSum;
end;

private macro PrintBottom(sumrest,sumlimit,sumAvailableSum)
  [├─────┴────────────────────┼───────────────┼───────────────┼───────────────┼────────────────┤
   │ Итого по всем счетам, руб│###############│###############│###############│################│
   └──────────────────────────┴───────────────┴───────────────┴───────────────┴────────────────┘]
   (money(sumrest),money(sumlimit),money(sumrest+sumlimit),money(sumAvailableSum));
end;

private macro GetData(dt1)
 var query:string = "select t_account, " +
                  //"       rsb_account.resta(t_account,:dt1,t_chapter,t_r0) " + TAM 31.01.2014 Adaptation_2031
                    "       rsb_account.restac(t_account, t_code_currency, :dt1, t_chapter, 0), " +
                    "       t_client \n" +
                    "  from daccount_dbt sks \n" +
                    /* EVG 27/12/2012 Изменение отчёта для GVB
                    " where t_usertypeaccount like '%G%' \n" +*/
                    " where t_usertypeaccount like '%T%' \n" +
                    "   --and t_type_account like '%O%' \n" +
                    "   and (" + ConvertMaskToSQLFormat( mask, "t_account" ) + ") "
                    "   and t_chapter = 1 \n" +
                    "   and substr(t_account,10,2)='09' \n" +
                    "   and ((t_open_close = CHR(0) or t_close_date >= :dt1)and \n" +
                    "        (t_open_date <= :dt1) \n" +
                    "       ) \n order by substr(t_account,1,8),substr(t_account,10)";
 var params:TArray = makeArray(SQLParam("dt1",dt1));
 var rs:RsdRecordSet = execSQLselect( query, params );
 PrintTop(dt1);
 while ((rs)and(rs.moveNext()))
  if (rs and (ValType(rs.value(0)) != 26))
   PrintLine(dt1,rs.value(0),rs.value(1), rs.value(2));
  end;
 end;
 PrintBottom(sumrest,sumlimit,sumAvailableSum);
 return 0;
end;

macro work

   if (getdate(dt1, "Выберите дату"))

      /* EVG 27/12/2012 Доработка для GVB */
      var mm, hh;
      while( GetString( ReportBegTime, "Пожалуйста, введите время, с|которого выпускать отчёт, в формате HH:MM") 
             and index( ReportBegTime, ":" ) == 0 )

         msgbox ("Вы указали неправильный формат времени. Необходимо использовать формат HH:MM");
      end;

      // Перевод в формат времени
      ReportBegTime = trim( ReportBegTime );
      hh = int( subStr( ReportBegTime, 1, index( ReportBegTime, ":" ) - 1 ) );
      mm = int( subStr( ReportBegTime, index( ReportBegTime, ":" ) + 1 ) );
      
      ReportBegTime = time( hh, mm );

      ReportBegTime = dttm( dt1, ReportBegTime );
      ReportEndTime = dttm( dt1, ReportEndTime );
      // msgbox (ReportBegTime, "|", ReportEndTime);
      /* EVG 27/12/2012 END Доработка для GVB */


      GetData(dt1);
   end;
  
end;

// init;
work;



























