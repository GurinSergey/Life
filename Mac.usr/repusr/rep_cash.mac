/*************************************************************************/
/*     Дополнение к автоматизированной банковской системе RS-Bank        */
/*                                                                       */
/*    Имя файла        : rep_cash.mac                                    */
/*                                                                       */
/*    Описание         : I-080163 Реализация отчетов - к переходу        */  
/*                                                                       */
/*    Программист      : Котов С.С.                                      */
/*                                                                       */
/*    Создан           : 10.12.2010                                      */
/*                                                                       */
/*************************************************************************/

import Globals;
import oralib, likepy, repforms, "KeyCodes.mac";
//import globals, oralib, bankinter, KeyCodes, repforms, likePy, xlrep, PTInter, fiinter, cb_sql;

var select:string;
var params:TArray;
var rs:object;     
var dt1={curdate},
    dt2={curdate};
var max_sum = $150000;

var out_type_n = 0;
var DpCode     = 1 ; // по какому подразделению выпускаю отчет
var DpClientName;    // по какому подразделению выпускаю отчет
var DpName;          // по какому подразделению выпускаю отчет


array StringArray;

var path = "",
    pathfile = "",
    filen = "rep_cash.lbr";

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,path);
pathfile = FindPath(filen,path);
	if (not pathfile)
		msgbox("Не найдена LBR");
		exit();
	end;

var dlg = TRecHandler("desk_ob",pathfile,True);

record Department ("dp_dep.dbt");

macro print_report_desk // Печать отчета

var КолКлиентов = 0;

var i=1;

record dpdep(dp_dep);


      [       Список клиентов имеющих обороты по кассе за период с ########### по ########## ](dt1,dt2);
      [       #############################################################################](("свыше "+max_sum+" рублей"):c);

      if (out_type_n == 1)
        [       #############################################################################](("Подразделение: "+DpClientName):c);
      end;
        
      [┌────┬──────────┬──────────────────────────┬────────────────────┬─────────────────────────────────────┬───────┐
       │ NN │    Код   │    Название организации  │        Счет        │               Обороты               │Наличие│      
       │    │  клиента │                          │                    │      Дебет       │      Кредит      │  ИК   │
       ├────┼──────────┼──────────────────────────┼────────────────────┼──────────────────┼──────────────────┼───────┤
       │  1 │    2     │            3             │          4         │        5         │        6         │   7   │
       ├────┼──────────┼──────────────────────────┼────────────────────┼──────────────────┼──────────────────┼───────┤];
 
      select = "select c.t_code,p.t_name,arhdoc.t_account,arhdoc.t_sum_debet,arhdoc.t_sum_credit, " + "\n" +
               "       case when instr(acc.t_usertypeaccount,'Ё')>0 then 'X' else ' ' end t_ik " + "\n" +
               "  from (select t_account, " + "\n" +
               "               sum(t_sum_debet) t_sum_debet, " + "\n" +
               "               sum(t_sum_credit) t_sum_credit " + "\n" +
               "          from (select case substr(arhdoc.t_account_receiver, 1, 1) " + "\n" +
               "                         when '4' then " + "\n" +
               "                          t_account_receiver " + "\n" +
               "                         else " + "\n" +
               "                          t_account_payer " + "\n" +
               "                       end t_account, " + "\n" +
               "                       case substr(arhdoc.t_account_receiver, 1, 1) " + "\n" +
               "                         when '4' then 0 else t_sum " + "\n" +
               "                       end t_sum_debet, " + "\n" +
               "                       case substr(arhdoc.t_account_receiver, 1, 1) " + "\n" +
               "                         when '4' then t_sum else 0 " + "\n" +
               "                       end t_sum_credit " + "\n" +
               "                  from darhdoc_dbt arhdoc " + "\n" +
               "                 WHERE arhdoc.t_chapter = 1 " + "\n" +
               "                   AND arhdoc.t_date_carry BETWEEN ? AND ? " + "\n";
      if (out_type_n == 1)
        select = select +
               "                   and arhdoc.t_branch = ? " + "\n";
      end;
      select = select +
               "                   and (((arhdoc.t_account_receiver LIKE '20202%' OR " + "\n"
               "                       arhdoc.t_account_receiver LIKE '20206%' OR " + "\n" +
               "                       arhdoc.t_account_receiver LIKE '20207%' OR " + "\n" +
               "                       arhdoc.t_account_receiver LIKE '20208%') and " + "\n" +
               "                       (substr(arhdoc.t_account_payer, 1, 3) between '405' and '408')) or " + "\n" +
               "                       ((arhdoc.t_account_payer LIKE '20202%' OR " + "\n" +
               "                       arhdoc.t_account_payer LIKE '20206%' OR " + "\n" +
               "                       arhdoc.t_account_payer LIKE '20207%' OR " + "\n" +
               "                       arhdoc.t_account_payer LIKE '20208%') and " + "\n" +
               "                       (substr(arhdoc.t_account_receiver, 1, 3) between '405' and '408')))) " + "\n" +
               "         group by t_account " + "\n" +
               "        having sum(t_sum_credit) > ? or sum(t_sum_debet) > ?) arhdoc, " + "\n" +
               "       daccount_dbt acc, " + "\n" +
               "       dobjcode_dbt c, " + "\n" +
               "       dparty_dbt p " + "\n" +
               " where acc.t_code_currency = 0 " + "\n" +
               "   and acc.t_account = arhdoc.t_account " + "\n" +
               "   and c.t_objecttype = 3 " +  "\n" +// Субъект
               "   and p.t_partyid = acc.t_client " + "\n" +
               "   and c.t_objectid = acc.t_client " + "\n" +
               "   and c.t_codekind = 20 " +  "\n" +// Код субъекта в номере лицевого счета
               " order by arhdoc.t_account";

//setoutput("..\\txtfile\\kot_cash.txt",true);
//println(select);
//setoutput();
      if (out_type_n == 1)
        params = makeArray(SQLParam(""  , dt1),
                           SQLParam(""  , dt2),
                           SQLParam(""  , DpCode),
                           SQLParam(""  , max_sum),
                           SQLParam(""  , max_sum));
      else
        params = makeArray(SQLParam(""  , dt1),
                           SQLParam(""  , dt2),
                           SQLParam(""  , max_sum),
                           SQLParam(""  , max_sum));
      end;

      rs = execSQLselect( select, params, false );

      while( rs and rs.moveNext())
        if (valtype(rs.value) != 26)

          StrSplit( rs.value("t_name"), StringArray, 26);

          КолКлиентов = КолКлиентов + 1;
      [│####│##########│##########################│####################│##################│##################│   #   │]
         (КолКлиентов,rs.value("t_code"):r,StringArray(0),rs.value("t_account"),money(rs.value("t_sum_debet")),money(rs.value("t_sum_credit")),rs.value("t_ik"));
          i = 1;
          while ( StrLen(StringArray(i) ) > 0)
           [│    │          │##########################│                    │                  │                  │       │]( StringArray(i) );
            i = i + 1;
          end;  
        end;
      end;

      [└────┴──────────┴──────────────────────────┴────────────────────┴──────────────────┴──────────────────┴───────┘];

end;

macro  print_report_kass_head(dt1,dt2,Account,Client)
 var str="СПРАВКА по кассовым оборотам за ";             

 if(dt1 == dt2)
   str=str+string(dt1);
 else               
   str=str+"период с "+string(dt1)+" по "+string(dt2);
 end;                 
 [ #######################################################################

   #################### 
   #
  
      Дата             Дебет         Кредит       Символ

 ](str:c,Account,Client);
end;

macro print_report_kass
  print_report_kass_head(dt1,dt2,"тест","тест");
end;

/*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key)

   var mess = "~F2~ Продолжить ~ESC~ Выход ";
   var PartyID:integer = {OurBank};
   var NoChangeDate:bool = false;

   var DpDepHead  = 1;// Номер для головы - точнее определим позже
   var DpDepHeadName = "000";// Имя головы

   var rs:object = NULL;
   var Query = "";

   var g;

   record fi( fininstr );
   record dp( dp_dep );

   array out_type;
   
   out_type(0) = "По всем подразделениям";
   out_type(1) = "По подразделению";
//   out_type(2) = "Справка по кассовым оборотам"; // Настрою позже. Или использую другой отчет

   if(cmd == DLG_INIT)

      dlg.rec.Date_report_begin = {curdate};
      dlg.rec.Date_report_end   = {curdate};
      dlg.rec.output            = out_type(out_type_n);
      dlg.rec.Max_Sum           = max_sum;
      dlg.rec.departCode = DpName = "000";
      DpClientName = dlg.rec.departName = GetClientName({OurBank});
      rs = execSQLSelect("select t_name,t_code from ddp_dep_dbt where t_partyid ="+{OurBank});
      if (rs and rs.movenext())
        DpCode = DpDepHead  = rs.value("t_code");
        dlg.rec.departCode = DpDepHeadName = DpName = rs.value("t_name");
      end;
      UpdateFields(dlg); 
   end;

   if (cmd==DLG_SETFOCUS)
       message(mess);
   end;
   
   if (cmd == DLG_REMFOCUS)
       if (FldName(dlg,id)=="Date_report_begin")
          if ( dlg.rec.Date_report_begin > dlg.rec.Date_report_end ) 
             MsgBox("Дата начала больше даты окончания отчетного периода");
             return CM_CANCEL;
          elif ( dlg.rec.Date_report_begin > {curdate} )
             MsgBox("Дата начала больше даты текущего операционного дня"); 
             return CM_CANCEL;
          end;
       elif (FldName(dlg,id)=="Date_report_end") 
          if (dlg.rec.Date_report_end < dlg.rec.Date_report_begin )
             MsgBox("Дата окончания меньше даты начала отчетного периода"); 
             return CM_CANCEL;
          elif ( dlg.rec.Date_report_end > {curdate} )
             MsgBox("Дата окончания больше даты текущего операционного дня");
             return CM_CANCEL;
          end;
        end;
      UpdateFields(dlg);
   end; 

   if (cmd==DLG_KEY)
       if (KEY==KEY_ESC)
           return exit(1);

       elif (KEY == KEY_F3)
         if ( (FldName(dlg,id) == "departCode") and (out_type_n == 1) )
              if(ListDepartment (dp))        
                DpCode             = dp.code;
                DpName =
                dlg.rec.departCode = dp.name;
                DpClientName =
                dlg.rec.departName = GetClientName(dp.partyid);
                UpdateFields(dlg);

              end;
         elif(FldName(dlg,id) == "output")
           g = menu (out_type, "Выбор типа отчета");
           if (g >= 0)
             out_type_n=g;
             dlg.rec.output = out_type(out_type_n);
             if (out_type_n != 1)
               dlg.rec.departCode = DpDepHeadName;
               dlg.rec.departName = GetClientName({OurBank});
             else
               dlg.rec.departCode = DpName;
               dlg.rec.departName = DpClientName;
             end;
             UpdateFields(dlg);
           end;
         end;

       elif ( KEY==KEY_F2 )

             dt1  = dlg.rec.Date_report_begin;
             dt2  = dlg.rec.Date_report_end;
             max_sum = dlg.rec.max_sum;

             if ((dt1 <= dt2) and (dt1 <= {curdate})
                and (dt2 >= dt1) and (dt2 <= {curdate})) 
               Return CM_SAVE;
             else
               MsgBox("Не все поля корректны!");
             end;
       end;
   end;

END;

if ( RunDialog(dlg, "Event"))
  if ( (out_type_n == 0) or (out_type_n == 1) )
    print_report_desk;
  else
    print_report_kass;
  end;
end;
