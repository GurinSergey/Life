/*Макрос печати ведомости учета выдач наличных денег  */
/* на заработную плату, выплаты социального характера */
/*                                        и стипендии.*/
/*Гогильчин А.В  R-Style Ukraine, Kiev 06.08.2008     */
/*Изменен 02/09/2008 Новиков С.С.*/
/*Версия 1.0                                          */

import globals, oralib, bankinter, "KeyCodes.mac", repforms, likePy, PTInter;

record Department ("dp_dep.dbt");
//record dlg (, "") dialog;
record superior(ptoffice);
record officet(ptoffice);

var path = "",
    pathfile = "",
    filen = "RSU.lbr";

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,path);
pathfile = FindPath(filen,path);
	if (not pathfile)
		msgbox("Не найдена библиотека ресурсов");
		exit();
	end;

var dlg = TRecHandler("vedZp",pathfile,True);

var Office     = {NumDprt},
    OfficeName = {Name_Bank},
    BeginDate,
    EndDate,
    rselect;
var PartyID:integer;
private macro PrintHead(_Date, _Bank)
[     
                           Ведомость учета выдач наличных денег на заработную плату, 
                                   выплаты социального характера и стипендии.


                                                 #############

                                    #####################################  
 
  По отделению:#################################################](_date, _Bank:c,OfficeName);

if (OrgRegStruct != "")
[ По подразделению:############################################################################################
](OrgRegStructName);
end; 
                                                                                            
[                                                                                            тыс.руб
┌─────┬────────────────────────┬────────────────────────┬───────────────────┬───────────┬──────────┐
│     │                        │                        │ Подлежит выдаче:  │           │          │
│     │                        │                        │                   │Обеспечено │Фактически│
│№ п/п│Наименование предприятия│      Номер счета       ├─────────┬─────────┤средствами │  выдано  │
│     │                        │                        │    В    │   Не    │ на счетах │          │
│     │                        │                        │установл.│выплачен.│на начало  │          │
│     │                        │                        │  сроки  │ в сроки │   дня     │          │
├─────┼────────────────────────┼────────────────────────┼─────────┼─────────┼───────────┼──────────┤
│     │                        │                        │         │         │           │          │
│     │                        │                        │         │         │           │          │
] 
end;

private macro printBottom(_sum1, _sum2)
[├─────┼────────────────────────┼────────────────────────┼─────────┼─────────┼───────────┼──────────┤
 │     │      ИТОГО:            │                        │         │         │ ######### │ ######## │
 └─────┴────────────────────────┴────────────────────────┴─────────┴─────────┴───────────┴──────────┘


    Исполнитель_______________________________#########################
 



](_sum1, _sum2, {Name_Oper});
end;

private macro PrintLine(_nr, _ClientName, _Account, _value1, _value2 )

[│#####│ ###################### │ ###################### │         │         │ ######### │ ######## │
 │     │                        │                        │         │         │           │          │
](_nr, _ClientName, _Account, _value1, _value2);
end;

private macro GetResultGrid(_department:string, _BeginDate:string, _EndDate:string);
   var rs:object = NULL;
   var script = "";
   var dpScript = "";
   if ( trim(_department) == "" )
      _department = {NumDprt};
   end;
   script = 

            "SELECT arh.t_date_carry,\n" +
            "       arh.t_department,\n" + 
            "       party.t_name,\n" + 
            "       arh.t_account_payer,\n" + 
            "       abs(symb.t_sum) as value2\n" + 
            "  FROM darhdoc_dbt arh\n" + 
            " inner join (select *\n" + 
            "               from dsymbcash_dbt\n" + 
            "              where trim(t_symbol) = '40'\n" + 
            "                 or trim(t_symbol) = '41') symb on (arh.t_applicationkey =\n" + 
            "                                                   substr(symb.t_applicationkey,\n" + 
            "                                                           6)),\n" + 
            " daccount_dbt acc,\n" + 
            " dparty_dbt party\n" + 
            " WHERE (substr(arh.t_account_payer, 1, 3) = '405' or\n" + 
            "       substr(arh.t_account_payer, 1, 3) = '406' or\n" + 
            "       substr(arh.t_account_payer, 1, 3) = '407' or\n" + 
            "       substr(arh.t_account_payer, 1, 3) = '408')\n" + 
            "   AND (arh.t_date_carry BETWEEN '"+trim(_BeginDate)+"' AND '"+trim(_EndDate)+"')\n" + 
            "   AND substr(arh.t_account_receiver, 1, 5) = '20202'\n" + 
            "   AND arh.t_department = "+_department+"\n"+
            "   AND (acc.t_account = arh.t_account_payer and\n" + 
            "       acc.t_code_currency = arh.t_code_currency)\n" + 
            "   AND (party.t_partyid = acc.t_client)\n";
   if (OperSelect != "")
     script = script +
            "   AND acc.t_oper like '"+OperSelect+ "'\n";
   end;
     script =  script +          
            " ORDER BY arh.t_date_carry, party.t_name;";

/*            "SELECT arh.t_date_carry,\n" +
            "       arh.t_department,\n" + 
            "       party.t_name,\n" + 
            "       arh.t_account_payer,\n" + 
            "       abs(symb.t_sum) as value2\n" + 
            "  FROM darhdoc_dbt   arh,\n" + 
            "       dsymbcash_dbt symb,\n" + 
            "       daccount_dbt  acc,\n" + 
            "       drestdate_dbt rest,\n" + 
            "       dparty_dbt    party\n" + 
            " WHERE (arh.t_account_payer like '405%' or arh.t_account_payer like '406%' or\n" + 
            "       arh.t_account_payer like '407%' or arh.t_account_payer like '408%')\n" + 
            "   AND (arh.t_date_carry BETWEEN '"+trim(_BeginDate)+"' AND '"+trim(_EndDate)+"')\n" + 
            "   AND arh.t_account_receiver LIKE '20202%'\n" + 
            "   AND arh.t_department = "+_department+"\n"+
            "   AND arh.t_applicationkey IN\n" + 
            "       (select substr(symb.t_applicationkey, 6)\n" + 
            "          FROM dsymbcash_dbt\n" + 
            "         WHERE trim(symb.t_symbol) = '40'\n" + 
            "            or trim(symb.t_symbol) = '41')\n" + 
            "   AND (acc.t_account = arh.t_account_payer and\n" + 
            "       acc.t_code_currency = arh.t_code_currency)\n" + 
            "   AND (party.t_partyid = acc.t_client)\n" + 
            "   AND (rest.t_account = acc.t_account and\n" + 
            "       rest.t_code_currency = arh.t_code_currency and\n" + 
            "       rest.t_date_value = arh.t_date_carry)\n" + 
            " ORDER BY arh.t_date_carry, party.t_name";
*/
   rs = execSQLSelect(script);

   if (rs and rs.movenext())
      return rs;
   else 
      return NULL;
   end;

   OnError(Err)
     MsgBox(Err.Message,"| at ",Err.Line,"|in ",Err.Module);
end;

private macro PrintItAll()
  var grid: object = Null;
  var Number = 0, 
      Vdate = date("01.01.0001"), 
      department;
  var value1 = 0,
      sum1 = 0, 
      sum2 = 0;

  grid = GetResultGrid(office, Begindate, EndDate);

  if (Valtype(grid) == V_UNDEF)
     PrintLn("Нет данных для отчета");
  else 
     while (grid.movenext())
       if (vdate != date(grid.value("t_date_carry")))
          if (Vdate != date("01.01.0001"))
             printBottom(sum1, sum2);
             sum1 = 0;
             sum2 = 0;
          end;
          Vdate = date(grid.value("t_date_carry"));
          department = grid.value("t_department");
          Number = 1;
          PrintHead(Vdate, OfficeName);
          value1 = IfThenElse( abs(RestA(grid.value("t_Account_payer"), Vdate-1, null, 1)) < grid.value("value2"), 
                                     abs(RestA (grid.value("t_Account_payer"), Vdate-1, null, 1))/1000, 
                                     grid.value("value2")/1000);
          PrintLine(number, grid.value("t_name"), grid.value("t_Account_payer"), string(value1:a), string(grid.value("value2")/1000:a));
          sum1 = sum1 + value1;
          sum2 = sum2 + grid.value("value2")/1000;
       else
          Number = Number + 1;
          value1 = IfThenElse( abs(RestA(grid.value("t_Account_payer"), Vdate-1, null, 1)) < grid.value("value2"), 
                                     abs(RestA (grid.value("t_Account_payer"), Vdate-1, null, 1))/1000, 
                                     grid.value("value2")/1000);

          PrintLine(number, grid.value("t_name"), grid.value("t_Account_payer"), string(value1:a), string(grid.value("value2")/1000:a));
          sum1 = sum1 + value1;
          sum2 = sum2 + grid.value("value2")/1000;
       end;
     end;

     printBottom(sum1, sum2);
     sum1 = 0;
     sum2 = 0;
  end;

end;

/*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key) 

   var mess = "~F2~ Продолжить ~F3~ Список ~ESC~ Выход ";
   var OrgDate:date = {curdate} + 1;
   var NoChangeDate:bool = false;

   if(cmd == DLG_INIT)
      PartyID = {OurBank};
      dlg.rec.Office    = OfficeName;
      dlg.rec.BeginDate = {curdate}-1;
      dlg.rec.EndDate   = {curdate};
      dlg.rec.oper   = "";
      dlg.rec.opername = "все операционисты";
      UpdateFields(dlg); 
   end;
   
   if (cmd==DLG_SETFOCUS)
     officeN = dlg.rec.numoffice;
     message(mess);
   end;
   
   if (cmd == DLG_REMFOCUS)
     if (FldName(dlg,id)=="BeginDate")
        if ( dlg.rec.BeginDate > dlg.rec.EndDate ) 
           MsgBox("Дата начала больше даты окончания отчетного периода");
           return CM_IGNORE;
           elif ( dlg.rec.BeginDate > {curdate} )
           MsgBox("Дата начала больше даты текущего операционного дня"); 
           return CM_IGNORE;
        end;
     elif (FldName(dlg,id)=="EndDate") 
        if (dlg.rec.EndDate < dlg.rec.BeginDate )
           MsgBox("Дата окончания меньше даты начала отчетного периода"); 
           return CM_IGNORE;
        elif ( dlg.rec.EndDate > {curdate} )
           MsgBox("Дата окончания больше даты текущего операционного дня"); 
           return CM_IGNORE;
        end;
     elif (FldName(dlg,id) == "numoffice")
         if (dlg.rec.numoffice != officeN)
             dlg.rec.oper   = "";
             dlg.rec.opername = "все операционисты";
         end;
         if (dlg.rec.numoffice == "")
             dlg.rec.Office = OfficeName;
         end;
     END;    
     UpdateFields(dlg); 
   end; 

   if (cmd==DLG_KEY)
     if (KEY==KEY_ESC)
        return exit(1);//CM_CANCEL;
     elif ( KEY == KEY_F3)
        if (FldName(dlg,id)=="numoffice")
           if (ListDepartment (Department))
              OfficeName = GetClientName(Department.PartyID);
              dlg.rec.numoffice = department.name;
              dlg.rec.office = GetClientName(Department.PartyID);
              office = Department.Code;
              UpdateFields(dlg);
              PartyID = Department.PartyID;
           end;
     elif(FldName(dlg,id) == "oper")
            rselect = "select t.t_oper, t.t_name " +   
                      " from dperson_dbt t inner join ddp_dep_dbt v on v.t_name = '"+dlg.rec.numoffice+"' and t.t_codedepart = v.t_code;";

            cmd = RSDCommand(rselect);                        
            rsrec = RSDRecordset( cmd, RSDVAL_CLIENT, RSDVAL_STATIC );
            if (RunScroll(rsrec, 0, 0, "Опера", @EvProc, "Список операционистов", "~Enter~ выбор сотрудника ~Esc~ выход", true))
              dlg.rec.oper = rsrec.value (0);
              dlg.rec.OperName = rsrec.value (1);
              UpdateFields(dlg);
              OperSelect = rsrec.value (0);
            end;
        end;

     elif ( KEY==KEY_F2 )
        if (StrLen(trim(dlg.rec.numoffice)) == 0)
           office     = {NumDprt};
           OfficeName = {Name_Bank};
           OperSelect = "";
           OrgRegStruct = "";
        end;
           BeginDate  = dlg.rec.BeginDate; 
           EndDate    = dlg.rec.EndDate;
        /*02/09/2008 Новиков С. С. добавил возможность формирования отчета только при корректных значениях*/
        if ((dlg.rec.BeginDate <= dlg.rec.EndDate) and (dlg.rec.BeginDate <= {curdate})
          and (dlg.rec.EndDate >= dlg.rec.BeginDate) and (dlg.rec.EndDate <= {curdate})) 
          Return CM_SAVE;
        else
          MsgBox("Не все поля корректны!");
        end;
        /***************************************************************************************************/


     elif ( (KEY==KEY_ENTER) and (FldName(dlg,id)=="EndDate") )
         SetFocus(dlg, 0);
         Return CM_IGNORE;
     end;
   end;
END;

if ( RunDialog(dlg, "Event"))
  PrintItAll();
end;


