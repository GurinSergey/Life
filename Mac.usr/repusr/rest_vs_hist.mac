import globals,rsbdataset,treport;

//table1.printhead();

var kolwo:integer;
var opisanie:string="Введите количество дней. В отчет попадут те счета, на которых есть расхождения в истории остатков и на карточке счета, прчем если в указанный период (текущий опердень минус введенное значение) были проводки, то счет не попадает в отчет";
var kolwo_all:integer=0;

macro bank_name()
   private var ds;
   private var s_bank_name:string="default bank";
   ds=trsbdataset("select t_name from dparty_dbt where t_partyid='"+{OurBank}+"'");
   if (ds.movenext) s_bank_name=ds.value("t_name");
   else msgbox(" чото не могу выбрать название для банка. останавливаюсь "); exit(7);
   end;
   return s_bank_name;
end;

macro day_valutu(qw);
   private var ds;
   ds=trsbdataset("select t_ccy from dfininstr_dbt where t_fiid="+qw);
   if (ds.movenext) return ds.value(0);
   else msgbox("  не могу выбрать название для валюты по коду "+qw+" останавливаюсь "); exit(7);
   end;

end;

macro printtable(valuta)
  var table1=ctablereport();
  var sql:string="";
  var first_line:bool=true;
  var ds;
  var ta:string,tr:string,tp:string,gol:string,uslovie_valuta:string;

  table1.addcolumn("счет",20, AL_RIGHT);
  table1.addcolumn("глава",4, AL_center);
  table1.addcolumn("последняя дата проводки",15, AL_LEFT); 
  table1.addcolumn("остаток из истории",10, AL_LEFT);
  table1.addcolumn("остаток на счете",10, AL_LEFT);
  table1.addcolumn("валюта",10, AL_LEFT);

 
  if (valuta=="rub")
    ta="daccount_dbt";
    tr="drestdate_dbt";
    tp="darhdoc_dbt";
    gol="Рублевые счета "+bank_name;
    uslovie_valuta=" = 0 ";
  elif (valuta=="val")
    ta="daccount$_dbt";
    tr="drestdat$_dbt";
    tp="darhdoc$_dbt";
    gol="Валютные счета "+bank_name;
    uslovie_valuta=">0";

  else msgbox("неверно переданы параметры валюты в процедуру"); exit(1);
  end;

sql="SELECT   * "+
"  FROM   (SELECT   * "+
"            FROM   (SELECT   acc.t_account c4et1, rest.t_account c4et2, acc.t_chapter glava, rest.t_date_value ddata, rest.t_rest ostatok, "+
"                             acc.t_r0 ostatok_karta, acc.t_code_currency vv "+
"                      FROM   "+ta+" acc, "+tr+" rest "+
"                     WHERE   acc.t_account = rest.t_account "+
"                         AND acc.t_chapter = rest.t_chapter "+
"                         AND acc.t_code_currency = rest.t_code_currency "+
"                         AND acc.t_code_currency "+uslovie_valuta +
"                         AND rest.t_date_value = "+
"                                (SELECT   MAX (dr2.t_date_value) "+
"                                   FROM   "+tr+" dr2 "+
"                                  WHERE   dr2.t_account = rest.t_account "+
"                                      AND dr2.t_chapter = rest.t_chapter "+
"                                      AND dr2.t_date_value <= (SELECT   MAX (t_curdate) FROM dcurdate_dbt) - "+kolwo+")) "+
"           WHERE   (ostatok - ostatok_karta) <> 0) "+
" WHERE   NOT EXISTS "+
"            (SELECT   1 "+
"               FROM   "+tp+" prov "+
"              WHERE   prov.t_date_carry > ddata "+
"                  AND ( (prov.t_account_payer = c4et1) OR (prov.t_account_receiver = c4et1)) "+
"                  AND prov.t_chapter = glava "+
"                  AND vv = prov.t_code_currency) ";


  ds=trsbdataset(sql);
  table1.printhead(gol);
  while (ds.movenext)
    kolwo_all=kolwo_all+1;
    if (not(first_line)) 	table1.printseparator; end;
    table1.printStringTransferByWord(ds.value("c4et1"),ds.value("glava"),substr(string(ds.value("ddata")),1,10),ds.value("ostatok"),ds.value("ostatok_karta"),day_valutu(ds.value("vv"))   );  
    first_line=false;
  end;        
  table1.printbottom(string(" всего счетов: "+kolwo_all));
  end;

if (getint(kolwo,opisanie,4,false)==false) exit(1);
end;

BegAction(0,"выборка рублевых счетов ......",false);
printtable("rub");
EndAction();
 println();
 println();
BegAction(0,"выборка валютных счетов ......",false);
printtable("val");
EndAction();



