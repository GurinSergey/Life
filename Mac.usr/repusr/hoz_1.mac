// ------------------------------------------------------------------------------------------------
// @filename: hoz_1.mac
// @author  : Тихомиров А.Н. 
// @desc    : Выгрузка данных для отчета по сверке остатков в Диасофт
// @modif   : 04.02.2014 Zlenko MP Адаптация под 31-й патч
// -------------------------------------------------------------------------------------------------

//Изменен : 06.03.2013 zmp C-638
Import BankInter, FIINTER, globals, календарь, RSD, rsbdataset, rsexts, oralib, likePy;
Import "fg_Life_parm.mac";

var Fulloutputl, outl, outputl = "hoz_60.lbr", dat, mon1, year;                    

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,outl);
Fulloutputl = FindPath(outputl, outl);
var dlg = TRecHandler("hoz", fulloutputl, TRUE); 

private Var fgBank = fg_life_subject({OurBank});         
/*06.03.2013 zmp C-638 добавлены маски счетов 60701* 610* */
const AccMask     = "^6031[1-4]|^6032[2-4]|^60701|^610|^61304|^61403";
const KEY_F2      = 316;
const KEY_F3      = 317;
const KEY_ESC     =  27;
const KEY_ENTER   =  13;

var RepDate;

MACRO Event (dlg, cmd, id, key) 
   var const_mess  = "~F2~ Продолжить ~SPACE~ По всем офисам ~ESC~ Выход ";
   var const_mess2 = "~F2~ Продолжить ~SPACE~ ~ESC~ Выход ";

   /*Первоначальная инициализация полей*/
   if(cmd == DLG_INIT)
      dlg.rec.date ={curdate};
      UpdateFields(dlg); 
   end;
   
   /*Установка подсказок в строке состояния*/   

   if (cmd == DLG_KEY)
      /*Выход из диалогового окна формирования отчета*/
      if (KEY == KEY_ESC)
         return exit(1);//CM_CANCEL;
      elif ( KEY == KEY_F3)
         dlg.rec.date = getdatebycalendar({curdate});
      elif (( KEY == KEY_F2 ) or (KEY == KEY_ENTER))         //Проверки при вводе
         if ( dlg.rec.date > {curdate} )
                MsgBox("Дата начала периода не может быть больше даты текущего операционного дня");
                return CM_IGNORE;
         else
            RepDate  = dlg.rec.date;
            return CM_SELECT;  
         end;
        
      end;
   end;    
END;

RunDialog (dlg, @Event);

macro getRecordCount(SQL, Param)
   var _sql = execSQLSelect(" select count(1) from ( " + SQL + " )", Param);

   if (_sql and _sql.moveNext())
       return _sql.value(0, null, v_integer);
   end;

   return 0;
end;

var ParamSQL = Tarray();
var sql = "  WITH param AS (SELECT   :repdate repdate, :mask mask FROM DUAL )        " +
          "  SELECT   acc.t_account account,                                         " +
          "            ACC.T_CODE_CURRENCY code_currency,                            " +
          "            NVL (ACC.T_PAIRACCOUNT, ' ') pacc,                            " +
          "            REST.T_REST restAcc,                                          " +
          "            REST.T_RESTDATE restDate                                      " +
          "     FROM   daccount_dbt acc, drestdate_dbt rest, param                   " +
          "    WHERE       REGEXP_LIKE (ACC.T_account, param.mask)                   " +
          "            AND ACC.T_ACCOUNTID = REST.T_ACCOUNTID                        " +     
          "            AND ACC.T_CODE_CURRENCY = REST.T_RESTCURRENCY                 " +
          "            AND acc.T_CLOSE_DATE <= param.repdate                         " +
          "            AND acc.T_OPEN_DATE <= param.repdate                          " +
          "            AND REST.T_RESTDATE =                                         " +
          "                  (SELECT   MAX (R.T_RESTDATE)                            " +
          "                     FROM   drestdate_dbt r                               " +
          "                    WHERE       ACC.T_ACCOUNTID = R.T_ACCOUNTID           " +
          "                            AND ACC.T_CODE_CURRENCY = R.T_RESTCURRENCY    " +
          "                            AND R.T_RESTDATE <= param.repdate)            " +
          "            AND REST.T_REST != 0                                          " +
          "            AND ACC.T_CHAPTER = 1                                         " ;         
                                                                                      
    sql = execSQLSelect(sql + " ORDER BY   ACC.T_ACCOUNT ", 
                        ParamSQL = makeArray(SQLParam("repdate" , RepDate),
                                             SQLParam("mask"    , ifThenElse(fgBank.is_GO, "60312", AccMask)))); 

 var i:integer = 0, sum_r, path, RateCur;
 var delimStr = StrFor(26); 
 record rate (ratedef);

 datesplit({curdate}, dat, mon1, year); 
 path = gettxtfilename(string(dat, "-", mon1, "-", year," .txt"));
 
 setoutput(path, false);                
 initprogress(getRecordCount(SQL.command.cmdText, ParamSQL), "Вывод результатов", "Вывод результатов");
 while (sql.movenext)     
     if(sql.value("code_currency", null, v_integer) != 0)         
         ПолучитьКурс(rate, 0, sql.value("code_currency", null, v_integer), 7);
         RateCur = rate.rate / rate.scale / pow(10, rate.point);
         sum_r = money(RateCur * sql.value("restAcc", null, v_money));
     else
         sum_r = sql.value("restacc", null, v_money);
     end;
    [########## # #################### # ###################### # #################### # #########################]    
    (sql.value("restDate", null, v_date):10:f, delimStr, sql.value("account"):20 , delimStr,  money(sql.value("restAcc")), delimStr,  sql.value("pacc"):20, delimStr, sum_r );
    useprogress(i = i + 1);
 end;
remprogress();
setoutput(null,true);               

var path1, _path1, error;
    path1 = "$C:\\TEMP\\";
    GetRegistryValue( "PRBB\\ИНТЕРФЕЙСЫ\\EXPORTDIASOFT",V_STRING,_path1,error);
    if(error == 0) path1 = _path1;  end;
if (CopyFile(path, string(path1, dat, "-", mon1, "-", year, ".txt"), true, "Копирование файла на терминал"))
else msgbox("Файл не скопирован на терминал");
end;
viewfile(path);
exit(1);