/************************************************************************/
/* ОТЧЕТ "ОБЩИЕ ПОКАЗАТЕЛИ ОБРАБОТКИ ДОКУМЕНТОВ РКО В RS-BANK"          */
/*                                                                      */
/* Юшманов Дмитрий                                                      */
/* Начато 24.04.2013                                                    */
/*                                                                      */
/* C-19151-6                                                            */
/*                                                                      */
/************************************************************************/

import bankinter, RsbDataSet, rsexts, globals, RsbFormsInter, rslx;
import "KeyCodes.mac";

private const
    TYPE_INTEGER = 1,
    TYPE_STRING = 7,
    TYPE_DATE = 9,
    TYPE_TIME = 10,
    RES_SIZE = 11, //размер структуры массива результатов
    DOCKIND_SIZE = 4, //размер структуры массива видов документов
    SOURCE_SIZE = 3, //размер структуры массива источников документов
    SCHEME_SIZE = 3, //размер структуры массива схем расчетов
    CIPHER_SIZE = 3, //размер структуры массива шифров документов
    COLOR_RED = #B5B5FF, //#BBGGRR
    COLOR_GREEN = #B5FFB5,
    CONST_ORIGLIST = "15,16,17,18,27,70,201,202,203,400,410,420,430", //виды документов, обрабатываемые процедурой ea.getOrigin
    REP_NAME = "Общие показатели обработки документов РКО в RS-BANK",
    REP_NAME_UPPER = "ОБЩИЕ ПОКАЗАТЕЛИ ОБРАБОТКИ ДОКУМЕНТОВ РКО В RS-BANK";

private var 
    aDockind = TArray(), 
    aSource = TArray(), 
    aScheme = TArray(), 
    aCipher = TArray(),
    aResult = TArray(),
    startDate : Date = {curdate} - 30, 
    endDate : Date = {curdate},
    startTime : Time = "00:00:00", 
    endTime : Time = "23:59:59",
    strDayExists : string = "",
    formRun : bool = false,
    debugMode = false,
    debugNumDocs = 10;

//диалоговая панель с параметрами фильтра
private class (TRsbPanel) RslPanel(caption: string)
/*----------------------------------------------------------------------------*/
var endOfMultiSelect : bool = false,
    ctrlName : string = "";
/*----------------------------------------------------------------------------*/
    //добавление на панель текстовой метки
    macro addLabelField(name: string, x: integer, y: integer)
    var m_label: TRSBLabel;

        m_label = TRSBLabel(x, y, name);
        addLabel(m_label);
    end;
/*----------------------------------------------------------------------------*/
    //добавление на панель поля ввода
    macro addEditField(name: string, dataType: integer, val: variant, textLen: integer, editFlag: bool, 
                       focusFlag: bool, posX: integer, posY: integer, sizeX: integer, sizeY: integer)
    var m_edit: TRSBEditField = TRsbEditField(dataType);

        m_edit.name = name;
        m_edit.setPosition(posX, posY);
        m_edit.setSize(sizeX, sizeY);
        m_edit.textLength = textLen;
        m_edit.editable = editFlag;
        m_edit.focusable = focusFlag;
        m_edit.value = val;
        addControl(m_edit);
    end;
/*----------------------------------------------------------------------------*/
    //добавление на панель чекбокса
    macro addCheckBox(name: string, posX: integer, posY: integer)
    var m_checkbox: TRSBCheckBox = TRSBCheckBox();

        m_checkbox.name = name;
        m_checkbox.setPosition(posX, posY);
        m_checkbox.checked = false;
        addControl(m_checkbox);
    end;
/*----------------------------------------------------------------------------*/
    //обработка выбора элемента скроллинга
    macro scrollElement (ctrlName, rs, pArray, structSize, offset, cmpStr)
    var i : integer = 0;

        for (i, 0, pArray.size/structSize)
            if (pArray[structSize*i+offset] == rs.value(cmpStr))
                pArray[structSize*i] = (pArray[structSize*i] == false); //инвертируем флаг выбора элемента
            end;
        end;

        return pArray;

    end;
/*----------------------------------------------------------------------------*/
    //обработчик событий скроллингов
    macro scrollEvent (rs, cmd, id, key)
    var i : integer = 0;

        if (cmd == DLG_INIT)
            endOfMultiSelect = false;
            addMultiAction(rs, KEY_ENTER);

        elif (cmd == DLG_MSEL)
            if   (ctrlName == "formDockindEdit")
                aDockind = scrollElement(ctrlName, rs, aDockind, DOCKIND_SIZE, 1, "t_dockind");
            elif (ctrlName == "formSourceEdit")
                aSource = scrollElement(ctrlName, rs, aSource, SOURCE_SIZE, 2, "srcname");
            elif (ctrlName == "formCipherEdit")
                aCipher = scrollElement(ctrlName, rs, aCipher, CIPHER_SIZE, 1, "t_shifr_oper");
            elif (ctrlName == "formSchemeEdit")
                aScheme = scrollElement(ctrlName, rs, aScheme, SCHEME_SIZE, 1, "csnumber");
            end;
            return CM_MSEL_CONT_CLEAR;

        elif (cmd == DLG_MSELEND)
            endOfMultiSelect = true;

        elif ((cmd == DLG_KEY) and (endOfMultiSelect))
            return CM_CANCEL;

        elif ((cmd == DLG_KEY) and (key == KEY_ENTER))
            if   (ctrlName == "formDockindEdit")
                aDockind = scrollElement(ctrlName, rs, aDockind, DOCKIND_SIZE, 1, "t_dockind");
            elif (ctrlName == "formSourceEdit")
                aSource = scrollElement(ctrlName, rs, aSource, SOURCE_SIZE, 2, "srcname");
            elif (ctrlName == "formCipherEdit")
                aCipher = scrollElement(ctrlName, rs, aCipher, CIPHER_SIZE, 1, "t_shifr_oper");
            elif (ctrlName == "formSchemeEdit")
                aScheme = scrollElement(ctrlName, rs, aScheme, SCHEME_SIZE, 1, "csnumber");
            end;
            return CM_SELECT;
        end;

    end;
/*----------------------------------------------------------------------------*/
    //вспомогательное заполнение массива для построения скроллинга
    macro addCol (ar, ind, fld, head, width, rdonly)
        ar.value (ind * 6) = fld;
        ar.value (ind * 6 + 1) = head;
        ar.value (ind * 6 + 2) = width;
        ar.value (ind * 6 + 3 ) = 2;  // fldType
        ar.value (ind * 6 + 4 ) = -1; // decPoint
        ar.value (ind * 6 + 5 ) = 0;  // reserv
    end;
/*----------------------------------------------------------------------------*/
    //общие для скроллингов действия
    macro quickScroll(rs, col, numCols, pArray, structSize, ctrlName)
    var tempstr : string = "",
        moreThanOne : bool = false,
        i : integer = 0;

        RunScroll(rs, numCols, col, Null, R2M(this, "scrollEvent"));

        //пересчёт содержимого "подвала" для выбранного фильтра
        for (i, 0, pArray.size/structSize)
            if (pArray[structSize*i])
                if (tempstr == "")
                    tempstr = pArray[structSize*i+2];
                else
                    moreThanOne = true;
                end;
            end;
        end;
        if (tempstr == "") tempstr = "ВСЕ" end;
        if (moreThanOne) tempstr = "Несколько" end;

        getControl(ctrlName).value = tempstr;

    end;
/*----------------------------------------------------------------------------*/
    //обработчик события "нажатие клавиши" для всей экранной формы
    macro onKeyPress (RsbEvent: object)
    var i : integer = 0;

        //сброс выбранного параметра фильтра
        if (RsbEvent.KeyCode == KEY_F4)

            ctrlName = RsbEvent.source.name;

            if   (ctrlName == "formDockindEdit")
                for (i, 0, aDockind.size/DOCKIND_SIZE) aDockind[DOCKIND_SIZE*i] = false; end;
                getControl(ctrlName).value = "ВСЕ";
            elif (ctrlName == "formSourceEdit")
                for (i, 0, aSource.size/SOURCE_SIZE) aSource[SOURCE_SIZE*i] = false; end;
                getControl(ctrlName).value = "ВСЕ";
            elif (ctrlName == "formSchemeEdit")
                for (i, 0, aScheme.size/SCHEME_SIZE) aScheme[SCHEME_SIZE*i] = false; end;
                getControl(ctrlName).value = "ВСЕ";
            elif (ctrlName == "formCipherEdit")
                for (i, 0, aCipher.size/CIPHER_SIZE) aCipher[CIPHER_SIZE*i] = false; end;
                getControl(ctrlName).value = "ВСЕ";
            end;

        //запуск скроллингов для параметров фильтров
        elif (RsbEvent.KeyCode == KEY_F3)
        var col = TArray(),
            tempstr : string = "",
            rs;

            ctrlName = RsbEvent.source.name;

            if (ctrlName == "formDockindEdit")

                addCol(col, 0, "chosen", "Выбор", 5, true);
                addCol(col, 1, "t_dockind", "Код вида документа", 15, true);
                addCol(col, 2, "t_name", "Вид документа", 30, true);

                //добавляем в скроллинг колонку с индикацией записей, которые уже выбраны пользователем
                for (i, 0, aDockind.size/DOCKIND_SIZE-1)
                    if (aDockind[DOCKIND_SIZE*i]) tempstr = tempstr + "," + aDockind[DOCKIND_SIZE*i+1] end;
                end;

                if (tempstr != "")
                    tempstr = "case when t_dockind in (" + substr(tempstr,2) + ") then 'X' else '' end chosen,";
                else
                    tempstr = "'' chosen,";
                end;

                rs = RsdRecordSet(string(
                    "select " + tempstr, 
                    "\n     t_dockind, ",
                    "\n     t_name, ",
                    "\n     substr(t_strorigin, ",
                    "\n            length(t_strorigin) + 2 - instr(REVERSE(t_strorigin),'/')) t_origin ",
                    "\n from ",
                    "\n ( select t.t_dockind, ",
                    "\n          t.t_name, ",
                    "\n          replace(SYS_CONNECT_BY_PATH(t.t_origin, '/'),  '/0', null) t_strorigin ",
                    "\n     from doprkdoc_dbt t ",
                    "\n    where CONNECT_BY_ISLEAF = 1 ",
                    "\n      and t_dockind != 320 ", //у 320-ого документа нет справочника происхождений, исправлено ниже
                    "\n    start with t.t_dockind = 29 ",
                    "\n  connect by prior t_dockind = t_parentdockind ",
                    "\n    UNION ",
                    "\n   select w.t_dockind, w.t_name, '/1660' t_strorigin ",
                    "\n     from doprkdoc_dbt w ",
                    "\n    where w.t_dockind = 200 ",
                    "\n    UNION ",
                    "\n   select w.t_dockind, w.t_name, '/1660' t_strorigin ",
                    "\n     from doprkdoc_dbt w ",
                    "\n    where w.t_dockind = 320) ",
                    "\n order by t_dockind " ),
                    RSDVAL_CLIENT, RSDVAL_STATIC);

                quickScroll(rs, col, 3, aDockind, DOCKIND_SIZE, ctrlName);

            elif (ctrlName == "formSchemeEdit")

                addCol(col, 0, "chosen", "Выбор", 5, true);
                addCol(col, 1, "csnumber", "Номер корсхемы", 15, true);
                addCol(col, 2, "csname", "Корсхема", 30, true);

                //добавляем в скроллинг колонку с индикацией записей, которые уже выбраны пользователем
                for (i, 0, aScheme.size/SCHEME_SIZE-1)
                    if (aScheme[SCHEME_SIZE*i]) tempstr = tempstr + "," + aScheme[SCHEME_SIZE*i+1] end;
                end;

                if (tempstr != "")
                    tempstr = "case when csnumber in (" + substr(tempstr,2) + ") then 'X' else '' end chosen,";
                else
                    tempstr = "'' chosen,";
                end;

                rs = RsdRecordSet(string(
                  "select " + tempstr + " t.* from ( ",
                  " SELECT '-1' csnumber, ",
                  "        '(без корсхемы)' csname ",
                  " FROM dual ",
                  "    UNION ",
                  " SELECT to_char(cs.t_number) csnumber, ",
                  "        cs.t_name csname ",
                  " FROM dcorschem_dbt cs ",
                  " WHERE cs.t_dogclosedate = '01.01.0001' ) t ",
                  " ORDER BY to_number(csnumber) " ),
                    RSDVAL_CLIENT, RSDVAL_STATIC);

                quickScroll(rs, col, 3, aScheme, SCHEME_SIZE, ctrlName);

            elif (ctrlName == "formCipherEdit")

                addCol(col, 0, "chosen", "Выбор", 5, true);
                addCol(col, 1, "t_shifr_oper", "Код шифра документа", 15, true);
                addCol(col, 2, "t_comment", "Шифр документа", 60, true);

                //добавляем в скроллинг колонку с индикацией записей, которые уже выбраны пользователем
                for (i, 0, aCipher.size/CIPHER_SIZE-1)
                    if (aCipher[CIPHER_SIZE*i]) tempstr = tempstr + ",''" + aCipher[CIPHER_SIZE*i+1] + "''" end;
                end;

                if (tempstr != "")
                    tempstr = "case when t_shifr_oper in (" + substr(tempstr,2) + ") then 'X' else '' end chosen,";
                else
                    tempstr = "'' chosen,";
                end;

                rs = RsdRecordSet(string(
                    "select " + tempstr + " t.* from ("
                    " SELECT '''' || to_char(t_shifr_oper) || '''' t_shifr_oper, t_comment FROM dcipher_dbt",
                    " UNION ",
                    " SELECT ''' ''' t_shifr_oper, '(Шифр не проставлен)' t_comment FROM dual ) t"),
                    RSDVAL_CLIENT, RSDVAL_STATIC);

                quickScroll(rs, col, 3, aCipher, CIPHER_SIZE, ctrlName);

            elif (ctrlName == "formSourceEdit")
            var strDockind : string = "";

                for (i, 0, aDockind.size/DOCKIND_SIZE)
                    if (aDockind[DOCKIND_SIZE*i] == true)
                        strDockind = strDockind + "," + aDockind[DOCKIND_SIZE*i+1];
                    end;
                end;

                if (substr(strDockind,1,1) == ",")
                    strDockind = substr(strDockind,2);
                end;

                if (strDockind != "")
                    strDockind = " and t.t_dockind in (" + strDockind + ") ";
                end;

                addCol(col, 0, "chosen", "Выбор", 5, true);
                addCol(col, 1, "srcname", "Источник документа", 30, true);

                //добавляем в скроллинг колонку с индикацией записей, которые уже выбраны пользователем
                for (i, 0, aSource.size/SOURCE_SIZE-1)
                    if (aSource[SOURCE_SIZE*i]) tempstr = tempstr + ",'" + aSource[SOURCE_SIZE*i+2] + "'"; end;
                end;

                if (tempstr != "")
                    //tempstr = "case when lval.t_name in (" + substr(tempstr,2) + ") then 'X' else '' end chosen,";
                    tempstr = "case when big.srcname in (" + substr(tempstr,2) + ") then 'X' else '' end chosen,";
                else
                    tempstr = "'' chosen,";
                end;

                rs = RsdRecordSet(string(
                  "select distinct " + tempstr,
                  "\n       big.* ",
                  "\n  from ( ",
                  "\n       select lval.t_name srcname ",
                  "\n         from ( ",
                  "\n              select t_dockind, ",
                  "\n                     t_name, ",
                  "\n                     substr(t_strorigin, length(t_strorigin) + 2 - instr(REVERSE(t_strorigin),'/')) t_origin ",
                  "\n                from ( ",
                  "\n                     select t.t_dockind, ",
                  "\n                            t.t_name, ",
                  "\n                            replace(SYS_CONNECT_BY_PATH(t.t_origin, '/'),  '/0', null) t_strorigin ",
                  "\n                       from doprkdoc_dbt t ",
                  "\n                      where CONNECT_BY_ISLEAF = 1 ",
                  "\n                        and t.t_dockind != 320 ", //у 320-ого документа нет справочника происхождений, исправлено ниже
                  "\n                      start with t.t_dockind = 29 ",
                  "\n                    connect by prior t_dockind = t_parentdockind ",
                  "\n                     UNION ",
                  "\n                     select w.t_dockind, ",
                  "\n                            w.t_name, ",
                  "\n                            '/1660' t_strorigin ",
                  "\n                       from doprkdoc_dbt w ",
                  "\n                      where w.t_dockind = 200 ",
                  "\n                     UNION ",
                  "\n                     select w.t_dockind, ",
                  "\n                            w.t_name, ",
                  "\n                            '/1660' t_strorigin ",
                  "\n                       from doprkdoc_dbt w ",
                  "\n                      where w.t_dockind = 320 ) ",
                  "\n              ) t, ",
                  "\n              dllvalues_dbt lval ",
                  "\n        where t.t_origin = lval.t_list ",
                  "\n " + strDockind,
                  "\n       ) big "),
                    RSDVAL_CLIENT, RSDVAL_STATIC);

                quickScroll(rs, col, 2, aSource, SOURCE_SIZE, ctrlName);

            end;

        //запуск отчёта
        elif (RsbEvent.KeyCode == KEY_F2)
            startDate = getControl("formDateStart").value;
            endDate   = getControl("formDateEnd").value;
            startTime = getControl("formTimeStart").value;
            endTime   = getControl("formTimeEnd").value;
            debugMode = getControl("formIDsCheckBox").checked;
            debugNumDocs = int(getControl("formIDsEdit").value);
            formRun = true;
            close(1);
        end;

    end;
/*----------------------------------------------------------------------------*/
//заполнение экранной формы
    macro init(caption: string)
    var formDockindArr = TArray(),
        formSourceArr = TArray(),
        formSchemeArr = TArray(),
        formCipherArr = TArray(),
        editString = 9000,
        editLength = 25,
        editHeight = 1,
        alLeft = 4, 
        Line = 2,
        alStartDate = alLeft + 20,
        alFinDate = alLeft + 32,
        alCombo = alLeft + 16;

        setCaption(caption);
        setposition(15,5);
        setsize(50,12);
        addEventHandler(RSB_EV_KEY_PRESSED, R2M(this, "onKeyPress"));
        setStatus("F2: Запуск отчёта, F3: Список значений, F4: Сброс поля");

        //инициализация массивов параметров фильтра:
        //вид первичного документа
        var rs = RsdRecordSet(string(
        "   select t_dockind, ",
        "\n        t_name, ",
        "\n        substr(t_strorigin, ",
        "\n               length(t_strorigin) + 2 - instr(REVERSE(t_strorigin),'/')) t_origin ",
        "\n from ",
        "\n ( select t.t_dockind, ",
        "\n          t.t_name, ",
        "\n          replace(SYS_CONNECT_BY_PATH(t.t_origin, '/'),  '/0', null) t_strorigin ",
        "\n     from doprkdoc_dbt t ",
        "\n    where CONNECT_BY_ISLEAF = 1 ",
        "\n      and t.t_dockind != 320 ",
        "\n    start with t.t_dockind = 29 ",
        "\n  connect by prior t_dockind = t_parentdockind ",
        "\n    UNION ",
        "\n   select w.t_dockind, w.t_name, '/1660' t_strorigin ",
        "\n     from doprkdoc_dbt w ",
        "\n    where w.t_dockind = 200 ",
        "\n    UNION ",
        "\n   select w.t_dockind, w.t_name, '/1660' t_strorigin ",
        "\n     from doprkdoc_dbt w ",
        "\n    where w.t_dockind = 320 )" ));
        var i : integer = 0;
        while (rs.moveNext)
            aDockind[DOCKIND_SIZE*i]   = false;
            aDockind[DOCKIND_SIZE*i+1] = rs.value("t_dockind");
            aDockind[DOCKIND_SIZE*i+2] = rs.value("t_name");
            aDockind[DOCKIND_SIZE*i+3] = rs.value("t_origin");
            i = i + 1;
        end;

        //происхождение документа
        rs = RsdRecordSet(string(
        "   SELECT distinct lval.t_element, ",
        "\n                 lval.t_name srcname ",
        "\n   FROM dllvalues_dbt lval, ",
        "\n        doprkdoc_dbt kdoc ",
        "\n  WHERE lval.t_list = kdoc.t_origin "));
        i = 0;
        while (rs.moveNext)
            aSource[SOURCE_SIZE*i]   = false;
            aSource[SOURCE_SIZE*i+1] = rs.value("t_element");
            aSource[SOURCE_SIZE*i+2] = rs.value("srcname");
            i = i + 1;
        end;

        //корсхемы
        rs = RsdRecordSet(string(
          " SELECT '-1' csnumber ",
          "       ,'(без корсхемы)' csname ",
          " FROM dual ",
          "    UNION ",
          " SELECT to_char(cs.t_number) csnumber ",
          "       ,cs.t_name csname ",
          " FROM dcorschem_dbt cs ",
          " WHERE cs.t_dogclosedate = '01.01.0001' ",
          " ORDER BY csnumber" ));
        i = 0;
        while (rs.moveNext)
            aScheme[SCHEME_SIZE*i] = false;
            aScheme[SCHEME_SIZE*i+1] = rs.value("csnumber");
            aScheme[SCHEME_SIZE*i+2] = rs.value("csname");
            i = i + 1;
        end;

        //шифры
        rs = RsdRecordSet(string(
          " SELECT '''' || to_char(t_shifr_oper) || '''' t_shifr_oper, t_comment FROM dcipher_dbt ",
          " UNION ",
          " SELECT ''' ''', '(Шифр не проставлен)' FROM dual "));
        i = 0;
        while (rs.moveNext)
            aCipher[CIPHER_SIZE*i] = false;
            aCipher[CIPHER_SIZE*i+1] = rs.value("t_shifr_oper");
            aCipher[CIPHER_SIZE*i+2] = rs.value("t_comment");
            i = i + 1;
        end;

        //создание элементов экранной формы
        addLabelField("Дата окончания обработки:", alLeft, Line);
        addLabelField("с", alStartDate-2, Line);
        addEditField("formDateStart", TYPE_DATE, startDate, 10, true, true, alStartDate, Line, 9, 1);
        addLabelField("по", alFinDate-2, Line);
        addEditField("formDateEnd", TYPE_DATE, endDate, 10, true, true, alFinDate, Line, 9, 1);
        Line = Line + 1;

        addLabelField("Время поступления:", alLeft, Line);
        addLabelField("с", alStartDate-2, Line);
        addEditField("formTimeStart", TYPE_TIME, startTime, 10, true, true, alStartDate, Line, 9, 1);
        addLabelField("по", alFinDate-2, Line);
        addEditField("formTimeEnd", TYPE_TIME, endTime, 10, true, true, alFinDate, Line, 9, 1);
        Line = Line + 2;

        addLabelField("Вид документа:", alLeft, Line);
        addEditField("formDockindEdit", TYPE_STRING, "ВСЕ", editString, true, true, alCombo, Line, editLength, editHeight);
        Line = Line + 1;

        addLabelField("Источник поступления:", alLeft, Line);
        addEditField("formSourceEdit", TYPE_STRING, "ВСЕ", editString, true, true, alCombo, Line, editLength, editHeight);
        Line = Line + 1;

        addLabelField("Схема расчетов:", alLeft, Line);
        addEditField("formSchemeEdit", TYPE_STRING, "ВСЕ", editString, true, true, alCombo, Line, editLength, editHeight);
        Line = Line + 1;

        addLabelField("Шифр документа:", alLeft, Line);
        addEditField("formCipherEdit", TYPE_STRING, "ВСЕ", editString, true, true, alCombo, Line, editLength, editHeight);
        Line = Line + 2;

        addLabelField("Со списком id документов", alLeft, Line);
        addCheckBox("formIDsCheckBox", alStartDate - 2, Line);
        addLabelField("Количество id:", alCombo + 10, Line);
        addEditField("formIDsEdit", TYPE_STRING, "10", editString, true, true, alFinDate + 5, Line, 4, editHeight);

    end;
/*----------------------------------------------------------------------------*/    
    InitTRsbPanel();
    init(caption);
/*----------------------------------------------------------------------------*/
end;


/*----------------------------------------------------------------------------*/
//проверка массива параметра фильтра на наличие выбранных элементов
private macro arrIsEmpty(pArr:TArray, pStr:integer)
var isEmpty : bool = true,
    i : integer = 0;
    while ((i < pArr.size/pStr) and isEmpty)
        isEmpty = (not pArr[pStr*i]);
        i = i + 1;
    end;
    return isEmpty;
end;
/*----------------------------------------------------------------------------*/
//простейшая сортировка для массива результатов
private macro sortme(pArr:TArray, pStr:integer)
var t, i, j, k;
    for (j, 1, pArr.size/pStr-1)
        for (i, 1, pArr.size/pStr-1)
            if ((pArr[pStr*(i)+0] < pArr[pStr*(i-1)+0])
            or  ((pArr[pStr*(i)+0] == pArr[pStr*(i-1)+0]) and (pArr[pStr*(i)+2] < pArr[pStr*(i-1)+2])))
                for (k, 0, pStr-1)
                    t = pArr[pStr*(i)+k];
                    pArr[pStr*(i)+k] = pArr[pStr*(i-1)+k];
                    pArr[pStr*(i-1)+k] = t;
                end;
            end;
        end;
    end;
    return pArr;
end;
/*----------------------------------------------------------------------------*/
//подготовка строки для вставки в запрос
private macro strFill (arr, structSize, compl_table)
var i : integer = 0,
    str : string = "";

    for (i, 0, arr.size/structSize-1)
        if (arr[structSize*i] == true)
            str = str + "," + arr[structSize*i+1];
        end;
    end;

    //для фильтра доккайнда, если значение фильтра не задано (стоит "ВСЕ"), заполняем условие всеми возможными
    //значениями этого фильтра чтобы избежать попадания документов с dockind = 320 и прочих.
    if ((str == "") and (compl_table == "t.t_dockind"))
        for (i, 0, arr.size/structSize-1)
            str = str + "," + arr[structSize*i+1];
        end;
    end;

    if (substr(str,1,1) == ",")
        str = substr(str,2);
    end;

    if (str != "")
        str = "\n and " + compl_table + " in (" + str + ") ";
    end;

    return str;

end;
/*----------------------------------------------------------------------------*/
//подготовка строки для вставки в запрос (для массива источников)
private macro strFillSource (arr, cmpStr1, cmpStr2)
var i : integer = 0,
    str : string = "",
    rs;

    if (arrIsEmpty(aSource,SOURCE_SIZE))

        //если источники не выбраны, в запросе достаточно указать доккайнды
        return strFill(aDockind, DOCKIND_SIZE, "t.t_dockind");

    else

        rs = RsdRecordSet(string(
            " select t.* from (",
            "\n  select distinct t.t_dockind, ",
            "\n       t.t_name dkndname, ",
            "\n       lval.t_element, ",
            "\n       lval.t_name ",
            "\n  from (select t_dockind, ",
            "\n               t_name, ",
            "\n               substr(t_strorigin, length(t_strorigin) + 2 - instr(REVERSE(t_strorigin),'/')) t_origin ",
            "\n          from ",
            "\n            (SELECT t.t_dockind, ",
            "\n                    t.t_name, ",
            "\n                    replace(SYS_CONNECT_BY_PATH(t.t_origin, '/'),  '/0', null) t_strorigin ",
            "\n               FROM doprkdoc_dbt t ",
            "\n              WHERE CONNECT_BY_ISLEAF = 1 ",
            "\n                AND t.t_dockind != 320 ",
            "\n              START WITH t.t_dockind = 29 ",
            "\n            CONNECT BY PRIOR t_dockind = t_parentdockind ",
            "\n             UNION ",
            "\n             select w.t_dockind, ",
            "\n                    w.t_name, ",
            "\n                    '/1660' t_strorigin ",
            "\n               from doprkdoc_dbt w ",
            "\n              where w.t_dockind = 200 ",
            "\n             UNION ",
            "\n             select w.t_dockind, ",
            "\n                    w.t_name, ",
            "\n                    '/1660' t_strorigin ",
            "\n               from doprkdoc_dbt w ",
            "\n              where w.t_dockind = 320 ",
            "\n            )) t, ",
            "\n       dllvalues_dbt lval ",
            "\n  where t.t_origin = lval.t_list ",
            "\n ) t ",
            "\n where ",
            "\n " + substr(strFill(aDockind, DOCKIND_SIZE, "t.t_dockind"),7),
            "\n " + strFill(aSource, SOURCE_SIZE, "t.t_element"),
            "\n order by t.t_dockind, t.t_element " ));

        while (rs.moveNext)
            str = str + "or (" + cmpStr1 + " = " + rs.value("t_dockind") + 
                       " and " + cmpStr2 + " = " + rs.value("t_element") + ") ";
        end;

        if (substr(str,1,2) == "or")
            str = substr(str,3);
        end;

        if (str != "")
            str = "\n and (" + str + ") ";
        end;

        return str;

    end;

end;
/*----------------------------------------------------------------------------*/
//основные расчёты статистики: подготовка и выполнение запроса, заполнение массива результатов
private macro core()
    var rs,
        cmd,
        query,
        j : integer = 0,
        i : integer = 0,
        dayExists : bool = false,
        resultSize : integer = 0,
        progressCounter : integer = 0,
        curDate : Date = startdate,
        srcFilled : bool = false,
        strDockind : string = "",
        strCipher : string = "",
        strSource : string = "",
        strScheme : string = "",
        roboList,
        errCode : integer = 0;

    //в случае неверного ввода меняем даты местами
    if (startDate > endDate)
        curDate = startDate;
        startDate = endDate;
        endDate = curDate;
        curDate = startDate;
    end;

    //подготовка строки с параметром "схема расчетов" для вставки в запрос
    strScheme = strFill(aScheme, SCHEME_SIZE, "t.t_corschem");

    //подготовка строки с параметром "шифр документа" для вставки в запрос
    strCipher = strFill(aCipher, CIPHER_SIZE, "t.t_shifroper");

    //подготовка строки с параметром "происхождение документа" для вставки в запрос
    strSource = strFillSource(aSource, "t.t_dockind", "t.origin");

    //промежуточное окно для этапа подсчёта документов
    InitProgress(1, "Идёт подсчёт документов...", REP_NAME);

    //массив инициализируется с ненулевым размером, хотя по факту он пуст. Исправляем.
    aResult.size = 0;

    //считываем список операционистов-роботов
    if (not getRegistryValue("PRBB\\МОНИТОР ОБРАБОТЧИКОВ\\ROBOTS", V_STRING, roboList, errCode, false))
        msgbox("Значение настройки реестра 'PRBB\\МОНИТОР ОБРАБОТЧИКОВ\\ROBOTS' не задано.",
               "Будет использовано значение по умолчанию.");
        roboList = string("10000,10001,20000,20001,20002,20003,20004,20005,20006,20007,20008,"+
                          "20009,20010,20011,20012,20013,20014,20050,20201,22222,13666,20015");
    end;

    //подсчитываем количество документов согласно параметрам фильтра, полученное значение используется в прогрессбаре
    rs = RsdRecordSet(string(
    "   select count(distinct t.t_paymentid) pid from ( ",
    "\n select pm.t_paymentid, ",
    "\n        pm.t_dockind, ",
    "\n        pr.t_corschem, ",
    "\n        rm.t_shifroper, ",
    "\n        case when (pm.t_dockind in (15,16,17,18,27,70,201,202,203,400,410,420,430)) ",
    "\n             then decode( user_ea.getOrigin(pm.t_docKind, pm.t_paymentID), ",
    "\n                          9999, decode( pm.t_primdocorigin, 9999, pm.t_origin, pm.t_primdocorigin ), ",
    "\n                          user_ea.getOrigin(pm.t_docKind, pm.t_paymentID) ) ",
    "\n             else decode( pm.t_primdocorigin, 9999, pm.t_origin, pm.t_primdocorigin ) end origin ",
    "\n   from dpmpaym_dbt pm ",
    "\n       ,dpmrmprop_dbt rm ",
    "\n       ,dpmprop_dbt pr ",
    "\n  where pm.t_paymentid = rm.t_paymentid ",
    "\n    and pm.t_paymentid = pr.t_paymentid ",
    "\n    and pm.t_paymstatus in (100, 32000) ",
    "\n    and pm.t_closedate between '" + startDate + "' and '" + endDate + "' ",
    "\n    and pm.t_creationtime between to_date('01.01.0001 " + startTime + "','dd.mm.yyyy hh24:mi:ss') ",
    "\n                              and to_date('01.01.0001 " +   endTime + "','dd.mm.yyyy hh24:mi:ss') ",
    "\n ) t where ",
        substr( strSource, 7 ), //без первого AND
        strScheme,
        strCipher ));

    rs.moveNext;
    j = rs.value("pid");

    RemProgress();
    InitProgress( j, "Идёт обработка документов...", REP_NAME);

    query = string(
        "   SELECT /*+ INDEX(rstep usr_doprstep_robot_dbt_idx0) */ distinct ",
        "\n        t.t_paymentid pid, ",
        "\n        t.t_dockind, ",
        "\n        t.origin ",
        "\n       ,sum ( ",
        "\n           case when (step.t_oper in (" + roboList + ") or "
        "\n                      rstep.t_is_Robot_Step = chr(88) or "
        "\n                      rstep.t_program = 'ate.exe') ",
        "\n                then 1 ",
        "\n                else 0 ",
        "\n                end) Robostep ",
        "\n       ,sum ( ",
        "\n           case when (step.t_oper in (" + roboList + ") or "
        "\n                      rstep.t_is_Robot_Step = chr(88) or "
        "\n                      rstep.t_program = 'ate.exe') ",
        "\n                then 0 ",
        "\n                else 1 ",
        "\n                end) Manstep ",
        "\n FROM ",
        "\n (select ",
        "\n      pm.t_paymentid, ",
        "\n      pm.t_dockind, ",
        "\n      rm.t_shifroper, ",
        "\n      pr.t_corschem, ",
        "\n      case when (pm.t_dockind in (15,16,17,18,27,70,201,202,203,400,410,420,430)) ",
        "\n           then decode( user_ea.getOrigin(pm.t_docKind, pm.t_paymentID), ",
        "\n                        9999, decode( pm.t_primdocorigin, 9999, pm.t_origin, pm.t_primdocorigin ), ",
        "\n                        user_ea.getOrigin(pm.t_docKind, pm.t_paymentID) ) ",
        "\n           else decode( pm.t_primdocorigin, 9999, pm.t_origin, pm.t_primdocorigin ) end origin ",
        "\n   from dpmpaym_dbt pm, ",
        "\n        dpmrmprop_dbt rm, ",
        "\n        dpmprop_dbt pr ",
        "\n  where pm.t_paymstatus in (100, 32000) ",
        "\n    and pm.t_closeDate = ? ",
        "\n    and pm.t_creationtime between to_date('01.01.0001 " + startTime + "','dd.mm.yyyy hh24:mi:ss') ",
        "\n                              and to_date('01.01.0001 " +   endTime + "','dd.mm.yyyy hh24:mi:ss') ",
        "\n    and pm.t_paymentid = rm.t_paymentid ",
        "\n    and pm.t_paymentid = pr.t_paymentid ",
        "\n  ) t ",
        "\n  ,doproper_dbt oper ",
        "\n  ,doprstep_dbt step ",
        "\n  ,usr_doprstep_robot_dbt rstep ",
        "\n WHERE oper.t_documentid = lpad(t.t_paymentid, 34, '0') ",
        "\n   AND oper.t_dockind = t.t_dockind ",
        "\n   AND oper.t_id_operation = step.t_id_operation ",
        "\n   AND step.t_id_operation = rstep.t_id_operation(+) ",
        "\n   AND step.t_id_step = rstep.t_id_step(+) ",
            strScheme,
            strCipher,
            strSource,
        "\n GROUP BY t.t_paymentid, t.t_dockind, t.origin ",
        "\n ORDER BY t.t_dockind, t.origin "
        );

    //запрос выполняется для каждого дня в отдельности, т.к. нужна статистика по дням с отсутствующими платежами
    while (curDate != endDate + 1)

        cmd = RSDCommand(query);
        cmd.addParam("currentDate", RSDBP_IN, curDate);
        rs = RSDRecordset(cmd);

        dayExists = false;

        while (rs.moveNext)
            dayExists = true;
            i = 0;
            resultSize = aResult.size/RES_SIZE;

            useProgress(progressCounter);
            progressCounter = progressCounter + 1;

            //ищем существующую запись с соответствующими параметрами
            while (not (
                ((aResult[RES_SIZE*i] == rs.value("t_dockind")) and (aResult[RES_SIZE*i+2] == rs.value("origin")))
                or (i == resultSize) ))
                    i = i + 1;
            end;

            //Инициализация структуры, если встретился следующий тип документа или источника
            if (i == resultSize)

                aResult[RES_SIZE*i] = rs.value("t_dockind");

                j = 0;
                while (aDockind[DOCKIND_SIZE*j+1] != rs.value("t_dockind"))
                    j = j + 1;
                end;
                aResult[RES_SIZE*i+1] = aDockind[DOCKIND_SIZE*j+2];

                j = 0;
                while (aSource[SOURCE_SIZE*j+1] != rs.value("origin"))
                    j = j + 1;
                end;
                aResult[RES_SIZE*i+2] = aSource[SOURCE_SIZE*j+1];
                aResult[RES_SIZE*i+3] = aSource[SOURCE_SIZE*j+2];

                aResult[RES_SIZE*i+4] = 0;  //всего документов
                aResult[RES_SIZE*i+5] = 0;  //проведённых вручную
                aResult[RES_SIZE*i+6] = 0;  //проведённых смешанно
                aResult[RES_SIZE*i+7] = 0;  //проведённых киборгами
                aResult[RES_SIZE*i+8] = ""; //paymentid проведённых вручную
                aResult[RES_SIZE*i+9] = ""; //paymentid проведённых смешанно
                aResult[RES_SIZE*i+10] = "";//paymentid проведённых киборгами

            end;
            aResult[RES_SIZE*i+4] = aResult[RES_SIZE*i+4] + 1;
            if   ((rs.value("Robostep") == 0) and (rs.value("Manstep") > 0)) //проведены вручную
                aResult[RES_SIZE*i+5] = aResult[RES_SIZE*i+5] + 1;
                if (aResult[RES_SIZE*i+5] <= debugNumDocs)
                    aResult[RES_SIZE*i+8] = aResult[RES_SIZE*i+8] + string(rs.value("pid") + ",");
                end;
            elif ((rs.value("Robostep") > 0)  and (rs.value("Manstep") > 0)) //проведены смешанно
                aResult[RES_SIZE*i+6] = aResult[RES_SIZE*i+6] + 1;
                if (aResult[RES_SIZE*i+6] <= debugNumDocs)
                    aResult[RES_SIZE*i+9] = aResult[RES_SIZE*i+9] + string(rs.value("pid") + ",");
                end;
            elif ((rs.value("Robostep") > 0)  and (rs.value("Manstep") == 0)) //проведены автоматически
                aResult[RES_SIZE*i+7] = aResult[RES_SIZE*i+7] + 1;
                if (aResult[RES_SIZE*i+7] <= debugNumDocs)
                    aResult[RES_SIZE*i+10] = aResult[RES_SIZE*i+10] + string(rs.value("pid") + ",");
                end;
            end;
        end;

        if (not dayExists)
            strDayExists = strDayExists + ", " + curDate;
        end;

        curDate = curDate + 1;
    end;

    //перегруппируем массив результатов: может возникнуть ситуация, когда запись с новым значением источника 
    //для существующего значения вида документа оторвана от группы предыдущих записей с этим видом документа
    if (aResult.size >= RES_SIZE+1) aResult = sortme(aResult, RES_SIZE); end;

    if (strDayExists != "")
        strDayExists = "За следующие дни платежи отсутствуют: " + substr(strDayExists,3);
    end;

    RemProgress();
    Message("Формирование Excel-файла...");
end;
/*----------------------------------------------------------------------------*/
//x - строка, y - столбец

private macro Put_Cell(Lst, x, y, string, al)
    if (valtype(al) == V_UNDEF) al = 3 end;
    Lst.Cells(x, y).Value = string;
    Lst.Cells(x, y).HorizontalAlignment = al;
end; /*End Of Put_Cell*/

private macro ExcelRep(ComObj)
    var i : integer = 0,
        rs,
        List = ComObj.Sheets(1),
        ListObj = List.Rows(1),
        tempstr : string = "",
        sumAll : integer = 0,
        sumMan : integer = 0,
        sumMix : integer = 0,
        sumRobo : integer = 0;
    
    List.Columns("A:A").ColumnWidth  = 11;
    List.Columns("B:B").ColumnWidth  = 28.5;
    List.Columns("C:C").ColumnWidth  = 30;
    List.Columns("D:D").ColumnWidth  = 19;
    List.Columns("E:E").ColumnWidth  = 16;
    List.Columns("F:G").ColumnWidth  = 14;
    List.Rows("15:15").RowHeight  = 27;

    List.Range("A2:G2").Merge;
    List.Range("A2:G2").Font.Bold = True;
    Put_Cell(List, 2, 1, REP_NAME_UPPER);

    List.Range("A4:C4").Merge;
    List.Range("A4:C4").Value = string("Подразделение (Банк): " + {Name_Bank});

    List.Range("D4:G4").Merge;
    List.Range("D4:G4").Value = string("За период с " + startDate + " по " + endDate);

    List.Range("A6:C6").Merge;
    List.Range("A6:C6").Value = string("Дата, время формирования отчёта: " + date + " " + time);

    List.Range("D6:G6").Merge;
    List.Range("D6:G6").Value = "Параметры фильтра:";

    rs = RsdRecordSet(string("select t_name from dperson_dbt where t_oper = " + {oper}));
    rs.movenext;

    List.Range("A8:C8").Merge;
    List.Range("A8:C8").Value = string("Исполнитель: {" + {oper} + "} " + rs.value("t_name"));

    tempstr = "";
    for (i, 0, aDockind.size/DOCKIND_SIZE)
        if (aDockind[DOCKIND_SIZE*i] == true) tempstr = tempstr + ", " + aDockind[DOCKIND_SIZE*i+2] end;
    end;
    if (substr(tempstr,1,1) == ",") tempstr = substr(tempstr,2) end;
    if (tempstr == "") tempstr = "ВСЕ" end;
    
    List.Range("D8:G8").Merge;
    List.Range("D8:G8").Value = string("Вид документа:" + tempstr);

    tempstr = "";
    for (i, 0, aSource.size/SOURCE_SIZE)
        if (aSource[SOURCE_SIZE*i] == true) tempstr = tempstr + ", " + aSource[SOURCE_SIZE*i+2] end;
    end;
    if (substr(tempstr,1,1) == ",") tempstr = substr(tempstr,2) end;
    if (tempstr == "") tempstr = "ВСЕ" end;

    List.Range("D9:G9").Merge;
    List.Range("D9:G9").Value = string("Источник поступления: " + tempstr);

    List.Range("D10:G10").Merge;
    List.Range("D10:G10").Value = string("Время поступления: " + startTime + " - " + endTime);

    tempstr = "";
    for (i, 0, aScheme.size/SCHEME_SIZE)
        if (aScheme[SCHEME_SIZE*i] == true) tempstr = tempstr + ", " + aScheme[SCHEME_SIZE*i+2] end;
    end;
    if (substr(tempstr,1,1) == ",") tempstr = substr(tempstr,2) end;
    if (tempstr == "") tempstr = "ВСЕ" end;

    List.Range("D11:G11").Merge;
    List.Range("D11:G11").Value = string("Схема расчетов: " + tempstr);

    tempstr = "";
    for (i, 0, aCipher.size/CIPHER_SIZE)
        if (aCipher[CIPHER_SIZE*i] == true) tempstr = tempstr + ", " + aCipher[CIPHER_SIZE*i+2] end;
    end;
    if (substr(tempstr,1,1) == ",") tempstr = substr(tempstr,2) end;
    if (tempstr == "") tempstr = "ВСЕ" end;

    List.Range("D12:G12").Merge;
    List.Range("D12:G12").Value = string("Шифр документа: " + tempstr);

    if (debugMode)
        List.Range("A14:M15").Interior.Color = #B5B5B5;
        List.Range("A14:M15").WrapText = True;
    else
        List.Range("A14:J15").Interior.Color = #B5B5B5;
        List.Range("A14:J15").WrapText = True;
    end;

    List.Range("A14:A15").Merge;
    List.Range("A14:A15").Borders.LineStyle = 1;
    Put_Cell(List,14,1,string("Код вида документа:")); 

    List.Range("B14:B15").Merge;
    List.Range("B14:B15").Borders.LineStyle = 1;
    Put_Cell(List,14,2,string("Наименование вида документа:"));

    List.Range("C14:C15").Merge;
    List.Range("C14:C15").Borders.LineStyle = 1;
    Put_Cell(List,14,3,string("Источник поступления:"));

    List.Range("D14:D15").Merge;
    List.Range("D14:D15").Borders.LineStyle = 1;
    Put_Cell(List,14,4,string("Количество документов (всего):"));

    if (debugMode) //развилка: выводить или нет paymentid платежей

        List.Range("E14:M14").Merge;
        List.Range("E14:M14").Borders.LineStyle = 1;
        Put_Cell(List,14,5,string("В том числе исполненных:"));

        List.Range("E15:G15").Merge;
        List.Range("E15:G15").Borders.LineStyle = 1;
        Put_Cell(List,15,5,string("Только ручная обработка"));

        List.Range("H15:J15").Merge;
        List.Range("H15:J15").Borders.LineStyle = 1;
        Put_Cell(List,15,8,string("Смешанная обработка"));

        List.Range("K15:M15").Merge;
        List.Range("K15:M15").Borders.LineStyle = 1;
        Put_Cell(List,15,11,string("Только роботами"));

        if (aResult.size/RES_SIZE != 0)
            for (i, 0, aResult.size/RES_SIZE-1)
                Put_Cell(List, 16+i, 1, aResult[RES_SIZE*i], 4);     //код вида
                Put_Cell(List, 16+i, 2, aResult[RES_SIZE*i+1], 2);   //наим. вида
                Put_Cell(List, 16+i, 3, aResult[RES_SIZE*i+3], 2);   //источник
                Put_Cell(List, 16+i, 4, aResult[RES_SIZE*i+4], 4);   //кол-во документов (всего)
                Put_Cell(List, 16+i, 5, aResult[RES_SIZE*i+5], 4);   //ручной обработки
                Put_Cell(List, 16+i, 6, money(double(100*aResult[RES_SIZE*i+5])/aResult[RES_SIZE*i+4], 2) + "%", 4); //процентов ручной обработки
                Put_Cell(List, 16+i, 7, aResult[RES_SIZE*i+8], 4); //debug
                Put_Cell(List, 16+i, 8, aResult[RES_SIZE*i+6], 4);   //смешанной обработки
                Put_Cell(List, 16+i, 9, money(double(100*aResult[RES_SIZE*i+6])/aResult[RES_SIZE*i+4], 2) + "%", 4); //процентов смешанной обработки
                Put_Cell(List, 16+i,10, aResult[RES_SIZE*i+9], 4); //debug
                Put_Cell(List, 16+i,11, aResult[RES_SIZE*i+7], 4);   //роботизированной обработки
                Put_Cell(List, 16+i,12, money(double(100*aResult[RES_SIZE*i+7])/aResult[RES_SIZE*i+4], 2) + "%", 4); //процентов роботизированной обработки
                Put_Cell(List, 16+i,13, aResult[RES_SIZE*i+10], 4); //debug
                //красим строки, в которых 100% либо ручной, либо автоматической обработки
                ListObj = List.Range(string(StrFor(CodeFor("A")-1+1), 16+i, ":", StrFor(CodeFor("A")-1+13), 16+i));
                if   (aResult[RES_SIZE*i+5] == aResult[RES_SIZE*i+4]) //100% вручную
                    ListObj.Interior.Color = COLOR_RED;
                elif (aResult[RES_SIZE*i+7] == aResult[RES_SIZE*i+4]) //100% роботами
                    ListObj.Interior.Color = COLOR_GREEN;
                end;
            end;
    
            Put_Cell(List,16+aResult.size/RES_SIZE, 2, "ИТОГО", 2);
    
            for (i, 0, aResult.size/RES_SIZE - 1)
                sumAll = sumAll + aResult[RES_SIZE*i+4];
            end;
            Put_Cell(List,16+aResult.size/RES_SIZE, 4, sumAll, 4);
    
            for (i, 0, aResult.size/RES_SIZE - 1)
                sumMan = sumMan + aResult[RES_SIZE*i+5];
            end;
            Put_Cell(List,16+aResult.size/RES_SIZE, 5, sumMan, 4);
    
            for (i, 0, aResult.size/RES_SIZE - 1)
                sumMix = sumMix + aResult[RES_SIZE*i+6];
            end;
            Put_Cell(List,16+aResult.size/RES_SIZE, 8, sumMix, 4);
    
            for (i, 0, aResult.size/RES_SIZE - 1)
                sumRobo = sumRobo + aResult[RES_SIZE*i+7];
            end;
            Put_Cell(List,16+aResult.size/RES_SIZE, 11, sumRobo, 4);
    
            Put_Cell(List,16+aResult.size/RES_SIZE, 6, money(double(100*sumMan)/sumAll, 2) + "%", 4);
            Put_Cell(List,16+aResult.size/RES_SIZE, 9, money(double(100*sumMix)/sumAll, 2) + "%", 4);
            Put_Cell(List,16+aResult.size/RES_SIZE,12, money(double(100*sumRobo)/sumAll,2) + "%", 4);

        end;

    else

        List.Range("E14:J14").Merge;
        List.Range("E14:J14").Borders.LineStyle = 1;
        Put_Cell(List,14,5,string("В том числе исполненных:"));

        List.Range("E15:F15").Merge;
        List.Range("E15:F15").Borders.LineStyle = 1;
        Put_Cell(List,15,5,string("Только ручная обработка"));

        List.Range("G15:H15").Merge;
        List.Range("G15:H15").Borders.LineStyle = 1;
        Put_Cell(List,15,7,string("Смешанная обработка"));

        List.Range("I15:J15").Merge;
        List.Range("I15:J15").Borders.LineStyle = 1;
        Put_Cell(List,15,9,string("Только роботами"));

        if (aResult.size/RES_SIZE != 0)
            for (i, 0, aResult.size/RES_SIZE-1)
                Put_Cell(List, 16+i, 1, aResult[RES_SIZE*i], 4);     //код вида
                Put_Cell(List, 16+i, 2, aResult[RES_SIZE*i+1], 2);   //наим. вида
                Put_Cell(List, 16+i, 3, aResult[RES_SIZE*i+3], 2);   //источник
                Put_Cell(List, 16+i, 4, aResult[RES_SIZE*i+4], 4);   //кол-во документов (всего)
                Put_Cell(List, 16+i, 5, aResult[RES_SIZE*i+5], 4);   //ручной обработки
                Put_Cell(List, 16+i, 6, money(double(100*aResult[RES_SIZE*i+5])/aResult[RES_SIZE*i+4], 2) + "%", 4); //процентов ручной обработки
                Put_Cell(List, 16+i, 7, aResult[RES_SIZE*i+6], 4);   //смешанной обработки
                Put_Cell(List, 16+i, 8, money(double(100*aResult[RES_SIZE*i+6])/aResult[RES_SIZE*i+4], 2) + "%", 4); //процентов смешанной обработки
                Put_Cell(List, 16+i, 9, aResult[RES_SIZE*i+7], 4);   //роботизированной обработки
                Put_Cell(List, 16+i,10, money(double(100*aResult[RES_SIZE*i+7])/aResult[RES_SIZE*i+4], 2) + "%", 4); //процентов роботизированной обработки
                //красим строки, в которых 100% либо ручной, либо автоматической обработки
                ListObj = List.Range(string(StrFor(CodeFor("A")-1+1), 16+i, ":", StrFor(CodeFor("A")-1+10), 16+i));
                if   (aResult[RES_SIZE*i+5] == aResult[RES_SIZE*i+4]) //100% вручную
                    ListObj.Interior.Color = COLOR_RED;
                elif (aResult[RES_SIZE*i+7] == aResult[RES_SIZE*i+4]) //100% роботами
                    ListObj.Interior.Color = COLOR_GREEN;
                end;
            end;
    
            Put_Cell(List,16+aResult.size/RES_SIZE, 2, "ИТОГО", 2);
    
            for (i, 0, aResult.size/RES_SIZE - 1)
                sumAll = sumAll + aResult[RES_SIZE*i+4];
            end;
            Put_Cell(List,16+aResult.size/RES_SIZE, 4, sumAll, 4);
    
            for (i, 0, aResult.size/RES_SIZE - 1)
                sumMan = sumMan + aResult[RES_SIZE*i+5];
            end;
            Put_Cell(List,16+aResult.size/RES_SIZE, 5, sumMan, 4);
    
            for (i, 0, aResult.size/RES_SIZE - 1)
                sumMix = sumMix + aResult[RES_SIZE*i+6];
            end;
            Put_Cell(List,16+aResult.size/RES_SIZE, 7, sumMix, 4);
    
            for (i, 0, aResult.size/RES_SIZE - 1)
                sumRobo = sumRobo + aResult[RES_SIZE*i+7];
            end;
            Put_Cell(List,16+aResult.size/RES_SIZE, 9, sumRobo, 4);
    
            Put_Cell(List,16+aResult.size/RES_SIZE, 6, money(double(100*sumMan)/sumAll, 2) + "%", 4);
            Put_Cell(List,16+aResult.size/RES_SIZE, 8, money(double(100*sumMix)/sumAll, 2) + "%", 4);
            Put_Cell(List,16+aResult.size/RES_SIZE,10, money(double(100*sumRobo)/sumAll,2) + "%", 4);

        end;

    end;

    List.Cells(16+aResult.size/RES_SIZE+2,1).Value = strDayExists;
end; /*End Of ExcelRep*/

Macro OpenExcel()
    var ExObj;
    var ComObj;
      
    ExObj  = CreateObject ("rsax", "TRsAxServer", "RsAxServer", IsStandAlone());
    ComObj = ExObj.CreateComObject ("Excel.Application", False);
    ComObj.Application.DisplayAlerts = True;
    ComObj.Workbooks.Add();

    ExcelRep(ComObj);
    
    ComObj.Visible = True;
    
End; /* End Of OpenExcel */



macro main();
    var form: TRsbPanel = RslPanel(REP_NAME);
    form.run();
    if (formRun)
        Message("Обработка результатов запроса...");
        core();
        Message("Формирование Excel-файла...");
        OpenExcel();
    end;
end;
                               
main();
exit(1);