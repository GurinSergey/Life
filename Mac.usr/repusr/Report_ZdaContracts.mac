// -------------------------------------------------------------------------------------------------
// @filename: Report_ZdaContracts.mac v.1
// @author  : 2013-09-11 TeleshovaAM C-23305
// @desc    : Отчет по договорам на ЗДА
// @changes : TAM 26.11.2013 I-00445991-2
//            TAM 11.07.2014 C-25668
// -------------------------------------------------------------------------------------------------

import RSD, globals, календарь;
import "fg_Life_parm.mac", "KeyCodes.mac", "likepy.mac";

//Константы
const FULL_NAME = 1;
//Глобальные переменные
var g_saveCurField; //текущее поле в панели
var dep_id:integer = 0; //ид всп

record department (dp_dep);
record client (party);

//Класс панели
private class (TRecHandler) ProcPanel(Fulloutputlbr)
   InitTRecHandler ("zdacntr", Fulloutputlbr, TRUE);
   var exitKey = 0;
end;

//Класс информации для отчета
class ReportInfo( _ContrNumberDate, _Branch, _DateZdaBegin, _DateZdaEnd, _ClientAcc, _ClientName, _ContrName, _NoteGround, _Note, _Limit)
   var ContrNumberDate = _ContrNumberDate;
   var Branch          = _Branch;
   var DateZdaBegin    = _DateZdaBegin;
   var DateZdaEnd      = _DateZdaEnd;
   var ClientAcc       = _ClientAcc;
   var ClientName      = _ClientName;
   var ContrName       = _ContrName;
   var NoteGround      = _NoteGround;
   var Note            = _Note;
   var Limit           = _Limit;
end;

//Массив строк для отчета
var InfoForReport: TArray = TArray();

//Определяем ресурс панели для отчета
var Fulloutputlbr, outlbr, outputlbr = "constatnt_contr.lbr";                    
GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,outlbr);
Fulloutputlbr = FindPath(outputlbr, outlbr);
if (not Fulloutputlbr)
   msgbox("Не найдена LBR");
   exit();
end;
var dlg:ProcPanel = ProcPanel(Fulloutputlbr);

//Получить наименовнаие офиса
private macro GetDepName( Name:string):string
   var select = " SELECT   nvl(PT.T_NAME, ' ') node_name FROM ddp_dep_dbt dp, dparty_dbt pt " +
                "  WHERE   dp.t_Name = ? AND DP.T_PARTYID = PT.T_PARTYID ";
   var cmd = RSDCommand(select);
   cmd.AddParam("name",  RSDBP_IN, Name);
   var rsl = RSDRecordset(cmd, null, RSDVAL_STATIC);
   if (rsl.movenext())
      return rsl.value("node_name");
   else
      return "";
   end;
end;

//Собрать информацию для отчета и заполнить массив
macro GatherInfoForReport(IdDepartment, IdClient)
   var select, rsdcmd, rsl;
   select = " SELECT   ZDA.T_CONTR_NUMBER contr_number,                                                        " +
            "          SUBSTR (ZDA.T_ACCOUNT, -6, 6) code_sort,                                                " +
            "          TO_CHAR (ZDA.T_CONTR_DATE, 'dd.mm.yyyy') contr_date,                                    " +
            "          TO_CHAR (ZDA.T_ZDA_ENTER_DATE, 'dd.mm.yyyy') date_beg,                                  " +
            "          DECODE (ZDA.T_ZDA_END_DATE,                                                             " +
            "               TO_DATE ('01.01.0001', 'dd.mm.yyyy'), 'Срок окончания не задан',                   " +
            "               ZDA.T_ZDA_END_DATE) date_end,                                                      " +
            "          nvl(ZDA.T_ACCOUNT,' ') client_acc,                                                      " +
            "          ACNT.T_NAMEACCOUNT acc_name,                                                            " +
            "          NVL(ZDA.T_CONTRAGENT_NAME, pt.t_name) contr_name,                                       " +
            "          nvl(ZDA.T_NOTE_FOR_GROUND,' ') note_ground,                                             " +
            "          nvl(ZDA.T_NOTE,' ') note,                                                               " +
            "         (SELECT   DEP.T_NAME FROM   ddp_dep_dbt dep WHERE   DEP.T_CODE = ACNT.T_branch) branch,  " +
           "           DECODE (ZDA.T_LIMITED_SUM, 0, 'Ограничений нет', ZDA.T_LIMITED_SUM) limit_sum           " +
            " FROM      usr_zda_contr_dbt zda LEFT JOIN dparty_dbt pt                                          " +
            "              ON PT.T_PARTYID = ZDA.T_CONTRAGENT_ID, daccount_dbt acnt                            " +
            " WHERE    ACNT.T_ACCOUNT = ZDA.T_ACCOUNT                                                          " +
            "      AND ACNT.T_CHAPTER = 1                                                                      ";

   if(IdDepartment != 0)
      select = select + "  AND ACNT.T_BRANCH = (SELECT   t_code  FROM   ddp_dep_dbt WHERE   t_partyid = ?) ";
   end;
   if(IdClient != 0)
      select = select + "  AND ZDA.T_CLIENT_ID = ?                                           ";
   end;
   select = select + " ORDER BY   code_sort ";
   rsdcmd = RSDCommand(select);
   //Заполняем параметры
   if(IdDepartment != 0)
      rsdcmd.AddParam("dep",    RSDBP_IN, IdDepartment);
   end;
   if(IdClient != 0)
      rsdcmd.AddParam("client", RSDBP_IN, IdClient);
   end;
   //Собираем рекордсет
   rsl = RSDRecordset(rsdcmd, null, RSDVAL_STATIC);
   while (rsl.movenext())
      InfoForReport(InfoForReport.size()) = ReportInfo(rsl.value("contr_number") + "\\ " + rsl.value("contr_date"),
                                                       rsl.value("branch"),
                                                       rsl.value("date_beg"),
                                                       rsl.value("date_end"),
                                                       rsl.value("client_acc"),
                                                       rsl.value("acc_name"),
                                                       rsl.value("contr_name"),
                                                       rsl.value("note_ground"),
                                                       rsl.value("note"),
                                                       rsl.value("limit_sum"));
   end;
end;

//Создать непосредственно отчет
macro CreateReport(Department,ClientName, ClientCode)
   var ReportName = getTxtFileName ("ZdaContracts");
   var table = CTableReport();
   var i=0;
   setoutput(ReportName,false);
   println (string ("Список договоров клиентов на заранее данные акцепты"):c:100);
   if(Department != "")
      println (string ("Фильтр по офису: ", Department):c:100);
   end;
   if(ClientCode != "")
      println (string ("Фильтр по клиенту: ", ClientName, " (", ClientCode, ")"):c:100);
   end;
   

   table.addColumn ("Счет клиента"                   , 22,  AL_CENTER);
   table.addColumn ("Наименование клиента"           , 25,  AL_CENTER);
   table.addColumn ("Номер\\Дата договора"           , 10,  AL_CENTER);
   table.addColumn ("Офис"                           , 5 ,  AL_CENTER);
   table.addColumn ("Начало действия ЗДА"            , 15,  AL_CENTER);
   table.addColumn ("Срок действия ЗДА"              , 15,  AL_CENTER);
   table.addColumn ("Наименование контрагента"       , 30,  AL_CENTER);
   table.addColumn ("Примечание для сверки основания", 25,  AL_CENTER);
   table.addColumn ("Примечание"                     , 20,  AL_CENTER);
   table.addColumn ("Ограничение суммы"              , 18,  AL_CENTER);

   table.printHead ();

   while (i < InfoForReport.size ())
      table.printStringTransferByWord(InfoForReport[i].ClientAcc,       InfoForReport[i].ClientName,
                                      InfoForReport[i].ContrNumberDate, InfoForReport[i].Branch,
                                      InfoForReport[i].DateZdaBegin,    InfoForReport[i].DateZdaEnd,
                                      InfoForReport[i].ContrName,       InfoForReport[i].NoteGround,
                                      InfoForReport[i].Note,            InfoForReport[i].Limit);
      i = i + 1;
      table.printSeparator ();
   end;

   table.printSeparator ();
   table.printStringTransferByWord ("Всего договоров:", i);
   table.printBottom ();  
end;

// Обработчик панели процедуры
private macro EventPanel(dlg, cmd, id, key)
   var curFld = FldName (dlg, id);
   var res, MenuChoice;
   var Name:TArray;
   if( cmd == DLG_INIT )
      var PartyID = 0;
      dlg.rec.BranchCode = "000";
      dlg.rec.BranchName = GetDepName(dlg.rec.BranchCode);
      dlg.rec.ClientCode = "";
      dlg.rec.ClientName = "";
      dlg.rec.ClientId   = "";
      UpdateFields(dlg); 
   elif( cmd == DLG_SETFOCUS )
      g_saveCurField = dlg.item(id);
   elif( cmd == DLG_REMFOCUS )
      UpdateFields(dlg); 
   elif( cmd == DLG_KEY )
      if ( key == KEY_F3 )
         if(curFld == "BranchCode")
            if ( ListDepartment (Department) )
               dlg.rec.BranchCode = Department.name;
               dep_id = Department.PartyID;
               Name = GetPartyNames(Department.PartyID, FULL_NAME);
               dlg.rec.BranchName = Name[0];
               UpdateFields(dlg);
            end;
         elif( curFld == "ClientCode" )
            if( ListPt(client, PTCK_ALL, null, PTLIST_ALLCLIENT, 0, PTCK_CONTR) );
               dlg.rec.ClientId = client.partyid;
               dlg.rec.ClientName = client.name;
               dlg.rec.ClientCode = ПолучитьКодСубъекта(client.partyid, PTCK_CONTR);
               UpdateFields(dlg);
            end;
         end;
      elif ( key == KEY_F2 )
         debugbreak;
         GatherInfoForReport(dep_id,ifthenelse((dlg.rec.ClientId == ""), 0, int(dlg.rec.ClientId)));
         CreateReport(dlg.rec.BranchCode,dlg.rec.ClientName,dlg.rec.ClientCode);
         return CM_SAVE;
      elif ( key == KEY_ESC )
         dlg.exitKey = key;
         return CM_SAVE;
      end;
      UpdateFields( dlg ); 
   end;   
end;

//That's enough
RunDialog(dlg, @EventPanel);