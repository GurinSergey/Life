/*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   Гуцу Е.В.

   Имя файла: Auffullung139i.mac

   Создан:    11.04.2014

   Описание:  Отчёт "Пополнение счета 139-и"

   ChangeLog:
-----------------------------------------------------------------------------------------------------------------------------------------------------
15/04/2014 Е.Гуцу Убран пустой столбец слева, добавлен столбец "Сумма" и итог по нему.
                  При фильтрации и печати ИНН учитывается без КПП.
                  Настроена ландшафтная печать, вывод на 1 страницу по ширине.
16/04/2014 Е.Гуцу Сделал сортировку по наименованию плательщика.
                  Зафиксировал ширину столбцов плательщик, получатель и назначение платежа, сделал перенос по словам внутри ячеек этих столбцов.
                  Сделал линии - по горизонтали сплошные, по вертикали пунктир.
+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/

import rsd, rslx, rcw, "or_const.mac";

var {CurDate}, {OurBank};
var ExcelApplication,
    ActiveSheet,
    startAX;
var LastRow;



macro SchaffenExcelDatei()

   if (ExcelApplication == null)
      if (isStandAlone())
         ExcelApplication = ActiveX("Excel.Application", null, true);
      else
         if (startAX == null)
            startAX = CreateObject("rsax", "TRsAxServer", "LoansAxServer", isStandalone());
         end;
         ExcelApplication = startAX.CreateComObject("Excel.Application");
      end;
   end;

   ExcelApplication.Workbooks.Add();
   ExcelApplication.Visible = false;
   ActiveSheet = ExcelApplication.ActiveSheet;
   return true;

   onError (err)
      msgbox( err.message );
      Exit(1);

end;


macro DruckenBerichtskopf( rDate )

   ActiveSheet.Cells( 2, 3 ).value = "Отчёт \"Пополнение счета 139-и\" за " + string( rDate:m );

   ActiveSheet.Cells( 4, 2 ).value = "ИНН плательщика";
   ActiveSheet.Cells( 4, 3 ).value = "Плательщик";
   ActiveSheet.Cells( 4, 4 ).value = "Сч. N.";
   ActiveSheet.Cells( 4, 5 ).value = "ИНН получателя";
   ActiveSheet.Cells( 4, 6 ).value = "Получатель";
   ActiveSheet.Cells( 4, 7 ).value = "Назначение платежа";
   ActiveSheet.Cells( 4, 8 ).value = "Сумма";

   ActiveSheet.columns("A:H").entirecolumn.autofit;

end;


macro AusfuhrenBericht( vDate )

   var sql, rs;
   var i = 6;

   /* EVG 15/04/2014 Сделал сравнение и вывод ИНН без КПП */
   /* EVG 16/04/2014 Добавил Order by */
   sql = " Select * From ( " +
         "                 Select REGEXP_SUBSTR( prop.t_payerinn, '([[:digit:]]){10,12}' ) payerinn,            " +
         "                        prop.t_payername,                                                             " +
         "                        pm.t_payeraccount,                                                            " +
         "                        REGEXP_SUBSTR( prop.t_receiverinn, '([[:digit:]]){10,12}' ) receiverinn,      " +
         "                        prop.t_receivername,                                                          " +
         "                        prop.t_ground,                                                                " +
         "                        pm.t_amount                                                                   " +
         "                   From dpmpaym_dbt pm, dpmrmprop_dbt prop                                            " +
         "                  Where pm.t_valuedate        = '" + vDate + "'" +
         "                    and pm.t_payerbankid      = " + {OurBank}    +       // Исходящие платежи
         "                    and pm.t_receiverbankid  <> " + {OurBank}    +       // ... в сторонний банк
         "                    and prop.t_paymentid      = pm.t_paymentid " +
         "                  Order by prop.t_payername                    " +
         "               )                                                                                      " +
         "  Where payerinn = receiverinn " ;   // Совпадение ИНН Плательщика и Получателя
   //msgbox (sql);
   rs = rsdRecordSet( sql );

   while( rs and rs.moveNext )

      ActiveSheet.Cells( i, 2 ).value = rs.value( 0, null, V_STRING );
      ActiveSheet.Cells( i, 3 ).value = rs.value( 1, null, V_STRING );
      ActiveSheet.Cells( i, 4 ).value = string( rs.value( 2, null, V_STRING ):f );
      ActiveSheet.Cells( i, 5 ).value = rs.value( 3, null, V_STRING );
      ActiveSheet.Cells( i, 6 ).value = rs.value( 4, null, V_STRING );
      ActiveSheet.Cells( i, 7 ).value = rs.value( 5, null, V_STRING );
      ActiveSheet.Cells( i, 8 ).value = rs.value( 6, null, V_MONEY  );

      i = i + 1;
   end;

   LastRow = i;

end;


macro FormatierenBericht()

//debugbreak;
   ActiveSheet.Range( "C2:G2" ).merge;
   ActiveSheet.Range( "C2:G2").HorizontalAlignment = ALIGN_LEFT;
   ActiveSheet.Cells( 2, 3 ).font.size = 14;

   ActiveSheet.rows( 4 ).HorizontalAlignment = ALIGN_CENTER;
   ActiveSheet.rows( 4 ).font.size = 12;
   ActiveSheet.rows( 4 ).font.bold = true;

   //ActiveSheet.columns( "B" ).entirecolumn.HorizontalAlignment = 2;  // По левому краю
   //ActiveSheet.columns( "E" ).entirecolumn.HorizontalAlignment = 2;  // По левому краю

   ActiveSheet.Range( "B6:G" + LastRow ).HorizontalAlignment = ALIGN_LEFT;
   ActiveSheet.Range( "B6:H" + LastRow ).VerticalAlignment = ALIGN_CENTER;

   ActiveSheet.columns( "A:G" ).entirecolumn.NumberFormat = "000000";
   ActiveSheet.columns( "A:H" ).entirecolumn.autofit;
   ActiveSheet.rows( "6:" + LastRow ).entirerow.autofit;

   // EVG 15/04/2014 Раз есть сумма, нужен и итог
   // Определим имя формулы, исходя из языка интерфейса
   var formulaName = "SUM";
   if( ExcelApplication.LanguageSettings.LanguageID( msoLanguageIDUI ) == EXCEL_INTERFACE_RUS )
      formulaName = "СУММ";
   end;

   ActiveSheet.Cells( LastRow + 1, 7 ).value = " Итого: ";
   ActiveSheet.Cells( LastRow + 1, 7 ).HorizontalAlignment = ALIGN_RIGHT;
   ActiveSheet.Cells( LastRow + 1, 8 ).FormulaR1C1 = "=" + formulaName + "(R[-" + LastRow + "]C:R[-1]C)";
   ActiveSheet.Cells( LastRow + 1, 8 ).NumberFormat = "# ##0,00р.";

   ActiveSheet.Range( "G"+(LastRow+1) + ":H"+(LastRow+1) ).font.size = 12;
   ActiveSheet.Range( "G"+(LastRow+1) + ":H"+(LastRow+1) ).font.bold = true;
   // EVG 15/04/2014 end

   ActiveSheet.columns( "A" ).delete;

   // EVG 15/04/2014 Ещё одна доработка - настройка параметров печати
   ExcelApplication.PrintCommunication  = False;
   ActiveSheet.PageSetup.Orientation    = PAPER_LANDSCAPE;
   ActiveSheet.PageSetup.FitToPagesWide = 1;
   ActiveSheet.PageSetup.FitToPagesTall = 100;
   ActiveSheet.PageSetup.LeftMargin     = 25;
   ActiveSheet.PageSetup.RightMargin    = 25;
   ActiveSheet.PageSetup.TopMargin      = 25;
   ActiveSheet.PageSetup.BottomMargin   = 25;
   ExcelApplication.PrintCommunication  = true;

//debugbreak;
//ExcelApplication.visible = true;

   // EVG 16/04/2014 Фиксируем ширину столбцов плательщик, получатель и назначение платежа
   ActiveSheet.columns( "B" ).entirecolumn.ColumnWidth = 35;
   ActiveSheet.columns( "E" ).entirecolumn.ColumnWidth = 35;
   ActiveSheet.columns( "F" ).entirecolumn.ColumnWidth = 50;
   ActiveSheet.Range( "B:B;E:E;F:F" ).WrapText = true;
   ActiveSheet.Range( "B:B;E:E;F:F" ).ShrinkToFit = false;

   // EVG 16/04/2014 Разлинуем отчёт: по горизонтали сплошные линии, по вертикали - пунктир
   var selection = ActiveSheet.Range( "A6:G"+(LastRow-1) );
   with ( selection )
      Borders( 11 ).LineStyle = -4118;  // xlInsideHorizontal -> xlDot
      Borders( 12 ).LineStyle = 1;      // xlInsideVertical -> xlContinuos
   end;

end;


macro SpeichernBericht();

   var ExcelDateiName = "$Auffullung139i_" + random(100) + "_" + UserNumber + ".xls";

   ExcelApplication.Visible = true;

   var wb = ExcelApplication.Workbooks.item(1);
       wb.SaveAs( ExcelDateiName, 56 );

   onError (err)
      return

end;



var repDate = {CurDate} - 1;
getDate( repDate, "Пожалуйста, введите дату формирования отчёта" );

SchaffenExcelDatei();
DruckenBerichtskopf( repDate );
AusfuhrenBericht( repDate );
FormatierenBericht();
SpeichernBericht();

exit( 1 );