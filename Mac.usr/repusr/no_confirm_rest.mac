/*******************************************************************************/
/*  RS-Bank 6.0                                          R-Style Software Lab  */
/*                                                                             */
/*  Имя файла   : no_confirm_rest.mac                                          */
/*  Создан      : 30.06.2014  Chesnokov D.S.                                   */
/*  Заявка      : C-28272 Отчет по неподтвержденным остаткам текущего года     */
/*  Описание    : В отчет попадают счета клиентов(405, 406, 407, 40802, 40807, */
/*                40821) по заданному подразделению ТС, на которых отсутствует */
/*                пользовательский тип <Y> и остаток по которым на 01 января   */
/*                текущего года, больше заданной в панели параметров суммы     */
/*                По валютным счетам сумма определяется покрытием              */
/*                29.12.2014 Chesnokov D.S. Адаптация для 31 патча             */
/*******************************************************************************/
import BankInter, RSD, globals, treport;

private var path, errCode, resname = "confirm_rest.lbr", pathfile, dlg_p;
var dep_code = 0;
record department ("dp_dep.dbt");

private macro ПрочитатьНастройки

  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",V_STRING, path, errCode);
  pathfile = FindPath(resname, path);
  if (not pathfile)
    msgbox("Не найдена LBR с именем " + resname);
    return false;
  end;
  if ( errCode > 0 )
    msgbox("Проверьте наличие настройки: BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR");
    return false;
  end;
    
  dlg_p = TRecHandler("dlg_pan", pathfile, True);
  
  return true;

end;

private macro GetNameParty(PartyID)

  var rs, cmd, sql;
    sql = "select pt.t_name from dparty_dbt pt where pt.t_partyid = ? ";
    cmd = RsdCommand(sql);
    cmd.AddParam("p_partyid", RSDBP_IN, PartyID);

    rs = RsdRecordSet(cmd);
    if (rs and rs.movenext)
      return rs.value(0);
    else
      return "Не найдено";
    end;
end;

private macro GetNameDO(CodDO:integer)
  
  var sql_str, rs;
  
  sql_str = " SELECT pt.t_name " +
            "   FROM ddp_dep_dbt dp, dparty_dbt pt " +
            "  WHERE dp.t_partyid = pt.t_partyid AND dp.t_code = " + CodDO;
            
  rs = RsdRecordSet(sql_str);
  if (rs and rs.movenext)
    return rs.value("t_name");
  else
    return "Не определено";
  end;
end;

macro PressKey_Event(dlg, cmd, id, key)
  
  if (cmd == DLG_INIT)
    dlg.rec.BranchCode = 0;
    dlg.rec.BranchName = "Все подразделения";
    dlg.rec.Sum = $0;
    
  elif (cmd == DLG_KEY)
    if (key == 27)
      if (GetTrue(TRUE,"Вы действительно хотите выйти?"))
        exit(1);
        return  CM_CANCEL;
      else
        return  CM_IGNORE;
      end;
    elif ((key == 317) and (FldName(dlg, id) == "BranchCode")) /*F3*/
      if(ListDepartment(department))
        dlg.rec.BranchCode = department.Name;
        dlg.rec.BranchName = GetNameParty(department.PartyID);
        dep_code = department.code;
      end;
    elif ((key == 32) and (FldName(dlg, id) == "BranchCode")) /*SPACE*/
      dlg.rec.BranchCode = 0;
      dlg.rec.BranchName = "Все подразделения";
      dep_code = 0;
    elif ((key == 316) or (key == 323)) /*F2 or F9*/
      return CM_SAVE;
    end;
  end;
  
  UpdateFields(dlg);
  
end;

macro ВыпуститьОтчет(dlg_p)
  
  var repDate = substr(string({curdate}), 7);
  
  var rep_tbl = ctablereport();
  var sql_str, rs, count:integer = 0;
  var old_code = 0, new_code = 0, amount_rub = $0.0;
  
  rep_tbl.addcolumn("N п/п.",              5, AL_RIGHT);
  rep_tbl.addcolumn("Код клиента",         $6.0, AL_RIGHT);
  rep_tbl.addcolumn("Наименование счета", 40, AL_LEFT);
  rep_tbl.addcolumn("Счет ",              22, AL_LEFT);
  rep_tbl.addcolumn("Опер",                5, AL_LEFT);
  rep_tbl.addcolumn("Рублевые остатки",   10, AL_RIGHT);
  rep_tbl.addcolumn("Валютные остатки",   10, AL_RIGHT);
  rep_tbl.addcolumn("Подразделение ТС",   10, AL_RIGHT);
  
  if ((dlg_p.rec.Sum != 0) and (dep_code != 0))
    rep_tbl.printhead("Неподтвержденные остатки больше " + dlg_p.rec.Sum  + " на 1-ое января " + repDate  + " года, по подразделению " + dlg_p.rec.BranchCode + " " + GetNameDO(dep_code));
  elif((dlg_p.rec.Sum == 0) and (dep_code != 0))
    rep_tbl.printhead("Неподтвержденные остатки на 1-ое января " + repDate  + " года, по подразделению " + dlg_p.rec.BranchCode + " " + GetNameDO(dep_code));
  elif((dlg_p.rec.Sum != 0) and (dep_code == 0))
    rep_tbl.printhead("Неподтвержденные остатки больше " + dlg_p.rec.Sum  + " на 1-ое января " + repDate  + " года.");
  else
    rep_tbl.printhead("Неподтвержденные остатки на 1-ое января " + repDate  + " года.");
  end;
  
  sql_str = " SELECT SUBSTR (cod.t_code, 7) AS code, " +
            "        ac.t_account c4et, " +
            "        ac.t_nameaccount name_acc, " +
            "        ac.t_oper oper, " +
            "        DECODE (ac.t_code_currency, 0, " +
            "                rsb_account.resta (ac.t_account, " +
            "                                   TRUNC (SYSDATE, 'YEAR') - 1, " +
            "                                   ac.t_chapter, " +
            "                                   0), " +
            "                rsb_account.restac (ac.t_account, " +
            "                                    ac.t_code_currency, " +
            "                                    TRUNC (SYSDATE, 'YEAR') - 1, " +
            "                                    ac.t_chapter, " +
            "                                    0, " +
            "                                    0)) rest_rub, " +
            "        DECODE (ac.t_code_currency, 0, 0, " +
            "                rsb_account.restac (ac.t_account, " +
            "                                    ac.t_code_currency, " +
            "                                    TRUNC (SYSDATE, 'YEAR') - 1, " +
            "                                    ac.t_chapter, " +
            "                                   0)) rest_val, " +
            "        (select t_name from ddp_dep_dbt where t_code = ac.t_branch) as branch " +
            "   FROM daccount_dbt ac, dobjcode_dbt cod " +
            "  WHERE ac.t_usertypeaccount NOT LIKE '%Y%' " +
            "        AND ac.t_type_account NOT LIKE '%П%' " +
            "        AND ac.t_type_account NOT LIKE '%Н%' " +
            "        AND ac.t_type_account NOT LIKE '%U%' " +
            "        AND ac.t_chapter = 1 " +
            "        AND (   SUBSTR (ac.t_account, 1, 3) = '405' " +
            "             OR SUBSTR (ac.t_account, 1, 3) = '406' " +
            "             OR SUBSTR (ac.t_account, 1, 3) = '407' " +
            "             OR SUBSTR (ac.t_account, 1, 5) = '40802' " +
            "             OR SUBSTR (ac.t_account, 1, 5) = '40807' " +
            "             OR SUBSTR (ac.t_account, 1, 5) = '40821') " +
            "        AND cod.t_objectid = ac.t_client " +
            "        AND cod.t_codekind = 1 " +
            "        AND t_objecttype = 3 " +
            "        AND cod.t_state = 0 ";
  if (int(dlg_p.rec.BranchCode) != 0)
    sql_str = sql_str + " AND ac.t_branch = " + dep_code;
  end;
  
  if (dlg_p.rec.Sum != 0)
    sql_str = sql_str + " AND DECODE (ac.t_code_currency, 0, " +
                        "             rsb_account.resta (ac.t_account, " +
                        "                                TRUNC (SYSDATE, 'YEAR') - 1, " +
                        "                                ac.t_chapter, " +
                        "                                0), " +
                        "             rsb_account.restac (ac.t_account, " +
                        "                                 ac.t_code_currency, " +
                        "                                 TRUNC (SYSDATE, 'YEAR') - 1, " +
                        "                                 ac.t_chapter, " +
                        "                                 0, " +
                        "                                 0)) > " + dlg_p.rec.Sum;
  end;
  
  sql_str = sql_str + "  ORDER BY code, c4et ";

  rs = RsdRecordSet(sql_str);
  
  while(rs.MoveNext)
    new_code = rs.value("code");
    count = count + 1;
    message("Обрабатываю запись " + count + ", счет № " + rs.value("c4et"));
    if ((new_code != old_code) and (count > 1))
      rep_tbl.printseparator;
    end;
    rep_tbl.printStringTransferByWord(count, rs.value("code"), rs.value("name_acc"), rs.value("c4et"), rs.value("oper"), rs.value("rest_rub"), rs.value("rest_val"), rs.value("branch"));
    amount_rub = amount_rub + rs.value("rest_rub");
    old_code = new_code;

  end;

  rep_tbl.printbottom("Всего обработано счетов: " +count+ " на сумму "+amount_rub:a);
  
end;

/***ТОЧКА ВХОДА***/
if (ПрочитатьНастройки)
  if (RunDialog(dlg_p, "PressKey_Event"))
    ВыпуститьОтчет(dlg_p);
  end;
else
  msgbox("Ошибка при определении глобальных настроек");
end;
