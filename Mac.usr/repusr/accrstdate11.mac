/*Остатки на расчетных счетах по датам                       */
/*                                                           */
/*                                                           */
/*Тихомиров А.Н. 26.01.2009                  Версия 1.2      */


import globals, oralib, bankinter, likePy, lib_lang, rsexts;

import RSD,rcw, rslx, Календарь, rsbdataset, "xlc.mac";
var SQL,cmd,ex,ob,obbook,obsheet, wdays, code, account, count, datec, nameaccount, partyid, n, i=4, sm, dprt_v, k, rsd, namedep;
array rest;
const  KEY_F1      =315;
const  KEY_F2      =316;
const  KEY_F3      =317;
const KEY_ESC      = 27;
const KEY_SPACE    = 32;
const KEY_ENTER    = 13;

record Department ("dp_dep.dbt");
var Fulloutputl, outl, outputl="accrest.lbr";                    

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,outl);
Fulloutputl = FindPath(outputl, outl);
var dlg = TRecHandler("accrest", fulloutputl, TRUE); 

var out, output = "accrest.xls", fulloutput;                            

var branch, Date_Begin,
    Date_End,
    Office:double  = 0,
    OfficeName = "По всем офисам";
var maxS:integer;   


/*Найдем имя по Partyid*/
private macro GetClientName(id)

var  sl=string(" select t_name from dparty_dbt t where t.t_PartyID=",id);

var  DataS=TRsbDataSet(sl);

  if( DataS.moveNext())
    return DataS.name;
  else
    if (id !=0)
       msgbox("Субъект не найден в party.dbt");
       return 0;
    else
      return "ОАО АКБ Пробизнесбанк";
    end;
  end;

END;


private macro OutAll()

var count=0, datec;
/*Определяем общее количество счетов*/
initprogress(-1, "Отбираются счета, ждите", "Отбираются счета");
var sl = "select count(*) cnt from (select  * from daccount_dbt ac where ac.t_account between '401%' and "+ 
      "'4089%'"+branch+") accs inner join (SELECT t.t_partyid "+
      "FROM dpartyown_dbt t "+
      "WHERE t.t_partykind = (select t_partykind from dptkind_dbt where T_NAME2='Клиент') "+ 
      "minus "+
      "select t.t_partyid   FROM dpartyown_dbt t "+
      "where t.t_partykind = (select t_partykind from dptkind_dbt where T_NAME2='Банк') "+ 
      "minus "+
      "select t.t_partyid   FROM dpartyown_dbt t "+
      "where t.t_partykind = (select t_partykind from dptkind_dbt where T_NAME2='Центральный банк')) "+
      "client on accs.t_client = client.t_partyid "+   
      "where (accs.t_close_date > TO_DATE ('"+date_begin+"', 'DD-MM-YYYY') or accs.t_open_close=chr(0) )"+
      "and accs.t_open_date < TO_DATE ('"+date_end+"', 'DD-MM-YYYY') "; 

var DataS = TRsbDataSet(sl);
DataS.movenext();
maxS=datas.cnt;

/* Отбираем все счета, по маске, клиентов, исключаем банки */
sql = "select t_account, t_nameaccount, t_partyid, t_code from (select ac.*, obj.t_code from daccount_dbt ac, dobjcode_dbt "+
      " obj where ac.t_account between '401%' and "+ 
      "'4089%'"+branch+" and  obj.t_codekind=1 and obj.t_objectid=ac.t_client) accs inner join (SELECT t.t_partyid "+
      "FROM dpartyown_dbt t "+
      "WHERE t.t_partykind = (select t_partykind from dptkind_dbt where T_NAME2='Клиент') "+ 
      "minus "+
      "select t.t_partyid   FROM dpartyown_dbt t "+
      "where t.t_partykind = (select t_partykind from dptkind_dbt where T_NAME2='Банк') "+ 
      "minus "+
      "select t.t_partyid   FROM dpartyown_dbt t "+
      "where t.t_partykind = (select t_partykind from dptkind_dbt where T_NAME2='Центральный банк')) "+
      "client on accs.t_client = client.t_partyid "+   
      "where (accs.t_close_date > TO_DATE ('"+date_begin+"', 'DD-MM-YYYY') or accs.t_open_close=chr(0)) "+
      "and accs.t_open_date < TO_DATE ('"+date_end+"', 'DD-MM-YYYY') order by t_code"; 

useprogress;
var DataSet = TRsbDataSet(sql);

remprogress;
    if (IsStandAlone()) // Двухзвенка
                 if ((ValType(ex)!=V_UNDEF) and (ex.Visible==False))
                   ex = ActiveX("Excel.Application",NULL,false); 
                 else
                   ex = ActiveX("Excel.Application",NULL,true);
                   
                 end;
  
      else // Трехзвенка
               ob = CreateObject ("rsax","TRsAxServer","RsAxServer",false);
               ex = ob.CreateComObject ("Excel.Application",true);

     end; 

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\USERTEMPLSDIR",2,out);
Fulloutput = FindPath(output, out);
obBook = ex.Workbooks.open(Fulloutput); 
               
obSheet = obBook.ActiveSheet(); 

/*Заполняем заголовок таблицы*/
obSheet.Range("A"+2).Value="Остатки на расчетных счетах клиентов за период с "+Date_Begin+" по "+Date_End+", в рублях РФ";
obSheet.Range("A"+3).Value=officename;
obSheet.Range("A"+4).Value="ID субъекта";
obSheet.Range("B"+4).Value="Код 5";
obSheet.Range("C"+4).Value="Наименование клиента";

count=0;
datec=date_begin;
wdays=workdays(date_begin, date_end);

/*Проставляем дни в столбцы*/
 while (count<wdays)
   obsheet.Cells (4, count+4).Value =  datec;
   count=count+1;
   datec=DateAfterWorkDays(datec,1);
 end;

initprogress(maxS,"Выводится документ, ждите", "Вывод документа");
 if (dataset.movenext())
   partyid=dataset.partyid;
   code=dataset.code;
   account=dataset.account;
   nameaccount=dataset.nameaccount;
 end;
count =0;
k = 0;
datec=date_begin;
wdays=workdays(date_begin, date_end);
 while (count<wdays)
   rest(count)=restA(account,datec);
   count=count+1;
   datec=DateAfterWorkDays(datec,1);
 end;

/*Выводим клиентов */
 while (DataSet.movenext())
   count =0;
   datec=date_begin;
   wdays=workdays(date_begin, date_end);
   k = k+1;
      if (dataset.partyid!=partyid)
         i=i+1;
         while (count<wdays)
            obsheet.Cells (i, count+4).Value =  rest(count);
            rest(count)=restA(dataset.account,datec);
            count=count+1;
            datec=DateAfterWorkDays(datec,1);
         end;
         obSheet.Range("A"+i).Value=code;
         obSheet.Range("B"+i).Value=substr(code,8,5);
         obSheet.Range("C"+i).Value=NameAccount;
      else
         while (count<wdays)
            rest(count)=rest(count)+restA(dataset.account,datec);
            count=count+1;
            datec=DateAfterWorkDays(datec,1);
         end;
      end;
   useprogress(k);
   partyid=dataset.partyid;
   code=dataset.code;
   account=dataset.account;
   nameaccount=dataset.nameaccount;
 end;
count=0;
datec=date_begin;
i=i+1;
 while (count<wdays)
   obsheet.Cells (i, count+4).Value =  rest(count);
   count=count+1;
   datec=DateAfterWorkDays(datec,1);
 end;
obSheet.Range("A"+i).Value=code;
obSheet.Range("B"+i).Value=substr(code,8,5);
obSheet.Range("C"+i).Value=NameAccount;
remprogress(k);
n=0;
i=i+1;
/*Выводим итоги*/
 if (i!=5)
   obsheet.Cells (i, 2).Value = "Итого по всем клиентам";
 end;
 while (n<count)
   if (i!=5)
      sm="=СУММ("+sum(n+3)+"5:"+sum(n+3)+(i-1)+")";
      obsheet.Cells (i, n+4).Value = sm;
   end;
   n=n+1;
 end;
/*Раскрашиваем итоговую таблицу и шапку*/
obSheet.Range("A4:"+sum(n+2)+i).Borders.Weight=2;
obSheet.Range("A4:"+sum(n+2)+4).interior.color=4035000;
Ex.visible = true;
END;


 /*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key) 

   var const_mess = "~F2~ Продолжить ~SPACE~ По всем офисам ~ESC~ Выход ";
   var const_mess2 = "~F2~ Продолжить ~SPACE~ ~ESC~ Выход ";

   /*Первоначальная инициализация полей*/
   if(cmd == DLG_INIT)
      dprt_v = 0;
      dlg.rec.dprt_code  ="";
      dlg.rec.dprt_name = officename;
      dlg.rec.Datebegin = {curDate};
      dlg.rec.DateEnd = {curDate};
      UpdateFields(dlg); 
   end;
   
   /*Установка подсказок в строке состояния*/
   if (cmd==DLG_SETFOCUS)
     if (FldName(dlg,id)=="dprt_code") 
       message(" ~F3~ Список подразделений "+const_mess);
     elif (FldName(dlg,id)=="Datebegin")
       message(" ~F3~ Выбор даты из календаря "+const_mess2);
     elif (FldName(dlg,id)=="DateEnd")
       message(" ~F3~ Выбор даты из календаря "+const_mess2);
    end;
   end;
   
   if (cmd == DLG_REMFOCUS)
     /*Проверка корректности дат отчета*/
     if (FldName(dlg,id) == "Datebegin")
       if ( dlg.rec.DateBegin > {curdate} )
         MsgBox("Дата начала периода не может быть больше даты текущего операционного дня");
        return CM_CANCEL;
       elif ( dlg.rec.DateBegin > dlg.rec.DateEnd )
         MsgBox("Дата начала периода не может быть больше даты окончания периода");
        return CM_CANCEL;
       end;
     end;
     if (FldName(dlg,id) == "DateEnd")
       if ( dlg.rec.DateEnd > {curdate} )
         MsgBox("Дата не может быть больше даты текущего операционного дня");
        return CM_CANCEL;
       elif ( dlg.rec.DateBegin > dlg.rec.DateEnd )
         MsgBox("Дата окончания периода не может быть меньше даты начала периода");
        return CM_CANCEL;
       end;

    end;
      UpdateFields(dlg); 
   end;

   if (cmd == DLG_KEY)
     /*Выход из диалогового окна формирования отчета*/
     if (KEY == KEY_ESC)
       return exit(1);//CM_CANCEL;
     /*Выбор данных из списка*/
     elif ( KEY == KEY_F3)
        /*Выбор подразделения банка из списка подразделений */
        if (FldName(dlg,id) == "dprt_code")
           if (ListDepartment (Department))
              dprt_v = department.code; 
              dlg.rec.dprt_code = Department.name;
              dlg.rec.dprt_name = GetClientName(Department.PartyID);
              message(" ~F3~ Список подразделений "+const_mess);
              UpdateFields(dlg);
           end;
        end;
        /*Выбор даты из календаря*/
        if (FldName(dlg,id) == "Datebegin")
          dlg.rec.datebegin = GetDateByCalendar ({curDate});
        end;
        if (FldName(dlg,id) == "DateEnd")
          dlg.rec.dateend = GetDateByCalendar ({curDate});
        end;
        
     elif (KEY == KEY_SPACE)
          /*Сброс установок в поле офис банка*/
          if (dlg.rec.dprt_code != 0 ) 
            dlg.rec.dprt_code="";
            dprt_v = 0;
            dlg.rec.dprt_name = "По всем офисам";
            UpdateFields(dlg);
          end;
          if (dlg.rec.dprt_code == "По всем офисам") 
            message(" ~F3~ Список подразделений "+const_mess);
            UpdateFields(dlg);
          end;

     elif (( KEY == KEY_F2 )  or (KEY == KEY_ENTER))        //Проверки при вводе
         if ( dlg.rec.DateBegin > {curdate} )
                MsgBox("Дата начала периода не может быть больше даты текущего операционного дня");
                return CM_IGNORE;
          elif ( dlg.rec.DateBegin > dlg.rec.DateEnd )
                MsgBox("Дата начала периода не может быть больше даты окончания периода");
                return CM_IGNORE;
         end;
          if ( dlg.rec.DateEnd > {curdate} )
                MsgBox("Дата не может быть больше даты текущего операционного дня");
                return CM_IGNORE;
          elif ( dlg.rec.DateBegin > dlg.rec.DateEnd )
                MsgBox("Дата окончания периода не может быть меньше даты начала периода");
                return CM_IGNORE;
          end;
        if ((strlen(dlg.rec.dprt_code) != 0) and (dlg.rec.dprt_name != "По всем офисам"))
           if (department.nodetype == 2)                   
           branch = "and ac.t_branch="+dprt_v;
           officename=dlg.rec.dprt_name;
           else
           branch = "and ac.t_department="+dprt_v;
           officename=dlg.rec.dprt_name;
           end;   

        elif (dlg.rec.dprt_Name == "По всем офисам") 
           branch = "and ac.t_department=1";
           officename=dlg.rec.dprt_name;
        elif (dprt_v=0)
           branch = "and ac.t_department=1";
           officename=dlg.rec.dprt_name;
        end;
        Date_begin  = dlg.rec.Datebegin;
        Date_end = dlg.rec.DateEnd;
        if (workdays(date_begin, date_end)>250)
         msgbox("Величина периода отчета превышает допустимую,\n"
                 +"пожалуйста уменьшите период");
         Return CM_IGNORE;
        end;
        if ((Date_begin < {curDate}))    
           Return CM_SAVE;
        elif (gettrue(true,"Дата равна текущему опрерационному дню. Продолжить?"))
           Return CM_SAVE;
           
        end;
      else
           Return CM_IGNORE;
     end;
   end;
        
END;

if ((GetCmdLineParm("date_begin",date_begin)) and (GetCmdLineParm("date_end",date_end)) and
(GetCmdLineParm("namedep",namedep)))
cmd = rsdcommand("select dep.t_code, party.t_name from ddp_dep_dbt dep, dparty_dbt party where party.t_partyid = dep.t_partyid and dep.t_name = ?");
cmd.addparam("name", RSDBP_IN, namedep);
rsd = rsdrecordset(cmd);
date_begin = date(date_begin);
date_end = date(date_end);
if (rsd.movenext())
 if (namedep == "000")
 branch = "and ac.t_department="+rsd.value(0);
 else
 branch = "and ac.t_branch="+rsd.value(0);
 end;
 officename=rsd.value(1);
 OutAll;
end;
else
if (RunDialog(dlg, "Event"))
  OutAll;
end;
end;