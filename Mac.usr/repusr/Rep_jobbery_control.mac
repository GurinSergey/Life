/*************************************************************************************************/
/*                      Автоматизированная банковская система RS-Bank v6.0                       */
/*************************************************************************************************/
/*  Имя файла        : Rep_jobbery_control.mac                                                   */
/*  Описание         : макрос отчета по контролю сомнительных операций                           */
/*************************************************************************************************/
/*  Программист      : GSP - Глушин С.П.                                                         */
/*  Создан           : 22.11.2013 по заявке C-24861                                              */
/*************************************************************************************************/
   //RR 07.03.2014 Адаптирую под 31-й патч

import globals;
import oralib,BANKINTER;
import keycodes;
import rcw,RSD;
import PTInter,cb_sql;
import Календарь,VBAConst;
import lib_const,lib_reporting;

   var reportTime,startTime;
   var ex,pathfile,path,head, obSheet;
   var filen = "RSU_Contr.lbr";
private var jobberySelect = "",
            columnCount = 13,
            param_cl_type,
            param_department,
            param_min_sum,
            param_bdate,
            param_edate,
            MenuChoise1;
private var depChoice = "",select_ofis,cmd_ofis,ofis_record,selectBranch = "";

   var column_format,div1, div2,parametrs,dataCl, legalForm2,legalForm1;
 array client_Type;

       GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,path);

       pathfile = FindPath(filen,path);
	if (not pathfile)
		msgbox("Не найдена LBR");
		exit();
	end;

var dlg = TRecHandler("somn_opr",pathfile,true);


MACRO jobberyControl() /* Конструктор запроса, вызов Excel_report */
    column_format = "g,t,t,t,g,g,g,g,g,g,t,t,t";
    head = "№ п/п"                                  + USR_REP_TXT_DELIMITER + 
           "Код  клиента"                           + USR_REP_TXT_DELIMITER + 
           "ИНН клиента"                            + USR_REP_TXT_DELIMITER + 
           "Наименование клиента"                   + USR_REP_TXT_DELIMITER + 
           "Входящий остаток (рубли)"               + USR_REP_TXT_DELIMITER + 
           "Дебетовый оборот (рубли)"               + USR_REP_TXT_DELIMITER + 
           "Дебетовый оборот (сомнительн.) (рубли)" + USR_REP_TXT_DELIMITER + 
           "Кредитовый оборот (рубли)"              + USR_REP_TXT_DELIMITER + 
           "Исходящий остаток (рубли)"              + USR_REP_TXT_DELIMITER + 
           "Доля сомнительных операций (в %)"       + USR_REP_TXT_DELIMITER +
           "Р./счёт клиента"                        + USR_REP_TXT_DELIMITER + 
           "Офис (подр. ТС)"                        + USR_REP_TXT_DELIMITER + 
           "Операционист (ФИО)"                     + USR_REP_TXT_DELIMITER ; 

    if(param_cl_type == "Юридические лица")
       legalForm1 = 1;
       legalForm2 = 1;
    elif(param_cl_type == "Предприниматели")
       legalForm1 = 2;
       legalForm2 = 2;
    else
       legalForm1 = 1;
       legalForm2 = 2;
    end;

    if((param_department) and (param_department != 1))
    selectBranch =
    "          AND T_BRANCH in ("+param_department+")                                                              ";
    end;

    jobberySelect =
    " SELECT   ROWNUM, j.* from(                                                                                   "+
    " SELECT   RS_CODE,                                                                                            "+
    "          NVL ( (SUBSTR (INN, 1, (INSTR (INN, '/') - 1))), '-') INN,                                          "+
    "          T_NAME,                                          "+
    "          rest_in,debet,j_summ,credit,rest_out,                                                               "+
    "          (SELECT DECODE (debet,0,0,j_summ/debet)FROM DUAL) part,                                             "+
    "          T_ACCOUNT,                                                                                          "+
    "          nvl((select T_NAME from ddp_dep_dbt where T_CODE = T_BRANCH),000) T_BRANCH,                         "+
    "          (select t_name from dperson_dbt where T_OPER = T_OPER_NUM) T_OPER                                   "+
    "   FROM   (  SELECT   T_NAME,                                                                                 "+
    "                      T_PARTYID,                                                                              "+
    "                      T_LEGALFORM,                                                                            "+
    "                      MIN (T_ACCOUNT) T_ACCOUNT,                                                              "+
    "                      T_BRANCH,                                                                               "+
    "                      MIN (T_OPER) T_OPER_NUM,                                                                "+
    "                      SUM (credit) credit,                                                                    "+
    "                      SUM (debet) debet,                                                                      "+
    "                      SUM (rest_out) rest_out,                                                                "+
    "                      SUM (rest_in) rest_in,                                                                  "+
    "                      SUM (j_summ) j_summ,                                                                    "+
    "                      (SELECT   T_CODE                                                                        "+
    "                         FROM   dobjcode_dbt                                                                  "+
    "                        WHERE   T_OBJECTID = T_PARTYID AND T_CODEKIND = 16                                    "+
    "                                AND (T_BANKCLOSEDATE = '01.01.0001'                                           "+
    "                                     OR T_BANKCLOSEDATE > :date_e1) AND ROWNUM = 1) INN,                      "+
    "                      (SELECT   T_CODE                                                                        "+
    "                         FROM   dobjcode_dbt                                                                  "+
    "                        WHERE   T_OBJECTID = T_PARTYID AND T_CODEKIND = 1 AND ROWNUM = 1) RS_CODE             "+
    "               FROM   (SELECT   ACC.T_ACCOUNT,                                                                "+
    "                                PART.T_PARTYID,                                                               "+
    "                                PART.T_NAME,                                                                  "+
    "                                PART.T_BRANCH,                                                                "+
    "                                PART.T_LEGALFORM,                                                             "+
    "                                ACC.T_OPER,                                                                   "+
    "                                rsb_account.kredita (ACC.T_ACCOUNT,                                           "+
    "                                                     ACC.T_CHAPTER,                                           "+
    "                                                     :date_b2,                                                "+
    "                                                     :date_e2) credit,                                        "+
    "                                rsb_account.debeta (ACC.T_ACCOUNT,                                            "+
    "                                                    ACC.T_CHAPTER,                                            "+
    "                                                    :date_b3,                                                 "+
    "                                                    :date_e3)debet,                                           "+
    "                                RSB_ACCOUNT.RESTA (ACC.T_ACCOUNT,                                             "+
    "                                                   :date_e4,                                                  "+
    "                                                   ACC.T_CHAPTER,                                             "+
    "                                                   null) rest_out,                                        "+
    "                                RSB_ACCOUNT.RESTA (ACC.T_ACCOUNT,                                             "+
    "                                                   CAST ( (:date_b4 - 1) AS date),                            "+
    "                                                   ACC.T_CHAPTER,                                             "+
    "                                                   null) rest_in,                                         "+
    "                                NVL (J_OPER.SUMM, 0) j_summ                                                   "+
    "                         FROM   daccount_dbt acc,                                                             "+
    "                                dparty_dbt part,                                                              "+
    "                                (  SELECT   PAYM.T_PAYERACCOUNT cl_acc,                                       "+
    "                                            PAYM.T_PAYER cl_code,                                             "+
    "                                            SUM (PAYM.T_AMOUNT) summ                                          "+
    "                                     FROM   dobjatcor_dbt objatcor,                                           "+
    "                                            dpmpaym_dbt paym                                                  "+
    "                                    WHERE   objatcor.t_Object =                                               "+
    "                                               LPAD (PAYM.T_PAYMENTID, 10, '0')                               "+
    "                                            AND SUBSTR (PAYM.T_PAYERACCOUNT, 6, 3) = '810'                    "+
    "                                            AND SUBSTR (PAYM.T_RECEIVERACCOUNT,6,3) = '810'                   "+
    "                                            AND PAYM.T_VALUEDATE BETWEEN :date_b5 AND :date_e5                "+
    "                                            AND OBJATCOR.T_ATTRID = 30                                        "+
    "                                            AND OBJATCOR.T_OBJECTTYPE = 501                                   "+
    "                                            AND OBJATCOR.T_GROUPID = 11                                       "+
    "                                            AND EXISTS                                                        "+
    "                                                  (SELECT   1                                                 "+
    "                                                     FROM   daccount_dbt exacc                                "+
    "                                                    WHERE   EXACC.T_ACCOUNT = PAYM.T_PAYERACCOUNT             "+
    "                                                            AND EXACC.T_TYPE_ACCOUNT LIKE '%Ч%'               "+
    "                                                            AND EXACC.T_OPEN_CLOSE <> 'З'                     "+
    "                                                            AND EXACC.T_CHAPTER = 1                           "+
    "                                                            AND SUBSTR (EXACC.T_ACCOUNT,6,3) = '810')         "+
    "                                 GROUP BY   PAYM.T_PAYERACCOUNT, PAYM.T_PAYER) j_oper                         "+
    "                        WHERE       ACC.T_CLIENT = PART.T_PARTYID                                             "+
    "                                AND ACC.T_CLIENT = j_oper.cl_code(+)                                          "+
    "                                AND ACC.T_ACCOUNT = j_oper.cl_acc(+)                                          "+
    "                                AND ACC.T_TYPE_ACCOUNT LIKE '%Ч%'                                             "+
    "                                AND ACC.T_OPEN_CLOSE <> 'З'                                                   "+
    "                                AND ACC.T_CHAPTER = 1                                                         "+
    "                                AND SUBSTR (ACC.T_ACCOUNT, 6, 3) = '810')                                     "+
    "           GROUP BY   T_NAME, T_BRANCH, T_PARTYID,T_LEGALFORM)                                                "+
    "  WHERE   (debet > :minsum1 OR credit > :minsum2)                                                             "+
    "          and T_LEGALFORM in (:legf1,:legf2)                                                                  "+
    selectBranch +
    "  order by part desc, j_summ desc) j                                                                          ";

       parametrs = makeArray(SQLParam ("date_e1",   date(param_edate)),
                             SQLParam ("date_b2",   date(param_bdate)),
                             SQLParam ("date_e2",   date(param_edate)),
                             SQLParam ("date_b3",   date(param_bdate)),
                             SQLParam ("date_e3",   date(param_edate)),
                             SQLParam ("date_e4",   date(param_edate)),
                             SQLParam ("date_b4",   date(param_bdate)),
                             SQLParam ("date_b5",   date(param_bdate)),
                             SQLParam ("date_e5",   date(param_edate)),
                             SQLParam ("minsum1",  money(param_min_sum)),
                             SQLParam ("minsum2",  money(param_min_sum)),
                             SQLParam ("legf1",      int(legalForm1)),
                             SQLParam ("legf2",      int(legalForm2)));

debugbreak;
    message("Выполнение запроса к БД");
    dataCl = ExecSQLselect(jobberySelect,parametrs);

    ex = Excel_Report("jobbery", head, columnCount, dataCl, column_format);

    obSheet = ExStandFormat(ex,columnCount);

END;/*macro ContragentsReport()*/

MACRO ExSpecFormat()
    message("Форматирование книги MS-Excel");
    ex.visible = false;
    recordcount = recordcount+1;
    obSheet.Range("A1:"+recalc_columns(columnCount)+recordcount).Font.Size = 8;
    obSheet.Rows("1:1").RowHeight = 30.00;
    obSheet.Columns("E:J").ColumnWidth = 13.57;
    obSheet.Columns("D:D").ColumnWidth = 55.00;
    obSheet.Columns("L:L").ColumnWidth = 8.00;
    obSheet.Columns("K:K").ColumnWidth = 18.00;
    obSheet.Columns("A:A").ColumnWidth = 5.00;
    obSheet.Columns("B:B").ColumnWidth = 10.71;
    obSheet.Columns("C:C").ColumnWidth = 9.00;
    obSheet.Range("E"+recordcount).FormulaR1C1  = "=СУММ(R[-"+(recordcount-1)+"]C:R[-1]C)";
    obSheet.Range("F"+recordcount).FormulaR1C1  = "=СУММ(R[-"+(recordcount-1)+"]C:R[-1]C)";
    obSheet.Range("G"+recordcount).FormulaR1C1  = "=СУММ(R[-"+(recordcount-1)+"]C:R[-1]C)";
    obSheet.Range("H"+recordcount).FormulaR1C1  = "=СУММ(R[-"+(recordcount-1)+"]C:R[-1]C)";
    obSheet.Range("I"+recordcount).FormulaR1C1  = "=СУММ(R[-"+(recordcount-1)+"]C:R[-1]C)";
    obSheet.Range("B"+recordcount) = "    ИТОГО:";
    obSheet.Range("A"+recordcount+":"+recalc_columns(columnCount)+recordcount).Font.Bold = true;
    obSheet.Range("A"+recordcount+":"+recalc_columns(columnCount)+recordcount).Interior.Color = RSU_Rep_xlHeadColor;
    obSheet.Range("A"+recordcount+":"+recalc_columns(columnCount)+recordcount).Borders.LineStyle = xlContinuous;
//    obSheet.Range("A"+recordcount+":"+"G"+(recordcount)).Merge;
    obSheet.Range("J"+recordcount).Formula  = "=ЕСЛИ(F"+recordcount+"=0;0;G"+recordcount+"/F"+recordcount+")";
    obSheet.Columns("J:J").NumberFormat = "0%";
    obSheet.Columns("L:L").HorizontalAlignment = xlCenter;
    obSheet.Range("E"+recordcount+":J"+recordcount).NumberFormat = 0;//"0.00";
//    obSheet.Range("A2:"+recalc_columns(columnCount)+recordcount).VerticalAlignment = xlCenter;
    ex.visible = true;
END;


MACRO HandleEvent (dlg, cmd, id, key)

    macro EventHandler (rs, cmd, id, key )
       if (cmd == DLG_INIT)
           if (not AddMultiAction (rs, 316)) 
               msgbox ("Не удалось выполнить AddMultiAction ()");
           end;
       end;
       if ((cmd == DLG_MSEL) and (key == 316)) // F2
           depChoice = depChoice+ "," + string (rs.value("otdel_ID"));
           return CM_MSEL_CONT_CLEAR;
       end;
       if ((cmd == DLG_KEY) and ((key == 13) or (KEY==KEY_ESC)))
           return CM_SELECT;
       end;
       if (cmd == DLG_MSELEND)// and (key == 316)) // F2
           return CM_MSEL_STOP_CLEARALL;
       end;
    end;                                 

    if(cmd == DLG_INIT)
       message("~F2~ продолжить,  ~ESC~ выход");
       dlg.rec.end_date = {curdate};
       dlg.rec.beg_date = "01." + substr(string({curdate}),4);
       UpdateFields(dlg); 
    end;
/*    if(cmd == DLG_SETFOCUS)

    end;
*/
    if (cmd == DLG_REMFOCUS)
       if (FldName(dlg,id)=="beg_date")
          if ( dlg.rec.beg_date > {curdate} )
             MsgBox("Дата начала периода больше даты текущего операционного дня"); 
             return CM_CANCEL;
          end;
       end;
       if (FldName(dlg,id)=="end_date")
          if ( dlg.rec.end_date > {curdate} )
             MsgBox("Дата конца периода больше даты текущего операционного дня"); 
             return CM_CANCEL;
          end;
       end;
       if ((FldName(dlg,id)=="beg_date") or (FldName(dlg,id)=="end_date"))
          if ( dlg.rec.end_date < dlg.rec.beg_date )
             MsgBox("Дата конца периода больше даты начала"); 
             return CM_CANCEL;
          end;
       end;
       UpdateFields(dlg); 
    end;
    if (cmd==DLG_KEY)
      if (KEY==KEY_ESC)
        if (GetTrue(TRUE,"Вы действительно хотите выйти?"))
          exit(1);
          return  CM_CANCEL;
        else
          return  CM_IGNORE;    
        end;
      elif (KEY==KEY_ENTER)                                     	
          return CM_Ignore;
      elif (KEY==KEY_F1)
       msgbox("HELP YOURSELF");
        return CM_Ignore;          
      elif (KEY==KEY_F9)
        return CM_Ignore;          
      elif (KEY==KEY_F3)
         if (FldName(dlg,id) == "client_type")
	    client_Type(0) = "Юридические лица";
            client_Type(1) = "Предприниматели";
            client_Type(2) = "Все";    
            MenuChoise1 = Menu(client_type, "Меню","Выберите фильтр",48,12);
            if (MenuChoise1 >= 0)
               dlg.rec.client_type = client_type(MenuChoise1);
               UpdateFields(dlg); 
            end;

         elif (FldName(dlg,id) == "department")
            depChoice = "";
               select_ofis = " Select dp.t_code as otdel_ID,dp.t_name as otdel_num, PART.T_NAME as otdel_name  "+
                             "   from ddp_dep_dbt dp, dparty_dbt part                                 "+
                             "  where t_status = 2                                                    "+
                             "    and DP.T_PARTYID = PART.T_PARTYID                                   "+
                             "  order by dp.t_name                                                    ";
            cmd_ofis = RSDCommand(select_ofis);
            ofis_record = RSDRecordset( cmd_ofis, RSDVAL_CLIENT, RSDVAL_STATIC );
            if (RunScroll(ofis_record, 0, 0, "Отделы", @EventHandler, "Список отделов", "~Enter~ выбор отдела  |  ~F2~ выбор выделенной группы отделов  |  ~Esc~ выход", true))
               if(depChoice)
                  dlg.rec.department = substr(depChoice,2);
               else
                  dlg.rec.department = ofis_record.value("otdel_ID");
               end;
               UpdateFields(dlg);
            end;
         end;

         if(FldName(dlg,id) == "beg_date")
            dlg.rec.beg_date = GetDateByCalendar();
            UpdateFields(dlg);
         end;

         if(FldName(dlg,id) == "end_date")
            dlg.rec.end_date = GetDateByCalendar();
            UpdateFields(dlg);
         end;

         return CM_ignore;

      elif(KEY==KEY_F2)          /* окончательная инициализация полей и запуск */
         param_department = dlg.rec.department;
         param_cl_type    = dlg.rec.client_type;
         param_bdate      = dlg.rec.beg_date;
         param_edate      = dlg.rec.end_date;
         param_min_sum    = dlg.rec.min_sum;
         return CM_Save;                 
      end;
    end; /* end cmd==DLG_KEY*/
end;/*macro HandleEvent*/

  if (RunDialog(dlg,"HandleEvent"))        
    startTime = time;
    jobberyControl();
    ExSpecFormat();
    reportTime = time - startTime;
      println("                                                         ");
      println("                                                         ");
      println("                                  ОТЧЕТ СФОРМИРОВАН      ");
      println("                         время выполнения: "+ reportTime  );
  end;













