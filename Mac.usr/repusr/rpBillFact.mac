/*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   Гуцу Е.В.

   Имя файла: rpBillFact.mac

   Создан:    20.03.2016

   Описание:  Счета-фактуры, неполностью оплаченные на дату

   ChangeLog:
-----------------------------------------------------------------------------------------------------------------------------------------------------
--/--/---- Е.Гуцу 


+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/
import globals, rsexts, activex, rsd;

var nameout = "..\\txtfile\\rpBillFact_" + random();
var repDate = {CurDate};
setOutput(nameout);


/* Класс для работы с Excel, заимствован из rep_corschem.mac */
PRIVATE CLASS myExcelReport

  private var myWorkBook;
  private var myWorkExcel;
  private var myWorkSheet;
  var numSheet = 1;

  private var myRecord = null;
 
  var cntRecord = 0;
  var amountRecord = $0.0;

  macro ClearSymble(str, split_char)
      str = StrSubst(str, strfor(13), "" );
      str = StrSubst(str, strfor(10), "" );
      str = StrSubst(str, split_char, " ");
      return string(str);   
  end;

  private macro SetTypeColumns();
      var result_arr = TArray(true), i;
 
      for(i, 1, 4)
        var templs_arr = TArray(true);         
        templs_arr(0) = i;
        templs_arr(1) = 2;
        if( i == 4 )
           templs_arr(1) = 1;
        end;

        result_arr(i - 1) = templs_arr;
      end; 

      return result_arr;
  end;

  private macro GetWorkDirName() : string
      var DirName:string = "", 
          StrErr :string = "";

      GetRegistryValue( "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, DirName, StrErr );        
      if( DirName )     
        if( SubStr(DirName, 1, 1) == "." ) DirName = "$" + GetCurDir(TRUE) + strSubst (DirName, "..",""); end;     
        if (existFile(DirName))  return DirName;  end;
      end;

      DirName = "$" + GetEnv("TEMP");

      if( NOT DirName ) DirName = "$" + GetEnv("TMP"); end;
      if (existFile(DirName)) return DirName; end;

      msgbox("Ошибка поиска папки для временных файлов на терминале");

      return "";
  end;

  private macro myCopyFile (Name_file)
      var Templs_Dir_Term = GetWorkDirName();

      var TermFileName = toAnsi(Name_file);
      var SPFileName;

      if ( Templs_Dir_Term == "" )  return "";  end;
      SPFileName = "..\\txtfile\\" + Name_file; 

      if (not existFile(SPFileName)) MsgBox(string("Транспортный временный файл не найден: |", SPFileName)); return ""; end;
      if( not CopyFile(SPFileName, Templs_Dir_Term + "\\" + TermFileName) ) RunError("Ошибка при передаче файла на терминал\n" + Templs_Dir_Term + "\\" + TermFileName); end;

      return subStr(Templs_Dir_Term + "\\" + TermFileName, 2);      
  end;

  private macro OpenExcel()
      private var objectXls, WorkExcel;

      objectXls = CreateObject ("rsax", "TRsAxServer", string("RsAxServer", UserNumber()), IsStandAlone());
      WorkExcel = objectXls.CreateComObject ("Excel.Application"); 

      myWorkExcel = WorkExcel;

      return true;

  OnError(er)
      msgbox(er.Message); 
      return false;
  end;

  private macro SetHeaderExcelTable()
      var i; 
      myWorkBook.Sheets(numSheet);
      myWorkSheet = myWorkBook.ActiveSheet();

      //myWorkSheet.Cells(1, 1).Value  = "Неоплаченные счета-фактуры " + strfor(13)+strfor(10) + " на " + repDate;
      myWorkSheet.Cells(1, 1).Value  = "Счета-фактуры, не оплаченные на " + repDate;
      myWorkSheet.Cells(1, 1).HorizontalAlignment = 3;
      myWorkSheet.Cells(1, 1).VerticalAlignment = 2;

      myWorkSheet.Range("A1:H1").merge;
      myWorkSheet.Rows("1:1").RowHeight = 40;
      myWorkSheet.Range("A1:H1").font.size = 13;

      myWorkSheet.Cells(3, 1).Value  = "№ п/п";
      myWorkSheet.Cells(3, 2).Value  = "Номер счёта-фактуры";
      myWorkSheet.Cells(3, 3).Value  = "Дата рег.";
      myWorkSheet.Cells(3, 4).Value  = "Сумма";
      myWorkSheet.Cells(3, 5).Value  = "Оплаченная сумма";
      myWorkSheet.Cells(3, 6).Value  = "Остаток счёта 60312";

      myWorkSheet.Range("G3:H3").merge;
      myWorkSheet.Cells(3, 7).Value  = "Получатель";
      myWorkSheet.Cells(4, 7).Value  = "Код";
      myWorkSheet.Cells(4, 8).Value  = "Наименование";

      for (i, 1, 8)
        myWorkSheet.Cells(3, i).HorizontalAlignment = 3;
        myWorkSheet.Cells(3, i).Font.Bold = true; 
        myWorkSheet.Cells(4, i).HorizontalAlignment = 3;
        myWorkSheet.Cells(4, i).Font.Bold = true; 

        myWorkSheet.Columns(i).EntireColumn.AutoFit;
      end; 
      //myWorkSheet.Range("A" + (cntRecord + 3) + ":D" + (cntRecord + 3)).Font.Bold = true; 
      myWorkSheet.Range("A3:H" + (cntRecord+3) ).Borders.Weight = 2;  
      myWorkSheet.Range("A3:H4").Interior.Color = #B5B5B5; 

      myWorkSheet.Columns(4).HorizontalAlignment = 4;
      myWorkSheet.Columns(5).HorizontalAlignment = 4;
      myWorkSheet.Columns(6).HorizontalAlignment = 4;

      myWorkSheet.columns(4).Entirecolumn.NumberFormat = "# ##0,00р.";
      myWorkSheet.columns(5).Entirecolumn.NumberFormat = "# ##0,00р.";
      myWorkSheet.columns(6).Entirecolumn.NumberFormat = "# ##0,00р.";

      return true;

   OnError
       return false;
   end;     

   macro CopyValueToExcel(myFileName)
       initprogress(-1, "", "Подготовка файла Excel");
       myFileName = myCopyFile(myFileName); 

       if ( OpenExcel() and  myFileName != "")      
        
         myWorkExcel.Workbooks.OpenText (myFileName, 866, 1, 1, 2, false, false, false, false, false, true, ";", SetTypeColumns(), 1, "."," ", true, false);
         myWorkBook = myWorkExcel.Workbooks.Application;

         SetHeaderExcelTable();

         remprogress;
         //MsgBox("Файл успешно записан");
         myWorkExcel.Visible = true;

         var wb = myWorkExcel.Workbooks.item(1);
         //wb.SaveAs( myFileName, 56 );         // xls
         wb.SaveAs( myFileName, 51 );           // xlsx

       end;                
   OnError(er)
       msgBox(er.Message);
   end;     

END;


/* Формирование рекордсета / подсчёт записей */
MACRO CriarRecordSet( isCount, pDate )

  var sql, cmd, rs;
  var SelectClause, RecCount;

  RecCount = 0;
  /*
  SelectClause = "   SELECT bil.t_facturanumber, bil.t_regdate, bil.t_totalamount, SUM (pm.t_amount), bil.t_receivername "
                 "     FROM dbilfactura_dbt bil, dsfdef_dbt sf, dpmpaym_dbt pm                                           "
                 "    WHERE sf.t_facturaid  = bil.t_facturaid                                                            "
                 "      AND pm.t_feetype    = sf.t_feetype                                                               "
                 "      AND pm.t_defcomid   = sf.t_id                                                                    "
                 "      AND pm.t_valuedate <= ?                                                                          "
                 "      AND bil.t_totalamount > ( SELECT sum(bo.t_amount) FROM dbilbookentry_dbt bo                      "
                 "                                 WHERE bo.t_facturaid = bil.t_facturaid           )                    ";
  */

  SelectClause = " SELECT bil.t_facturanumber, bil.t_regdate, bil.t_totalwithnds, nvl(bo.t_amount, 0), bil.t_receivername, "+
                 "        ABS(nvl(rst.t_rest, 0)), nvl(cod.t_code, chr(0))                                                 "+
                 "   FROM dbilfactura_dbt bil, dbilbookentry_dbt bo, daccount_dbt acc, drestdate_dbt rst, dobjcode_dbt cod "+
                 "  WHERE bo.t_facturaid  (+) = bil.t_facturaid                                                            "+
                 "    AND bo.t_department (+) = bil.t_department                                                           "+
                 "    AND bo.t_branch     (+) = bil.t_branch                                                               "+
                 "    AND bil.t_totalamount > 0                                                                            "+
                 "    AND bil.t_totalamount > ( SELECT nvl(sum(bo1.t_amount), 0) FROM dbilbookentry_dbt bo1                "+
                 "                               WHERE bo1.t_facturaid  (+) = bo.t_facturaid                               "+
                 "                                 AND bo1.t_department (+) = bo.t_department                              "+
                 "                                 AND bo1.t_branch     (+) = bo.t_branch )                                "+
                 "    AND bil.t_regdate          <= ?                                                                      "+
                 "    AND acc.t_client        (+) = bil.t_receiverid                                                       "+
                 "    AND acc.t_chapter       (+) = 1                                                                      "+
                 "    AND acc.t_code_Currency (+) = 0                                                                      "+
                 "    AND acc.t_account       (+) like '60312810_23%'                                                      "+
                 "    AND rst.t_accountid     (+) = acc.t_accountid                                                        "+
                 "    AND rst.t_restcurrency  (+) = acc.t_code_Currency                                                    "+
                 "    AND nvl( rst.t_restdate, TO_DATE('01.01.0001','dd.mm.rrrr') ) =                                      "+
                 "                              ( SELECT nvl( max(rst1.t_restdate), TO_DATE('01.01.0001','dd.mm.rrrr') )   "+
                 "                                  FROM drestdate_dbt rst1                                                "+
                 "                                 WHERE rst1.t_accountid     = rst.t_accountid                            "+
                 "                                   AND rst1.t_restcurrency  = rst.t_restcurrency                         "+
                 "                                   AND rst1.t_restdate     <= ? )                                        "+
                 "    AND cod.t_objecttype (+) = 3                                                                         "+
                 "    AND cod.t_codekind   (+) = 1                                                                         "+
                 "    AND cod.t_objectid   (+) = bil.t_receiverid                                                          "+
                 "    AND cod.t_state      (+) = 0                                                                         "+
                 " ORDER BY bil.t_regdate desc, bil.t_facturanumber                                                        ";
        
  sql = SelectClause;
  if( isCount )
     sql = " SELECT count(0) FROM ( " + SelectClause + " ) ";
  end;

  cmd = rsdCommand( sql );
  cmd.addParam( "", RSDBP_IN, pDate );
  cmd.addParam( "", RSDBP_IN, pDate );
  rs = rsdRecordSet( cmd );

  if( not isCount )
     return rs;
  end;

  if( rs and rs.moveNext() )
     RecCount = rs.value( 0, null, V_INTEGER );
  end;

  return RecCount;

END;


/*
/* Проверка наличия счёта в массиве */
MACRO VerifiqueMatriz( pAccount )
  var n = 0;

  while( n < IwpAccountsArray.Size )

     if( IwpAccountsArray( n ) == pAccount )
        return true;
     end;
     n = n + 1;

  end;

  return false;
END;
*/


MACRO GerarRelatorio( pDate, pFileName )

  var sql, cmd, rs;
  var repSfNumber,
      repSfRegdate,
      repSfAmount,
      repPayAmount,
      repSfReceiver,
      repAc60312Rest,
      repReceiverCode;

  var my_report = myExcelReport();
  var separator = ";", i = 1, amount = $0.0, text = "", vCount, ind = 1;


  /* Начинаем с 3 строки */
  println(separator);println(separator);println(separator);

  
  /* Выборка всех счетов-фактур, неоплаченных на отчётную дату */
  vCount = CriarRecordSet( true, pDate );

  initprogress( vCount, "Формирование отчёта...", "Обрабатываем счета-фактуры" );

  rs = CriarRecordSet( false, pDate );
  while( rs and rs.moveNext() )

     useProgress( i );
     i = i + 1;      

     repSfNumber     = rs.value( 0, null, V_STRING );
     repSfRegdate    = rs.value( 1, null, V_DATE   );
     repSfAmount     = rs.value( 2, null, V_MONEY  );
     repPayAmount    = rs.value( 3, null, V_MONEY  );
     repSfReceiver   = rs.value( 4, null, V_STRING );
     repAc60312Rest  = rs.value( 5, null, V_MONEY  );
     repReceiverCode = rs.value( 6, null, V_STRING );

     text = my_report.ClearSymble(string( ind             ), ";") + separator +
            my_report.ClearSymble(string( repSfNumber     ), ";") + separator +
            my_report.ClearSymble(string( repSfRegdate    ), ";") + separator +
            my_report.ClearSymble(string( repSfAmount     ), ";") + separator +
            my_report.ClearSymble(string( repPayAmount    ), ";") + separator +
            my_report.ClearSymble(string( repAc60312Rest  ), ";") + separator +
            my_report.ClearSymble(string( repReceiverCode ), ";") + separator +
            my_report.ClearSymble(string( repSfReceiver   ), ";") + separator ;

     println(text);        
     ind = ind + 1;

  end;

  remProgress();

  setoutput(null, true);                 
  my_report.cntRecord = ind-1;
  my_report.CopyValueToExcel( pFileName );

END;



 if( getDate( repDate, "Пожалуйста, введите дату выпуска отчёта" ) )
    GerarRelatorio( repDate, nameout );
 end;

 exit( 1 );