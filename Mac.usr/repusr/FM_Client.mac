/*Выгруженные операции по определенному клиенту за период                                                   */
/*                                                                                                          */
/*                                                                                                          */
/*В модуле "Финансовый мониторинг" необходимо реализовать возможность создания                              */
/*отчета по выгруженным операциям по определенному клиенту за период.                                       */
/*Поля, которые должны содержаться в отчете: Статус, Код ОК, № опер, Дата представления,                    */
/*Валюта, Сумма в валюте, Сумма в руб, Дата операции, Основание операции, Клиент.                           */
/*Отчет должен формироваться в формате Excel и быть доступен для печати на А4.                              */
/*Название отчета: "Операции /название клиента/, направленные в Уполномоченный орган за период с ... по ... */
/*                                                                                                          */
/*                                                                                                          */
/*Тихомиров А.Н. 15.02.2010                                                              Версия 1.0         */


import globals, oralib, bankinter, rsexts, ptinter;

import RSD, rcw, Календарь;
var SQL,cmd,ex,ob,obbook,obsheet, i=4, client, n:integer, name;

const  KEY_F1      =315;
const  KEY_F2      =316;
const  KEY_F3      =317;
const KEY_ESC      = 27;
const KEY_SPACE    = 32;
const KEY_ENTER    = 13;

record party ("party.dbt");
var Fulloutputl, outl, outputl="fm_client.lbr";                    

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,outl);
Fulloutputl = FindPath(outputl, outl);
var dlg = TRecHandler("fm_cl", fulloutputl, TRUE); 

var out, output = "fm_client.xls", fulloutput;                            
var branch, DateBegin,
    DateEnd,
    Office:double  = 0,
    OfficeName = "По всем офисам";
var maxS:integer, client_id = 0;


/*Найдем код по Partyid*/
private macro GetClientCode(id)
var  sl=string(" SELECT t_code "+
"  FROM dobjcode_dbt "+
" WHERE t_objectid = "+id +
"   AND t_state = 0 "+
"   AND t_objecttype = 3 "+
"   AND t_codekind = 1");
var  DataS=rsdrecordset(sl);
  if( DataS.moveNext())
    return DataS.value(0);
  else
    if (id !=0)
       msgbox("Субъект не найден в party.dbt");
       return 0;
    else
      return "000000000000";
    end;
  end;
END;


/*Найдем клиента по Коду*/
private macro GetClientID(code)
var  slw=rsdcommand("  SELECT party.t_name, party.t_partyid "+
"  FROM dobjcode_dbt obj, dparty_dbt party "+
" WHERE obj.t_state = 0  "+
"   AND obj.t_objecttype = 3  "+
"   AND obj.t_codekind = 1 "+
"   AND obj.t_objectid = party.t_partyid "+
"   AND obj.t_code = ?");
slw.addparam("code",RSDBP_IN,code);
var  DataSw=rsdrecordset(slw);
  if( DataSw.moveNext())
    client_id = datasw.value(1);
    return datasw.value(0);
  end;

END;



private macro OutAll()
initprogress(-1, "Отбираются операции, ждите", "Отбираются операции");
var count=0, datec;
/*Определяем общее количество счетов*/
var sl = RSDCOMMAND("SELECT COUNT(1) as CNT "+
"  FROM dopcontr_dbt contr, "+
"       dopcntrpt_dbt payer, "+
"       dfininstr_dbt fininstr, "+
"       dopcntrpt_dbt receiver "+
" WHERE payer.t_kind = 1 "+
"   AND contr.t_operationid = payer.t_operationid "+
"   AND receiver.t_kind = 4 "+
"   AND contr.t_date between ? AND ? "+
"   AND (   payer.t_account IN (SELECT t_account "+
"                                 FROM daccount_dbt "+
"                                WHERE t_client = ? AND t_chapter = 1) "+
"        OR receiver.t_account IN (SELECT t_account "+
"                                    FROM daccount_dbt "+
"                                   WHERE t_client = ? AND t_chapter = 1) "+
"       ) "+
"   AND contr.t_operationid = receiver.t_operationid "+
"   AND contr.t_status >= 5 "+
"   AND contr.t_code_currency = fininstr.t_fiid"); 

sl.addparam("dateb",RSDBP_IN,datebegin);
sl.addparam("datee",RSDBP_IN,dateend);
sl.addparam("ID",RSDBP_IN,client_id);
sl.addparam("ID2",RSDBP_IN,client_id);
var DataS = rsdrecordset(sl);
DataS.movenext();
maxS=datas.value(0);

sql = RSDCOMMAND("SELECT DECODE (contr.t_status, "+
"               5, 'Выгружена', "+
"               6, 'Приостановлена', "+
"               7, 'К проводке', "+
"               8, 'Отвергнута' "+
"              ) AS stat, "+
"       contr.t_operationid, contr.t_numb_p, decode(trunc(contr.t_date_p),'01.01.01','',contr.t_date_p) as t_date_p, "+
"       fininstr.t_fi_code, "+
"       contr.t_sumcur, contr.t_sumrub, contr.t_date_carry, contr.t_ground, "+
"       payer.t_name AS payer, receiver.t_name AS receiver, "+
"       code.t_codeoc as code "+
"  FROM dopcontr_dbt contr, "+
"       dopcntrpt_dbt payer, "+
"       dfininstr_dbt fininstr, "+
"       dopcntrpt_dbt receiver, "+
"       dfm_opr_scrol_view code "+
" WHERE payer.t_kind = 1 "+
"   AND contr.t_operationid = payer.t_operationid "+
"   AND contr.t_date between ? AND ? "+
"   AND receiver.t_kind = 4 "+
"   AND (   payer.t_account IN (SELECT t_account "+
"                                 FROM daccount_dbt "+
"                                WHERE t_client = ? AND t_chapter = 1) "+
"        OR receiver.t_account IN (SELECT t_account "+
"                                    FROM daccount_dbt "+
"                                   WHERE t_client = ? AND t_chapter = 1) "+
"       ) "+
"   AND contr.t_operationid = receiver.t_operationid "+
"   AND contr.t_status = 5 "+
"   AND code.t_operationid = contr.t_operationid "+
"   AND contr.t_code_currency = fininstr.t_fiid");

sql.addparam("dateb",RSDBP_IN,datebegin);
sql.addparam("datee",RSDBP_IN,dateend);
sql.addparam("ID",RSDBP_IN,client_id);
sql.addparam("ID2",RSDBP_IN,client_id);

useprogress;

var DataSet = rsdrecordset(sql);
remprogress;

    if (IsStandAlone()) // Двухзвенка
                 if ((ValType(ex)!=V_UNDEF) and (ex.Visible==False))
                   ex = ActiveX("Excel.Application",NULL,false); 
                 else
                   ex = ActiveX("Excel.Application",NULL,true);
                   
                 end;
  
      else // Трехзвенка
               ob = CreateObject ("rsax","TRsAxServer","RsAxServer",false);
               ex = ob.CreateComObject ("Excel.Application",true);

     end; 
GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\USERTEMPLSDIR",2,out);
Fulloutput = FindPath(output, out);                    
               
obBook = ex.Workbooks.open(fulloutput); 
obSheet = obBook.ActiveSheet(); 

/*Заполняем заголовок таблицы*/
obSheet.Range("A"+2).Value="Операции "+name+", направленные в Уполномоченный орган за период с "+DateBegin+" по "+DateEnd;
obSheet.Range("A"+4).Value="Статус";
obSheet.Range("B"+4).Value="Код ОК";
obSheet.Range("C"+4).Value="№ опер";
obSheet.Range("D"+4).Value="Дата представления";
obSheet.Range("E"+4).Value="Валюта";
obSheet.Range("F"+4).Value="Сумма в валюте";
obSheet.Range("G"+4).Value="Сумма в руб";
obSheet.Range("H"+4).Value="Дата операции";
obSheet.Range("I"+4).Value="Основание операции";
obSheet.Range("J"+4).Value="Плательщик";
obSheet.Range("K"+4).Value="Получатель";
n = 0;
initprogress(maxs, "Выводится отчет, ждите", "Выводится отчет");
while (dataset.movenext())
useprogress(n);
i=i+1;
n=n+1;
obSheet.Range("A"+i).Value = dataset.value("stat");
obSheet.Range("B"+i).Value = dataset.value("code");
obSheet.Range("C"+i).Value = dataset.value("t_numb_p");
obSheet.Range("D"+i).Value = substr(dataset.value("t_date_p"),1,8);
obSheet.Range("E"+i).Value = dataset.value("t_fi_code");
obSheet.Range("F"+i).numberformat = "0,00";
obSheet.Range("F"+i).Value = dataset.value("t_sumcur");
obSheet.Range("G"+i).numberformat = "0,00";
obSheet.Range("G"+i).Value = dataset.value("t_sumrub");
obSheet.Range("H"+i).Value = date(dataset.value("t_date_carry"));
obSheet.Range("I"+i).Value = dataset.value("t_ground");
obSheet.Range("J"+i).Value = dataset.value("payer");
obSheet.Range("K"+i).Value = dataset.value("receiver");
end;

remprogress(n);
/*Раскрашиваем итоговую таблицу и шапку*/
obSheet.Range("A4:K"+i).Borders.Weight=2;
obSheet.Range("A4:K"+4).interior.color=4035000;
Ex.visible = true;       
END;


 /*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key) 

   var const_mess = "~F2~ Продолжить ~SPACE~ По всем офисам ~ESC~ Выход ";
   var const_mess2 = "~F2~ Продолжить ~SPACE~ ~ESC~ Выход ";

   /*Первоначальная инициализация полей*/
   if(cmd == DLG_INIT)
      dlg.rec.client_code  ="";
      dlg.rec.client_name = "";
      client_id = 0;
      dlg.rec.Datebegin = {curDate};
      dlg.rec.DateEnd = {curDate};
      UpdateFields(dlg); 
   end;
   
   /*Установка подсказок в строке состояния*/
   if (cmd==DLG_SETFOCUS)
     if (FldName(dlg,id)=="dprt_code") 
       message(" ~F3~ Список клиентов "+const_mess);
     elif (FldName(dlg,id)=="Datebegin")
       message(" ~F3~ Выбор даты из календаря "+const_mess2);
     elif (FldName(dlg,id)=="DateEnd")
       message(" ~F3~ Выбор даты из календаря "+const_mess2);
    end;
   end;
   
   if (cmd == DLG_REMFOCUS)
     /*Проверка корректности дат отчета*/
     if (FldName(dlg,id) == "Datebegin")
       if ( dlg.rec.DateBegin > {curdate} )
         MsgBox("Дата начала периода не может быть больше даты текущего операционного дня");
        return CM_CANCEL;
       elif ( dlg.rec.DateBegin > dlg.rec.DateEnd )
         MsgBox("Дата начала периода не может быть больше даты окончания периода");
        return CM_CANCEL;
       end;
     end;
     if (FldName(dlg,id) == "DateEnd")
       if ( dlg.rec.DateEnd > {curdate} )
         MsgBox("Дата не может быть больше даты текущего операционного дня");
        return CM_CANCEL;
       elif ( dlg.rec.DateBegin > dlg.rec.DateEnd )
         MsgBox("Дата окончания периода не может быть меньше даты начала периода");
        return CM_CANCEL;
       end;
     end;
     if (FldName(dlg,id) == "client_code")
       if ( strlen(dlg.rec.client_code) > 0 ) 
         dlg.rec.client_name = GetClientID(dlg.rec.client_code);
       end;
    end;
      UpdateFields(dlg); 
   end;

   if (cmd == DLG_KEY)
     /*Выход из диалогового окна формирования отчета*/
     if (KEY == KEY_ESC)
       return exit(1);//CM_CANCEL;
     /*Выбор данных из списка*/
     elif ( KEY == KEY_F3)
        /*Выбор подразделения банка из списка подразделений */
        if (FldName(dlg,id) == "client_code")
           if (listpt(party, 1, 0, PTLIST_PSCLIENT, 0, PTCK_ALL))
              dlg.rec.client_code = GetClientCode(party.partyid);
              dlg.rec.client_name = party.name;
              client_id = party.partyid;
              message(" ~F3~ Список клиентов"+const_mess);
              UpdateFields(dlg);
           end;
        end;
        /*Выбор даты из календаря*/
        if (FldName(dlg,id) == "Datebegin")
          dlg.rec.datebegin = GetDateByCalendar ({curDate});
        end;
        if (FldName(dlg,id) == "DateEnd")
          dlg.rec.dateend = GetDateByCalendar ({curDate});
        end;
        
     elif ( KEY == KEY_F2 )         //Проверки при вводе
         if ( dlg.rec.DateBegin > {curdate} )
                MsgBox("Дата начала периода не может быть больше даты текущего операционного дня");
                return CM_IGNORE;
          elif ( dlg.rec.DateBegin > dlg.rec.DateEnd )
                MsgBox("Дата начала периода не может быть больше даты окончания периода");
                return CM_IGNORE;
         end;
          if ( dlg.rec.DateEnd > {curdate} )
                MsgBox("Дата не может быть больше даты текущего операционного дня");
                return CM_IGNORE;
          elif ( dlg.rec.DateBegin > dlg.rec.DateEnd )
                MsgBox("Дата окончания периода не может быть меньше даты начала периода");
                return CM_IGNORE;
          end;

          if (FldName(dlg,id) == "client_code")
             if ( strlen(dlg.rec.client_code) > 0 ) 
                dlg.rec.client_name = GetClientID(dlg.rec.client_code);
             end;
          end;

        if (strlen(dlg.rec.client_code) != 0) 
           if (client_id >0)
           dlg.rec.client_name = GetClientID(dlg.rec.client_code);
//           client = dlg.rec.client_code;
           name = dlg.rec.client_name;
           end;
        else
           MsgBox("Введите клиента");
           return CM_IGNORE;
        end;
        Datebegin  = dlg.rec.Datebegin;
        Dateend = dlg.rec.DateEnd;
        if ((Datebegin < {curDate}))    
           Return CM_SAVE;
        elif (gettrue(true,"Дата равна текущему опрерационному дню. Продолжить?"))
           Return CM_SAVE;
        else
           Return CM_IGNORE;
        end;
/*      else
           Return CM_IGNORE;*/
     end;
   end;
        
END;



/*Точка входа в макрос*/
if (RunDialog(dlg, "Event"))
  OutAll;
end;
     
end;