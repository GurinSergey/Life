// 09.08.2012 vihrov c-13351 (наполнить группу операционистами из файла)

import rsbdataset;
import globals;
import rsexts;
import treport;

const TEXTDIR_REESTR		= "BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR";

private var nomer_group:integer = 0;
private var file_name:string = "";
private var textdir:string = "";

private Array Text;
private Array Buttons;
private	var vybor:integer=-3;

Text(0) = "Отчет или работа?";

Buttons(0) = "Отмена";
Buttons(1) = "Отчет";
Buttons(2) = "Внести пользовтелей в группу";


macro chekReestrRead(m_name:string, m_status:integer, m_err:integer)
	if ((m_status == V_UNDEF) or (m_err != 0))
    	msgbox("Ошибка получения значения настройки реестра " + m_name + " \n Процедура GetRegistryValue вернула код ошибки "+m_err);
   		exit(0);
	end;
end;

macro dayString(m_reestrName:string):string
	private var m_errCode			:integer = NULL;
	private var m_statusGetRegistry :integer = NULL;
    private var m_zna4enie          :string  = NULL;
	if (m_reestrName == "")
		msgbox("При чтении реестра не задана строка реестра");
		exit(0)
	end;
	m_statusGetRegistry=GetRegistryValue(m_reestrName, V_STRING, m_zna4enie, m_errCode);
	chekReestrRead(m_reestrName, m_statusGetRegistry, m_errCode);
	return(m_zna4enie);
end;

private macro net_takoy_gr(g);
	private var sql = "select 1 from dacsgroup_dbt where t_groupid=" + g;
	private var ds = trsbdataset(sql);
	if (ds and ds.movenext())
		return false;
	else
		return true;
	end;
end;

macro copy_file(src_full_path:string, dst_dir:@string)
	dst_dir = dst_dir + "\\file"+date+"_"+time;
	dst_dir = StrSubst ( dst_dir, ".", "_" );
	dst_dir = StrSubst ( dst_dir, ":", "_" );
	dst_dir = StrSubst ( dst_dir, " ", "_" );
	dst_dir = dst_dir + ".txt";
	if (substr(dst_dir,1,2) == "__")
		dst_dir = ".."+substr(dst_dir, 3)
	end;


	if (not copyfile("$"+src_full_path, dst_dir))
		println("не смог скопировать файл " + src_full_path + "с локального диска на СП: " + dst_dir);
	else
		println("файл " + src_full_path + " скопирован на СП успешно в " + dst_dir);
	end;
end;


private macro day_nomer_gr
	if (getint(nomer_group,"введите номер группы для наполнения")); 
		if (net_takoy_gr(nomer_group))
			println("группа ", nomer_group + " НЕ НАЙДЕНА в справочнике групп");
			exit(0);
		end;
		println("группа " + nomer_group + " есть в справочнике групп.");
	else 
		println("отказ пользователя от выбора группы");
		exit(0);
	end;
end;

private macro select_file
	private var err_stat:bool = false;
	err_stat = selectfile(file_name,"*.txt","выбор файла с локального диска", 0, true);
	if (err_stat)
		println("выбран файл c локального диска пользователя "+file_name);
	else
		println("отказ от выбора файла");
		exit(0);
	end; // окно выбора файла
end;

private macro process_file(fn)

    private macro oper_exists(n:integer) : bool;
    	private var ds = trsbdataset ("select 1 from dperson_dbt where t_oper = " + n);
    	if (ds and ds.movenext())
    		return true;
    	else
    		return false;
    	end;
    end;

    private macro oper_in_group(no:integer, ng:integer):bool
    	private var ds = trsbdataset ("select 1 from dacsgroupoper_dbt where t_oper = " + no + " and t_groupid = " + ng);
    	if (ds and ds.movenext())
    		return true;
    	else
    		return false;
    	end;
    end;

	private var s:string = "";
	private var oper_nomer:integer = 0;
	private var sql_insert:string = "";
	private var iinsert;

	FILE f1 () txt;
	if ( not open (f1, fn))
		println ("не смог открыть скопированный ранее с локального диска пользователя на СП файл " + fn);
		exit(0);
	else
		println("файл на СП " + fn + " открыт успешно");
	end;
	while (next(f1))
		oper_nomer = int(f1.str);
		s = "считал строку: " + f1.str + ", номер операциониста " + "(" + oper_nomer + ") ";
		if (oper_exists(oper_nomer))
			if (oper_in_group(oper_nomer, nomer_group))
				s = s + " существует в справочнике оперов, но УЖЕ присутсвует в группе " + string(nomer_group:5) + ". ПРОПУСК";
			else
				sql_insert = "insert into dacsgroupoper_dbt values (" + nomer_group + "," + oper_nomer + ", chr(0)) ";
				iinsert = rsdcommand (sql_insert);
				iinsert.execute;
				s = s + " добавлен в группу " + nomer_group;
			end;
		else
			s = s + " не существует в справочнике операционистов. ПРОПУСК";
		end;
		println(s);
	end
end;

private macro delete_file(fn);
	if (not removeFile(fn))
		println("Не смог удалить файл " + fn + " с сервера приложений. Ну да и бог с ним.");
	else
		println("файл " + fn + " удален с СП успешно");
	end;
end;

private macro ot4et
    private var kolwo:integer = 0;
    private var t = ctablereport();
	private var ds = trsbdataset ("SELECT	p1.t_oper nop, p1.t_name nname " + 
      "  FROM	dperson_dbt p1 " +
      " WHERE	p1.t_userblocked <> CHR (88) " +
      "	  AND p1.t_userclosed <> CHR (88) " +
      "	  AND p1.t_userunactive <> CHR (88) " +
      "	  AND NOT EXISTS (SELECT	1 " +
      "							  FROM	dacsgroupoper_dbt g1 " +
      "							 WHERE	p1.t_oper = g1.t_oper AND g1.t_groupid > 1000) ");
    t.addcolumn("номер",10, AL_RIGHT);
	t.addcolumn("имя",40, AL_LEFT);
	t.printhead("пользователи не имеющие флага закрытый, заблокированный или неиспользуемый и не состоящие ни в одной группе с номером больше 1000");
	while (ds.movenext())
		t.printStringTransferByWord(ds.value("nop"),ds.value("nname"));  
		kolwo = kolwo + 1;
	end;
	t.printbottom("всего выбрано:" + kolwo);
end;

vybor = ConfWin(Text, Buttons); //точка входа

if (vybor == 1)  
	ot4et();
elif (vybor == 2)
	day_nomer_gr();  
	select_file();
	textdir = dayString(TEXTDIR_REESTR);
	copy_file(file_name, @textdir);
	process_file(textdir);
	delete_file(textdir);
else
	msgbox("отмена по выбору пользователя");
end;

println();
println();
	
