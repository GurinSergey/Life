 /*Макрос печати отчета по закрытым счетам.            */
/*Гогильчин А.В  R-Style Ukraine, Kiev 07.08.2008     */
/*Версия 1.0                                          */

import globals, oralib, bankinter, "KeyCodes.mac", repforms, likePy, xlrep, PTInter, fiinter, cb_sql;

record Department ("dp_dep.dbt");

var path = "",
    pathfile = "",
    filen = "RSU.lbr";

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,path);
pathfile = FindPath(filen,path);
	if (not pathfile)
		msgbox("Не найдена LBR");
		exit();
	end;

var dlg = TRecHandler("clopAcc",pathfile,True);

record superior(ptoffice);
record officet(ptoffice);

var toExcel    = False,
    Date_Begin = {curdate},
    Date_End   = {curdate},
    mask_RUR   = "",
    mask_CUR   = "",
    statusAc   = 0, // По умолчанию - по закрытым
    CurCode    = -1,// По умолчанию - по всем валютам
    DpDepHead  = 1 ,// Номер для головы - точнее определим позже
    DpDep      = 1 ,// По умолчанию - по всему банку
    rselect = "";

/*переменные для шаблона .xlt*/
var ob,ex,obBook,obSheet;
var Template:string = "clopacc.xlt";

const typesPrior = "ЧLYЯБКИ";  // KS 02.09.2010 I-059324 Признаки V6, определяющие вид счета считать первичными: "Ч", "L", "Y", "Я", "Б", "К", "И"   

Private macro PrintHead(_beginDate, _EndDate)

  if (not toExcel)
    if ( statusAc == 0)
[                       Сведения о закрытых счетах
];
    else
[                       Сведения об открытых счетах
];
    end;

if (_BeginDate == date(0,0,0))
[                                     на дату ###########]
(trim(string(_EndDate)):c);
else
[                            за период с ########### по ###########]
(trim(string(_BeginDate)):c, trim(string(_EndDate)):c);
end;

[
 ┌─────┬─────────────┬─────────────────────┬───────────────────────┬────────────────────┬─────────────────┬────────────┬───────────────┬───────────────┬──────────────┐
 │  №  │ Код клиента │    Номер счета      │ Наименование  клиента │        ИНН         │ Номер  договора │ Вид (цель) │ Дата открытия │ Дата закрытия │  Примечание  │
 │ п.п.│             │                     │                       │                    │банковского счета│    счета   │               │               │              │
 ├─────┼─────────────┼─────────────────────┼───────────────────────┼────────────────────┼─────────────────┼────────────┼───────────────┼───────────────┼──────────────┤
];
  else
        ex = ActiveX("Excel.Application",NULL,false);

        if (IsStandAlone()) // Двухзвенка
          if ((ValType(ex)!=V_UNDEF) and (ex.Visible==False))
            ex = ActiveX("Excel.Application",NULL,false); 
          else
            ex = ActiveX("Excel.Application",NULL,true);
          end;
        else // Трехзвенка
          ob = CreateObject ("rsax","TRsAxServer","RsAxServer",false);
          ex = ob.CreateComObject ("Excel.Application",true);
        end; 

        obBook = ex.Workbooks.open(Template);
        obSheet = obBook.ActiveSheet(); 

        obSheet.Range("a6:j6").select;
        ex.Selection.MergeCells = true;
 
        if ( statusAc == 0)
          obSheet.Range("a6").Value="Сведения об закрытых счетах ";
        else
          obSheet.Range("a6").Value="Сведения об открытых счетах ";
        end;

        if (_BeginDate == date(0,0,0))
          obSheet.Range("a6").Value=obSheet.Range("a6").Value+"на дату "+trim(string(_EndDate));
        else
          obSheet.Range("a6").Value=obSheet.Range("a6").Value+"за период с "+trim(string(_BeginDate))+" по "+trim(string(_EndDate));
        end;

        ex.Selection.HorizontalAlignment = xlCenter;

  end;
end;

private macro PrintLine(_Nr, _clientName, _INN, _sfCode, _accountType, _account, _openDate:date, _closeDate:date, _code, _accountTypePrim, _Line)
  if (not toExcel)
[│#####│#############│#####################│ ##################### │####################│   ############  │############│###############│###############│##############│
 │     │             │                     │                       │                    │                 │            │               │               │              │
](_Nr:c, _code, _account:c:w, _clientName:w, _INN:w, _sfCode:c:w, _accountType:c:w, trim(string(_openDate)):c, trim(string(_closeDate)):c, _accountTypePrim:c:w);
  else

          obSheet.Range("a"+(_Line+8)).Value=_Nr;

          obSheet.Range("a"+(_Line+8)+":c"+(_Line+8)).select;
          ex.Selection.NumberFormat = "@";
          obSheet.Range("b"+(_Line+8)).Value=_code;
          ex.Selection.HorizontalAlignment = xlCenter;

          obSheet.Range("c"+(_Line+8)).Value=_account;

          obSheet.Range("d"+(_Line+8)).select;
          obSheet.Range("d"+(_Line+8)).Value=_clientName;
          ex.Selection.HorizontalAlignment = xlJustify;
          ex.Selection.VerticalAlignment = xlBottom;

          obSheet.Range("e"+(_Line+8)).select;
          ex.Selection.NumberFormat = "@";
          obSheet.Range("e"+(_Line+8)).Value=_INN;
          ex.Selection.HorizontalAlignment = xlCenter;

          obSheet.Range("f"+(_Line+8)).select;
          ex.Selection.NumberFormat = "@";
          obSheet.Range("f"+(_Line+8)).Value=_sfCode;
          ex.Selection.HorizontalAlignment = xlCenter;

          obSheet.Range("g"+(_Line+8)).Value=_accountType;

          obSheet.Range("h"+(_Line+8)).select;
          ex.Selection.NumberFormat = "@";
          obSheet.Range("h"+(_Line+8)).Value=trim(string(_openDate));
          ex.Selection.HorizontalAlignment = xlCenter;

          obSheet.Range("i"+(_Line+8)).select;
          ex.Selection.NumberFormat = "@";
          obSheet.Range("i"+(_Line+8)).Value=trim(string(_closeDate));
          ex.Selection.HorizontalAlignment = xlCenter;
          obSheet.Range("j"+(_Line+8)).Value=_accountTypePrim;

  end;
end;

private macro PrintLineBalance(_Balance, _countOpen, _countClose, _Line)
if (_countOpen==0)
  _countOpen="";
end;
if (_countClose==0)
  _countClose="";
end;
  if (not toExcel)
[├─────┴─────────────┴─────────────────────┴───────────────────────┴────────────────────┴─────────────────┴────────────┴───────────────┴───────────────┴──────────────┤
 │ Счет #####                                                                                                            #############   #############                │
](_Balance:c:w, _countOpen:c, _countClose:c);
  else
    obSheet.Range("a"+(_Line+8)+":g"+(_Line+8)).select;
    ex.Selection.MergeCells = true;
    obSheet.Range("a"+(_Line+8)).Value="Счет "+_Balance;
    ex.Selection.Font.Bold = True;

    obSheet.Range("h"+(_Line+8)+":i"+(_Line+8)).select;
    obSheet.Range("h"+(_Line+8)).Value=_countOpen;
    obSheet.Range("i"+(_Line+8)).Value=_countClose;
    ex.Selection.HorizontalAlignment = xlCenter;
    ex.Selection.Font.Bold = True;
  end;
end;

private macro PrintLineBR
  if (not toExcel)
[├─────┬─────────────┬─────────────────────┬───────────────────────┬────────────────────┬─────────────────┬────────────┬───────────────┬───────────────┬──────────────┤
];
  end;
end;

Private macro PrintBottom(_countOpen, _countClose, _Line)
var     Range;
  if (_countOpen==0)
    _countOpen="";
  end;
  if (not toExcel)
[├────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┤
 │ ВСЕГО по отчету                                                                                                       #############   #############                │
 └────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────┘
](_countOpen:c, _countClose:c);
  else

    _Line = _Line +1;
   
    obSheet.Range("a"+(_Line+8)+":g"+(_Line+8)).select;
    ex.Selection.MergeCells = true;
    obSheet.Range("a"+(_Line+8)).Value="ВСЕГО по отчету";
    ex.Selection.Font.Bold = True;

    obSheet.Range("h"+(_Line+8)+":i"+(_Line+8)).select;
    obSheet.Range("h"+(_Line+8)).Value=_countOpen;
    obSheet.Range("i"+(_Line+8)).Value=_countClose;
    ex.Selection.HorizontalAlignment = xlCenter;
    ex.Selection.Font.Bold = True;

    Range = "A9:J"+(_Line+8);
    obSheet.Range (Range).Select;
    ex.Selection.Borders (xlEdgeLeft).LineStyle       = xlContinuous;
    ex.Selection.Borders (xlEdgeTop).LineStyle        = xlContinuous;
    ex.Selection.Borders (xlEdgeBottom).LineStyle     = xlContinuous;
    ex.Selection.Borders (xlEdgeRight).LineStyle      = xlContinuous;
    ex.Selection.Borders (xlInsideVertical).LineStyle = xlContinuous;
    ex.Selection.Borders (xlInsideHorizontal).LineStyle = xlContinuous;

    obSheet.Range ("A1").Select;
    ex.visible=true;
    Println("\tОтчет отправлен в Excel...");
  end;
end;

private macro GetResultGrid(AccRurList, AccCurList, DateBegin, DateEnd, DpDep, CurCode, statusAc);
  private macro MakeStr(str:string): string;

    if (strlen(str) == 0)
       return "NULL"
    else 
       /*Пробелы заменим на запятые */
       str = StrSubst(str, ", ", ",");
       str = StrSubst(str, " ", ",");
       /*уберем повторяющеися запятые*/
       while (index(str,",,") != 0)
          str = strSubst(str, ",,", ",");
       end;
       /*расставим кавычки*/
       str = strSubst(str, ",", "','");
       str = "'"+str+"'";

       return(str);
    end;
  end;

   var rs:object = NULL;
   var Query = "";
   query = 
           "select distinct client.t_name,\n" +
           "                client.t_partyid,\n" + 
           "                nvl((select t.t_code\n" + 
           "                             from dobjcode_dbt t\n" + 
           "                            where t.t_objectid = accs.t_client\n" + 
           "                              and t.t_codekind = 16\n" + 
           "                              and t.t_state = 0),' ') as INN,\n" + 
           "                accs.t_type_account,\n" + 
           "                accs.t_account,\n" + 
           "                accs.t_open_date,\n" + 
           "                accs.t_close_date,\n" + 
           "                (select t.t_code\n" + 
           "                   from dobjcode_dbt t\n" + 
           "                  where t.t_objectid = accs.t_client\n" + 
           "                    and t.t_codekind = 1\n" + 
           "                    and t.t_state = 0) as clientCode,\n" + 
           "                nvl((select t.t_number\n" + 
           "                   from dsfcontr_dbt t\n" + 
           "                  where t.t_object = accs.t_account\n" + 
           "                    and t.t_partyid = accs.t_client),' ') as sfCode\n" + 
           "  from (\n";
   if ( CurCode <=  0 ) // По всем или по рублям
     query = query +
           "        select *\n" + 
           "          from daccount_dbt acc\n" + 
           "         where acc.t_balance in\n" + 
           "               ("+makeStr(AccRurList)+")\n";
     if ( DpDep != DpDepHead ) // Если по голове - то и по всему банку - для простоты. Иначе только по определенному ВСП
       query = query +
           "           and acc.t_branch = " + DpDep + "\n";  
     end;
   end;
   if ( CurCode == -1 ) // По всем
     query = query +
           "        UNION ALL\n";
   end;
   if ( CurCode !=  0 ) // По всем или по вылюте
     query = query +
           "        select *\n" + 
           "          from daccount$_dbt accv\n" + 
           "         where accv.t_balance in\n" + 
           "               ("+makeStr(AccCurList)+")\n";
     if ( DpDep != DpDepHead ) // Если по голове - то и по всему банку - для простоты. Иначе только по определенному ВСП
       query = query +
           "           and accv.t_branch = " + DpDep + "\n";  
     end;
     if ( CurCode > 0) // По валюте
       query = query +
           "           and t_code_currency = " + CurCode + "\n"; 
     end;
   end;
       query = query +
           "       ) accs\n" + 
           " inner join dparty_dbt client on accs.t_client = client.t_partyid\n" + 
           " where ";

   if ( statusAc == 0) // По закрытым
     query = query +
             "  accs.t_close_date ";
   else // По открытым
     query = query +
             "  accs.t_open_date ";
   end;
   // период
     query = query + "between "+getSqlDate(datebegin)+" and "+getSqlDate(dateEnd)+"\n"; 
   if (OperSelect != "")
     query = query +
           "   AND accs.t_oper like '"+OperSelect+ "'\n";
   end;
     query = query +
           " order by substr(accs.t_account,1,5) asc, clientcode asc";
//println(query);
   rs = execSQLSelect(query);
//   getstring(query);
//   debugbreak;
   if (rs and rs.movenext())
      return rs;
   else 
      return NULL;
   end;

   OnError(Err)
     MsgBox(Err.Message,"| at ",Err.Line,"|in ",Err.Module);
end;

private macro PrintItAll()

  /*переменные для шаблона .xlt*/
  var RegParam:string = "";

  var grid: object = Null;
  var Balance = "0";     // Текущий балансовый номер
  var CountBalance    = 0; // Сколько всего описано балансовых счетов
  var NumberBalance   = 0; // Количество закрытых счетов по балансовому Balance
  var NumberBalanceOp = 0; // Количество открытых счетов по балансовому Balance
  var Number   = 0;        // Количество закрытых счетов
  var NumberOp = 0;        // Количество открытых счетов
  var Next = true;

  private macro ТипСчета(Str:string,typeP):string // typeP = 0 - выводим приоритетные, иначе остальные
     var rs:object = NULL;
     var next = TRUE;
     var Query = "";
     var result = "";
     query = 
             "select tac.*\n" +
             "  from dtypeac_dbt tac\n" + 
             " where tac.t_inumtype = 1\n";
     if (typeP == 0)
       query =  query +
             "   and instr('"+typesPrior+"',tac.t_type_account) != 0\n";
     else
       query =  query +
             "   and instr('"+typesPrior+"',tac.t_type_account)  = 0\n";
     end;
       query =  query +
             "   and instr('"+trim(str)+"',tac.t_type_account) != 0";

     rs = execSQLSelect(query);

     while (rs.movenext())
        result = result +" "+ rs.value("t_name_type");
     end;

     return result;

     OnError(Err)
       MsgBox(Err.Message,"| at ",Err.Line,"|in ",Err.Module);
  end;

  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\USERTEMPLSDIR",2,RegParam);
//  Template = FindPath("\\cb\\" + Template,regparam);
  Template = FindPath(Template,regparam);

  grid = GetResultGrid(mask_RUR, mask_CUR, date_Begin, Date_end, DpDep, CurCode, statusAc);

  if (Valtype(grid) == V_UNDEF)
     PrintLn("\t\t\tНет данных для отчета");
  else 
        printHead(date_begin, date_end);
        while (next)
           if   ( statusAc == 0 ) // По закрытым
             number = number + 1;
             NumberBalance = NumberBalance + 1;     // прибавляем закрытых
           elif ( statusAc != 0 ) // По открытым
             numberOp = numberOp + 1;
             NumberBalanceOp = NumberBalanceOp + 1; // прибавляем открытых
             if (grid.value("t_close_date")!="")
               number = number + 1;
               NumberBalance = NumberBalance + 1;   // прибавляем закрытых
             end;
           end;
           printline(max(number,numberOp),  grid.value("t_name"), grid.value("INN"),
                     grid.value("sfCode"),
                     ТипСчета(grid.value("t_type_account"),0), 
                     grid.value("t_account"), grid.value("t_open_date"), grid.value("t_close_date"), grid.value("clientCode"),
                     ТипСчета(grid.value("t_type_account"),1),CountBalance+max(number,numberOp));
           next = grid.movenext();
           if (Balance=="0")
             Balance = substr(grid.value("t_account"),1,5);
           elif( (not next) or (substr(grid.value("t_account"),1,5) != Balance))
             CountBalance    = CountBalance + 1; // Описали на один балансовый больше
             PrintLineBalance(Balance, NumberBalanceOp, NumberBalance,CountBalance+max(number,numberOp)); // вывожу данные по балансовому
             if (next)
               PrintLineBR; // Начнем следующий балансовый
             end;
             NumberBalance   = 0; // переходим дальше
             NumberBalanceOp = 0; // переходим дальше
             Balance = substr(grid.value("t_account"),1,5);
           end;
        end;
        printBottom(numberOp,number,CountBalance+max(number,numberOp));
  end;

end;

/*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key)

   var mess = "~F2~ Продолжить ~ESC~ Выход ";
   var PartyID:integer = {OurBank};
   var OrgDate:date = {curdate} + 1;
   var NoChangeDate:bool = false;

   var rs:object = NULL;
   var Query = "";

   record fi( fininstr );
   record dp( dp_dep );

   if(cmd == DLG_INIT)

      dlg.rec.Date_report_begin = {curdate}-1;
      dlg.rec.Date_report_end   = {curdate}-1;
      dlg.rec.status            = "Закрытые";
      dlg.rec.curName           = "Все валюты";
      dlg.rec.output            = "текст";
      dlg.rec.departCode = "000";
      dlg.rec.departName = GetClientName({OurBank});
      dlg.rec.account_RUR       = "40106, 40107, 40203, 40206, 40502, 40503, 40602, 40603, 40701, 40702, 40703, 40802, 40804, 40805";
      dlg.rec.account_CUR       = "40502, 40503, 40602, 40603, 40701, 40702, 40703, 40802, 40807";
      dlg.rec.oper     = "";
      dlg.rec.opername = "все операциониты";
      rs = execSQLSelect("select t_name,t_code from ddp_dep_dbt where t_partyid ="+{OurBank});
      if (rs and rs.movenext())
        DpDep = DpDepHead  = rs.value("t_code");
        dlg.rec.departCode = rs.value("t_name");
      end;
      UpdateFields(dlg); 
   end;
   if (cmd==DLG_SETFOCUS)
       message(mess);
   end;
   
   if (cmd == DLG_REMFOCUS)
       if (FldName(dlg,id)=="Date_report_begin")
          if ( dlg.rec.Date_report_begin > dlg.rec.Date_report_end ) 
             MsgBox("Дата начала больше даты окончания отчетного периода");
             return CM_CANCEL;
          elif ( dlg.rec.Date_report_begin > {curdate} )
             MsgBox("Дата начала больше даты текущего операционного дня"); 
             return CM_CANCEL;
          end;
       elif (FldName(dlg,id)=="Date_report_end") 
          if (dlg.rec.Date_report_end < dlg.rec.Date_report_begin )
             MsgBox("Дата окончания меньше даты начала отчетного периода"); 
             return CM_CANCEL;
          elif ( dlg.rec.Date_report_end > {curdate} )
             MsgBox("Дата окончания больше даты текущего операционного дня");
             return CM_CANCEL;
          end;
        end;
      UpdateFields(dlg);
   end; 

   if (cmd==DLG_KEY)
       if (KEY==KEY_ESC)
           return exit(1);
       elif (KEY==KEY_SPACE)
          if (FldName(dlg,id) == "status")
              if (dlg.rec.status == "Открытые")
                 dlg.rec.status =  "Закрытые";
                 statusAc       = 0;
              else
                 dlg.rec.status = "Открытые";
                 statusAc       = 1;
              end;
          end;
          if (FldName(dlg,id) == "output")
              if (dlg.rec.output == "Excel")
                 dlg.rec.output =  "текст";
              else
                 dlg.rec.output = "Excel";
              end;
          end;
       elif (KEY == KEY_F3)
         if(FldName(dlg,id) == "oper")
              rselect = "select t.t_oper, t.t_name " +   
                        " from dperson_dbt t inner join ddp_dep_dbt v on v.t_name = '000' and t.t_codedepart = v.t_code;";
   
              cmd = RSDCommand(rselect);
              rsrec = RSDRecordset( cmd, RSDVAL_CLIENT, RSDVAL_STATIC );
              if (RunScroll(rsrec, 0, 0, "Опера", @EvProc, "Список операционистов", "~Enter~ выбор сотрудника ~Esc~ выход", true))
                dlg.rec.oper = rsrec.value (0);
                dlg.rec.OperName = rsrec.value (1);
                UpdateFields(dlg);
                OperSelect = rsrec.value (0);
              end;
         elif(FldName(dlg,id) == "curCode")
              if(ListFI(FIKIND_CURRENCY,0,FI))
                CurCode         = fi.fiid;
                dlg.rec.curCode = fi.fi_code;
                dlg.rec.curName = fi.name;
                UpdateFields(dlg);
              end;
         elif(FldName(dlg,id) == "departCode")
              if(ListDepartment (dp))        
                DpDep              = dp.code;
                dlg.rec.departCode = dp.name;
                dlg.rec.departName = GetClientName(dp.partyid);
                UpdateFields(dlg);
              end;
         end;

       elif ( KEY==KEY_F2 )
             Date_begin = dlg.rec.Date_report_begin;
             Date_end   = dlg.rec.Date_report_end;
             mask_RUR   = dlg.rec.account_RUR;
             mask_CUR   = dlg.rec.account_CUR;
             toExcel = ifThenElse(dlg.rec.output == "Excel", TRUE, FALSE);
             /*02/09/2008 Новиков С. С. добавил возможность формирования отчета только при корректных значениях*/
             if ((Date_begin <= Date_end) and (Date_begin <= {curdate})
                and (Date_end >= Date_begin) and (Date_end <= {curdate}) and (CurCode > -2)) 
               Return CM_SAVE;
             else
               MsgBox("Не все поля корректны!");
             end;
             /***************************************************************************************************/
       
             
       elif ( (KEY==KEY_ENTER) and (FldName(dlg,id)=="output") )
          SetFocus(dlg, 0);
          Return CM_IGNORE;
       end;


       if ( (dlg.rec.curCode == "0") or (dlg.rec.curCode == "") ) // Если код валюты ввели руками
         CurCode         = -1;
         dlg.rec.curName = "Все валюты";
       elif (ПолучитьФинИнПоISO(int(dlg.rec.curCode),fi))
         CurCode         = fi.fiid;
         dlg.rec.curName = fi.name;
       else
         CurCode         = -2;
         dlg.rec.curName = "" ;
       end;

   end;
END;

if ( RunDialog(dlg, "Event"))
   printItAll();
end;