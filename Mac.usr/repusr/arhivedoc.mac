/*********************************************************************/
/*  Отчет по проводкам выполненным в архивном дне                    */
/*  Коркин Иван                                								*/
/*  13.10.2010                                 								*/
/*  Реализовано по заявке I-072215.                                  */
/*********************************************************************/

import rsbdataset, bankinter, globals, календарь, "KeyCodes.mac";

var ReportDate1, ReportDate2, account;
var documentid, datee, notice1 = "";

var Fulloutputl, outl, outputl="arhivdoc.lbr";                    
GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,outl);
Fulloutputl = FindPath(outputl, outl);
if (not Fulloutputl)
	msgbox("Не найдена LBR");
	exit();
end;

var dlg = TRecHandler("dates", fulloutputl, TRUE); 

 /*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key) 
   var const_mess = " ~F3~ Выбор значения из списка ~F2~ Продолжить ~ESC~ Выход ",
   SQLQuery, SQLQuery1, rsrec, cmd1;
   /*Первоначальная инициализация полей*/
   if(cmd == DLG_INIT)
      dlg.rec.reportdate1 = {curdate};
      dlg.rec.reportdate2 = {curdate};
      ReportDate1  = dlg.rec.reportDate1;
      ReportDate2  = dlg.rec.reportDate2;
      dlg.rec.account = "";
      UpdateFields(dlg); 
   end;
   /*Установка подсказок в строке состояния*/
   if (cmd==DLG_SETFOCUS)
      message(const_mess);
   end;
   if (cmd == DLG_REMFOCUS)
      /*Проверка корректности дат отчета*/
      UpdateFields(dlg); 
   end;
   if (cmd == DLG_KEY)
      /*Выход из диалогового окна формирования отчета*/
      if (KEY == KEY_ESC)
         //return exit;//
         return CM_CANCEL;
      /*Выбор данных из списка*/
      elif ( KEY == KEY_F3)
         if (FldName(dlg,id) == "Reportdate1")
            dlg.rec.reportdate1 = GetDateByCalendar ({curDate}-1);
         end;
         if (FldName(dlg,id) == "Reportdate2")
            dlg.rec.reportdate2 = GetDateByCalendar ({curDate}-1);
         end;
      elif (( KEY == KEY_F2 ) or (KEY == KEY_ENTER))         //Проверки при вводе
         if ( dlg.rec.reportDate1 > {curDate} )
            MsgBox("Дата начала периода должна быть не больше текущей даты");
            return CM_IGNORE;
         end;
         if ( dlg.rec.reportDate2 > {curDate} )
            MsgBox("Дата начала периода должна быть не больше текущей даты");
            return CM_IGNORE;
         end;
         if ( dlg.rec.reportDate1 > dlg.rec.reportDate2 )
            MsgBox("Дата начала периода должна быть не больше даты конца");
            return CM_IGNORE;
         end;

         if ( dlg.rec.account == "" )
            MsgBox("Укажите счет");
            return CM_IGNORE;
         end;
         ReportDate1  = dlg.rec.reportDate1;
         ReportDate2  = dlg.rec.reportDate2;
         account = dlg.rec.account;
         Return CM_SAVE;
      end;
   else
      Return CM_IGNORE;
   end;
END;

macro printhead()	
	println("                     Отчет по проводкам выполненным в архивном дне");
	println();
	println("По счету " + account);
	println();
	println("С " + reportdate1 +" по " + reportdate2);
	println();
end;

macro printtablehead(datee)	
   [        
     За ##########

            ┌───────────┬─────────────┬────────────────────┬────────────────────┬───────────────┬───────┐
            │№ док.     │ Сист. дата  │     Счет Дт.       │     Счет Кт.       │     Сумма     │  Опер.│
            ├───────────┼─────────────┼────────────────────┼────────────────────┼───────────────┼───────┤](date(datee));
end;

private macro printtableLine(par1, par2, par3, par4, par5, par6)	
	[        │###########│#############│####################│####################│###############│#######│
	](par1, date(par2), par3, par4, par5, par6);	
end;

private macro printSeparator()
	[        ├───────────┼─────────────┼────────────────────┼────────────────────┼───────────────┼───────┤]
end;


macro printtablefooter()	
   [        └───────────┴─────────────┴────────────────────┴────────────────────┴───────────────┴───────┘] 
end;


macro calculate()
   var query, data, chapter, first, curdatee;
   printhead();
   query = " select ACC.T_CHAPTER from daccount_dbt acc "
   + "\n" + "where ACC.T_ACCOUNT = '" + account + "' "
   + "\n" + "union all "
   + "\n" + "select ACC.T_CHAPTER from daccount$_dbt acc "
   + "\n" + "where ACC.T_ACCOUNT = '" + account + "' ";
   data = TRsbDataset(query);
   if (data.movenext)
   	chapter = data.value(0);
      query = "select * from ("
      + "\n" + " select ARH.T_NUMB_DOCUMENT, ARH.T_DATE_CARRY, ARH.T_SYSTEMDATE, ARH.T_ACCOUNT_PAYER, ARH.T_ACCOUNT_RECEIVER, ARH.T_SUM, ARH.T_OPER from darhdoc_dbt arh "
      + "\n" + "where (ARH.T_ACCOUNT_PAYER = '" + account + "' "
      + "\n" + "    or ARH.T_ACCOUNT_RECEIVER = '" + account + "') "
      + "\n" + "    and ARH.T_CHAPTER = " + chapter
      + "\n" + "    and ARH.T_DATE_CARRY between to_date('" + reportdate1 + "') and to_date('" + reportdate2 + "') "
      + "\n" + "    and ARH.T_SYSTEMDATE != ARH.T_DATE_CARRY "
      + "\n" + "union all   "
      + "\n" + "select ARH.T_NUMB_DOCUMENT, ARH.T_DATE_CARRY, ARH.T_SYSTEMDATE, ARH.T_ACCOUNT_PAYER, ARH.T_ACCOUNT_RECEIVER, ARH.T_SUM, ARH.T_OPER from darhdoc$_dbt arh "
      + "\n" + "where (ARH.T_ACCOUNT_PAYER = '" + account + "' "
      + "\n" + "    or ARH.T_ACCOUNT_RECEIVER = '" + account + "') "
      + "\n" + "    and ARH.T_CHAPTER = " + chapter
      + "\n" + "    and ARH.T_DATE_CARRY between to_date('" + reportdate1 + "') and to_date('" + reportdate2 + "') "
      + "\n" + "    and ARH.T_SYSTEMDATE != ARH.T_DATE_CARRY"
      + "\n" + ") order by t_date_carry, T_SYSTEMDATE";
      data = TRsbDataset(query);
      first = true;
      curdatee = "";
      while (data.movenext)
      	if (data.value(1) != curdatee)
      		curdatee = data.value(1);
      		if (not first)
      			printtablefooter();
      		else
      			first = false;
      		end;
      		printtablehead(curdatee);
      	else
      		printseparator();
      	end;

      	printtableline(data.value(0),data.value(2),data.value(3),data.value(4),data.value(5),data.value(6));

      end;	
      if (first)
      	print("Проводок выполненных в архивном дне не найдено");
	   else
	   	printtablefooter();
      end;
   else
   	print("Счет не найден");
   end;
end;

/*Точка входа в макрос*/
if (RunDialog(dlg, "Event"))                  
   calculate();
end; 




