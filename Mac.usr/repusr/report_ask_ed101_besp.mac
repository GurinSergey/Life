// @filename: report_ask_ed101_besp.mac
// @author  : Gurin S.
// @desc    : Отчет о результатах квитовки ED101 БЭСП
// @request : C-25393
// @modify  : none
// -------------------------------------------------------------------------------------------------
import SbCrdInter, RSD, treport;

var dateBegin, dateEnd; 
var reportName = getTxtFileName ("report_ask_ed101_besp");

private class Ask(_DocNum, _Amount, _PayerAcc, _FileName, _Result)
    var m_DocNum   = _DocNum;
    var m_Amount   = _Amount;
    var m_PayerAcc = _PayerAcc;
    var m_FileName = _FileName;
    var m_Result   = _Result;
end;

private macro Report(BegDate, EndDate)
    var sql, cmd, rs, title;

    var table_ask    = CTableReport();
    var table_no_ask = CTableReport();

    var array_ask     : TArray = TArray();
    var array_no_ask  : TArray = TArray();

    var i = 0;

    if (BegDate == EndDate) 
        title = string(BegDate); 
    else 
        title = string("период с ", BegDate," по ", EndDate); 
    end;

    SetOutput (reportName, false);    
    println (string ("                Отчет о результатах квитовки ED101 БЭСП за ", title):c:100);
    println (string ("                                   Дата - ", date, ", время - ", time):c:70);

    sql = " SELECT  /*+FIRST_ROWS(200) LEADING(pmpaym t outprop inprop pmrmprop wlmeslnk wlmes wlsess oproper oprstep)                       " +
          "                INDEX(pmpaym dpmpaym_dbt_idx15)                                                                                   " +
          "                INDEX(t dwlpm_dbt_idx1) USE_NL(pmpaym t outprop inprop pmrmprop wlmeslnk wlmes wlsess oproper oprstep)*/          " +
          "         pmrmprop.t_number DocNum,                                                                                                " +
          "         pmpaym.t_amount Amount,                                                                                                  " +
          "         pmpaym.t_payeraccount PayerAcc,                                                                                          " +
          "         SUBSTR (wlsess.t_filename, -(INSTR (reverse (wlsess.t_filename), '\\') - 1)) FileName,                                   " +
          "         CASE                                                                                                                     " +
          "            WHEN (REGEXP_SUBSTR (usr.xml_ask.EXTRACT ('/','xmlns=urn:cbr-ru:ed:v2.0').getstringval (),'ED[0-9]{3}') = 'ED206')    " +
          "            THEN                                                                                                                  " +
          "               'Сквитован'                                                                                                        " +
          "            WHEN (REGEXP_SUBSTR (usr.xml_ask.EXTRACT ('/','xmlns=urn:cbr-ru:ed:v2.0').getstringval (),'ED[0-9]{3}') = 'ED201')    " +
          "            THEN                                                                                                                  " +
          "               'Не сквитован. '                                                                                                   " +
          "               || TO_CHAR(EXTRACTVALUE (usr.xml_ask,'/ED201/Annotation','xmlns=urn:cbr-ru:ed:v2.0'))                              " +
          "               || ', '                                                                                                            " +
          "               || TO_CHAR(EXTRACTVALUE (usr.xml_ask,'/ED201/ErrorDiagnostic','xmlns=urn:cbr-ru:ed:v2.0'))                         " +
          "            ELSE                                                                                                                  " +
          "               'Не найден ED206/ED201 '                                                                                           " +
          "         END Result                                                                                                               " +
          "    FROM   dwlpm_dbt t, dpmprop_dbt inprop, doproper_dbt oproper, doprstep_dbt oprstep, dpmprop_dbt outprop, dpmpaym_dbt pmpaym,  " +
          "           dpmrmprop_dbt pmrmprop, dwlmes_dbt wlmes, dwlmesfrm_dbt wlmesfrm, dwlmeslnk_dbt wlmeslnk, dwlmesrls_dbt wlmesrls,      " +
          "           dwlsess_dbt wlsess, dwltransp_dbt wltransp, usr_uf_container_besp usr                                                  " +
          "   WHERE   (t.t_direct = CHR (0) AND t.t_propstatus = 32000                                                                       " + 
          "            AND t.t_valuedate BETWEEN TO_DATE ('01.01.0001', 'dd.mm.yyyy')                                                        " +
          "                                  AND  TO_DATE ('31.12.9999', 'dd.mm.yyyy')                                                       " +
          "            AND t.t_payfiid BETWEEN 0 AND 2147483647 AND t.t_corschem BETWEEN 0 AND 32767)                                        " +
          "            AND (    pmpaym.t_PaymentID = t.t_PaymentID AND pmrmprop.t_PaymentID = t.t_PaymentID                                  " +
          "                 AND pmpaym.t_PaymentID = usr.Paymentid(+) AND t.t_WlPmNum = 0                                                    " +
          "                 AND inprop.t_PaymentID = t.t_PaymentID AND inprop.t_IsSender = CHR (88)                                          " +
          "                 AND outprop.t_PaymentID = t.t_PaymentID AND outprop.t_IsSender = CHR (0)                                         " +
          "                 AND pmpaym.t_Department = 1 AND oproper.t_DocumentID = LPAD (pmpaym.t_PaymentID, 34, '0')                        " +
          "                 AND TO_NUMBER (oproper.t_DocumentID) = pmpaym.t_PaymentID                                                        " +
          "                 AND oproper.t_DocKind IN (16, 17, 27, 201, 202, 450, 320, 322, 311)                                              " +
          "                 AND oprstep.t_ID_Operation(+) = oproper.t_ID_Operation                                                           " +
          "                 AND oprstep.t_IsExecute(+) = CHR (87) AND wlmeslnk.t_ObjKind = 501                                               " +
          "                 AND wlmeslnk.t_ObjID = t.t_WlPmID                                                                                " +
          "                 AND wlmeslnk.t_MesID =                                                                                           " +
          "                       (SELECT   MIN (lnk.t_MesID)                                                                                " +
          "                          FROM   dwlmeslnk_dbt lnk, dwlmes_dbt mes1                                                               " +
          "                         WHERE       lnk.t_ObjKind = 501 AND lnk.t_ObjID = wlmeslnk.t_ObjID                                       " +
          "                                 AND mes1.t_MesID = lnk.t_MesID AND mes1.t_State <> 7)                                            " +
          "                 AND wlmes.t_MesID = wlmeslnk.t_MesID AND wlmes.t_SessionID = wlsess.t_SessionID(+)                               " +
          "                 AND wlmesrls.t_RlsFormID = wlmes.t_RlsFormID AND wlmesfrm.t_FormID = wlmesrls.t_FormID                           " +
          "                 AND wltransp.t_TpID = wlmesfrm.t_TpID AND pmpaym.t_PaymStatus = 32000                                            " + 
          "                 AND NOT (t.t_WlPmID <                                                                                            " +
          "                             ANY (SELECT   newwlpm.t_WlPmID                                                                       " +
          "                                    FROM   dwlpm_dbt newwlpm                                                                      " +
          "                                   WHERE   newwlpm.t_PaymentID = t.t_PaymentID AND newwlpm.t_Direct = t.t_Direct)))               " +
          "            AND (pmpaym.t_CloseDate BETWEEN TO_CHAR (:BegDate, 'dd.mm.yyyy')                                                      " +
          "                                        AND TO_CHAR (:EndDate, 'dd.mm.yyyy')                                                      " +
          "                 AND NOT EXISTS                                                                                                   " +
          "                       (SELECT   1                                                                                                " +
          "                          FROM   dpmlink_dbt link                                                                                 " +
          "                         WHERE   link.t_PurposePayment = pmpaym.t_PaymentID AND link.t_LinkKind = 9)                              " +
          "                 AND pmrmprop.t_PaymentKind = CHR (145))                                                                          " +
          " ORDER BY   t.t_direct, t.t_propstatus, t.t_valuedate, t.t_payfiid, t.t_corschem, t.t_wlpmid ";                                  
                                                                                                                                             
    cmd = RsdCommand(sql);                                                                                                                   
                                                                                                                                             
    cmd.AddParam("BegDate", RSDBP_IN, BegDate);
    cmd.AddParam("EndDate", RSDBP_IN, EndDate);
    cmd.execute();
    rs = RsdREcordset(cmd);
    
    while (rs and rs.movenext())
        if (substr(rs.value ("Result"),1,9) == "Сквитован")
            array_ask(array_ask.size()) = Ask(rs.value ("DocNum"), rs.value ("Amount"), rs.value ("PayerAcc"), rs.value ("FileName"), rs.value ("Result"));
        else
            array_no_ask(array_no_ask.size()) = Ask(rs.value ("DocNum"), rs.value ("Amount"), rs.value ("PayerAcc"), rs.value ("FileName"), rs.value ("Result"));
        end;

        i = i + 1;
    end;

    if (array_ask.size ())
        table_ask.addColumn ("№ п/п",                   10, AL_RIGHT);
        table_ask.addColumn ("Сумма п/п",               20, AL_RIGHT);
        table_ask.addColumn ("Счет плательщика",        20, AL_LEFT );
        table_ask.addColumn ("Имя файла",               20, AL_LEFT );
        table_ask.addColumn ("Результат обработки",     40, AL_LEFT );
        table_ask.printHead ("Сквитованные");

        i = 0;
        while (i < array_ask.size ())
            table_ask.printStringTransferByWord (array_ask[i].m_DocNum, 
                                                 array_ask[i].m_Amount, 
                                                 array_ask[i].m_PayerAcc, 
                                                 array_ask[i].m_FileName, 
                                                 array_ask[i].m_Result);
            i = i + 1;
        end;
        table_ask.printBottom ();     
        println ("Всего файлов: ", i); 
    else
        println ("Сквитованных платежей нет");
    end;

    println("\n");

    if (array_no_ask.size ())
        table_no_ask.addColumn ("№ п/п",                   10, AL_RIGHT);
        table_no_ask.addColumn ("Сумма п/п",               20, AL_RIGHT);
        table_no_ask.addColumn ("Счет плательщика",        20, AL_LEFT );
        table_no_ask.addColumn ("Имя файла",               20, AL_LEFT );
        table_no_ask.addColumn ("Результат обработки",     40, AL_LEFT );
        table_no_ask.printHead ("Не сквитованные");

        i = 0;
        while (i < array_no_ask.size ())
            table_no_ask.printStringTransferByWord (array_no_ask[i].m_DocNum, 
                                                 array_no_ask[i].m_Amount, 
                                                 array_no_ask[i].m_PayerAcc, 
                                                 array_no_ask[i].m_FileName, 
                                                 array_no_ask[i].m_Result);
            i = i + 1;
        end;
        table_no_ask.printBottom ();     
        println ("Всего файлов: ", i); 
    else
        println ("Не сквитованных платежей нет");
    end;

    SetOutput (NULL, true);
    ViewFile(reportName);
end; 
  
if (ВвестиПериодДат (dateBegin, dateEnd))
    Report(dateBegin, dateEnd);    
end;