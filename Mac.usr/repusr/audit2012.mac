// ------------------------------------------------------------------------------------------------
// @filename: audit2012.mac
// @author  : Zlenko M.P. 
// @desc    : Выгрузка проводок для аудиторов 
// @request : C-26374  Сделана панельная форма
// -------------------------------------------------------------------------------------------------

import rsd, RsbFormsInter, globals, rsbdataset, rsexts; 
import keycodes, likepy;

private macro day_sql(date_beg:string, 
                      date_end:string, 
                      spod    :bool , 
                      branch  :integer):string  
   var Sql = " SELECT  t_date_carry postdate,                                            "+
             "         t_date_carry effectdate,                                          "+
             "         TO_CHAR (t_date_carry, 'mm') period,                              "+
             "         acc1.t_nameaccount name1,                                         "+
             "         acc1.t_account account1,                                          "+
             "         acc2.t_nameaccount name2,                                         "+
             "         acc2.t_account account2,                                          "+
             "         prov.t_oper user1,                                                "+
             "         prs.t_name username,                                              "+
             "         'dk' drcr,                                                        "+
             "         prov.t_acctrnid unique1,                                          "+
             "         PROV.T_SUM_NATCUR postvalue,                                      "+
             "         prov.t_ground desc1                                               "+
             "    FROM   daccount_dbt acc1,                                              "+
             "           daccount_dbt acc2,                                              "+
             "           dacctrn_dbt prov,                                               "+
             "           dperson_dbt prs                                                 "+
             "   WHERE   t_date_carry BETWEEN TO_DATE ('" + date_beg + "', 'dd.mm.rrrr') "+
             "                           AND  TO_DATE ('" + date_end + "', 'dd.mm.rrrr') "+
             "           AND prov.t_account_payer = acc1.t_account(+)                    "+
             "           AND PROV.T_FIID_PAYER = acc1.t_code_currency(+)                 "+
             "           AND prov.t_account_receiver = acc2.t_account(+)                 "+
             "           AND prov.t_fiid_receiver = acc2.t_code_currency(+)              "+
             "           AND prov.t_chapter = acc1.t_chapter(+)                          "+
             "           AND prov.t_chapter = acc2.t_chapter(+)                          "+
             "           AND prov.t_oper = prs.t_oper(+)                                 "+
             "           AND ACC1.T_KIND_ACCOUNT != 'АП'                                 "+
             "           AND ACC2.T_KIND_ACCOUNT != 'АП'                                 ";    
      if (not SPOD   ) Sql = Sql + " AND instr(PROV.T_TYPEDOCUMENT,'З') = 0 " ;  end;
      if (branch != 1) Sql = Sql + " and PROV.T_BRANCH = " + int(branch) ; end;   
   return(sql);
end;

private macro ClearSymble(str, split_char)
    str = StrSubst(str, strfor(13), "" );
    str = StrSubst(str, strfor(10), "" );
    str = StrSubst(str, split_char, " ");
    return string(str);
end;

macro getCountRecord(sql)
   var rs = RsdRecordset("Select count(1) from (" + sql + ")");
   if (rs and rs.moveNext())
      return rs.value(0, null, v_integer);
   end;
   return 0;
end;

private macro Go(d1:string, d2:string, spod, split_char, branch)
   var main_sql = day_sql(d1, d2, spod, branch) ;
   var ds = trsbdataset(main_sql); // рубли и валюта
   var AuditTmpFile = String("..\\TxtFile\\Audit\\", strSubSt(d1, ".", "_"), "__", strSubSt(d2, ".", "_"), ".", UserNumber());
   var user1:string="",  username:string="", desc1:string, postdate:date, numb = 0;          
   initprogress(getCountRecord(main_sql), "Ожидайте, идет выгрузка проводок...");
   setOutput(AuditTmpFile, false );
   while (ds and ds.movenext())
      useprogress(Numb = (Numb + 1));
      user1 = ds.user1; 
      username = ds.username;
      desc1 = ds.desc1; 
      postdate = ds.postdate;

      desc1    = ClearSymble(desc1   , split_char);
      ds.name1 = ClearSymble(ds.name1, split_char);
      ds.name2 = ClearSymble(ds.name2, split_char);


      if (valtype(username) == V_UNDEF) 
         username = "Панькова Е.В."; 
         user1 = "433";
      end; // для неверных оперов поставили Панькову

      if (valtype(postdate) == V_UNDEF) postdate = ds.effectdate;  end; //если проводка не имела платежа, то дату дублируем с проводки
      println(string(date(postdate):f) + split_char + string(date(ds.effectdate):f) + split_char + ds.period + split_char + ds.name1 + split_char + ds.account1 + split_char + user1 + split_char + username + split_char + "RUR" + split_char + "D" + split_char + ds.unique1 + split_char + ds.postvalue + split_char + desc1) ;     
      println(string(date(postdate):f) + split_char + string(date(ds.effectdate):f) + split_char + ds.period + split_char + ds.name2 + split_char + ds.account2 + split_char + user1 + split_char + username + split_char + "RUR" + split_char + "K" + split_char + ds.unique1 + split_char + ds.postvalue + split_char + desc1);
   end;
   setOutput(null, true);
   remprogress;
   return AuditTmpFile;
end;     


class (TRsbPanel) MainFrame    
   private var Split_Code = 126;      

   class ( TRsbEditField ) EditField( type: integer, x: integer, y: integer, width: integer, height: integer, bindVal, active: bool )  
      var bindString = bindVal;       
      InitTRsbEditField( type );
      SetPosition( x, y );
      SetSize( width, height );        
      if( active == false ) editable = focusable = false; end;                                               
      if  ( type == 7 ) bindValue( this, "bindString", 100 );
      elif( bindVal != null ) value = bindVal;
      end;    
   end;

   class (TRSBCheckBox) CheckBoxField(x: integer, y: integer, active: bool)
      initTRSBCheckBox();
      SetPosition( x, y );
      checked = active;
   end;
     // получить имя подразделения по его коду
   macro DepartmentName ( Code: string )    
      var rs,cmd = RsdCommand( " SELECT   PARTY.T_SHORTNAME "
                               "   FROM   DPARTY_DBT PARTY, DDP_DEP_DBT DEPARTMENT "
                               "  WHERE   DEPARTMENT.T_NAME = :NAME AND PARTY.T_PARTYID = DEPARTMENT.T_PARTYID " );
      cmd.addparam( "Code", RSDBP_IN, Code );
      rs = RsdRecordSet( cmd );
      if( rs.movenext ) return rs.value( "T_SHORTNAME" );
      else              return "";
      end;       
   End;

   macro DepartmentNumber ( Name: string )
      var rs,cmd = RsdCommand( " SELECT   T_CODE "
                               "   FROM   DDP_DEP_DBT "
                               "  WHERE   T_NAME = :NAME " );
      cmd.addparam( "Name", RSDBP_IN, Name );
      rs = RsdRecordSet( cmd );
      if( rs.movenext ) return rs.value( "T_CODE" );
      else              return "";
      end;       
   End;
   private var yyyy;
   dateSplit({curdate}, null, null, yyyy);      

   var eDateBegin = EditField( 9 , 15, 1 , 10, 1, date(String("01.01.", yyyy)), true ); 
   var eDateEnd   = EditField( 9 , 15, 2 , 10, 1, {curdate}, true ); 
   var eDepCode   = EditField( 7 , 15, 3 , 4 , 1, "000", true); 
   var eDepName   = EditField( 7 , 20, 3 , 25, 1, DepartmentName("000"), false ); 
   var eDeterCode = EditField( 0 , 15, 5 , 3,  1, split_Code, true ); 
   var eDeterChar = EditField( 7 , 26, 5 , 4,  1, strFor(Split_Code), false); 
   var eSPOD      = CheckBoxField( 15, 4, true);
   var eSendTerm  = CheckBoxField( 22, 6, true);
   var eDirName   = EditField( 7 , 17, 7 , 28, 1, "", true); 
   var eDepCodeInt= 1;
  
   InitTRsbPanel();
   SetSize( 46, 8 );    
   setCaption("   Параметры для выгрузки проводок");
   setStatus("~F2~ Запуск процедуры ~F3~ Выбор из справочника");
   addLabel(TRsbLabel( 2, 1 , "Начало периода"));
   addLabel(TRsbLabel( 2, 2 , "Конец  периода"));
   addLabel(TRsbLabel( 2, 3 , "Подразделение" ));
   addLabel(TRsbLabel( 2, 4 , "Выгружать СПОД"));
   addLabel(TRsbLabel( 2, 5 , "Разделитель код" ));
   addLabel(TRsbLabel( 20,5 , "символ" ));
   addLabel(TRsbLabel( 2, 6 , "Переслать файл на терминал" ));
   addLabel(TRsbLabel( 2 ,7 , "Каталог для выгрузки" ));


   eDepCode.addEventHandler  ( RSB_EV_KEY_PRESSED , R2M( this, "oneDepFieldKeyPressed"       ));   
   eDeterCode.addEventHandler( RSB_EV_REMOVE_FOCUS, R2M( this, "oneDeterCodeFieldKeyPressed" ));
   eSendTerm.addEventHandler ( RSB_EV_KEY_PRESSED,  R2M( this, "oneSendTermFieldKeyPressed"  ));
   eDirName.addEventHandler  ( RSB_EV_KEY_PRESSED,  R2M( this, "oneDirNameFieldKeyPressed"   ));
   addEventHandler           ( RSB_EV_KEY_PRESSED , R2M( this, "onFramedKeyPressed"          ));   

   macro onFramedKeyPressed( RsbEvent:object )
      var tmpFileDirAudit = "..\\TxtFile\\audit\\", tmpFileAudit = "";
      if ( rsbEvent.KeyCode == KEY_F2 )
         if(eSendTerm.checked and (eDirName.value == ""))            
            msgBox("Не выбран каталог для пересылки файла");
         else
            if (not existfile(tmpFileDirAudit))
               if (not makeDir(tmpFileDirAudit))
                   msgBox("Не удалось создать временную папку Audit на СП");
                   return ;
               end;
            end;
            tmpFileAudit = Go(eDateBegin.value, eDateEnd.value, eSPOD.checked, strFor(split_code), eDepCodeInt);
            var tmpFileAuditName = "", tmpFileAuditExt = ""; 
            SplitFile(tmpFileAudit, tmpFileAuditName, tmpFileAuditExt);
            if (eSendTerm.checked)
               if(copyFile(tmpFileAudit, String("$", eDirName.value, "\\", tmpFileAuditName, tmpFileAuditExt)))
                  RemoveFile(tmpFileAudit);
                  msgBox("Файл успешно перемещен в каталог " + eDirName.value);
               else
                  msgBox("Не удалось выполнить копирование файла на терминал. | Вы можете забрать его с СП с каталога " + tmpFileDirAudit);
               end;
            else
               msgBox("Выгрузка проводок завернеша успешно. | Файл находится на СП в каталоге " + tmpFileDirAudit);
            end;
         end;
      end;
   end;

   macro oneSendTermFieldKeyPressed( RsbEvent:object )
      eDirname.editable = eDirname.focusable = eSendTerm.checked;              
      redraw();
   end;

   macro oneDirNameFieldKeyPressed( RsbEvent:object )
      if ( rsbEvent.KeyCode == KEY_F3 )
          selectfolder(eDirName.value, null, "Выберите каталог для пересылки файла", true);
      end;
   end;

   macro oneDepFieldKeyPressed( RsbEvent:object )
      record Department( dp_dep );
         if  ( rsbEvent.KeyCode == KEY_F3 )
            if(ListDepartment( Department ) )
               eDepCode.value = Department.Name;
               eDepCodeInt    = Department.Code;
               eDepName.value = DepartmentName( Department.Name );
            end;    
         elif((rsbEvent.KeyCode == KEY_DOWN  ) or
              (rsbEvent.KeyCode == KEY_UP    ) or
              (rsbEvent.KeyCode == KEY_TAB   ) or
              (rsbEvent.KeyCode == KEY_ENTER ) )              
              eDepName.value = DepartmentName(eDepCode.value);
              eDepCodeInt    = DepartmentNumber(eDepCode.value);
         end;
   end;

   macro oneDeterCodeFieldKeyPressed( RsbEvent:object )            
      eDeterChar.value = strFor(eDeterCode.value);
      Split_Code = eDeterCode.value
   end;

   addControl(eDateBegin);
   addControl(eDateEnd);
   addControl(eDepCode);
   addControl(eDepName);
   addControl(eSPOD);
   addControl(eDeterCode);
   addControl(eDeterChar);
   addControl(eSendTerm);
   addControl(eDirName);

end;


var panel = MainFrame;
    panel.setPosition(30, 10);
    panel.run;
    exit(1);







