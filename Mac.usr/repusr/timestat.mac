/**************************************************************/
/* Библиотека функций для замера времени выполнения операций  */
/* Коркин И.Н 11.06.2010                                      */
/**************************************************************/

import rsbdataset, globals;

/* Сохранение времени начала операции */

macro SaveBeginTime (param, amount)
        var query, rs, truncate, values;
        truncate = "TRUNCATE TABLE USR_TIMESTAT_TMP; " ;        
   rs = rsdcommand(truncate);
   rs.execute;
        query =  "insert into USR_TIMESTAT_TMP (t_timebegin";
        values = "values(systimestamp";
        if (ValType(amount)!=V_UNDEF)
                query = query + ", t_amount";
                values = values + ", " + amount;
        end;
        if (ValType(param)!=V_UNDEF)
                query = query + ", t_param";
                values = values + ", '" + param + "'";
        end;
        query = query + ") " + values + ");";
   rs = rsdcommand(query);
   if(rs)
      rs.execute;
      return true;
   else 
      return false;
   end;      
  
end;

// KS 03.06.2011 Вариант без темповой таблицы
macro KSaveBeginTime (param, amount)

    var id,values;

    var query = "SELECT USR_TIMESTAT_DBT_SEQ.nextval FROM dual";

    var cmd = RsdCommand(query);

    var rs = RSDRecordset(cmd);

    if(rs.moveNext())
      if ( ValType(rs.value(0)) != 26 )
        id = rs.value(0);
      else
        return 0;
      end;
    end;

    query =  "insert into usr_timestat_dbt (t_autokey, t_operationid, t_timebegin, t_oper";
    values = "values (" + id + ", 0, systimestamp, " + {oper};
    if (ValType(amount)!=V_UNDEF)
      query = query + ", t_amount";
      values = values + ", " + amount;
    end;
    if (ValType(param)!=V_UNDEF)
      query = query + ", t_param";
      values = values + ", '" + param + "'";
    end;
    query = query + ") " + values + ");";
    rs = rsdcommand(query);
    if(rs)
      rs.execute;
      return id;
    else 
      return 0;
    end;      
  
end;


/* Сохранение количества */

macro SaveAmount (amount)
        var query, rs;
        if (ValType(amount)!=V_UNDEF)
        query = "update USR_TIMESTAT_TMP set t_amount = " + amount;
      rs = rsdcommand(query);
      if(rs)
         rs.execute;
         return true;
      else 
         return false;
      end;
   else 
                return false;
   end;
 end;

// KS 03.06.2011 Вариант без темповой таблицы
macro KSaveAmount (amount,id)
   var query, rs;

   if ((ValType(amount)!=V_UNDEF)and(ValType(id)!=V_UNDEF))
     query = "update USR_TIMESTAT_DBT set t_amount = " + amount + " where t_autokey=" + id;
     rs = rsdcommand(query);
     if(rs)
       rs.execute;
       return true;
     else 
       return false;
     end;
   else 
     return false;
   end;

end;


/* Сохранение времени окончания операции */

macro SaveEndTime (operation)
   var query, rs, oper;
   if (ValType(operation)!=V_UNDEF)
                oper = {oper};
      query = " DECLARE "
      + "\n" + "    tmp USR_TIMESTAT_TMP%ROWTYPE; "
      + "\n" + "BEGIN "
      + "\n" + "   SELECT * INTO tmp FROM USR_TIMESTAT_TMP where rownum = 1; "
      + "\n" + "   INSERT INTO USR_TIMESTAT_DBT (T_AUTOKEY, "
      + "\n" + "                                 T_OPERATIONID, "
      + "\n" + "                                 T_PARAM, "
      + "\n" + "                                 T_AMOUNT, "
      + "\n" + "                                 T_TIMEBEGIN, "
      + "\n" + "                                 T_TIMEEND, T_OPER) "
      + "\n" + "     VALUES   (NULL, "
      + "\n" + "               " + operation + ", "
      + "\n" + "               tmp.t_param, "
      + "\n" + "               tmp.t_amount, "
      + "\n" + "               tmp.t_timebegin, "
      + "\n" + "               systimestamp,"  
      + "\n" + "               " + oper + "); "
      + "\n" + "END;";
      rs = rsdcommand(query);
      if(rs)
         rs.execute;
         return true;
      else 
         return false;
      end;
   else
                return false;           
   end; 
   
end;

// KS 03.06.2011 Вариант без темповой таблицы
macro KSaveEndTime (operation,id)
   var query, rs;

   if ((ValType(operation)!=V_UNDEF)and(ValType(id)!=V_UNDEF))

     query = "update USR_TIMESTAT_DBT set t_operationid = " + operation + ","+
                                         "t_timeend     = systimestamp where t_autokey=" + id;
     rs = rsdcommand(query);
     if(rs)
       rs.execute;
       return true;
     else 
       return false;
     end;

   else 
     return false;
   end;
   
end;