/****************************************************/
/* ОСВ в эксель                                     */
/* Коркин И.Н.                                      */
/* 11.10.2010                                       */
/* Реализовано по заявке I-060068.                  */
/* Выпуск ОСВ в эксель за период по главе           */
/* только л/с без подитогов,                        */
/* требовалось для проверки ЦБ                      */
/****************************************************/

// 12.02.2014 R-326546-3 DPN - Адаптация под 31-й патч
// 24.09.2014 R-455476-3 DPN - Добавил проверку на перевод каретки
// 01.10.2014 R-459679-2 dpn - Выводились все валюты, убрал рубли 
// 03.10.2014 I-00519729-2 dpn - Попадали не все счета. Сделал название конечного файла непредсказуемым
// 10.11.2014 R-488442-2 DPN - Изменилась пакетная функция нахождения остатков\оборотов

import rsexts;
import RsbDataSet, globals, календарь;
import "KeyCodes.mac";
import "fastexcel.mac","fg_Life_parm.mac"; 
var 	exst, i, i_old, page, flag, ex, ob, out, f, countlist,
		output="osv.xlsx",
		fulloutput, obBook, obsheet;
private var DecimalSeparator;

var begindate, enddate, chapter, che, acc_mask;

var account_bal,restinA_bal,restinP_bal,debet_bal,kredit_bal,restoutA_bal,restoutP_bal;

var SheetBalance= false;

array    Text;
array    Buttons;
File RepFile() txt;
var dat,mon,year;
datesplit({curdate},dat,mon,year);
var RepFileName = gettxtfilename(string(dat,mon,year,".csv"));

private Var fgBank = fg_life_subject({OurBank});         

che = 3;
countlist = 1;
var _i=0,bal2="",bal="";    

var Fulloutputlbr, outlbr, outputlbr="osvinexcel.lbr";                    
GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,outlbr);
Fulloutputlbr = FindPath(outputlbr, outlbr);
if (not Fulloutputlbr)
	msgbox("Не найдена LBR");
	exit();
end;

var dlg = TRecHandler("dates", fulloutputlbr, TRUE); 

 /*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key)
   array gname;
   gname(0)="А 'Балансовые счета'";
   gname(1)="В 'Внебалансовые счета'";
   gname(2)="Г 'Срочные сделки'";
   gname(3)="Все главы ";
   var g;
   var const_mess = " ~F3~ Выбор значения из списка ~F2~ Продолжить ~ESC~ Выход ",
   SQLQuery, SQLQuery1, rsrec, cmd1;
   /*Первоначальная инициализация полей*/
   if(cmd == DLG_INIT)
      dlg.rec.begindate = {curdate};
      BeginDate  = dlg.rec.beginDate;
      dlg.rec.enddate = {curdate};
      EndDate  = dlg.rec.EndDate;
      dlg.rec.chapter = "А";
      chapter = 1;
      dlg.rec.chaptername = "Балансовые счета";
      acc_mask = "";
      dlg.rec.account = "";
      SheetBalance = false;
      UpdateFields(dlg); 
   end;
   /*Установка подсказок в строке состояния*/
   if (cmd==DLG_SETFOCUS)
      if(FldName(dlg,id) == "Account")
        message("Введите маску счета. Например, '40702*,40802*' ~F2~ Продолжить ~ESC~ Выход");
      else
        message(const_mess);
      end;
   end;
   if (cmd == DLG_REMFOCUS)
      /*Проверка корректности дат отчета*/
      UpdateFields(dlg); 
   end;
   if (cmd == DLG_KEY)
      /*Выход из диалогового окна формирования отчета*/
      if (KEY == KEY_ESC)
         return CM_CANCEL;
      /*Выбор данных из списка*/
      elif ( KEY == KEY_F3)
         if (FldName(dlg,id) == "begindate")
            dlg.rec.begindate = GetDateByCalendar ({curDate});
         end;
         if (FldName(dlg,id) == "enddate")
            dlg.rec.enddate = GetDateByCalendar ({curDate});
         end;
         if (FldName(dlg,id) == "chapter")
            dlg.rec.chapter = 1;
            dlg.rec.chaptername = 1;
            g=menu(gname,"Глава счетов");
            if (g==0)  
              dlg.rec.chapter = "А";
              dlg.rec.chaptername = "Балансовые счета";
              chapter = 1;
            elif (g==1)
              dlg.rec.chapter = "В";
              dlg.rec.chaptername = "Внебалансовые счета";
              chapter = 3;
            elif (g==2)
              dlg.rec.chapter= "Г";
              dlg.rec.chaptername = "Срочные сделки";
              chapter = 4;
            elif (g==3)
              dlg.rec.chapter= " ";
              dlg.rec.chaptername = "Все главы ";
              chapter = 0;
            else 
              message(" ~F3~ Список глав счетов "+const_mess);
              UpdateFields(dlg);
            end;
              message(" ~F3~ Список глав счетов "+const_mess);
              UpdateFields(dlg);
         end;
      elif (( KEY == KEY_F2 ) or (KEY == KEY_ENTER))         //Проверки при вводе
         if ( dlg.rec.begindate > {curDate} )
            MsgBox("Дата начала периода должна быть не больше текущей даты");
            return CM_IGNORE;
         end;
         if ( dlg.rec.begindate > dlg.rec.enddate )
            MsgBox("Дата начала периода должна быть не больше конечной даты");
            return CM_IGNORE;
         end;
         if ( dlg.rec.chapter == "" )
            MsgBox("Выберите главу");
            return CM_IGNORE;
         end;
         BeginDate  = dlg.rec.beginDate;
         endDate  = dlg.rec.endDate;
         acc_mask = dlg.rec.account;
         Return CM_SAVE;
      elif (KEY==KEY_SPACE)
         if(FldName(dlg,id)=="button")
            if (dlg.rec.button == "")
                dlg.rec.button = "X";
                SheetBalance = true;
            else
                dlg.rec.button = "";
                SheetBalance = false;
            end;
            UpdateFields(dlg);
         end;
      end;
   else
      Return CM_IGNORE;
   end;
END;


//12.02.2014 R-326546-3 DPN - Адаптация под 31-й патч (Переписал запрос)
macro calculate(chapter, begindate, enddate)
	var query, data;
   query = "   SELECT t_account, restinA, restinP, debet, kredit, restoutA, restoutP, t_nameaccount "
   + "\n" + "    FROM   (SELECT ac.t_account, "
   + "\n" + "                     CASE "
   + "\n" + "                        WHEN ac.T_KIND_ACCOUNT = 'А' "
   + "\n" + "                        THEN "
   + "\n" + "                         /*  ABS */-(rsb_account.restac ( "
   + "\n" + "                                  ac.t_account, ac.t_code_currency, "
   + "\n" + "                                  TO_DATE ('" + begindate + "', 'dd.mm.yyyy') - 1, "
   + "\n" + "                                  ac.t_chapter, "
   + "\n" + "                                  rest.T_REST, AC.T_CODE_CURRENCY "  //10.11.2014 R-488442-2 DPN
   + "\n" + "                               )) "
   + "\n" + "                        ELSE "
   + "\n" + "                           0 "
   + "\n" + "                     END "
   + "\n" + "                        AS restinA, "
   + "\n" + "                     CASE "
   + "\n" + "                        WHEN ac.T_KIND_ACCOUNT = 'А' "
   + "\n" + "                        THEN "
   + "\n" + "                           0 "
   + "\n" + "                        ELSE "
   + "\n" + "                       /*  ABS */(rsb_account.restac ( "
   + "\n" + "                                  ac.t_account, ac.t_code_currency, "
   + "\n" + "                                  TO_DATE ('" + begindate + "', 'dd.mm.yyyy') - 1, "
   + "\n" + "                                  ac.t_chapter, "
   + "\n" + "                                  rest.T_REST, AC.T_CODE_CURRENCY "   //10.11.2014 R-488442-2 DPN
   + "\n" + "                               )) "
   + "\n" + "                     END "
   + "\n" + "                        AS restinP, "
   + "\n" + "                     ABS (rsb_account.debetac (ac.t_account, ac.t_chapter, ac.t_code_currency, " //10.11.2014 R-488442-2 DPN
   + "\n" + "                                            TO_DATE ('" + begindate + "', 'dd.mm.yyyy'), "
   + "\n" + "                                            TO_DATE ('" + enddate + "', 'dd.mm.yyyy'), ac.t_code_currency) "  //10.11.2014 R-488442-2 DPN
   + "\n" + "                     ) "
   + "\n" + "                        debet, "
   + "\n" + "                     ABS (rsb_account.kreditac (ac.t_account, ac.t_chapter, ac.t_code_currency, " //10.11.2014 R-488442-2 DPN
   + "\n" + "                                             TO_DATE ('" + begindate + "', 'dd.mm.yyyy'), "
   + "\n" + "                                             TO_DATE ('" + enddate + "', 'dd.mm.yyyy'), ac.t_code_currency) " //10.11.2014 R-488442-2 DPN
   + "\n" + "                     ) "
   + "\n" + "                        kredit, "
   + "\n" + "                     CASE "
   + "\n" + "                        WHEN T_KIND_ACCOUNT = 'А' "
   + "\n" + "                        THEN "
   + "\n" + "                         /*  ABS */-(rsb_account.restac ( "
   + "\n" + "                                  ac.t_account, ac.t_code_currency, "
   + "\n" + "                                  TO_DATE ('" + enddate + "', 'dd.mm.yyyy'), "
   + "\n" + "                                  ac.t_chapter, "
   + "\n" + "                                  rest.T_REST, AC.T_CODE_CURRENCY "  //10.11.2014 R-488442-2 DPN
   + "\n" + "                               )) "
   + "\n" + "                        ELSE "
   + "\n" + "                           0 "
   + "\n" + "                     END "
   + "\n" + "                        AS restoutA, "
   + "\n" + "                     CASE "
   + "\n" + "                        WHEN ac.T_KIND_ACCOUNT = 'А' "
   + "\n" + "                        THEN "
   + "\n" + "                           0 "
   + "\n" + "                        ELSE "
   + "\n" + "                         /*  ABS */(rsb_account.restac ( "
   + "\n" + "                                  ac.t_account, ac.t_code_currency, "
   + "\n" + "                                  TO_DATE ('" + enddate + "', 'dd.mm.yyyy'), "
   + "\n" + "                                  ac.t_chapter, "
   + "\n" + "                                  rest.T_REST, AC.T_CODE_CURRENCY)) "  //10.11.2014 R-488442-2 DPN
   + "\n" + "                     END "
   + "\n" + "                        AS restoutP, "
   + "\n" + "                     ac.T_NAMEACCOUNT, "
   + "\n" + "                     ac.t_sort "
   + "\n" + "              FROM   daccount_dbt ac, drestdate_dbt rest"
   + "\n" + "             WHERE  (1=1)  "
   + "\n" + "             AND ac.t_accountid = rest.t_accountid  ";
/*SDA - вывод по всем главам  */
if (chapter)
   query = query
   + "\n" + "             AND  ac.t_chapter = " + chapter;
end;
// KS 28.01.2011 Фильтр по лицевому счету
if (acc_mask!="")
   query = query
   + "\n" + "               AND   (" + ConvertMaskToSQLFormat(acc_mask, "ac.t_Account") +")";
end;
   query = query
   + "\n" + "                     AND (ac.t_open_date BETWEEN TO_DATE ('" + begindate + "', 'dd.mm.yyyy') "
   + "\n" + "                                          AND  TO_DATE ('" + enddate + "', 'dd.mm.yyyy') "
   + "\n" + "                          OR ac.t_close_date BETWEEN TO_DATE ('" + begindate + "', 'dd.mm.yyyy') "
   + "\n" + "                                              AND  TO_DATE ('" + enddate + "', 'dd.mm.yyyy') "
   + "\n" + "                          OR (ac.t_open_date <= TO_DATE ('" + begindate + "', 'dd.mm.yyyy') "
   + "\n" + "                              AND (ac.t_close_date >= "
   + "\n" + "                                      TO_DATE ('" + enddate + "', 'dd.mm.yyyy') "
   + "\n" + "                                   OR ac.t_close_date = "
   + "\n" + "                                        TO_DATE ('01.01.0001', 'dd.mm.yyyy'))))"
   + "\n" + "              AND (rest.t_restdate = ( SELECT MAX (r.t_restdate) "
   + "\n" + "                                       FROM drestdate_dbt r RIGHT JOIN daccount_dbt acc "
   + "\n" + "                                                           ON acc.t_accountid = r.t_accountid "
   + "\n" + "                                       WHERE ac.t_accountid = acc.t_accountid "
  // + "\n" + "                                       AND rest.t_restcurrency = 0 "
   + "\n" + "                                       AND r.t_restdate <= TO_DATE ('" + enddate + "', 'dd.mm.yyyy') "
   + "\n" + "                                       and r.T_RESTCURRENCY !=0      ) " //03.10.2014 I-00519729-2 dpn
   //+ "\n" + "                  ) "
   + "\n" + "              OR rest.t_restdate IS NULL)  and AC.T_CODE_CURRENCY !=0 and REST.T_RESTCURRENCY !=0)" 
  // + "\n" + "              AND (rest.t_restcurrency = 0 OR rest.t_restcurrency IS NULL)) "   //01.10.2014 R-459679-2 dpn 
   + "\n" + "   WHERE   debet != 0 OR kredit != 0 OR restinA != 0 OR restinP != 0 OR restoutA != 0 OR restoutP != 0 ";
   if (SheetBalance)
     query = query
     + "\n" + "ORDER BY  SUBSTR (t_account, 1, 8) desc, t_sort"; 
   else
     query = query
     + "\n" + "ORDER BY  SUBSTR (t_account, 1, 8)  asc, t_sort";
   end;
   message("Расчет...");
   data = TRsbDataset(query);
   return data;
end;    
    

macro openExcel(output)
	var out;

   ob = CFastExcel(False);
	GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\USERTEMPLSDIR",2,out);
   Fulloutput = FindPath(output, out);
  if (not Fulloutput)
      msgbox("Не найден шаблон");
      exit();
   end;
  

     obBook = ob.Open(Fulloutput);
     obSheet = obBook.ActiveSheet(); 	

end;

macro writeHead()
	obSheet.Cells(1,1).Value= "ОСВ в валюте";	
	obSheet.Cells(2,1).Value= "За период с " + begindate + " по " + enddate;
	obSheet.Cells(3,1).Value= "По главе " + chapter;
	obSheet.Cells(che + 1,1).Value= "Счет";	
	obSheet.Cells(che + 1,2).Value= "Актив";
	obSheet.Cells(che + 1,3).Value= "Пассив";
	obSheet.Cells(che + 1,4).Value= "Дебет";
	obSheet.Cells(che + 1,5).Value= "Кредит";
	obSheet.Cells(che + 1,6).Value= "Актив";
	obSheet.Cells(che + 1,7).Value= "Пассив";
	obSheet.Cells(che + 1,8).Value= "Наименование";
   obSheet.Range("A"+(che+1)+":h"+(che+1)).Borders.Weight=2;
   obSheet.Range("A"+(che+1)+":h"+(che+1)).interior.color=4035000;
end;

macro writeSum(ln,st,fin)

        obBook.ActiveSheet.Range("A"+(ln)).value="Итого по "+bal2;
        obBook.ActiveSheet.Range("B"+(ln)).value="=СУММ(B"+(st)+":B"+(fin)+")";
        obBook.ActiveSheet.Range("C"+(ln)).value="=СУММ(C"+(st)+":C"+(fin)+")";
        obBook.ActiveSheet.Range("D"+(ln)).value="=СУММ(D"+(st)+":D"+(fin)+")";
        obBook.ActiveSheet.Range("E"+(ln)).value="=СУММ(E"+(st)+":E"+(fin)+")";
        obBook.ActiveSheet.Range("F"+(ln)).value="=СУММ(F"+(st)+":F"+(fin)+")";
        obBook.ActiveSheet.Range("G"+(ln)).value="=СУММ(G"+(st)+":G"+(fin)+")";

end;

macro AddNewSheet(bal,i);
   obBook.sheets("пустышка").select;
   obBook.sheets("пустышка").Copy(obBook.sheets(1));
   obBook.sheets(1).name = bal;
   obBook.sheets(bal).select;
end;

macro writeBody(data)
	var account,restinA,restinP,debet,kredit,restoutA,restoutP,nameaccount;
	var first= true;
	i = che + 2;
        i_old = i;
	InitProgress(-1, "", "ОСВ в эксель. Глава " + chapter + ". С " + begindate + " по " + enddate);
   while (data.movenext)
      bal = substr(string(data.value(0)),1,5);
      if (bal2!=bal) // Новый балансовый
        if (SheetBalance)
         ob.Show; // Показываем
         if (bal2!="") 
           obBook.ActiveSheet.Range("A4:h"+(i)).Borders.Weight=2;
           writeSum(i,i_old,i-1);
         end;              
         AddNewSheet(bal,i); // Открываем новый лист
         bal2=bal;
         i = che + 2;
        else
         if (bal2!="") 
          writeSum(i,i_old-1,i-1);
          i = i + 1;
         end;
         i_old = i + 1;
         bal2=bal;
        end;
     end;

     account=string(data.value("t_account"));
     restinA=round(money(data.value("restinA")),2);
     restinP= round(money(data.value("restinP")),2);
     debet= round(money(data.value("debet")),2);
     kredit= round(money(data.value("kredit")),2);
     restoutA= round(money(data.value("restoutA")),2);
     restoutP= round(money(data.value("restoutP")),2);
     nameaccount= strsubst(strsubst(data.value("t_nameaccount"),strfor(13),""),strfor(10),""); // 24.09.2014 R-455476-3 DPN


     InsertElement(ob,"A"+i,account);
     InsertElement(ob,"B"+i,restinA);
     InsertElement(ob,"C"+i,restinP);
     InsertElement(ob,"D"+i,debet);
     InsertElement(ob,"E"+i,kredit);
     InsertElement(ob,"F"+i,restoutA);
     InsertElement(ob,"G"+i,restoutP);
     InsertElement(ob,"H"+i,nameaccount);

     i = i + 1;
     UseProgress(_i=_i + 1);

   end;

   RemProgress();
/*   obBook.sheets(bal2).Range("A1:h"+(i-1)).Borders.Weight=2;*/
end;

macro osvInExcel()
	var data;
	data = calculate(chapter, begindate, enddate);
	openExcel(output);
	writeHead();
        obBook.sheets(1).select;
        obBook.sheets(1).name = "пустышка";
        if (not SheetBalance) 
          AddNewSheet("Все",i);
        end;
	writeBody(data);
        ob.Show;
	obBook.ActiveSheet.Range("A4:h"+(i)).Borders.Weight=2;

        if (not SheetBalance)
          i_old=i_old-1;
        end;
        writeSum(i,i_old,i-1);

        obBook.Sheets("пустышка").Visible = False;
end;




macro PintBody(data)
	var account,restinA,restinP,debet,kredit,restoutA,restoutP,nameaccount;
	var acc1,cur,;
	var first= true;
	i = che + 2;
        i_old = i;
	InitProgress(-1, "", "ОСВ в CSV. Глава " + chapter + ". С " + begindate + " по " + enddate);
        UseProgress(_i);
   while (data.movenext)    
     account=string("=",strfor(34)+data.value("t_account")+strfor(34)); 
     restinA=strsubst(string(round(money(data.value("restinA")),2)),strfor(46),DecimalSeparator);
     restinP=strsubst(string(round(money(data.value("restinP")),2)),strfor(46),DecimalSeparator);
     debet=strsubst(string(round(money(data.value("debet")),2)),strfor(46),DecimalSeparator);
     kredit=strsubst(string(round(money(data.value("kredit")),2)),strfor(46),DecimalSeparator);
     restoutA=strsubst(string(round(money(data.value("restoutA")),2)),strfor(46),DecimalSeparator);
     restoutA=strsubst(string(round(money(data.value("restoutA")),2)),strfor(46),DecimalSeparator);
     restoutP=strsubst(string(round(money(data.value("restoutP")),2)),strfor(46),DecimalSeparator);

     nameaccount = strsubst( toANSI(string(data.value("t_nameaccount")),true),";"," ");
     cur = substr(data.value("t_account"),6,3);
     acc1 = substr(data.value("t_account"),1,5);
if ((fgBank.is_GO) ) //03.10.2014 I-00519729-2 dpn
      /*"Date	Acc1	Cur	Acc2	 Debit 	 Credit 	 Assets 	 Liabilities 	Client	Title_Acc2"*/
Println(enddate,";",acc1,";",cur,";",account,";",debet,";",kredit,";",restoutA,";",restoutP,";",nameaccount);
else
Println(account,";",restinA,";",restinP,";",debet,";",kredit,";",restoutA,";",restoutP,";",nameaccount);
end;
     i = i + 1;
     UseProgress(_i=_i + 1);
   end;

   RemProgress();
   return(_i);
end;


macro osvInCSV();
private var str="",i=0,r =0, column="H";
SetOutput (RepFileName,false);
   nmob = CreateObject ("rsax","TRsAxServer",string("RsAxServer", UserNumber()),IsStandAlone());
   nmtlg = nmob.CreateComObject ("Excel.Application",true);
   DecimalSeparator = nmtlg.DecimalSeparator;
   if ((fgBank.is_GO)) //03.10.2014 I-00519729-2 dpn 
     Println("─рЄр",";","┴\\╤",";","┬ры",";","╤ўхЄ",";","─хсхЄ",";","╩ЁхфшЄ",";","└ъЄшт",";","╧рёёшт",";","═ршьхэютрэшх ёўхЄр"); //03.10.2014 I-00519729-2 dpn
     column="J";
   else
     str = string("╤ўхЄ",";","└ъЄшт",";","╧рёёшт",";","─хсхЄ",";","╩ЁхфшЄ",";","└ъЄшт",";","╧рёёшт",";","═ршьхэютрэшх"); //03.10.2014 I-00519729-2 dpn
     Println(ToAnsi(str,true));
   end;
 var data = calculate(chapter, begindate, enddate);
     i = PintBody(data);
 SetOutput (Null,True);
 var fname = "$C:\\rspar\\OSV" + abs(random(1000000)) + {oper} + abs(random(1000000)) + ".csv"; //03.10.2014 I-00519729-2 dpn
 if (CopyFile(RepFileName,fname)) //03.10.2014 I-00519729-2 dpn
     DelFile(RepFileName);
 end;
   fname = substr(fname,2); //03.10.2014 I-00519729-2 dpn
   nmtlg.Workbooks.open(fname); //03.10.2014 I-00519729-2 dpn
   nmtlg.ActiveSheet.Columns("A:AA").EntireColumn.AutoFit;

   nmtlg.ActiveSheet.Rows("1:1").Insert; i=i+1;
   nmtlg.ActiveSheet.Cells(1,1).Value= "ОСВ в валюте";	
   nmtlg.ActiveSheet.Rows("2:2").Insert; i=i+1;
   nmtlg.Cells(2,1).Value= "За период с " + begindate + " по " + enddate;
   nmtlg.ActiveSheet.Rows("3:3").Insert; i=i+1;
   nmtlg.Cells(3,1).Value= "По главе " + chapter;
  
   nmtlg.ActiveSheet.Range("A4:"+column+"4").interior.color=4035000;
   nmtlg.ActiveSheet.Range("A4:"+column+(i+1)).Borders.Weight=2;

   nmtlg.Visible = true;
end;
/*Точка входа в макрос*/

if (RunDialog(dlg, "Event"))                  
    Text(0) = "Укажите формат вывода";
    Buttons(0) = "Текст - *.csv";
    Buttons(1) = "Excel - *.xls";

    if (ConfWin( Text, Buttons )==0)
	osvInCSV();
    else
	osvInExcel();
    end;	

end; 
exit(1);


                                             