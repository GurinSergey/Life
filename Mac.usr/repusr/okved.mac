/***********************************************/
/*  Клиенты по кодам ОКВЭД                     */
/*  Коркин Иван                                */
/*  20.10.2010                                 */
/***********************************************/
//11.07.2012 vihrov C-12122 смена диапазонов кодов

import rsbdataset, bankinter, globals;

var branch, branchcode, okvedid;
record Department ("dp_dep.dbt");

const  KEY_F1      =315;
const  KEY_F2      =316;
const  KEY_F3      =317;
const KEY_ESC      = 27;
const KEY_SPACE    = 32;
const KEY_ENTER    = 13;

var Fulloutputl, outl, outputl="okved.lbr";                    
GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,outl);
Fulloutputl = FindPath(outputl, outl);
var dlg = TRecHandler("okved", fulloutputl, TRUE); 


private macro printHead()
   [
			                            Клиенты по кодам ОКВЭД
	]
end;

private macro printHeadTable()
	 
   [        ┌────────────┬──────────────────────────────┬────────────────────────────┬────────────────────────────┐
            │№ клиента   │    Наименование клиента      │           ОКВЭД            │           Отделение        │
            ├────────────┼──────────────────────────────┼────────────────────────────┼────────────────────────────┤]
    
end;

private macro printLine(num, name, numinlist, part)	
	[        │############│##############################│############################│############################│
	](num, name, numinlist, part);	
end;

private macro printSeparator()
	[        ├────────────┼──────────────────────────────┼────────────────────────────┼────────────────────────────┤]
end;


private macro getPartName(code)
	var query, data;
   query = "select PARTY.T_NAME from ddp_dep_dbt dp_dep, dparty_dbt party"
   + "\n" +"where DP_DEP.T_PARTYID = PARTY.T_PARTYID"
   + "\n" +"AND DP_DEP.T_CODE = " + code
   + "\n" +"order by DP_DEP.T_NAME;";
   data = TRsbDataset(query);
   if (data.movenext)
   	return data.value("t_name");
  	else
  		return "Не найден";
   end;	
end; 


private macro GetClientName(id)
var  sl=string(" select t_name from dparty_dbt t where t.t_PartyID=",id);
var  DataS=TRsbDataSet(sl);
  if( DataS.moveNext())
    return DataS.name;
  else
    if (id !=0)
       msgbox("Субъект не найден в party.dbt");
       return 0;
    else
      return "ОАО АКБ Пробизнесбанк";
    end;
  end;

END;


macro EvProc (rsrec, cmd, id, key)
    if(( cmd == DLG_KEY ) and ( key == 13 ))
      return CM_SELECT;
    end;
end;


 /*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key) 

   var const_mess = " ~F3~ Выбор значения из списка ~F2~ Продолжить ~ESC~ Выход ",
   SQLQuery, SQLQuery1, rsrec, cmd1;

   /*Первоначальная инициализация полей*/
   if(cmd == DLG_INIT)
      dlg.rec.branch = "Все подразделения";
      dlg.rec.name = "";
      branchcode = 0;
      dlg.rec.okved = "";
      UpdateFields(dlg); 
   end;
   
   /*Установка подсказок в строке состояния*/
   if (cmd==DLG_SETFOCUS)
       message(const_mess);
   end;
   
   if (cmd == DLG_REMFOCUS)
     /*Проверка корректности дат отчета*/
      UpdateFields(dlg); 
   end;

   if (cmd == DLG_KEY)
     /*Выход из диалогового окна формирования отчета*/
     if (KEY == KEY_ESC)
       return exit(1);//CM_CANCEL;
     /*Выбор данных из списка*/
     elif ( KEY == KEY_F3)
        if (FldName(dlg,id) == "Name")
           if (ListDepartment (Department))
              branchcode = department.code; 
              dlg.rec.branch = GetClientName(Department.PartyID);
              dlg.rec.name = Department.name;
              message(" ~F3~ Список подразделений "+const_mess);
              UpdateFields(dlg);
            end
		  elif (FldName(dlg,id) == "okved")
				SQLQuery1 = " select objattr.t_numinlist as num, OBJATTR.T_FULLNAME as name, objattr.t_attrid from dobjattr_dbt objattr " + 
                       	" where OBJATTR.T_OBJECTTYPE = 3 " +
                       	" and OBJATTR.T_GROUPID = 17 " +
                       	" order by objattr.t_numinlist;";
            cmd1 = RSDCommand(SQLQuery1);
            rsrec = RSDRecordset( cmd1, RSDVAL_CLIENT, RSDVAL_STATIC );
            if (RunScroll(rsrec, 0, 0, "Опера", @EvProc, "Список кодов ОКВЭД", "~Enter~ выбор кода ~Esc~ выход", true))
            		if (dlg.rec.okved == "")
	                  dlg.rec.okved = rsrec.value (0);
	                  okvedid = rsrec.value (2);
	               else
	               	dlg.rec.okved = dlg.rec.okved + ", " + rsrec.value (0);
	               	okvedid = okvedid + ", " + rsrec.value (2);
            		end;
                  UpdateFields(dlg);
            end;
        end;
     elif (( KEY == KEY_F2 ) or (KEY == KEY_ENTER))         //Проверки при вводе
         if ( dlg.rec.branch == "" )
                branchcode = 0;
         end;
         if ( dlg.rec.okved == "" )
                MsgBox("Заполните поле Код ОКВЭД");
                return CM_IGNORE;
         end;
         Return CM_SAVE;
     end;
      else
           Return CM_IGNORE;
   end;
        
END;

macro calculate()
   var query, data, query_count, datacount, branch_sql, first;
   printHead();
   if (branchcode == 0)
   	branch_sql = "";
   else
   	branch_sql = "dp_dep.t_code = " + branchcode + " and ";
   end;
//   query =  "select substr(account.t_account, 16) num, party.t_name, objattr.t_fullname, dp_dep.t_code "
   query =  "select substr(cod.t_code,7) num, party.t_name, objattr.t_fullname, dp_dep.t_code " //11.07.2012 vihrov C-12122 смена диапазонов кодов
//   + "\n" + "from daccount_dbt account, ddp_dep_dbt dp_dep, dparty_dbt party, dclient_dbt client, dobjatcor_dbt objatcor, dobjattr_dbt objattr"
   + "\n" + "from daccount_dbt account, ddp_dep_dbt dp_dep, dparty_dbt party, dclient_dbt client, dobjatcor_dbt objatcor, dobjattr_dbt objattr, dobjcode_dbt cod" //11.07.2012 vihrov C-12122 смена диапазонов кодов
//   + "\n" + "where " + branch_sql 
   + "\n" + "where " + branch_sql + " cod.t_codekind=1 and cod.t_objecttype=3 and cod.t_objectid=account.t_client and cod.t_state=0 and "//11.07.2012 vihrov C-12122 смена диапазонов кодов
   + "\n" + "OBJATCOR.T_ATTRID in (" +  okvedid + ")"
   + "\n" + "and OBJATCOR.T_OBJECTTYPE = 3"
   + "\n" + "and OBJATCOR.T_GROUPID = 17"
   + "\n" + "and OBJATCOR.T_ATTRID = OBJATTR.T_ATTRID"
   + "\n" + "and OBJATCOR.T_OBJECTTYPE = OBJATTR.T_OBJECTTYPE"
   + "\n" + "and OBJATCOR.T_GROUPID = OBJATTR.T_GROUPID"
   + "\n" + "and dp_dep.t_code = ACCOUNT.T_BRANCH"
   + "\n" + "and CLIENT.T_PARTYID = ACCOUNT.T_CLIENT"
   + "\n" + "and CLIENT.T_PARTYID = PARTY.T_PARTYID "
   + "\n" + "and lpad(CLIENT.T_PARTYID, 10 ,0) = OBJATCOR.T_OBJECT"
   + "\n" + "and t_account like '40%'"
   + "\n" + "order by num";
   data = TRsbDataset(query);
   first = true;
   while (data.movenext)
   	if (first)
  			printHeadTable();
  			first = false; 
   	end;
   	printLine(data.value(0), data.value(1), data.value(2), getPartName(data.value(3)));
   	printSeparator();
   end;
   if (first)
		println("По данному запросу ничего не найдено.");
	end;
end;

/*Точка входа в макрос*/
if (RunDialog(dlg, "Event"))                  
  calculate();
end; 

