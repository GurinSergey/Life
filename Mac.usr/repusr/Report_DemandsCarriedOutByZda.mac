// -------------------------------------------------------------------------------------------------
// @filename: DemandsCarriedOutByZda.mac v.1
// @author  : 2013-09-11 TeleshovaAM C-23305
// @desc    : Отчет по проведенным за период требованиям с условием ЗДА
// @changes : none
// -------------------------------------------------------------------------------------------------

import Календарь, rsd, globals,BankInter;
import "fg_Life_parm.mac";

class ReportInfo( _PaymentNumber, _PayerAcc     , _PayerName, _RecAcc    , _RecName    , _RecBank,
                  _Amount       , _PaymentGround, _StepNameStatus, _ContrNumberDate)
   var PaymentNumber    = _PaymentNumber;
   var PayerAcc         = _PayerAcc;
   var PayerName        = _PayerName;
   var RecAcc           = _RecAcc;
   var RecName          = _RecName;
   var RecBank          = _RecBank;
   var Amount           = _Amount;
   var PaymentGround    = _PaymentGround;
   var StepNameStatus   = _StepNameStatus;
   var ContrNumberDate  = _ContrNumberDate;
end;

var InfoForReport: TArray = TArray();

macro CreateReport(Date)                                                                           
   var ReportName = getTxtFileName ("DemandsCarriedOutByZda");
   var table = CTableReport();
   var i=0;
   setoutput(ReportName,false);
   println (string ("Отчет о проведенных платежных требованиях за ", date):c:100);

   table.addColumn ("№ док.",                         7 ,  AL_CENTER);
   table.addColumn ("Счет плательщика",               20,  AL_CENTER);
   table.addColumn ("Наименование плательщика",       25,  AL_CENTER);
   table.addColumn ("Счет получателя",                20,  AL_CENTER);
   table.addColumn ("Наименование получателя",        25,  AL_CENTER);
   table.addColumn ("БИК получателя",                 10,  AL_CENTER);
   table.addColumn ("Сумма ",                         10,  AL_CENTER);
   table.addColumn ("Основание платежа",              25,  AL_CENTER);
   table.addColumn ("Номер\\Дата договора",           15,  AL_CENTER);
   table.addColumn ("Последний шаг\\Состояние",       15,  AL_CENTER);
   
   table.printHead ();

   while (i < InfoForReport.size ())
      table.printStringTransferByWord(InfoForReport[i].PaymentNumber,   InfoForReport[i].PayerAcc, 
                                      InfoForReport[i].PayerName ,      InfoForReport[i].RecAcc, 
                                      InfoForReport[i].RecName,         InfoForReport[i].RecBank, 
                                      InfoForReport[i].Amount,          InfoForReport[i].PaymentGround,
                                      InfoForReport[i].ContrNumberDate, InfoForReport[i].StepNameStatus);
      table.printSeparator ();
      i = i + 1;
   end;

   table.printSeparator ();
   table.printStringTransferByWord ("Всего:", i);
   table.printBottom ();  
end;

macro CollectInfoForReport(RepDate)
   var select, rsdcmd, rsl;
   select = " WITH t                                                                                  " +
            "        AS (SELECT   PM.T_PAYMENTID payment_id,                                          " +
            "                     PM.T_DOCKIND doc_kind,                                              " +
            "                     PM.T_PAYERACCOUNT payer_account,                                    " +
            "                     PM.T_RECEIVERACCOUNT receiver_account,                              " +
            "                     PM.T_AMOUNT amount,                                                 " +
            "                     PAYORD.T_USERFIELD1 zda_cntr_id                                     " +
            "            FROM   dpmpaym_dbt pm, dpspaydem_dbt pmdmd, dpspayord_dbt payord             " +
            "            WHERE       PM.T_VALUEDATE = ?                                               " +
            "                    AND PM.T_DOCKIND = 201                                               " +
            "                    AND PMDMD.T_ORDERID = PM.T_PAYMENTID                                 " +
            "                    AND PMDMD.T_ACCEPTTERM = 2                                           " +
            "                    AND PAYORD.T_ORDERID = PM.T_PAYMENTID                                " +
            "                    AND PAYORD.T_USERFIELD1 != CHR (1))                                  " +
            " SELECT                        /*+ leading(t) INDEX(pmdmd DPSPAYDEM_DBT_IDX0)*/          " +
            "           RMPROP.T_NUMBER pm_number,                                                    " +
            "           t.payer_account payer_acc,                                                    " +
            "           RMPROP.T_PAYERNAME payer_name,                                                " +
            "           t.receiver_account rec_acc,                                                   " +
            "           RMPROP.T_RECEIVERNAME rec_name,                                               " +
            "           PROP.T_BANKCODE rec_bank,                                                     " +
            "           t.amount amount,                                                              " +
            "           RMPROP.T_GROUND pm_ground,                                                    " +
            "           STEP_INFO.T_NAME step_name,                                                   " +
            "           DECODE (STEP.T_ISEXECUTE,                                                     " +
            "                   'R', 'Готов к выполнению',                                            " +
            "                   'X', 'Выполнен',                                                      " +
            "                   'Отвергнут') step_status,                                             " +
            "           ZDA_CONTR.T_CONTR_NUMBER cntr_number,                                         " +
            "           TO_CHAR (ZDA_CONTR.T_CONTR_DATE, 'dd.mm.yyyy') cntr_date                      " +
            " FROM   t,                                                                               " +
            "        dpmprop_dbt prop,                                                                " +
            "        dpmrmprop_dbt rmprop,                                                            " +
            "        usr_zda_contr_dbt zda_contr,                                                     " +
            "        doproper_dbt opr,                                                                " +
            "        doprstep_dbt step,                                                               " +
            "        doprostep_dbt step_info                                                          " +
            " WHERE       PROP.T_PAYMENTID = t.payment_id                                             " +
            "         AND PROP.T_DEBETCREDIT = 1                                                      " +
            "         AND RMPROP.T_PAYMENTID = t.payment_id                                           " +
            "         AND ZDA_CONTR.T_CONTR_ID = TO_NUMBER (t.zda_cntr_id)                            " +
            "         AND OPR.T_DOCUMENTID = LPAD (t.payment_id, 34, 0)                               " +
            "         AND OPR.T_DOCKIND = t.doc_kind                                                  " +
            "         AND STEP.T_ID_OPERATION = OPR.T_ID_OPERATION                                    " +
            "         AND STEP.T_ID_STEP = (SELECT   MAX (t_id_step)                                  " +
            "                                FROM   doprstep_dbt                                      " +
            "                                WHERE   t_id_operation = OPR.T_ID_OPERATION)             " +
            "         AND STEP_INFO.T_BLOCKID = STEP.T_BLOCKID                                        " +
            "         AND STEP_INFO.T_NUMBER_STEP = STEP.T_NUMBER_STEP                                ";
   rsdcmd = RSDCommand(select);   
   rsdcmd.AddParam("date", RSDBP_IN, RepDate);
   rsl = RSDRecordset(rsdcmd, null, RSDVAL_STATIC);
   while (rsl.movenext())
      InfoForReport(InfoForReport.size()) = ReportInfo(rsl.value("pm_number"), 
                                                       rsl.value("payer_acc"),
                                                       rsl.value("payer_name"),
                                                       rsl.value("rec_acc"),
                                                       rsl.value("rec_name"),
                                                       rsl.value("rec_bank"),
                                                       rsl.value("amount"),
                                                       rsl.value("pm_ground"),
                                                       rsl.value("step_name")+ "\\ " + rsl.value("step_status"),
                                                       rsl.value("cntr_number") + "\\ " + rsl.value("cntr_date")
                                                       );
   end;
end;
   
//Show Must Go On
var ReportDate = {curdate};
if (not(getDate(ReportDate,"Укажите дату значения документов"))) 
   exit(1);
end;
CollectInfoForReport(ReportDate);
CreateReport(ReportDate);