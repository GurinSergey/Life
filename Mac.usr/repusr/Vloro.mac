/*Выписки по лоро счетам                                                           */
/*                                                                                 */
/*                                                                                 */
/*Тихомиров А.Н. 11.10.2009                    Версия 1.0                          */
/*                                                                                 */
/* GSP  22.02.2013 рефакторинг по R-157144-2                                       */
/* VDN  10.09.2014 сделал обертку макроса для возможности  вызова из ТК по С-25805 */
/* Gurin S. 13.01.2015 R-526394-1 не учитываем статус проводки state = 4           */
/* Gurin S. 14.01.2015 R-527373-2 исключил проводки курсовой разницы и переоценки  */

import rsd, globals, currinter, rsexts, lib_pm_check;
private var cmd, rsd, cmd1, rsd1, cmd2, rsd2, datereport = {curdate}, in, pm, rest2, rest1, sum1, sum2, p, output="\\post\\", out, fulloutput, ob, i = 0, kind2, kind1, final;
var k, prstr = tarray(), cmd3, rsd3, dpd, client, num, len, numdoc;
var pathreport = "", msg_err = "no_error", c = 0, timeclause = ""; 

class pmline(p1, p2, p3, p4, p5, p6, p7, p8)
  var pr1 = p1;
  var pr2 = p2;
  var pr3 = p3;
  var pr4 = p4;
  var pr5 = p5;
  var pr6;
  if (strlen(p6) > 1)
    pr6 = p6;
  else
    pr6 = "";
  end;
  var pr7 = p7;
  var pr8 = p8;
end;


private macro fiiname(fiid)
  var cmd = rsdcommand ("select t_name from dfininstr_dbt where t_fiid = ?");
  cmd.addparam("fiid", RSDBP_IN, fiid);
  cmd.execute();
  var rsd = rsdrecordset(cmd);
  if (rsd.movenext())
    return rsd.value(0);
  else
    return fiid;
  end;
end;



private macro buffline(arh);
var line;
/*Gurin S. 30.01.2014 Адаптация 2031
var cmd = rsdcommand("select * from dpmpaym_dbt pm, dpmdocs_dbt doc, dpmrmprop_dbt rm "+
" where pm.t_paymentid = doc.t_paymentid and doc.t_applicationkey = ? and rm.t_paymentid = pm.t_paymentid");
cmd.addparam("app", RSDBP_IN, arh.value("t_applicationkey"));
*/
  var cmd = rsdcommand("select * from dpmpaym_dbt pm, dpmdocs_dbt doc, dpmrmprop_dbt rm "+
  " where pm.t_paymentid = doc.t_paymentid and doc.t_acctrnid = ? and rm.t_paymentid = pm.t_paymentid");
  cmd.addparam("app", RSDBP_IN, arh.value("t_acctrnid"));
  cmd.execute();
  var rsd = rsdrecordset(cmd);
//debugbreak;
  if (rsd.movenext())
    if (arh.value(9) == "Дебет");
      line = pmline(rsd.value("t_valuedate"), arh.value("t_date_carry"), rsd.value("t_shifroper"),rsd.value("t_number"),arh.value("t_account_receiver"),arh.value("t_ground"), arh.value("t_sum"),0);
    else
      line = pmline(rsd.value("t_valuedate"), arh.value("t_date_carry"), rsd.value("t_shifroper"),rsd.value("t_number"),arh.value("t_account_payer"),arh.value("t_ground"),0,arh.value("t_sum"));
    end;
  else
    if (arh.value(9) == "Дебет");
      line = pmline(" ", arh.value("t_date_carry"), arh.value("t_shifr_oper"),arh.value("t_numb_document"),arh.value("t_account_receiver"),arh.value("t_ground"), arh.value("t_sum"),0);
    else
      line = pmline(" ", arh.value("t_date_carry"), arh.value("t_shifr_oper"),arh.value("t_numb_document"),arh.value("t_account_payer"),arh.value("t_ground"),0,arh.value("t_sum"));
    end;
  end;
  return line;
end;

/*VDN C-25805 Обертка для ТК*/
macro CreateReport (pathreport, reptimestart, reptimeend, accounts )

  if ( not RSL_IsIdentProgram("Н") )
     timeclause = " AND t_systemtime between to_date('01.01.0001 "+reptimestart+"', 'dd.mm.yyyy hh24:mi:ss') "+ 
                  "                      and to_date('01.01.0001 "+reptimeend  +"', 'dd.mm.yyyy hh24:mi:ss') ";
     if (accounts != "") accounts   = " WHERE t_account in ("+accounts+") "; end;
  end;
  initprogress(-1,"Обрабатываются счета","Обрабатываются счета");
//Gurin S. 30.01.2014 Адаптация 2031
  cmd = rsdcommand("SELECT   * "+
    " FROM (SELECT acc.t_account, acc.t_nameaccount, acc.t_oper, "+
    "              acc.t_code_currency, acc.t_final_date, "+
    "              acc.t_kind_account, obj.t_code, acc.t_balance "+
    "       FROM daccount_dbt acc, dobjcode_dbt obj, daccblnc_dbt blnc "+
    "       WHERE acc.t_balance IN ('30109', '30111', '30231') "+
    "         AND ( acc.t_type_account NOT LIKE '%П%' "+
    "         AND acc.t_type_account NOT LIKE '%U%' "+
    "         AND acc.t_type_account NOT LIKE '%Н%' ) "+
    "         AND acc.t_chapter = 1 "+
    "         AND acc.t_department = 1 "+
    "         AND obj.t_codekind = 1 "+
    "         AND obj.t_objecttype = 3 "+
    "         AND obj.t_objectid = acc.t_client "+
    "         AND blnc.t_account = acc.t_account "+
    "       UNION ALL "+
    "       SELECT acc1.t_account, acc1.t_nameaccount, acc1.t_oper, "+
    "              acc1.t_code_currency, acc1.t_final_date, "+
    "              acc1.t_kind_account, obj1.t_code, acc1.t_balance "+
    "       FROM daccount_dbt acc1, dobjcode_dbt obj1, daccblnc_dbt blnc1 "+
    "       WHERE acc1.t_balance IN ('30109', '30111', '30231') "+
    "         AND acc1.t_chapter = 1 "+
    "         AND obj1.t_codekind = 1 "+
    "         AND obj1.t_objecttype = 3 "+
    "         AND acc1.t_department = 1 "+
    "         AND obj1.t_objectid = acc1.t_client "+
    "         AND blnc1.t_account = acc1.t_account ) "+ accounts +
    " GROUP BY t_account, t_nameaccount, t_oper, t_code_currency, t_final_date, t_kind_account, t_code, t_balance "+
    " ORDER BY t_balance, t_code, t_account ");
  cmd.execute();
  rsd = rsdrecordset(cmd);
  client = 0;
  num = 0;
  while (rsd.movenext())
    i = i+1;
    useprogress(int(i));

    cmd3 = rsdcommand(" SELECT t_date_carry FROM dacctrn_dbt "+
    "                   WHERE ( t_account_payer = ? OR t_account_receiver = ? ) "+
    "                     AND t_date_carry < ? "+
    "                     AND t_chapter = 1 AND t_state != 4 and t_result_carry not in (82, 83)"
    "                   ORDER BY t_date_carry desc ");
    cmd3.addparam("acc", RSDBP_IN, rsd.value("t_account"));
    cmd3.addparam("acc1", RSDBP_IN, rsd.value("t_account"));
    cmd3.addparam("date", RSDBP_IN, datereport);
    cmd3.execute();
    rsd3 = rsdrecordset(cmd3);

    if (rsd3.movenext())
      dpd = rsd3.value(0);
    else
      dpd = datereport;
    end;

    cmd1 = rsdcommand(" SELECT count(1) as cnt FROM dacctrn_dbt "+
    "                   WHERE ( t_account_payer = ? OR t_account_receiver = ? ) "+
    "                     AND t_date_carry = ? "+
    "                     AND t_chapter = 1 AND t_state != 4 and t_result_carry not in (82, 83)");
    cmd1.addparam("acc", RSDBP_IN, rsd.value("t_account"));
    cmd1.addparam("acc1", RSDBP_IN, rsd.value("t_account"));
    cmd1.addparam("date", RSDBP_IN, datereport);
    cmd1.execute();
    rsd1 = rsdrecordset(cmd1);

    if ((rsd1.movenext()) and (rsd1.value(0)))
    //Gurin S. 30.01.2014 Адаптация 2031
      cmd2 = rsdcommand("SELECT   trn.t_acctrnid, trn.t_shifr_oper, trn.t_numb_document, trn.t_sum, trn.t_ground, trn.t_priority, trn.t_date_carry, "+
      "         trn.t_account_receiver, trn.t_account_payer, "+
      "         DECODE (acc.t_kind_account, "+
      "                 'П', trn.ПасОборот, "+
      "                 trn.АктОборот "+
      "                ) AS Оборот "+
      "    FROM daccount_dbt acc, "+
      "         (SELECT t_acctrnid, "+
      "                 t_shifr_oper, "+
      "                 t_numb_document, "+
      //Gurin S. 14.01.2015 R-527373-2
      //"                 t_sum_payer, "+
      "                 DECODE (t_account_payer, ?, t_sum_payer, t_sum_receiver) t_sum, "+
      "                 t_ground, "+
      "                 t_priority, "+
      "                 t_date_carry, "+
      "                 t_account_receiver, "+
      "                 t_account_payer, "+
      "                 DECODE (t_account_payer,    ?, 'Дебет', 'Кредит') AS ПасОборот, "+
      "                 DECODE (t_account_receiver, ?, 'Дебет', 'Кредит') AS АктОборот "+
      "            FROM dacctrn_dbt "+
      "           WHERE ( t_account_payer = ? OR t_account_receiver = ? ) "+
      "             AND t_date_carry = ? "+
      "             AND t_chapter = 1 and t_state != 4 and t_result_carry not in (82, 83) "+timeclause+" ) trn "+
      "   WHERE acc.t_account = ? AND acc.t_chapter = 1 "+
      "   ORDER BY Оборот, trn.t_sum ");
      cmd2.addparam("acc",  RSDBP_IN, rsd.value("t_account"));
      cmd2.addparam("acc1", RSDBP_IN, rsd.value("t_account"));
      cmd2.addparam("acc2", RSDBP_IN, rsd.value("t_account"));
      cmd2.addparam("acc3", RSDBP_IN, rsd.value("t_account"));
      cmd2.addparam("acc4", RSDBP_IN, rsd.value("t_account"));
      cmd2.addparam("date", RSDBP_IN, datereport);
      cmd2.addparam("acc5", RSDBP_IN, rsd.value("t_account"));
      cmd2.execute();
      rsd2 = rsdrecordset(cmd2);
      if (rsd.value("t_code_currency"));
        rest1 = restac(rsd.value("t_Account"),rsd.value("t_code_currency"),(datereport-1),null,1);
        rest2 = restac(rsd.value("t_Account"),rsd.value("t_code_currency"),(datereport),null,1);
      else
        rest1 = resta(rsd.value("t_Account"),(datereport-1),null,1);
        rest2 = resta(rsd.value("t_Account"),(datereport),null,1);
      end;

      final = rsd.value("t_Final_date");
      if (final == "01.01.0001")
        final = datereport;
      end;

      if (rest1>0)
        kind1 = "пассив";
      elif (rest1<0)
        kind1 = "актив";
      else
        kind1 = " ";
      end;

      if (rest2>0)
        kind2 = "пассив";
      elif (rest2<0)
        kind2 = "актив";
      else
        kind2 = " ";
      end;

      if (client == (substr(rsd.value("t_Account"),6,3)+substr(rsd.value("t_Account"),16,5)))
        num = "2";
      else
        num = "1";
      end;

//debugbreak;                                                               
/* GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR",2,out);  */

      var uploadpath:string;
      out = pathreport;
      Fulloutput = out;
      /*VDN C-25805 Подмена пути для ручного запуска выписки согласно настройке*/
      if ( pathreport == "" )
        GetRegistryValue("PRBB\\REPUSR\\VLOROUPLOADDIR", V_STRING, uploadpath);
        out = uploadpath;
        
        ob = TDirList (out+"\\post","d");
        ob.Sort (0);
        if( ob.Count!=1)
          //msgbox("Новая папка");
          MAKEDIR (out+"\\post");
        end;
        Fulloutput = out + output;
        //Gurin S. 30.06.2015 R-597439-1
        //Fulloutput = Fulloutput+substr(strsubst(string(datereport):2,".",""),1,4)+"V"+substr(rsd.value("t_Account"),6,3)+num+substr(rsd.value("t_Account"),16,5)+".vyp";
        Fulloutput = Fulloutput+substr(strsubst(string(datereport):2,".",""),1,4)+"V"+substr(rsd.value("t_Account"),6,3)+num+rsd.value("t_Account")	+".vyp";
      else
        Fulloutput = Fulloutput+substr(strsubst(string(datereport):2,".",""),1,4)+substr(reptimeend, 1, 4)+"V"+substr(rsd.value("t_Account"),6,3)+num+substr(rsd.value("t_Account"),16,5)+".vyp";
      end; 

      setoutput(fulloutput,false);                                                                                         
      prstr.size = 0;
      client = substr(rsd.value("t_Account"),6,3)+substr(rsd.value("t_Account"),16,5);
      if (rsd.value("t_code_currency"))
[

];

[                              Выписка за ##########

  Наименование счета:   #####################################
  Лицевой счет                 Отв.исп.       ДПД
    ####################         #####     ##########
  ####################
  Входящий остаток ######                           ########################

Дата пров.  Дата вал. ВО  Док. Счет                             Дебет   Кредит        Основание   ]
        (datereport:f, rsd.value("t_nameaccount"), rsd.value("t_account"), rsd.value("t_oper"):l, date(dpd), fiiname(rsd.value("t_code_currency")), kind1:l, abs(money(rest1)):a);
        sum1 = 0;
        sum2 = 0;
        while (rsd2.movenext())
          pm = buffline(rsd2);
          /* Коркин И.Н. по заявке I-087329 */
          len = strlen(pm.pr4)-5+1; 
          if (len < 1)
            len = 1;
          end; 
          numdoc = substr(pm.pr4, len, 5);

          if (pm.pr7)
            //[########## ##########  #  ### #################### ####################        #######################################]
            prstr(prstr.size) = string(date(pm.pr1):f:10:r, " ", date(pm.pr2):f:10:r, "  ", pm.pr3:2:r, " ", numdoc:5:r, " ",pm.pr5:20:r,  "   ",numeric(pm.pr7):z:a:17:r, "           ", pm.pr6:l:47:z);
          else
            //[########## ##########  #  ### ####################          #################### #######################################]
            prstr(prstr.size) = string(date(pm.pr1):f:10:r, " ", date(pm.pr2):f:10:r, "  ", pm.pr3:2:r, " ", numdoc:5:r, " ",pm.pr5:20:r,  "            ",numeric(pm.pr8):z:a:17:r, "  ", pm.pr6:l:47:z);
          end;

          if (strlen(pm.pr6)>47)
            p = 47;
            while (strlen(pm.pr6)>p)
              if (pm.pr7)
              //[                                                                             ###############################################]
                prstr(prstr.size)  = string("                                                                                  ", substr(pm.pr6,(p+1)):l:47:z);
              else
              //[                                                                              ###############################################]
                prstr(prstr.size)  = string("                                                                                   ", substr(pm.pr6,(p+1)):l:47:z);
              end;
              p = p+47;
            end;
          end;

          sum1 = sum1+pm.pr7;
          sum2 = sum2+pm.pr8;
        end;
        k = 0;

        while (k<prstr.size)
          println(prstr(k));
          k = k+1;
        end;

[
  Итог оборотов по дебету                       ######################
  Итог оборотов по кредиту                               ######################

  Исходящий остаток ############                         ######################

------ ОАО АКБ "Пробизнесбанк", г.Москва -------------------------------------------------------------------------------]
        (money (sum1):a:r, money (sum2):a:r, kind2:l, abs(money (rest2)):a:r);
        sum1 = 0;
        sum2 = 0;
//viewfile(fulloutput);


      else


[

];

[                             Выписка за ##########

  Наименование счета:   #####################################
  Лицевой счет           Отв.исп.       ДПД
  ####################      #####     ##########
 
  Входящий остаток ######                 #########################

  ВО  Ном.    Корреспондент                       Дебет         Кредит   Основание   
      док.    Бик]
        (datereport:f, rsd.value("t_nameaccount"):l, rsd.value("t_account"), rsd.value("t_oper"):l, date(dpd), kind1:l, abs(money(rest1)):a);
        sum1 = 0;
        sum2 = 0;
        while (rsd2.movenext())
          pm = buffline(rsd2);
          /* Коркин И.Н. по заявке I-087329 */
          len = strlen(pm.pr4)-5+1; 
          if (len < 1)
            len = 1;
          end; 
          numdoc = substr(pm.pr4, len, 5);

          if (pm.pr7)
            //[  ## ### #########  ####################  ################             ################################################]
            //[__######__#########_####################_################_____________###############################################]
            prstr(prstr.size) = string(" ",pm.pr3:2:r, " ", numdoc:5:r, "           ", pm.pr5:20:r, " ",numeric(pm.pr7):z:17:a:r, "             ", pm.pr6:l:47:z);
          else
            //[  ## ### #########  ####################              ################ ################################################]
            //[__######__#########_####################_____________################_###############################################]
            prstr(prstr.size) = string(" ",pm.pr3:2:r, " ", numdoc:5:r, "           ", pm.pr5:20:r, "             ",numeric(pm.pr8):17:z:a:r, " ",pm.pr6:l:47:z);
          end;

          if (strlen(pm.pr6)>47)
            p = 47;
            while (strlen(pm.pr6)>p)
              //[                                                                      ###############################################]
              prstr(prstr.size)  = string("                                                                       ", substr(pm.pr6,(p+1)):l:47:z);
              p = p+47;
            end;
          end;
          sum1 = sum1+pm.pr7;
          sum2 = sum2+pm.pr8;
        end;
        k = 0;

        while (k<prstr.size)
          println(prstr(k));
          k = k+1;
        end;

[

  Итог оборотов по дебету              #####################
  Итог оборотов по кредиту                           #####################

  Исходящий остаток ############                     ######################

       ОАО АКБ "Пробизнесб	анк", г.Москва]
        (money (sum1):a:r, money (sum2):a:r, kind2:l, abs(money (rest2)):a:r);
        sum1 = 0;
        sum2 = 0;

      end;
      setoutput(null,true);
    end;
  end;

  if (c == 0) msg_err = "Нет данных в промежуток с " + reptimestart + " по " + reptimeend; end;
  remprogress(int(i));

[Выписки по банкам сформированы. \\fsbelomor\post\post\SWIFT\RSB];
  return msg_err;
OnError(err)
  return err.Message;
End;/*End CreateReport*/

/*VDN 10.09.2014 C-25805 Добавил проверку на подсистему, точка входа*/
if (RSL_IsIdentProgram("Н"))
  if (not getdate(datereport));
    return 0;
  end;
  CreateReport("");
end;