// -------------------------------------------------------------------------------------------------
// @filename: Report_TaxPaymentsWaitingIncome v.1
// @author  : 2013-09-19 TeleshovaAM C-23431
// @desc    : Отчет по налоговым документам на ожидающих поступлений
// @changes : none
// -------------------------------------------------------------------------------------------------

import rsd, globals;
import "fg_Life_parm.mac";

class ReportInfo( _PaymentNumber, _PayerAcc, _RecAcc, _Amount, _PaymentGround, _Priority, _PaymentType)
   var PaymentNumber    = _PaymentNumber;
   var PayerAcc         = _PayerAcc;
   var RecAcc           = _RecAcc;
   var Amount           = _Amount;
   var PaymentGround    = _PaymentGround;
   var Priority         = _Priority;
   var PaymentType      = _PaymentType;
end;

var InfoForReport: TArray = TArray();

macro CreateReport()   
   debugbreak;                                                                        
   var ReportName = getTxtFileName ("NalogNaOP");
   var table = CTableReport();
   var i=0;
   setoutput(ReportName,false);
   println (string ("Отчет о налоговых платежах, находящихся на ожидании поступлений c ", {curdate} - 10, " по ", {curdate}):c:100);

   table.addColumn ("Номер платежа",     10,  AL_CENTER);
   table.addColumn ("Счет плательщика",  20,  AL_CENTER);
   table.addColumn ("Счет получателя",   20,  AL_CENTER);
   table.addColumn ("Сумма платежа",     15,  AL_CENTER);
   table.addColumn ("Основание платежа", 30,  AL_CENTER);
   table.addColumn ("Очередность",        7,  AL_CENTER);
   table.addColumn ("Признак оплаты",    15,  AL_CENTER);
   
   table.printHead ();

   while (i < InfoForReport.size ())
      table.printStringTransferByWord(InfoForReport[i].PaymentNumber, InfoForReport[i].PayerAcc, InfoForReport[i].RecAcc, InfoForReport[i].Amount,    
                                      InfoForReport[i].PaymentGround, InfoForReport[i].Priority, InfoForReport[i].PaymentType);
      table.printSeparator ();
      i = i + 1;
   end;

   table.printSeparator ();
   table.printStringTransferByWord ("Всего платежей:", i);
   table.printBottom ();  
end;

macro CollectInfoForReport()
   var select, rsdcmd, rsl;
   select =    " SELECT   RMPROP.T_NUMBER payment_number,                                              " +
               "          PM.T_PAYERACCOUNT payer_acc,                                                 " +
               "          PM.T_RECEIVERACCOUNT receiver_acc,                                           " +
               "          PM.T_AMOUNT pm_amount,                                                       " +
               "          RMPROP.T_GROUND pm_ground,                                                   " +
               "          RMPROP.T_PRIORITY pm_priority,                                               " +
               "          DECODE (PM.T_PAYTYPE, 2, 'Таможенный', 1, 'Налоговый', 'Обычный') pm_paytype " +
               " FROM     dpmpaym_dbt pm,                                                              " +
               "          dpmrmprop_dbt rmprop,                                                        " +
               "          doproper_dbt opr,                                                            " +
               "          doprstep_dbt step                                                            " +
               " WHERE    PM.T_VALUEDATE BETWEEN TO_DATE ('" + ({curdate} - 10) + "', 'dd.mm.yyyy')    " +
               "                            AND  TO_DATE ('" + {curdate} + "', 'dd.mm.yyyy')           " +
               "      AND PM.T_DOCKIND = 201                                                           " +
               "      AND PM.T_ORIGIN = 2                                                              " +
               "      AND PM.T_RECEIVERACCOUNT LIKE '40101%'                                           " +
               "      AND RMPROP.T_PAYMENTID = PM.T_PAYMENTID                                          " +
               "      AND OPR.T_DOCUMENTID = LPAD (PM.T_PAYMENTID, 34, 0)                              " +
               "      AND OPR.T_DOCKIND = PM.T_DOCKIND                                                 " +
               "      AND STEP.T_ID_OPERATION = OPR.T_ID_OPERATION                                     " +
               "      AND STEP.T_BLOCKID = 10000141                                                    " +
               "      AND STEP.T_NUMBER_STEP = 10                                                      " +
               "      AND STEP.T_ISEXECUTE = 'R'                                                       " +
               " ORDER BY   PM.T_PAYERACCOUNT                                                          ";
   rsdcmd = RSDCommand(select);   
   rsl = RSDRecordset(rsdcmd, null, RSDVAL_STATIC);
   while (rsl.movenext())
      InfoForReport(InfoForReport.size()) = ReportInfo(rsl.value("payment_number"), 
                                                       rsl.value("payer_acc"),
                                                       rsl.value("receiver_acc"),
                                                       rsl.value("pm_amount"),
                                                       rsl.value("pm_ground"),
                                                       rsl.value("pm_priority"),
                                                       rsl.value("pm_paytype"));
   end;
end;
   
//Show Must Go On
CollectInfoForReport();
CreateReport();