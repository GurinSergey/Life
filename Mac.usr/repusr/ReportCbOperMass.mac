  /***************************************************************************************************************
   Имя файла..:       .mac
   Назначение.:       Отчет по корр.счетах
   Заявка.....:       от Вараксина Евгения C-30171
   Дата.......:       17.06.2014
   Автор......:       Ващенко Дмитрий 
                      RS-Bank V6 20.30
****************************************************************************************************************/
  import RSD, "ldmklbr.mac", "fg_Life_parm.mac", "KeyCodes.mac", globals, activeX, lib_sqltools;
  var StatusBar = "F2 Выполнить ESC Выход"; 
  var startDate;
  var endDate;
  FILE f_rep_out() txt;

  Class (TRecHandler) TParm(LbrName)    
     InitTRecHandler ("statCB", (LbrName), true);   
     
     private macro OpenExcel()
        var objectXls, myWorkExcel, myWorkBook;

        objectXls = CreateObject ("rsax", "TRsAxServer", string("RsAxServer", UserNumber()), IsStandAlone());
        myWorkExcel = objectXls.CreateComObject ("Excel.Application");
        myWorkBook = myWorkExcel.Workbooks.Add();
        SetParm(1, myWorkBook);
        SetParm(2, myWorkExcel);
        return true;
     OnError
        return false;
     end;

     private macro SetHeaderExcelTable(work_sheet, myWorkBook, numSheet)
        var i;
        work_sheet = myWorkBook.Sheets(numSheet);

        work_sheet.Cells.NumberFormat = "@";
        work_sheet.Cells(2, 1).Value = "Дата операции";
        work_sheet.Cells(2, 2).Value = "Сумма операции";
        work_sheet.Cells(2, 3).Value = "Наименование плательщика";
        work_sheet.Cells(2, 4).Value = "ИНН плательщика";
        work_sheet.Cells(2, 5).Value = "Наименование получателя";
        work_sheet.Cells(2, 6).Value = "ИНН получателя";
        work_sheet.Cells(2, 7).Value = "БИК обслуживающено банка";
        work_sheet.Cells(2, 8).Value = "Корреспондирующий счет";
        work_sheet.Cells(2, 9).Value = "Назначение платежа";
     
        for (i, 1, 9)
           work_sheet.Cells(2, i).HorizontalAlignment = 3;
           work_sheet.Cells(2, i).Font.Bold = true;
           work_sheet.Columns( i).EntireColumn.AutoFit;
        end;

        SetParm(1, work_sheet);
        return true;
     OnError
        return false;
     end;

     private macro SetValueToExcel(myWorkBook, myRecord)
        var i, j = 3, numSheet = 1;
        var work_sheet;
        initprogress(-1, "", "Ожидайте, идет выгрузка данных");
        
        if (SetHeaderExcelTable(work_sheet, myWorkBook, numSheet))
           while (myRecord.MoveNext())
              work_sheet.Range("A2:I" + (j + 1)).Borders.Weight = 2;
              for (i, 1, 9)                 
                    work_sheet.Cells(j, i).Value = myRecord.value(i - 1);                     
              end;
              if (j <= 65535)
                 j = j + 1;
              else 
                 numSheet = numSheet + 1;
                 if (SetHeaderExcelTable(numSheet, work_sheet))
                    j = 3;
                 end;
              end;
              useprogress(j);
           end;
        end;     
        remprogress;
        return true;
     OnError
        return false;      
     end;

     private macro FillRecor()
        var cmd = RSDRecordSet("SELECT /*+FIRST_ROWS LEADING(t pmpaym pmrmprop prop2 oproper oprstep pk rk) INDEX(t dpmprop_dbt_idx1)                            "+                           
        "     INDEX(pmpaym dpmpaym_dbt_idx0) INDEX(oprstep doprstep_dbt_idx0) USE_NL(t pmpaym pmrmprop prop2 oproper oprstep wlpm wlmeslnk wlmes pk rk)*/        "+
        "   TO_CHAR (pmpaym.t_valuedate, 'dd.mm.yyyy'),                                                                                                          "+
        "   pmpaym.t_amount,                                                                                                                                     "+
        "   pmrmprop.t_payername,                                                                                                                                "+
        "   pmrmprop.t_payerinn,                                                                                                                                 "+
        "   pmrmprop.t_receivername,                                                                                                                             "+
        "   pmrmprop.t_receiverinn,                                                                                                                              "+
        "   pk.t_code,                                                                                                                                           "+
        "   pmpaym.t_receiveraccount,                                                                                                                            "+
        "   pmrmprop.t_ground                                                                                                                                    "+
        "FROM dpmprop_dbt t, doproper_dbt oproper, doprstep_dbt oprstep, dobjcode_dbt pk, dpmpaym_dbt pmpaym, dpmrmprop_dbt pmrmprop, dpmprop_dbt prop2,         "+
        "     dobjcode_dbt rk, dwlmes_dbt wlmes, dwlmeslnk_dbt wlmeslnk, dwlpm_dbt wlpm, dwltransp_dbt wltransp                                                  "+
        "WHERE ( t.t_issender = CHR (88) AND t.t_group = 1 AND t.t_transferdate between TO_DATE ('"+startDate+"', 'dd.mm.yyyy')                                  "+
        "   AND TO_DATE ('"+endDate+"', 'dd.mm.yyyy'))  AND ( wlpm.t_paymentid(+) = pmpaym.t_paymentid  AND wlpm.t_direct(+) = CHR (88)                          "+
        "   AND wlmeslnk.t_objkind(+) = 501 AND wlmeslnk.t_objid(+) = wlpm.t_wlpmid  AND wlmeslnk.t_mesid = wlmes.t_mesid(+)                                     "+
        "   AND t.t_paymentid = prop2.t_paymentid(+) AND t.t_debetcredit <> prop2.t_debetcredit(+) AND t.t_paymentid = pmpaym.t_paymentid                        "+
        "   AND t.t_paymentid = pmrmprop.t_paymentid  AND t.t_issender = CHR (88)  AND pmpaym.t_dockind IN (320, 322, 321) AND wltransp.t_tpid(+) = t.t_tpid     "+
        "   AND pmpaym.t_receiverbankid = rk.t_objectid(+) AND rk.t_codekind(+) = 3 AND rk.t_objecttype(+) = 3 AND rk.t_state(+) = 0                             "+
        "   AND pmpaym.t_payerbankid = pk.t_objectid(+) AND pk.t_codekind(+) = 3 AND pk.t_objecttype(+) = 3 AND pk.t_state(+) = 0                                "+
        "   AND LPAD (pmpaym.t_paymentid, 34, '0') = oproper.t_documentid(+) AND oproper.t_dockind(+) = DECODE (pmpaym.t_dockind, 322, 320,                      "+
        "   DECODE (pmpaym.t_primdockind, 0, pmpaym.t_dockind, pmpaym.t_primdockind)) AND oprstep.t_id_operation(+) = oproper.t_id_operation                     "+
        "   AND oprstep.t_isexecute(+) = CHR (87)) AND (pmpaym.t_startdepartment = 1 AND t.t_transferdate between TO_DATE ('"+startDate+"', 'dd.mm.yyyy')        "+
        "   AND TO_DATE ('"+endDate+"', 'dd.mm.yyyy')) AND pm_scrhlp.getcoraccount (t.t_payfiid, t.t_corschem) LIKE '30102%'                                     "+
        "UNION ALL                                                                                                                                               "+
        "SELECT /*+FIRST_ROWS LEADING(pmpaym t pmrmprop prop2 oproper oprstep) INDEX(pmpaym dpmpaym_dbt_idx11) INDEX(t dpmprop_dbt_idx0)                         "+
        "    USE_NL(pmpaym t pmrmprop prop2 oproper oprstep)*/                                                                                                   "+
        "   TO_CHAR (pmpaym.t_valuedate, 'dd.mm.yyyy'),                                                                                                          "+
        "   pmpaym.t_amount,                                                                                                                                     "+
        "   pmrmprop.t_payername,                                                                                                                                "+
        "   pmrmprop.t_payerinn,                                                                                                                                 "+
        "   pmrmprop.t_receivername,                                                                                                                             "+
        "   pmrmprop.t_receiverinn,                                                                                                                              "+
        "   prop2.t_bankcode,                                                                                                                                    "+
        "   pmpaym.t_receiveraccount,                                                                                                                            "+
        "   pmrmprop.t_ground                                                                                                                                    "+
        "FROM dpmprop_dbt t, doproper_dbt oproper, doprstep_dbt oprstep, dpmpaym_dbt pmpaym, dpmrmprop_dbt pmrmprop, dpmprop_dbt prop2, dwltransp_dbt wltransp   "+
        "WHERE (t.t_issender = CHR (0) AND t.t_group = 1 AND t.t_propstatus = 32000  AND t.t_transferdate between TO_DATE ('"+startDate+"', 'dd.mm.yyyy')        "+
        "   AND TO_DATE ('"+endDate+"', 'dd.mm.yyyy')) AND (t.t_paymentid = prop2.t_paymentid AND prop2.t_debetcredit = DECODE (t.t_debetcredit, 0, 1, 0)        "+
        "   AND t.t_paymentid = pmpaym.t_paymentid AND t.t_paymentid = pmrmprop.t_paymentid AND t.t_group = 1 AND t.t_issender = CHR (0)                         "+
        "   AND wltransp.t_tpid(+) = t.t_tpid AND oproper.t_dockind IN (201, 202, 450, 320, 322, 311, 16, 17, 27)                                                "+
        "   AND LPAD (pmpaym.t_paymentid, 34, '0') = oproper.t_documentid(+) AND oproper.t_dockind(+) = DECODE (pmpaym.t_dockind, 322, 320,                      "+
        "   DECODE (pmpaym.t_primdockind, 0, pmpaym.t_dockind, pmpaym.t_primdockind))  AND oprstep.t_id_operation(+) = oproper.t_id_operation                    "+
        "   AND oprstep.t_isexecute(+) = CHR (87)) AND (pmpaym.t_department = 1 AND pmpaym.t_valuedate between TO_DATE ('"+startDate+"', 'dd.mm.yyyy')           "+
        "   AND TO_DATE ('"+endDate+"', 'dd.mm.yyyy') AND NOT EXISTS (SELECT 1 FROM dpmlink_dbt link                                                             "+
        "   WHERE link.t_purposepayment = pmpaym.t_paymentid  AND link.t_linkkind = 9)) AND pm_scrhlp.getcoraccount (t.t_payfiid, t.t_corschem) LIKE '30102%'");
        return cmd;
     end;
  
     Macro Handler (dlg, cmd, id, key)            
        if (cmd == DLG_KEY)	
           if (KEY == KEY_F2)
              var myWorkBook, myWorkExcel;
              startDate = dlg.("BeginDate");
              endDate = dlg.("EndDate"); debugbreak;
              if (OpenExcel(myWorkBook, myWorkExcel))                   
                 if (SetValueToExcel(myWorkBook, FillRecor()));
                    MsgBox("Файл успешно записан");
                    myWorkExcel.Visible = true;
                 end;
              end;    
           elif (KEY == KEY_ESC)
              return CM_CANCEL;
           end;
        end; 
     End;
     
     Macro Run 
        return RunDialog (this, R2M (this, "Handler"));  
     End;
  End; 

  Macro MyPanel( lbr )    
     var rc = lbr.AddPanel( "statCB", 40, 10, 30, 8, lbr.EColour.SHLP, lbr.EFRame.UNARY ), elem;
     rc.Title = "Настройка отчета";
     rc.StatusLine = StatusBar;
    
     elem = rc.AddFrame(0, 0, 30, 10, 2);
     elem = rc.AddLabel( 1, 1, "Начало периода:");
     elem = rc.AddField( 19, 1,lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_DATE, "BeginDate", 10, 1);
     elem = rc.AddLabel( 1, 2, "Конец  периода:");
     elem = rc.AddField( 19, 2,lbr.EFieldType.EDIT, lbr.EFocus.YES, lbr.EDataType.V_DATE, "EndDate", 10, 1);
   
  end;
  Macro MyLbr
    var Fulloutputlbr, outputlbr = "statCB.lbr", outlbr, lbr;
    GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\TEXTDIR", V_STRING, outlbr);
    Fulloutputlbr = outlbr + "\\" + outputlbr;
    lbr = TLbrFile( Fulloutputlbr, "C" );
    MyPanel( lbr );

    lbr.Save(); 
    lbr = NULL;
    return Fulloutputlbr;
  end;

  private var LbrName, Parm;
  LbrName = MyLbr();

  Parm = TParm(LbrName);

  Parm.rec.BeginDate = Date() - 365;
  Parm.rec.EndDate = Date();

  if( not Parm.Run() )
     Parm = null;
     DelFile(LbrName);
     Exit (1);
  end;
end;