// ------------------------------------------------------------------------------------------------
// @filename: statForExchequer.mac
// @author  : Zlenko M.P. 
// @desc    : информация о платежных агентах (субагентах) и банковских платежных агентах (субагентах) 
// @request : C-32053 
// -------------------------------------------------------------------------------------------------

import lib_sqltools, oralib, likepy, rcw;


private macro openExcel
    var exObj;
    var comObj;
    
    exObj  = createObject( "rsax", "TRsAxServer", "RsAxServer", isStandAlone );
    comObj = exObj.createComObject( "Excel.Application", false );
    comObj.application.displayAlerts = true;
    comObj.workbooks.add();
    comObj.visible = false;
    setParm( 0, comObj );

    return true;
  onerror
    return false; 
end;


private macro getRecord(templ_account)
   startQueryCapture ();        
   [     
      SELECT   ACC.T_ACCOUNT ACCOUNT,
               P.T_NAME PARTY_NAME,
               ACC.T_OPEN_DATE OPEN_CLOSE,
               (SELECT   c.t_code
                  FROM   dobjcode_dbt c
                 WHERE       c.T_OBJECTID = P.T_PARTYID
                         AND C.T_STATE = 0
                         AND C.T_CODEKIND = 27
                         AND ROWNUM = 1)
                  OGRN,
               (SELECT   c.t_code
                  FROM   dobjcode_dbt c
                 WHERE       c.T_OBJECTID = P.T_PARTYID
                         AND C.T_STATE = 0
                         AND C.T_CODEKIND = 16
                         AND ROWNUM = 1)
                  INN,
               ad.T_ADRESS ADRESS,
               (SELECT   PER.T_NAME1 || ' ' || PER.T_NAME2 || ' ' || PER.T_NAME3
                  FROM   dofficer_dbt o, dpersn_dbt per
                 WHERE   O.T_PARTYID(+) = P.T_PARTYID
                         AND PER.T_PERSONID(+) = O.T_PERSONID
                         AND (RSBSESSIONDATA.CURDATE BETWEEN O.T_DATEFROM  AND O.T_DATETO or  O.T_DATETO = to_date('01.01.0001', 'dd.mm.yyyy')) 
                         AND (o.t_post(+) = 'Директор'  OR o.t_isfirstperson(+) = 'X')
                         AND ROWNUM = 1)
                  FIO,
               (SELECT   O.T_PHONENUMBER
                  FROM   dofficer_dbt o, dpersn_dbt per
                 WHERE   O.T_PARTYID(+) = P.T_PARTYID
                         AND PER.T_PERSONID(+) = O.T_PERSONID
                         AND (RSBSESSIONDATA.CURDATE BETWEEN O.T_DATEFROM  AND O.T_DATETO or  O.T_DATETO = to_date('01.01.0001', 'dd.mm.yyyy')) 
                         AND (o.t_post(+) = 'Директор'  OR o.t_isfirstperson(+) = 'X')
                         AND ROWNUM = 1)
                  PHONENUMBER,
               CASE
                  WHEN SUBSTR (ACC.T_ACCOUNT, 10, 2) = '01' THEN 1
                  WHEN SUBSTR (ACC.T_ACCOUNT, 10, 2) = '02' THEN 2
                  ELSE 3
               END
                  sortacc
        FROM   dparty_dbt p,               
               daccount_dbt acc,
               dadress_dbt ad
       WHERE       ad.t_partyid(+) = P.T_PARTYID
               AND ad.t_type(+) = 1
               AND ACC.T_CLIENT = P.T_PARTYID
               AND ACC.T_ACCOUNT  like :TEMPACCOUNT
               AND ACC.T_OPEN_CLOSE != 'З'
        ORDER BY   sortacc
   ];   
   var sql = endQueryCapture ();        
   sql = strsubst(sql, ":TEMPACCOUNT", "'" + templ_account + "%'");
   return execSQLSelect(sql);
end;


private macro run()
  var ExcelReport, acc_balance, list;

  if( not openExcel( @ExcelReport ) )
     msgBox("Не удается вызвать приложение Excel");
     return;
  end;

  if(not getInt(acc_balance, "Задайте балансовый счет"))
     msgBox("Не задан былансовый счет!");
     return;
  end;

  list = excelReport.Worksheets.Add();
  var rs = getRecord(acc_balance);
  var count = 2;
     list.cells(1, 1).value = "Номер счета";                                   
     list.cells(1, 2).value = "Наименование клиента";                               
     list.cells(1, 3).value = "Дата открытия счета";                                
     list.cells(1, 4).value = "ОГРН";                                
     list.cells(1, 5).value = "ИНН/КПП";                                
     list.cells(1, 6).value = "Юр. адрес" ;                       
     list.cells(1, 7).value = "ФИО руководителя";                      
     list.cells(1, 8).value = "Телефон";
     list.range(list.cells(1, 1), list.cells(1, 8)).font.bold = true; 
  while (rs.moveNext())
     list.cells(count, 1).numberFormat  = "@" ;
     list.cells(count, 1).value = rs.value("account"                 );                                   
     list.cells(count, 2).value = rs.value("party_name"              );                                   
     list.cells(count, 3).value = rs.value("open_close", null, v_date);                                   
     list.cells(count, 4).numberFormat  = "0" ;
     list.cells(count, 4).value = rs.value("OGRN"                    );                                   
     list.cells(count, 5).numberFormat  = "0" ;
     list.cells(count, 5).HorizontalAlignment = -4152; 
     list.cells(count, 5).value = rs.value("INN"                     );                                   
     list.cells(count, 6).value = rs.value("adress"                  );                                   
     list.cells(count, 7).value = rs.value("FIO"                     );                                   
     list.cells(count, 8).HorizontalAlignment = -4152;
     list.cells(count, 8).value = rs.value("phonenumber"             );                                   
     count = count + 1;
  end;
     list.range(list.cells(1, 1), list.cells(count, 8)).columns.autofit;  
     ExcelReport.visible = True;
end;

run ();
exit(1);





