// ------------------------------------------------------------------------------------------------
// @filename: hoz_18.mac
// @author  : Тихомиров А.Н. 
// @desc    : Выгрузка данных по переоценке в Диасофт
// @modif   : 06.02.2014 Zlenko MP Адаптация под 31-й патч
// -------------------------------------------------------------------------------------------------

import BankInter, календарь, RSD, rsexts, fiinter; 
import likepy, oralib, globals;
import KeyCodes;

var Fulloutputl, outl, outputl = "hoz_60.lbr", dat, mon1, year, sum, account;                    
record rate (ratedef);

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,outl);
Fulloutputl = FindPath(outputl, outl);
var dlg = TRecHandler("hoz", fulloutputl, TRUE); 
var RepDate;
var accMask = "^6031[3-4]|^61403";

MACRO Event (dlg, cmd, id, key)
   /*Первоначальная инициализация полей*/
   if(cmd == DLG_INIT)
      dlg.rec.date ={curdate};
      UpdateFields(dlg); 
   end;   
   
   if (cmd == DLG_KEY)
      /*Выход из диалогового окна формирования отчета*/
      if (KEY == KEY_ESC)
         return exit(1);//CM_CANCEL;
      elif ( KEY == KEY_F3)
         dlg.rec.date = getdatebycalendar({curdate});
      elif (( KEY == KEY_F2 ) or (KEY == KEY_ENTER))         //Проверки при вводе
         if ( dlg.rec.date > {curdate} )
                MsgBox("Дата начала периода не может быть больше даты текущего операционного дня");
                return CM_IGNORE;
         else
            RepDate  = dlg.rec.date;
            return CM_SELECT;  
        end;        
      end;
   end;    
END;
RunDialog (dlg, @Event);

private macro getRecordCount(SQL, Param)
   var _sql = execSQLSelect(" select count(1) from ( " + SQL + " )", Param);

   if (_sql and _sql.moveNext())
       return _sql.value(0, null, v_integer);
   end;

   return 0;
end;


private var ParamSQL = Tarray(), sql;

sql = " WITH param AS (SELECT :repdate repdate, :mask mask FROM DUAL)             "+
      "  SELECT   acc.t_account account,                                          "+
      "           arh.t_date_carry date_carry,                                    "+
      "           arh.t_sum_natcur sum_natcur,                                    "+
      "           rsb_account.restac (acc.t_account,                              "+
      "                               acc.t_code_currency,                        "+
      "                               param.repdate,                              "+
      "                               acc.t_chapter,                              "+
      "                               0) AS restcur,                              "+      
      "           acc.t_code_currency code_currency                               "+
      "    FROM   daccount_dbt acc, dacctrn_dbt arh, param                        "+
      "   WHERE   REGEXP_LIKE (acc.t_account, param.mask) AND acc.t_chapter = 1   "+
      "           AND (arh.t_account_payer = acc.t_account                        "+
      "                OR arh.t_account_receiver = acc.t_account)                 "+
      "           AND arh.t_chapter = acc.t_chapter                               "+
      "           AND arh.t_result_carry   = 18                                   "+
      "           AND acc.t_code_currency !=  0                                   "+
      "           AND arh.t_date_carry = param.repdate                            ";
                                                                                      
sql = execSQLSelect(sql, ParamSQL = makeArray(SQLParam("repdate", RepDate),
                                              SQLParam("mask"   , AccMask)));                                                                                 

var i:integer = 0, path, sum_r, RateCur; 
datesplit({curdate}, dat, mon1, year);
path = gettxtfilename(string(dat, mon1, year, "_diasoft_curr.txt"));

initprogress(getRecordCount(SQL.command.cmdText, ParamSQL), "Вывод результатов", "Вывод результатов");
setoutput(path,false);               
while (sql.moveNext())  
   ПолучитьКурс(rate, 0, sql.value("code_currency", null, v_integer), 7);
   RateCur = rate.rate / rate.scale / pow(10, rate.point);
   sum_r = money(RateCur * sql.value("restcur", null, v_money));
   println(string(string(dat, "/", mon1, "/", year), ";", sql.value("account"), ";" , sum_r, ";", sql.value("restcur", null, v_money), ";", sql.value("sum_natcur", null, v_money)));
   useprogress(i = i + 1);   
end;
remprogress();

setoutput(null, true);               
CopyFile(path, string("$\\", dat, "-",mon1, "-", year, "_diasoft_curr.txt"));
viewfile(path);
exit(1);
