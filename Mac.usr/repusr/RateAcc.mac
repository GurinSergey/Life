/*Несоответствие проводок курсовой разницы                                    */
/*                                                                            */
/*                                                                            */
/*Суть: если есть разница между фактическими оборотами в рублях по счету      */
/* и рассчитанными оборотами (по остаткам на текущую и предыдущую             */
/* даты), следовательно на эту разницу должна по счету покрытия               */
/* пройти переоценка. Если эта сумма не совпадает с суммой, что               */
/* прошла по сч. покрытия (виды проводок 18,83,82,84), то                     */
/* отчет выводит эту разницу                                                  */
/*                                                                            */
/*Тихомиров А.Н. 24.02.2009                  Версия 1.1      */
/*Изменения:*/
/*14-02-2013 Жаворонкова Н. joy I-00324846-2 для 30-ки нужен еще вид проводки 83 + параметризирована часть запросов*/
//zmp 14.03.2014 предварительная адаптация на 31 патч

import globals, oralib, bankinter, likePy, lib_lang, rsexts, FIinter;

import RSD,rcw, rslx, Календарь, rsbdataset, CurrInter;
var  n, i, reportdate, chapter, chap, restvalue, fii, fi, Chaptername, dprt_v, und = 0.05;
var calcsum_all, factsum_all, unduesum_all,calcsum= $0.00,calcsum1= $0.00,calcsum2= $0.00,factsum1=$0.00,factsum2=$0.00,factsum, unduesum;
var maxs:integer, k:integer;

var fulloutput = GetTxtFileName("rateacc");

const  KEY_F1      =315;
const  KEY_F2      =316;
const  KEY_F3      =317;
const KEY_ESC      = 27;
const KEY_SPACE    = 32;
const KEY_ENTER    = 13;

record Department ("dp_dep.dbt");
record Chapt ("obchaptr.dbt");
var Fulloutputl, outl, outputl="rateacc.lbr";                    

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,outl);
Fulloutputl = FindPath(outputl, outl);
var dlg = TRecHandler("rateacc", fulloutputl, TRUE); 

var branch, DateBegin,
    DateEnd,
    Office:double  = 0,
    OfficeName = "По всем офисам";
array fiidcode, rate1, rate2;

import "fg_Life_parm.mac";
var fgBank = fg_life_subject({OurBank});
if (fgBank.is_go)
    und = 0;
end;

private macro RestC(acc, dat)
var sl = "select * from drestdate_dbt where T_ACCOUNTID= :acc and T_RESTDATE < :dat order by T_RESTDATE desc";
var  DataS=execSQLSelect (sl, makeArray (SQLParam ("acc", acc), 
                                         SQLParam ("dat", dat  )));
    if (datas.movenext())
        return DataS.value("t_rest");
    else
        return 0;
    end;
END;

private macro fiid(fiid)
var sl = "select t_codeinaccount as cia from dfininstr_dbt where t_fiid= :fiid";
var DataS=execSQLSelect (sl, makeArray (SQLParam ("fiid", fiid) ));
    if (datas.movenext())
        return DataS.value("cia");
    end;
END;

private macro fiidname(fiid)
var sl = "select name from dfininstr_dbt where t_fiid= :fiid";
var DataS=execSQLSelect (sl, makeArray (SQLParam ("fiid", fiid) ));
    if (datas.movenext())
        return DataS.value("name");
    end;
END;


private macro curdate(dat)
var sq = "select * from dcurdate_dbt where t_curdate <= :dat order by t_curdate desc ";
var DataS=execSQLSelect (sq, makeArray (SQLParam ("dat", dat) ));
    while (datas.movenext())
        if (DataS.value("t_curdate") == dat)
            datas.movenext();
            return (DataS.value("t_curdate", NULL, V_DATE));
        end;
    end;
END;


private macro curdat(dat)
var sq = "select * from dcurdate_dbt where t_curdate < :dat order by t_curdate desc";
var DataS=execSQLSelect (sq, makeArray (SQLParam ("dat", dat) ));
    if (datas.movenext())
        if (datas.value ("t_isclosed")=="X")
            return true;
        else 
            return false;
        end;
        msgbox ("На вашем стенде нет предыдущего операционного дня.");
        exit;
    end;
END;

/*Найдем имя по Partyid*/
private macro GetClientName(id)
var  sl=string(" select t_name from dparty_dbt t where t.t_PartyID= :id");
var  DataS = execSQLSelect (sl, makeArray (SQLParam ("id", id) ));
    if( DataS.moveNext())
        return DataS.value("t_name");
    else
        if (id !=0)
            msgbox("Субъект не найден в party.dbt");
            return 0;
        else
            return "ОАО АКБ Пробизнесбанк";
        end;
    end;
END;

private macro acct (acc, dat)
    var sq = "select count(*) as cnt from daccount_dbt where t_account= :acc and t_type_account not like '%U%' and t_type_account not like '%Н%' and t_code_currency = 0";
    var datasq = execSQLSelect (sq, makeArray (SQLParam ("acc", acc) ));
    return ((datasq.movenext() and (datasq.value ("cnt") > 0)));
onError return false;
END;


//Тихомиров А.Н. - Доработка для нескольких проводок по просьбе Паньковой Е.
private macro arhd (acc, dat)
/*Виды проводок
18-проводка переоценки
82-переоценка исходящего остатка
84-доп.переоценка при выполнениии операций
83-курсовая разница в мультивалютной проводке
*/
    var sum = $0.00;
    var sq = "select T_SUM_NATCUR t_sum, t_account_receiver, t_account_payer "+
             "from dacctrn_dbt where t_date_carry = :dat and (t_account_payer = :accp "+ 
             " or t_account_receiver = :accr) and t_result_carry in (18, 84, 82, 83)"; //14-02-2013 Жаворонкова Н. joy I-00324846-2 для 30-ки нужен еще вид проводки 83

    var datasq = execSQLSelect (sq, makeArray (SQLParam ("dat", dat),
                                               SQLParam ("accp", acc), 
                                               SQLParam ("accr", acc)));
    while (datasq.movenext())
        if (datasq.value ("t_account_payer")  == acc)
            sum = sum - round(datasq.value ("t_sum"),2,1);
        elif (datasq.value ("t_account_receiver") == acc)
            sum = sum + round(datasq.value ("t_sum"),2,1);
        end;
    end;
return sum;
END;



private macro rate(rateid,dat)
    var sq = "select t_sincedate, t_rate, t_point, t_scale from dratedef_dbt where t_rateid= :rateid";
    var datasq = execSQLSelect (sq, makeArray (SQLParam ("rateid", rateid)));
    if (datasq.movenext())
        if (datasq.value ("t_sincedate") ==dat)
            return (datasq.value ("t_rate")/((pow(10,datasq.value ("t_point")))*datasq.value ("t_scale")));
        end;
    end;
    
    sq = "select * from dratehist_dbt where t_rateid= :rateid and t_sincedate <= :dat  order by t_sincedate desc";
    datasq = execSQLSelect (sq, makeArray (SQLParam ("rateid", rateid),
                                           SQLParam ("dat", dat)));
    if (datasq.movenext())
        return (datasq.value ("t_rate")/((pow(10,datasq.value ("t_point")))*datasq.value ("t_scale")));
    else
        return -1;
    end;
END;


private macro outall()
initprogress(-1, "Отбираются счета, ждите...", "Отбираются счета");
    if (chapter!=0)
        chap = "and t_chapter = "+chapter;
    else
        chap="";
    end;
var sq = "SELECT  count(*) as cnt "+
          "FROM (SELECT * "+
          "   FROM daccount_dbt "+
          "  WHERE (t_open_close = chr(0) OR t_close_date > TO_DATE ('"+reportdate+"', 'DD-MM-YYYY')) "+chap+branch+" and t_code_currency != 0) accs "+
          "INNER JOIN "+
          "(SELECT * "+
          "   FROM dratedef_dbt "+
          "  WHERE t_type = 7 AND t_isdominant = CHR(88) and t_fiid = 0) fiid "+
          "ON accs.t_code_currency = fiid.t_otherfi ";
var datasq=trsbdataset(sq);
datasq.movenext();
maxS=datasq.cnt;

var sql = "SELECT  * "+
          "FROM (SELECT * "+
          "   FROM daccount_dbt "+
          "  WHERE (t_open_close = chr(0) OR t_close_date > TO_DATE ('"+reportdate+"', 'DD-MM-YYYY') and t_code_currency != 0 ) "+chap+branch+") accs "+
          "INNER JOIN "+
          "(SELECT * "+
          "   FROM dratedef_dbt "+
          "  WHERE t_type = 7 AND t_isdominant = CHR(88) and t_fiid = 0) fiid "+
          "ON accs.t_code_currency = fiid.t_otherfi order by t_otherfi, t_account";
useprogress();
remprogress();

var dataset=TRsbDataSet(sql);
calcsum_all = 0;
factsum_all = 0;
unduesum_all = 0;
i=0;
k=0;
fii=0;
initprogress(maxs,"Производится расчет, ждите...", "Несоответствие проводок курсовой разницы");
[Ведомость несоотвествия проводок курсовой разницы рублевых покрытий на дату ########## г.
По главе # #####################
по подразделению ##################################################
┌──────┬───┬────────────────────┬──────────────────┬────────────────┬─────────────────┐
│Валюта│Тип│        Счет        │Сумма проводки    │Курсовая разница│       Сумма     │
│      │сч.│                    │курсовой разницы  │    (расчетная) │  несоответствия │
├──────┼───┼────────────────────┼──────────────────┼────────────────┼─────────────────┤]
(reportdate, chapter, Chaptername, officename);
n=0;
    while (dataset.movenext());
        if (fii!=dataset.code_currency)
            fii=dataset.code_currency;
            rate1(fii)=rate(dataset.rateid,Curdate(reportdate));
            rate2(fii)=rate(dataset.rateid,reportdate);
            fi=fiid(dataset.code_currency);
        end;
        i=i+1;
        if (fiidcode(k)!=dataset.code_currency)
            if ((rate1(fii)!=-1) and (rate2(fii)!=-1) and (acct(dataset.account)))
                convsum (calcsum1,(restAC(dataset.account,dataset.code_currency,curdate(reportdate), null, dataset.chapter)),reportdate,dataset.code_currency,0,7);
                convsum (calcsum2,(restAC(dataset.account,dataset.code_currency,curdate(reportdate), null, dataset.chapter)),curdate(reportdate),dataset.code_currency,0,7);
                calcsum = calcsum1 - calcsum2;
                
                if (calcsum==0)
                    calcsum = (rate2(fii)-rate1(fii))*restC(dataset.accountid,reportdate);
                end;
                factsum = arhd(dataset.account, reportdate);
                unduesum = (calcsum - factsum);
                convsum (factsum2,(restAC(dataset.account,dataset.code_currency,reportdate, null, dataset.chapter)),reportdate,dataset.code_currency,0,7);
                factsum1 = restA(dataset.account,reportdate,null,dataset.chapter);
                
                if ((round(abs(unduesum),2,2)>und) and  (round(abs(factsum1-factsum2),2,2) >= (round(abs(unduesum),2,2))))
                    calcsum_all = round ((calcsum_all+calcsum),2);
                    factsum_all = factsum_all+factsum;
                    unduesum_all = round ((unduesum_all+unduesum),2);
                    n=n+1;
                    [│######│###│####################│##################│################│#################│]
                    (fi, dataset.kind_account, dataset.account, factsum, calcsum, unduesum);
                end;
            elif ((fiidcode(k)!=dataset.code_currency) and (acct(dataset.account)))
                k=k+1;
                fiidcode(k)=dataset.code_currency;
            end;
        end;
        useprogress(i);
    end;
[└──────┴───┴────────────────────┴──────────────────┴────────────────┴─────────────────┘];

remprogress(i);
[                                                                       ###############]
(unduesum_all);
[Итого по документам курсовой разницы (факт) ############### ]
(factsum_all);
[Итого по документам курсовой разницы (расч) ###############](calcsum_all);
[                                            ###############](factsum_all-calcsum_all);
[Итого количество документов  #################](n);
sql =   "SELECT * "+
        "   FROM dratedef_dbt "+
        "  WHERE t_type != 7 AND t_isdominant = CHR(88) ";
dataset=TRsbDataSet(sql);
i=0;
[ ];[ ];
    while (dataset.movenext())
        [Внимание! Для валюты ##### ########################## в качестве основного курса определен курс отличный от ЦБ РФ. Проверка несоответствия проводок курсовой разницы по счетам в этой валюте не выполнялась]
        (fiid(dataset.otherfi), dataset.definition);
    end;
 setoutput(null,true);
END;



 /*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key) 

var const_mess = "~F2~ Продолжить ~SPACE~ По всем офисам ~ESC~ Выход ";
var const_mess2 = "~F2~ Продолжить ~SPACE~ ~ESC~ Выход ";

   /*Первоначальная инициализация полей*/
    if(cmd == DLG_INIT)
        dprt_v = 0;
        dlg.rec.dprt_code  ="";
        dlg.rec.und  = round(und,2);
        dlg.rec.dprt_name = officename;
        dlg.rec.ReportDate = {curDate};
        dlg.rec.chapter = 0;
        dlg.rec.chaptername="По всем главам";
        UpdateFields(dlg); 
    end;
   
   /*Установка подсказок в строке состояния*/
    if (cmd==DLG_SETFOCUS)
        if (FldName(dlg,id)=="dprt_code") 
            message(" ~F3~ Список подразделений "+const_mess);
        elif (FldName(dlg,id)=="ReportDate")
            message(" ~F3~ Выбор даты из календаря "+const_mess2);
        elif (FldName(dlg,id)=="Chapter")
            message(" ~F3~ Выбор главы счетов "+const_mess2);
        end;
    end;
   
    if (cmd == DLG_REMFOCUS)
     /*Проверка корректности дат отчета*/
        if (FldName(dlg,id) == "ReportDate")
            if ( dlg.rec.ReportDate > {curdate} )
                MsgBox("Дата начала периода не может быть больше даты текущего операционного дня");
                return CM_CANCEL;
            end;
        end;
        UpdateFields(dlg); 
    end;

    if (cmd == DLG_KEY)
        /*Выход из диалогового окна формирования отчета*/
        if (KEY == KEY_ESC)
            return exit(1);//CM_CANCEL;
        /*Выбор данных из списка*/
        elif ( KEY == KEY_F3)
            /*Выбор подразделения банка из списка подразделений */
            if (FldName(dlg,id) == "dprt_code")
                if (ListDepartment (Department))
                    dprt_v = department.code; 
                    dlg.rec.dprt_code = Department.Code;
                    dlg.rec.dprt_name = GetClientName(Department.PartyID);
                    message(" ~F3~ Список подразделений "+const_mess);
                    UpdateFields(dlg);
                end;
            end;
            if (FldName(dlg,id) == "Chapter")
                if (ListChapter (Chapt))
                    dlg.rec.chapter = chapt.chapter;
                    dlg.rec.Chaptername = chapt.name;
                    message(" ~F3~ Список глав счетов "+const_mess);
                    UpdateFields(dlg);
                end;
            end;
        
            /*Выбор даты из календаря*/
            if (FldName(dlg,id) == "ReportDate")
                dlg.rec.ReportDate = GetDateByCalendar ({curDate});
            end;
        
        elif (KEY == KEY_SPACE)
            /*Сброс установок в поле офис банка*/
            if (dlg.rec.dprt_code != 0 ) 
                dlg.rec.dprt_code="";
                dprt_v = 0;
                dlg.rec.dprt_name = "По всем офисам";
                UpdateFields(dlg);
            end;
            if (dlg.rec.chapter!=0)
                dlg.rec.chapter = 0;
                dlg.rec.chaptername="По всем главам";
                UpdateFields(dlg);
            end;
            if (dlg.rec.dprt_code == "По всем офисам") 
                message(" ~F3~ Список подразделений "+const_mess);
                UpdateFields(dlg);
            end;

        elif (( KEY == KEY_F2 ) /* or (KEY == KEY_ENTER) */ )         //Проверки при вводе
            if ( dlg.rec.ReportDate > {curdate} )
                MsgBox("Дата начала периода не может быть больше даты текущего операционного дня");
                return CM_IGNORE;
            end;
            if ((strlen(dlg.rec.dprt_code) != 0) and (dlg.rec.dprt_name != "По всем офисам"))
                if (department.nodetype == 2)                   
                    branch = "and t_branch="+dprt_v;
                    officename=dlg.rec.dprt_name;
                else
                    branch = "and t_department="+dprt_v;
                    officename=dlg.rec.dprt_name;
                end;   

            elif (dlg.rec.dprt_Name == "По всем офисам") 
                branch = "";
                officename=dlg.rec.dprt_name;
            elif (dprt_v==0)
                branch = "";
                officename=dlg.rec.dprt_name;
            end;
            ReportDate  = dlg.rec.ReportDate;
            chapter = dlg.rec.chapter;
            und = round(dlg.rec.und,2);
            Chaptername=dlg.rec.Chaptername;
            if (curdat(ReportDate))    
                Return CM_SAVE;
            elif (gettrue(true,"Операционный день предшествующий текущему не закрыт. \n Остатки на счетах могут измениться. Продолжить?"))
                Return CM_SAVE;
            end;
        else
            Return CM_DEFAULT;
        end;
    end;
        
END;

/*Точка входа в макрос*/
if (RunDialog(dlg, "Event"))
    OutAll;
end;

end;