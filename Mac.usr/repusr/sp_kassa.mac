/************************************************************************/
/*          Автоматизированная банковская система RS-Bank v6.0          */
/*                                                                      */
/*               Copyright (c) R-Style Softlab Kiev                     */
/*                                                                      */
/*  Имя файла        : sp_kassa.mac                                     */
/*                                                                      */
/*  Описание         : Справка по кассовым операциям клиента за период  */
/*                                                                      */
/*  Программист      : Солошенко В. А.                                  */
/*                                                                      */
/*  Создан           : 21.12.2009                                       */
/*                                                                      */
/*  Модифицирован    :                                                  */
/*                                                                      */
/************************************************************************/
import globals, oralib, bankinter, "KeyCodes.mac", repforms, Календарь, xlrep, PTInter ;

var path = "",
    pathfile = "",
    file_lbr = "RSU.lbr",
    Templ = "\\sp_kassa.xlt";   

var dlg;
var Date_report_begin, Date_report_end, ClAcc, ClName, IsTxtRep = True;
var Numb = 0;

record AccRec (account);

private macro PrintHead()
[                                                  
       СПРАВКА по кассовым оборотам за период с ########## по ##########

#########################
####################################################################################################       
┌────────────┬─────────────────────────────────────┬────────┐
│    Дата    │               Обороты               │ Символ │
│            │      Дебет       │      Кредит      │        │
├────────────┼──────────────────┼──────────────────┼────────┤
│     1      │        2         │        3         │   4    │
├────────────┼──────────────────┼──────────────────┼────────┤
](Date_report_begin, Date_report_end, ClAcc, ClName);
end;


private macro PrintLine(_date, _dt, _ct, _symb);

[│ ########## │ ################ │ ################ │  ####  │
 │            │                  │                  │        │
](Date(_date), _dt:c, _ct:c, _symb);

end;


Private macro PrintBottom()
[│____________│__________________│__________________│________│
]
end;


/* Макрос печати отчета */
Macro MakeReport()
var select = "";
var rs:object = NULL;
var type:object = NULL; 

/*переменные для шаблона .xlt*/
var Templ:string = "";
var Rep:object = NULL ;
var RegParam:string = "";

var i:integer = 0;

  /*Шаблон для создания отчета в Excel*/  
  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\USERTEMPLSDIR", 2, RegParam);
  
  if (IsTxtRep) 
    PrintHead(Date_report_begin, Date_report_end);
  end;

  // Коркин Иван: изменен запрос по заявке A53991
  // Gurin S. N. в 30 патче нет поля t_date_value в таблице darhdoc_dbt
  // TAM 30.04.2014 I-00483236-2 Адаптация 2031
  select = "SELECT * FROM"
  + "\n" + "(SELECT trn.t_date_carry as t_date, symb.t_sum AS dt, 0 AS ct, symb.t_symbol  "
  + "\n" + "FROM dacctrn_dbt trn, dsymbcash_dbt symb  "
  + "\n" + "WHERE TRN.T_ACCTRNID = SYMB.T_ACCTRNID AND  "
  + "\n" + "      (trn.t_date_carry BETWEEN '" + Date_report_begin + "' "
  + "\n" + "                           AND  "
  + "\n" + "                           '" + Date_report_end + "') "
  + "\n" + "      and substr(TRN.T_ACCOUNT_RECEIVER, 1, 5) in ('20202', '20206', '20207') "
  + "\n" + "      AND  "
  + "\n" + "      (trn.t_account_payer = '" + ClAcc + "')"
  + "\n" + "UNION ALL  "
  + "\n" + "SELECT trn.t_date_carry as t_date, 0 AS dt, symb.t_sum AS ct, symb.t_symbol  "
  + "\n" + "FROM dacctrn_dbt trn, dsymbcash_dbt symb  "
  + "\n" + "WHERE TRN.T_ACCTRNID = SYMB.T_ACCTRNID AND  "
  + "\n" + "      (trn.t_date_carry BETWEEN '" + Date_report_begin + "' "
  + "\n" + "                           AND  "
  + "\n" + "                           '" + Date_report_end + "') "
  + "\n" + "      and substr(TRN.T_ACCOUNT_PAYER, 1, 5) in ('20202', '20206', '20207')                            "
  + "\n" + "      AND  "
  + "\n" + "      (trn.t_account_receiver = '" + ClAcc + "'))"
  + "\n" + "ORDER BY t_date";

  /* Выборка кассовых оборотов по счету */
/*  select = "SELECT symb.t_date, arh.t_sum AS dt, 0 AS ct, symb.t_symbol " + 
           "FROM darhdoc_dbt arh, dsymbcash_dbt symb " +
           "WHERE (arh.t_applicationkey = SUBSTR (symb.t_applicationkey, 6)) " +
           "      AND " +
           "      (symb.t_date BETWEEN '" + Date_report_begin + "' " +
           "                           AND " +
           "                           '" + Date_report_end + "') " +
           "      AND " +
           "      (arh.t_account_payer = '" + ClAcc + "') " +
           "UNION ALL " +
           "SELECT symb.t_date, 0 AS dt, arh.t_sum AS ct, symb.t_symbol " +
           "FROM darhdoc_dbt arh, dsymbcash_dbt symb " +
           "WHERE (arh.t_applicationkey = SUBSTR (symb.t_applicationkey, 6)) " +
           "      AND " +
           "      (symb.t_date BETWEEN '" + Date_report_begin + "' " +
           "                           AND " +
           "                           '" + Date_report_end + "') " +
           "      AND " +
           "      (arh.t_account_receiver = '" + ClAcc + "')";
  */
  Message("Идет построение выборки кассовых оборотов по счету...");
  rs = ExecSQLSelect(select);

  if (rs)
    InitProgress(-1," ~CtrlBreak~ Прекратить","Печатаются данные по клиентам");

    while (rs.MoveNext())
      UseProgress(Numb = Numb + 1);
      if (IsTxtRep)
        PrintLine(rs.Value("t_date"), rs.Value("dt"), rs.Value("ct"), rs.Value("t_symbol"));
      else
        Rep.WriteReportRow(rs.Value("t_date"), rs.Value("dt"), rs.Value("ct"), rs.Value("t_symbol"));
      end;
    end;
  else
    MsgBox("Ошибка при формировании выборки!");
  end;
   
  if (IsTxtRep)
    PrintBottom();                                                                    
  end;
    
  RemProgress();
/*
  /*вывод отчета --> Excel*/
  if (not IsTxtRep)
    Rep.TransferReportData("DATARANGE");
    Rep.DefaultPaintRange("DATARANGE", n);//Rep.RowsWritten);

    Rep.SetRangeValue("date_begin",_date_begin);
    Rep.SetRangeValue("date_end",_date_end);

    Rep.axExcel.Visible = true;
    Rep.axExcel.WindowState = xlWindowMaximized;
  end;
*/
  OnError(Err)
    MsgBox(Err.Message,"| at ",Err.Line,"|in ",Err.Module);
END;


/*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key) 
var const_message = "~F2~ Продолжить F3~ Выбор даты из календаря  ~Space~ Выбор формата отчета  ~ESC~ Выход ";
var OrgDate:date = {curdate} + 1;

  /*Первоначальная инициализация полей*/
  if(cmd == DLG_INIT)
     Message(const_message);
     dlg.rec.BeginDate ={curDate}-1;
     dlg.rec.EndDate = {curDate};
     dlg.rec.text = "X";
     dlg.rec.ClAcc   = "";
     dlg.rec.ClName   = "";
     UpdateFields(dlg); 
  end;
  
  if (cmd == DLG_REMFOCUS)
    if (FldName(dlg,id)=="BeginDate")
       if ( dlg.rec.BeginDate > dlg.rec.EndDate ) 
          MsgBox("Дата начала больше даты окончания отчетного периода");
          return CM_CANCEL;
       elif ( dlg.rec.BeginDate > {curdate} )
          MsgBox("Дата начала больше даты текущего операционного дня"); 
          return CM_CANCEL;
       end;
    elif (FldName(dlg,id)=="EndDate") 
       if (dlg.rec.EndDate < dlg.rec.BeginDate )
          MsgBox("Дата окончания меньше даты начала отчетного периода"); 
          return CM_CANCEL;
       elif ( dlg.rec.EndDate > {curdate} )
          MsgBox("Дата окончания больше даты текущего операционного дня");
          return CM_CANCEL;
       end;
    elif (FldName(dlg,id) == "ClAcc")
      if (dlg.rec.ClAcc == "")
        MsgBox("Счет не может быть пустым");
        return CM_CANCEL;
      end;
    end;

    UpdateFields(dlg); 
  end; 

  if (cmd == DLG_KEY)
    /*Выход из диалогового окна формирования отчета*/
    if (KEY == KEY_ESC)
      return exit(1);//CM_CANCEL;
    /*Выбор данных из списка*/
    elif ( KEY == KEY_F3)
    /*Выбор даты из календаря*/
      if (FldName(dlg,id) == "BeginDate")
        dlg.rec.BeginDate = GetDateByCalendar ({curDate}-1);
      elif (FldName(dlg,id) == "EndDate")
        dlg.rec.EndDate = GetDateByCalendar ({curDate}-1);
      elif (FldName(dlg,id) == "ClAcc")
        if (ListAccount (AccRec, 1, 0, dlg.rec.ClAcc));
          dlg.rec.ClAcc = AccRec.Account;
          dlg.rec.ClName = AccRec.NameAccount;
          UpdateFields(dlg); 
        end;

      end;
    /*Возможность выбора формата отчета текстовый вид, вывод в Excel*/
    elif (KEY==KEY_SPACE)
    /*Параметр текстовый вид*/
      if (FldName(dlg,id)=="text")
        if (dlg.rec.text == "")
          dlg.rec.text = "X";
          dlg.rec.excel = "";
          IsTxtRep = True;
          UpdateFields(dlg); 
        end;
    /*Параметр вывод в Excel*/
      elif(FldName(dlg,id)=="excel")
        if (dlg.rec.excel == "")
          dlg.rec.excel = "X";
          dlg.rec.text = "";
          IsTxtRep = False;
          UpdateFields(dlg); 
        end;
      end;
    elif ( KEY == KEY_F2 )
       Date_report_begin = dlg.rec.BeginDate;
       Date_report_end = dlg.rec.EndDate;  
       ClAcc = dlg.rec.ClAcc;  
       ClName = dlg.rec.ClName;

       if (dlg.rec.ClAcc == "")
         MsgBox("Не задан счет клиента!"); 
         Return CM_IGNORE;
       end;

       if (Date_report_begin <= Date_report_end)
         Return CM_SAVE;
       else
         MsgBox("Выбран не корректный период!"); 
       end;

    elif ( (KEY == KEY_ENTER) and (FldName(dlg,id) == "ClAcc") )
        
      SetFocus(dlg, 0);
      Return CM_IGNORE;
    end;
  
  end;
END;

GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR", 2, path);
pathfile = FindPath(file_lbr, path);
	if (not pathfile)
		msgbox("Не найдена LBR");
		exit();
	end;

Dlg = TRecHandler("sp_kassa", pathfile, True);

if (RunDialog(dlg, "Event"))
  MakeReport();
end;