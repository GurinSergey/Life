/*Макрос печати Журнала регистрации платежных требований   */
/*и инкассовых поручений.                                  */
/*Гогильчин А.В  R-Style Ukraine, Kiev 07.08.2008          */
/*Изменен 02/09/2008 Новиков С.С.                          */
/*Версия 1.0                                               */

import globals, oralib, bankinter, "KeyCodes.mac", repforms, likePy, PTInter;
import rcw,RSD;

record Department ("dp_dep.dbt");
//record dlg (, "") dialog;
record superior(ptoffice);
record officet(ptoffice);

var path = "",
    pathfile = "",
    filen = "RSU.lbr";

    GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,path);
    pathfile = FindPath(filen,path);
	if (not pathfile)
		msgbox("Не найдена LBR");
		exit();
	end;

    var dlg = TRecHandler("oldclnt",pathfile,True);

var Office     = 0,
    OfficeName = "По всем офисам",
    Date_report,
    rselect;
var PartyID:integer = {OurBank};

Private macro PrintHead(_office, _date)
[                       Журнал регистрации платежных требований и инкассовых поручений

  По отделению: #######################](_office);

if (OrgRegStruct != "")
[ По подразделению: ##############################################
](OrgRegStructName);
end;

[  На дату: ############


 ┌─────┬──────────────┬──────┬────────────────────┬─────────────────────┬────────────┬────────────────────┬─────────┬────────┬────────────┐
 │  №  │    Дата      │ Шифр │     Название       │    Номер счета      │   Сумма    │     Взыскатель     │ Оплата/ │  Опер. │ Примечание │
 │ док.│  документа   │ док. │    организации     │                     │ документа  │                    │  карт.  │        │            │
 ├─────┼──────────────┼──────┼────────────────────┼─────────────────────┼────────────┼────────────────────┼─────────┼────────┼────────────┤
](_date);
end;

private macro PrintLine(_Nr, _docDate, _docShifr, _Payer, _AccountNum, _DocValue, _receiver, _PayOrCard, _Oper)

[│#####│ ############ │ #### │####################│#####################│############│####################│#########│########│            │
 │     │              │      │                    │                     │            │                    │         │        │            │
](_Nr:c, trim(string(_docDate)):c, _docShifr:c, _Payer:w, _AccountNum:w, string(_DocValue):c:w, _receiver:c:w, _PayOrCard:w:c, _Oper:c);
end;

Private macro PrintBottom()
[└─────┴──────────────┴──────┴────────────────────┴─────────────────────┴────────────┴────────────────────┴─────────┴────────┴────────────┘
]
end;

private macro GetResultGrid(_department:string, _Date:string);
   var rs:object = NULL;
   var Query = "";
   var Condition = "";
   var OrderBy = "";

   query = 
          "select pm.t_paymentid,\n" +
          "       rm.t_date,\n" + 
          "       rm.t_shifroper,\n" + 
          "       rm.t_payername,\n" + 
          "       pm.t_payeraccount,\n" + 
          "       pm.t_amount,\n" + 
          "       rm.t_receivername,\n" + 
          "       pm.t_oper,\n" + 
          "       acc.t_department,\n" + 
          "       case\n" + 
          "         when  (pm.t_paymstatus = 32000 and pm.t_valuedate = '"+trim(_date)+"' \n) then\n" + 
          "          1\n" + 
          "         else\n" + 
          "          0\n" + 
          "       end as \"payed\",\n" + 
          "       case\n" + 
          "         when exists (select 1\n" + 
          "                 from doproper_dbt oper, doprstep_dbt step\n" + 
          "                where oper.t_id_operation = step.t_id_operation\n" + 
          "                  and step.t_symbol = '1'\n" + 
          "                  and oper.t_documentid like '%' || pm.t_paymentid\n" + 
          "                  and step.t_plan_date = '"+trim(_date)+"') then\n" + 
          "          1\n" + 
          "         else\n" + 
          "          0\n" + 
          "       end as \"index1\",\n" + 
          "       case\n" + 
          "         when pm.t_i2placedate = '"+trim(_date)+"' then\n" + 
          "          1\n" + 
          "         else\n" + 
          "          0\n" + 
          "       end as \"INDEX2\"\n" + 
          "  from dpmpaym_dbt pm, dpmrmprop_dbt rm, daccount_dbt acc\n";

   Condition = 
          " where pm.t_paymentid = rm.t_paymentid\n" + 
          "   and rm.t_shifroper in ('06', '02')\n" + 
          "   and (acc.t_account = pm.t_payeraccount and acc.t_chapter = pm.t_chapter)\n" + 
          "   and ((pm.t_i2placedate = '"+trim(_date)+"') or\n" + 
          "       (pm.t_paymstatus = 32000 and pm.t_valuedate = '"+trim(_date)+"' \n" + 
          "        ) or \n" + 
          "        (exists (select 1\n" + 
          "                   from doproper_dbt oper, doprstep_dbt step\n" + 
          "                  where oper.t_id_operation = step.t_id_operation\n" + 
          "                    and step.t_symbol = '1'\n" + 
          "                    and oper.t_documentid like '%' || pm.t_paymentid\n" + 
          "                    and step.t_plan_date = '"+trim(_date)+"'\n" +
          "                )\n" 
          "        )\n" +
          "       )";
  if (OperSelect != "")
    Condition = Condition +    
           "   AND pm.t_oper like '"+OperSelect+ "'\n";
  end;
   
   OrderBy = "order by 10 desc, 11 desc, 12 desc, t_date";
   
   if ( (valtype(_department)!= V_UNDEF) and (trim(_department) != 0) )
      condition = condition + "\n   and acc.t_department = "+trim(_department);
   end;

   query = query + condition + orderBy;

   rs = execSQLSelect(query);

   if (rs and rs.movenext())
      return rs;
   else 
      return NULL;
   end;

   OnError(Err)
     MsgBox(Err.Message,"| at ",Err.Line,"|in ",Err.Module);
end;

macro PrintItAll()
   var Grid: object = NULL;
   var Number = 0,
       PayCard = "";

   PrintHead(OfficeName, Date_report);

   grid = GetResultGrid(office, Date_report);

   if (Valtype(grid) == V_UNDEF)
      PrintLn("\t\t Нет данных для отчета");
   else
      Number = Number + 1;
      PayCard = IfThenElse(grid.value("payed") == 1, "Опл. ", "");
      PayCard = IfThenElse(grid.value("index1") == 1, PayCard + "K1 ", PayCard);
      PayCard = IfThenElse(grid.value("index2") == 1, PayCard + "K2 ", PayCard);
      PrintLine(Number, date(grid.value("t_date")), grid.value("t_shifroper"), grid.value("t_PayerName"), grid.value("t_PayerAccount"), 
                string(grid.value("t_amount"):a), grid.value("t_receiverName"), PayCard, grid.value("t_oper"));

      while (grid.movenext())
         Number = Number + 1;
         PayCard = IfThenElse(grid.value("payed") == 1,  "Опл. ", "");
         PayCard = IfThenElse(grid.value("index1") == 1, PayCard + "K1 ", PayCard);
         PayCard = IfThenElse(grid.value("index2") == 1, PayCard + "K2 ", PayCard);

         PrintLine(Number, date(grid.value("t_date")), grid.value("t_shifroper"), grid.value("t_PayerName"), grid.value("t_PayerAccount"), 
                   string(grid.value("t_amount"):a), grid.value("t_receiverName"), PayCard, grid.value("t_oper"));
      end;

   end;
   PrintBottom;
end;

/*Обработчик диалоговой панели ввода исходных данных для печати*/
MACRO Event (dlg, cmd, id, key) 

   var mess = "~F2~ Продолжить ~SPACE~ \"По всем офисам\" ~F3~ Список ~ESC~ Выход ";
   
   var OrgDate:date = {curdate} + 1;
   var NoChangeDate:bool = false;

   if(cmd == DLG_INIT)
      dlg.rec.Office    = OfficeName;
      dlg.rec.Date      = {curdate};
      dlg.rec.operID   = "";
      dlg.rec.opername = "все операционисты";
      UpdateFields(dlg); 
   end;
   
   if (cmd==DLG_SETFOCUS)
       officeN = dlg.rec.numoffice;
       message(mess);
   end;
   
   if (cmd == DLG_REMFOCUS)
       if (FldName(dlg,id)=="Date")
           if ( dlg.rec.Date > {curdate} )
              MsgBox("Дата начала больше даты текущего операционного дня"); 
              return CM_CANCEL;
           end;
       elif (FldName(dlg,id)=="numoffice")
         if (dlg.rec.numoffice != officeN)
             dlg.rec.operID   = "";
             dlg.rec.opername = "все операционисты";
         end;
         if (dlg.rec.numoffice == "")
             dlg.rec.Office = OfficeName;
         end;
       end;
       UpdateFields(dlg); 
   end; 

   if (cmd==DLG_KEY)
       if (KEY==KEY_ESC)
           return exit(1);
       elif (KEY==KEY_SPACE)
           dlg.rec.office = "По всем офисам";
           office     = 0;
       elif (KEY == KEY_F3)
           if (FldName(dlg,id)=="numoffice")
              if (ListDepartment (Department))
                 OfficeName = 
                 dlg.rec.numoffice = department.name;
                 dlg.rec.office    = GetClientName(Department.PartyID);
                 office        = Department.Code;
                 PartyId       = Department.PartyID;
                 UpdateFields(dlg);
              end;
           elif(FldName(dlg,id) == "OperID")
                          rselect = "select t.t_oper, t.t_name " +   
                      " from dperson_dbt t inner join ddp_dep_dbt v on v.t_name = '"+dlg.rec.numoffice+"' and t.t_codedepart = v.t_code;";

               cmd = RSDCommand(rselect);
               rsrec = RSDRecordset( cmd, RSDVAL_CLIENT, RSDVAL_STATIC );
               if (RunScroll(rsrec, 0, 0, "Опера", @EvProc, "Список операционистов", "~Enter~ выбор сотрудника ~Esc~ выход", true))
                  dlg.rec.operID = rsrec.value (0);
                  dlg.rec.OperName = rsrec.value (1);
                  UpdateFields(dlg);
                  OperSelect = rsrec.value (0);
               end;
           end;
       elif ( KEY==KEY_F2 )
           if (StrLen(trim(dlg.rec.numoffice)) == 0)
              office     = {NumDprt};
              OfficeName = {Name_Bank};
              OperSelect = "";
              OrgRegStruct = "";
           end;  
              date_report  = dlg.rec.Date;
         /*02/09/2008 Новиков С. С. добавил возможность формирования отчета только при корректных значениях*/
           if ( dlg.rec.Date <= {curdate} )
            Return CM_SAVE;
          else
            MsgBox("Дата начала больше даты текущего операционного дня"); 
          end;
         /**************************************************************************************************/
        elif ( (KEY==KEY_ENTER) and (FldName(dlg,id)=="Date") )
           SetFocus(dlg, 0);
           Return CM_IGNORE;
       end;
   end;
END;

if ( RunDialog(dlg, "Event"))
  PrintItAll();
end;