/*+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

   Гуцу Е.В.

   Имя файла: -

   Создан:    12.06.2014

   Описание:  Выгрузка для проверки ЦБ

   ChangeLog:
-----------------------------------------------------------------------------------------------------------------------------------------------------

+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++*/

import rsd, cb_sql, "or_const.mac", "keycodes.mac", Календарь;

var {CurDate}, {OurBank};
var ExcelApplication,
    ActiveSheet,
    startAX;
var LastRow;

var DateBegin, DateEnd, CommNumb = 0, CommType = 0, CommCode = "";
var col = TArray;

var path, resname = "Pcom.lbr", pathfile;

  GetRegistryValue("BANK_INI\\ОБЩИЕ ПАРАМЕТРЫ\\ДИРЕКТОРИИ\\LBRDIR",2,path);
  pathfile = FindPath(resname, path);
  if (not pathfile)
    msgbox("Не найдена LBR " + resname);
    exit();
  end;

var dlg = TRecHandler("com_indx", pathfile, True);


macro SchaffenExcelDatei()

   if (ExcelApplication == null)
      if (isStandAlone())
         ExcelApplication = ActiveX("Excel.Application", null, true);
      else
         if (startAX == null)
            startAX = CreateObject("rsax", "TRsAxServer", "LoansAxServer", isStandalone());
         end;
         ExcelApplication = startAX.CreateComObject("Excel.Application");
      end;
   end;

   ExcelApplication.Workbooks.Add();
   ExcelApplication.Visible = false;
   ActiveSheet = ExcelApplication.ActiveSheet;
   return true;

   onError (err)
      msgbox( err.message );
      Exit(1);

end;


macro DruckenBerichtskopf( startDate, finishDate )

   ActiveSheet.Cells( 2, 2 ).value = "Отчёт по оплаченным комиссиям за период с " + string( startDate:m ) + " по " + string( finishDate:m );

   ActiveSheet.Cells( 4, 2 ).value = "Пункт комиссии";
   ActiveSheet.Cells( 4, 3 ).value = "N р/сч клиента";
   ActiveSheet.Cells( 4, 4 ).value = "Сумма начисленная";
   ActiveSheet.Cells( 4, 5 ).value = "Остаток на " + {CurDate};
   ActiveSheet.Cells( 4, 6 ).value = "Дата";
   ActiveSheet.Cells( 4, 7 ).value = "Статус";

   ActiveSheet.columns("A:D").entirecolumn.autofit;

end;


macro AusfuhrenBericht( startDate, finishDate, filter )

   var sql, cmd, rs, from;
   var rowCount, i = 0;

   from = " From ( " +
         "         Select pm.t_payeraccount                                             vAccount,       " +
         "                pm.t_baseamount                                               vAmount,        " +
         "                pm.t_futurebaseamount                                         vRest,          " +
         "                pm.t_valuedate                                                vDate,          " +
         "                Case                                                                          " +
         "                   When pm.t_futurebaseamount = 0                                             " +
         "                   Then 'Оплачен'                                                             " +
         "                   When pm.t_futurebaseamount = pm.t_baseamount                               " +
         "                   Then 'Не оплачен'                                                          " +
         "                   When pm.t_futurebaseamount between 0.01                                    " +
         "                                                  and (pm.t_baseamount - 0.01)                " +
         "                   Then 'Частично оплачен'                                                    " +
         "                End                                                           vStatus         " +
         "           From dpmpaym_dbt pm, dsfdef_dbt sf                                                 " +
         "          Where sf.t_feetype         = ?                                                      " +
         "            and sf.t_commnumber      = ?                                                      " +
         "            and sf.t_status         in ( 30, 40 )                                             " +
         "            and pm.t_valuedate between ? and ?                                                " +
         "            and pm.t_feetype         = sf.t_feetype                                           " +
         "            and pm.t_defcomid        = sf.t_id                                                " +
         "            and pm.t_partpaymnumber  = 0                                                      " +
         "               )                                                                              " ;

   sql = "Select count(0)" + from;
   cmd = rsdCommand( sql );
   cmd.addParam( "", RSDBP_IN, CommType  );
   cmd.addParam( "", RSDBP_IN, CommNumb  );
   cmd.addParam( "", RSDBP_IN, DateBegin );
   cmd.addParam( "", RSDBP_IN, DateEnd   );
   rs = rsdRecordSet( cmd );
   if( rs and rs.moveNext )
      rowCount = rs.value( 0, null, V_INTEGER );
   end;

   if( rowCount > 0)
      initProgress( rowCount, "", "Обработка списка клиентов" );

      sql = "Select *" + from;
      cmd = rsdCommand( sql );
      cmd.addParam( "", RSDBP_IN, CommType  );
      cmd.addParam( "", RSDBP_IN, CommNumb  );
      cmd.addParam( "", RSDBP_IN, DateBegin );
      cmd.addParam( "", RSDBP_IN, DateEnd   );
      rs = rsdRecordSet( cmd );
      while( rs and rs.moveNext )

         i = i + 1;
         useProgress( i );
         ActiveSheet.Cells( i+5, 2 ).value = string(CommCode + ".");
         ActiveSheet.Cells( i+5, 3 ).value = string( rs.value( 0, null, V_STRING ):f );
         ActiveSheet.Cells( i+5, 4 ).value = rs.value( 1, null, V_MONEY );
         ActiveSheet.Cells( i+5, 5 ).value = rs.value( 2, null, V_MONEY );
         ActiveSheet.Cells( i+5, 6 ).value = string( SQL_convtypedate( rs.value( 3, null, V_DATE ) ) );
         ActiveSheet.Cells( i+5, 7 ).value = rs.value( 4, null, V_STRING );
      end;
   end;

   LastRow = i;
   remProgress();

end;


macro FormatierenBericht()

//ExcelApplication.Visible = true;
//debugbreak;

   ActiveSheet.Range( "B2:G2" ).merge;
   ActiveSheet.Range( "B2:G2").HorizontalAlignment = ALIGN_LEFT;
   ActiveSheet.Cells( 2, 2 ).font.size = 15;

   ActiveSheet.columns( "A:C" ).entirecolumn.NumberFormat = "000000";
   ActiveSheet.columns( "A:G" ).entirecolumn.autofit;
   ActiveSheet.columns( "A:C" ).HorizontalAlignment = ALIGN_LEFT;
   ActiveSheet.columns( "E:G" ).HorizontalAlignment = ALIGN_RIGHT;

end;


macro SpeichernBericht();

   var ExcelDateiName = "$CommIndex_" + random(100) + "_" + UserNumber + ".xls";

   ExcelApplication.Visible = true;

   var wb = ExcelApplication.Workbooks.item(1);
       wb.SaveAs( ExcelDateiName, 56 );

   onError (err)
      return

end;


/*Обработчик RunScrolla*/
macro EvProc (rs, cmd, id, key )

  if (cmd == DLG_KEY) 
    if ((key == KEY_ENTER) or (key == KEY_F2))
      return CM_SELECT;
    end;
  end;
end;

macro AddCol (ar,ind, fld, head, width, rdonly)
  ar.value (ind * 6)     = fld;
  ar.value (ind * 6 + 1) = head;
  ar.value (ind * 6 + 2) = width;
  ar.value (ind * 6 + 3 ) = 0;   // fldType
  ar.value (ind * 6 + 4 ) = -1;  // decPoint
  ar.value (ind * 6 + 5 ) = 0;   // reserv
end;

/*Обработчик панели*/
macro HandleEvent( dlg, cmd, id, key )

var rs, sql;
var const_message = "~F2~ Выполнить ";

  if (cmd == DLG_INIT)

    dlg.rec.BeginDate = {curdate};
    dlg.rec.EndDate   = {curdate};

    UpdateFields(dlg);
  
  elif (cmd == DLG_SETFOCUS)
  
    if (FldName(dlg, id) == "CommNumb")
      message(const_message + "~F3~ Выбор комиссий для отчета");
    elif ((FldName(dlg, id) == "BeginDate") or (FldName(dlg, id) == "EndDate"))
      message(const_message + "~F3~ Календарь");
    else
     message(const_message);
    end;
    
    UpdateFields(dlg);
  
  elif (cmd == DLG_KEY)
  
    if (key == KEY_ESC)
      if (GetTrue(TRUE,"Вы действительно хотите выйти?"))
        exit(1);
        return  CM_CANCEL;
      else
        return  CM_IGNORE;    
      end;
  
    elif (key == KEY_F3)
    
      if(FldName(dlg, id) == "CommNumb")
      
        sql = " SELECT case t_feetype                   " +
              "         WHEN 1 THEN 'Периодическая'     " +
              "         WHEN 3 THEN 'Единовременная'    " +
              "         WHEN 6 THEN 'Разовая'           " +
              "        END as type,                     " +
              "        t_code,                          " +
              "        t_name,                          " +
              "        t_number,                        " +
              "        t_feetype                        " +
              "   FROM dsfcomiss_dbt                    " +
              "  WHERE t_servicekind = 3                " +
              "    AND t_number >= 1000                 " +
              "  ORDER BY t_number                      ";

        cmd = RSDCommand(sql);
        rs = RSDRecordset( cmd, RSDVAL_CLIENT, RSDVAL_STATIC );
        
        AddCol (col, 0, "type",     "Тип взимания", 15, true);
        AddCol (col, 1, "t_code",   "Код",          10, true);
        AddCol (col, 2, "t_name",   "Наименование", 40, true);
        AddCol (col, 3, "t_number", "Номер",         5, true);
        
        if (RunScroll (rs, 4, col, "Comission", "EvProc", "Выберите комисии", "Для выбора комиссии нажмите ~Enter~"))
          dlg.rec.CommNumb = rs.value(1) + " " + rs.value(2);
          CommNumb = rs.value(3);
          CommType = rs.value(4);
          CommCode = rs.value(1);
          UpdateFields(dlg);
        else
          msgbox("Вы отказались от выбора комиссий");
          dlg.rec.CommNumb = "";
          CommNumb = 0;
          CommType = 0;
          CommCode = "";
          return  CM_IGNORE;
        end;
      
      elif(FldName(dlg, id) == "BeginDate")
        dlg.rec.BeginDate = GetDateByCalendar({curdate});
        UpdateFields(dlg);
      elif(FldName(dlg, id) == "EndDate")
        dlg.rec.EndDate = GetDateByCalendar({curdate});
        UpdateFields(dlg);
      end;
      
      UpdateFields(dlg);
      
    elif (key == KEY_F9)
      return  CM_IGNORE;

    elif (key == KEY_F2)
      if (CommNumb == 0)
        msgbox("Не указана комиссия. | Необходимо указать комиссию.");
        return CM_IGNORE;
      end;
      DateBegin = dlg.rec.BeginDate;
      DateEnd   = dlg.rec.EndDate;

      UpdateFields(dlg);
      return  CM_SAVE;

    end;/*End key*/
  
  end;

end;





if (RunDialog(dlg,"HandleEvent"))

SchaffenExcelDatei();
DruckenBerichtskopf( DateBegin, DateEnd );
AusfuhrenBericht( DateBegin, DateEnd );
FormatierenBericht();
SpeichernBericht();

 
end;





exit( 1 );